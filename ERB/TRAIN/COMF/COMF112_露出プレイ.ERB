;露出プレイ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME112
LOCALS:0 = 露出play
RESULTS:0 = %LOCALS:0%
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被动)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE112
;共通部分
CALL COM_ABLE_COMMON(112)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人
SIF MPLY_NUM != 1
	RETURN 0

SIF ITEM:項圈 == 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0

;宮殿以外or既に露出プレイ中なら不可
SIF TFLAG:54 > 0 && !FLAG:RECHECKING
	RETURN 0

SIF IS_BIND(MPLY:0) && !FLAG:RECHECKING
	RETURN 0

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM112
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

IF MPLY:0 == MASTER
	PRINTFORML 去哪里……？
	;TranslatedMark どこにいこう……？

	FOR LOCAL, 0, VARSIZE("IAN_PLACE")
		PRINTFORML [{LOCAL}] %IAN_PLACE:LOCAL%
	NEXT
	PRINTFORML [99]停止
	;TranslatedMark [99] やめておく

	$INPUT_LOOP
	INPUT
ELSE
	RESULT = RAND(0, VARSIZE("IAN_PLACE"))
	PRINTFORM %ANAME(MPLY:0)%是
	;TranslatedMark %ANAME(MPLY:0)%は
ENDIF

IF RESULT == 99
	PRINTFORML 还是算了
	;TranslatedMark やはりやめておいた
	RETURN 0
ELSEIF INRANGE(RESULT, 0, VARSIZE("IAN_PLACE"))
	PRINTFORML 决定去%IAN_PLACE:RESULT%
	;TranslatedMark %IAN_PLACE:RESULT%に行くことにした
	TFLAG:105 = RESULT
	TSTR:0 = %TRAIN_PLACE%
	TRAIN_PLACE = %IAN_PLACE:RESULT%
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 40

	SOURCE:(LOCAL:2):嗜虐 = 20
	SOURCE:(LOCAL:2):露出 = 800
	SOURCE:(LOCAL:2):逸脱 = 20
	SOURCE:(LOCAL:2):不安 = 30

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 160)
NEXT

;●プレイヤー以外の全参加者について処理
FOR LOCAL:0, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN(LOCAL) || LOCAL:0 == MPLY:0
		CONTINUE

	DOWNBASE:LOCAL:体力 += 80

	SOURCE:LOCAL:逸脱 = 80
	SOURCE:LOCAL:露出 = 1500
	SOURCE:LOCAL:不安 = 30

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL, 160, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

TFLAG:54 = 8
TFLAG:103 = 0

RETURN 1


;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP112
;継続コマンドかつフィルタリング不可
RETURN 2
;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP112(ARG:0)
;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):嗜虐 += 10
	SOURCE:(LOCAL:2):露出 += 500 + 50 * TFLAG:104
	SOURCE:(LOCAL:2):逸脱 += 10
	SOURCE:(LOCAL:2):不安 += 15

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 10, 50)

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 3
NEXT


;●プレイヤー以外の全参加者について処理
FOR LOCAL:0, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN(LOCAL) || LOCAL:0 == MPLY:0
		CONTINUE

	DOWNBASE:LOCAL:体力 += 40

	SOURCE:LOCAL:逸脱 = 10
	SOURCE:LOCAL:露出 = 1000 + 100 * TFLAG:104
	SOURCE:LOCAL:不安 = 10

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL, 80, 20)

	;倒錯度変化基準値
	TCVAR:LOCAL:50 += 3
NEXT

;継続ターン増加
TFLAG:103 ++

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE112(ARG:0)
RESULTS = 露出プレイ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP112(ARG:0)

IF TFLAG:44 >= 8
	RETURN 
ENDIF

IF RAND:MAX(TFLAG:103, 1) * 4 - TFLAG:104 > RAND:20
	CALL CREATE_IAN_MOB(TFLAG:105, 1, 0)
	LOCAL:2 = CHARANUM - 1
	TALENT:(LOCAL:2):露出playモブ = 1
	PRINTFORML 听到骚动的%ANAME(LOCAL:2)%参加了…
	;TranslatedMark 騒ぎを聞きつけた%ANAME(LOCAL:2)%が参加した…
	TFLAG:104 ++
	TFLAG:44 ++
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP112(ARG:0)
TFLAG:54 = 0
TFLAG:103 = 0
TFLAG:104 = 0
;モブはここで削除する
LOCAL:2 = CHARANUM
FOR LOCAL:0, 0, LOCAL:2
	LOCAL:1 = LOCAL:2 - LOCAL:0 - 1
	IF TALENT:(LOCAL:1):露出playモブ
		SIF FLAG:主導権所有者 == LOCAL:1
			FLAG:主導権所有者 = -1
		CALL DEL_MPLY(LOCAL:1)
		CALL DEL_MTAR(LOCAL:1)
		CALL RELEASE_ALL_EQUIP(LOCAL:1)
		CALL DELETE_CHARA(LOCAL:1)
		TFLAG:44 --
	ENDIF
NEXT
PRINTFORMW 回到屋里去了
;TranslatedMark 屋内に戻った
TRAIN_PLACE = %TSTR:0%

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER112(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):不知羞恥
	CALL COM_ORDER_ELEMENT(ARG:0, "不知羞恥", 5)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE112

LOCALS:0 = %ANAME(MPLY:0)%

PRINTFORMW %LOCALS:0%决定在室外活动…
;TranslatedMark %LOCALS:0%は屋外で行為をすることにした…


;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN112
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_112()
RETURN POWER(2, 性的嗜好_辱めたい)

@COM_PREFERENCE_TARGET_112()
RETURN POWER(2, 性的嗜好_辱められたい) + POWER(2, 性的嗜好_露出したい)


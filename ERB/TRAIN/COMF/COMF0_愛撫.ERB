;愛撫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME0
IF MTAR_NUM >= 2
	LOCALS:0 = 同時愛撫
ELSEIF MPLY_NUM >= 2
	LOCALS:0 = 一斉愛撫
ELSE
	LOCALS:0 = 愛撫
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE0
CALL COM_ABLE_COMMON(0)
SIF RESULT == 0
	RETURN 0
;1+
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットはプレイヤーの倍まで
SIF MTAR_NUM <= 0 || MTAR_NUM > MPLY_NUM * 2
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF !CAN_REACH_BODY(MPLY:(LOCAL:0),MTAR:(LOCAL:1)) 
			RETURN 0
	NEXT
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM0
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
	CASE 4
		TIMES LOCAL:10, 0.50
	CASE 5
		TIMES LOCAL:10, 0.45
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 = 100
	SOURCE:(LOCAL:2):性行動 = 90

	;主人公がターゲットなら愛情のソース追加
	IF IS_MTAR(MASTER)
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 50)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 100, 20)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 60

	SOURCE:(LOCAL:2):露出 = 100
	SOURCE:(LOCAL:2):接触 = 100
	SOURCE:(LOCAL:2):性行動 = 180

	;主人公がプレイヤーなら愛情のソース追加
	IF IS_MPLY(MASTER)
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 100)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 0)


NEXT

;1-1のときのみキスを追加
IF MPLY_NUM == 1 && MTAR_NUM == 1
	;どちらもキス未経験でない and 口が絡む行為を行っていない and キスができない態勢ではない
	IF !TALENT:(MPLY:0):無接吻経験 && !TALENT:(MTAR:0):無接吻経験 && !IS_EQUIP_PLAYER(MPLY:0, 2, 11, 12, 20, 104) && !IS_EQUIP_PLAYER(MTAR:0, 2, 11, 12, 20, 104) && !IS_RIDDEN(MPLY:0) && !IS_RIDDEN(MTAR:0)
		TFLAG:17 = 1

		EXP:(MPLY:0):接吻経験 += 1
		EXP:(MTAR:0):接吻経験 += 1

		SOURCE:(MPLY:0):快Ｍ += SENSE_HOUSHI(MTAR:0, MPLY:0, 100)
		SOURCE:(MTAR:0):快Ｍ += SENSE_HOUSHI(MPLY:0, MTAR:0, 100)

		SOURCE:(MTAR:0):奉仕 += SERVE_HOUSHI(LOCAL:2, 50)

		TIMES SOURCE:(MPLY:0):愛情, 2.00
		TIMES SOURCE:(MTAR:0):愛情, 2.00
	ENDIF
ENDIF

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;挿入中だと性能低下
	IF IS_INSERT_SINGLE(LOCAL:1, "全")
		TIMES SOURCE:(LOCAL:1):快Ｃ, 0.80
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP0
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP0(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)


	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 30

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400) * LOCAL:10 / 100


	NEXT

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 1
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):露出 += 50
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 60

	;1-1のときのみキスを追加
	IF MPLY_NUM == 1 && MTAR_NUM == 1
		;どちらもキス未経験でない and 口が絡む行為を行っていない and キスができない態勢ではない
		IF !TALENT:(LOCAL:3):無接吻経験 && !TALENT:(LOCAL:2):無接吻経験 && !IS_EQUIP_PLAYER(LOCAL:3, 2, 11, 12, 20, 104) && !IS_EQUIP_PLAYER(LOCAL:2, 2, 11, 12, 20, 104) && !IS_RIDDEN(LOCAL:3) && !IS_RIDDEN(LOCAL:2)
			TFLAG:17 = 1

			EXP:(LOCAL:3):接吻経験 += 1
			EXP:(LOCAL:2):接吻経験 += 1

			SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 100)
			SOURCE:(LOCAL:2):快Ｍ += SENSE_HOUSHI(LOCAL:3, LOCAL:2, 100)

			SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 50)

			TIMES SOURCE:(LOCAL:3):愛情, 2.00
			TIMES SOURCE:(LOCAL:2):愛情, 2.00
		ENDIF
	ENDIF



	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 1
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE0(ARG:0)
LOCAL:2 = 0
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%愛撫%EQUIP_TARGET_ANAME(ARG:0)%中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP0(ARG:0)

PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正责弄著%EQUIP_TARGET_ANAME(ARG:0)%的身体…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP0(ARG:0)


;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER0(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 60

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE0
;1-1のときのみキスを追加
LOCALS:0 = 
IF MPLY_NUM == 1 && MTAR_NUM == 1
	;キスをした場合
	IF TFLAG:17
		LOCALS:0 = 一邊親吻一邊
	ENDIF
ENDIF

;ターゲットが2人
IF MTAR_NUM == 2
	LOCALS:1 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;ターゲットが1人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

;プレイヤーが複数
IF MPLY_NUM >= 3
	LOCALS:2 = %ANAME(MPLY:0)%們
;プレイヤーが2人
ELSEIF MPLY_NUM == 2
	LOCALS:2 = %ANAME(MPLY:0)%和%ANAME(MPLY:1)%
;プレイヤーが1人
ELSE
	LOCALS:2 = %ANAME(MPLY:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:2%%LOCALS:0%溫柔地用手指來回撫摸著%LOCALS:1%的肌膚…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:2%聽從%LOCALS:1%的指示、%LOCALS:0%慢慢地來回撫摸著%LOCALS:1%的肌膚…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:2%去愛撫%LOCALS:1%的身體
		PRINTFORMW %LOCALS:2%%LOCALS:0%溫柔地用手指來回撫摸著%LOCALS:1%的肌膚…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST0
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):快Ｃ + CUP:(LOCAL:1):快Ｂ + CUP:(LOCAL:1):快Ｍ

		IF TCVAR:(LOCAL:1):52
			;気絶中
			PRINTFORML %ANAME(LOCAL:1)%失去了意識身體不住地顫抖
		ELSEIF LOCAL:2 < 100
			IF TALENT:(LOCAL:1):反抗的 && !TALENT:(LOCAL:1):合意
				PRINTFORML %ANAME(LOCAL:1)%対被愛撫的事採取著反抗的態度
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%因為瘙癢扭動著身體好像並沒有感到快感的樣子
			ENDIF
		ELSEIF LOCAL:2 < 300
			PRINTFORML %ANAME(LOCAL:1)%因為愛撫的刺激身體微顫
		ELSEIF LOCAL:2 < 1000
			PRINTFORML %ANAME(LOCAL:1)%好像被少量電流傳遍身體一般輕輕顫抖
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %ANAME(LOCAL:1)%能明顯的感覺到快感発出了嬌喘
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %ANAME(LOCAL:1)%因愛撫感到了激烈的快感、主動摟住了対方的脖子央求著更多
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%僅僅因為愛撫就能得到猛烈的快感、渴求著更多刺激緊緊的依偎過來
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN0
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_0()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_0()
RETURN POWER(2, 性的嗜好_愛撫されたい)


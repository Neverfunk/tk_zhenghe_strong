;イラマチオ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME90
;挿入中
LOCALS:0 = 
SIF IS_INSERT_SINGLE(MPLY:0, "Ｐ")
	LOCALS:0 += "掃除"

SELECTCASE MPLY_NUM
	CASE 1
	CASE 2
		LOCALS:0 += "二本"
	CASE 3
		LOCALS:0 += "三本"
	CASEELSE
		LOCALS:0 += "輪"
ENDSELECT

LOCALS:0 += "強制口交"

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE90
;共通部分
CALL COM_ABLE_COMMON(90)
SIF RESULT == 0
	RETURN 0
;プレイヤーが必要
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーに竿がなく、ペニスバンド装着中でもないと不可
SIF !HAS_PENIS(MPLY:0) && !IS_EQUIP_PLAYER((MPLY:0), 50)
	RETURN 0
SIF !CAN_LICK_GROIN(MTAR:0, MPLY:0, 1)
	RETURN 0
SIF !P_STACKABLE(MTAR:0, MPLY:0, 90)
	RETURN 0
SIF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) > 0
	RETURN 0
SIF IS_RIDE(MPLY:0, MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;快Ｍソースの倍率を取得する関数 ARG:0=PLAYERのキャラ番号
;-------------------------------------------------
@COM90_RATE_M(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):奉仕
	CASE 0
		TIMES LOCAL:5, 0.25
		;TIMES SOURCE:(LOCAL:2):不潔, 4.00
	CASE 1
		TIMES LOCAL:5, 0.50
		;TIMES SOURCE:(LOCAL:2):不潔, 2.50
	CASE 2
		TIMES LOCAL:5, 0.60
		;TIMES SOURCE:(LOCAL:2):不潔, 1.50
	CASE 3
		TIMES LOCAL:5, 0.80
		;TIMES SOURCE:(LOCAL:2):不潔, 1.00
	CASE 4
		TIMES LOCAL:5, 1.00
		;TIMES SOURCE:(LOCAL:2):不潔, 0.50
	CASEELSE
		LOCAL:5 = LOCAL:5 * (100 + (ABL:(ARG:0):奉仕 - 5) * 3) / 100
		;TIMES SOURCE:(LOCAL:2):不潔, 0.10
ENDSELECT

SELECTCASE ABL:(ARG:0):性技
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(ARG:0):性技 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM90
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;プレイヤーの挿入状態を解除


;●プレイヤーについて判定
FOR LOCAL, 0, MPLY_NUM
	LOCAL:1 = MPLY:LOCAL
	CALL CLEAR_INSERT_FLAG(LOCAL:1, "Ｐ")
	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):嗜虐 = 50
	SOURCE:(LOCAL:1):逸脱 = 50
	SOURCE:(LOCAL:1):性行動 = 180
	EXP:(LOCAL:1):嗜虐経験値 += 1

	LOCAL:2 = SENSE_HOUSHI_P(MTAR:0, LOCAL:1, 600)
	IF TALENT:(MTAR:0):舌頭霊活
		TIMES LOCAL:2, 1.50
	ENDIF

	SOURCE:(LOCAL:1):快Ｐ += SENSE_HOUSHI_P(LOCAL:1, LOCAL:1, 800) + LOCAL:2

	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 160, 60)

NEXT

;●ターゲットについて判定
EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):口淫経験 += 1
EXP:(MTAR:0):被虐経験値 += 1

SOURCE:(MTAR:0):奉仕 = SERVE_HOUSHI(MTAR:0, 100)
SOURCE:(MTAR:0):接触 = 50
SOURCE:(MTAR:0):快Ｍ = 600 * COM90_RATE_M(MTAR:0) / 1000
SOURCE:(MTAR:0):逸脱 = 100
SOURCE:(MTAR:0):苦痛 = 130 + MIN(ABL:(MPLY:0):武闘, 90) / 2
SOURCE:(MTAR:0):性行動 = 300

;主導権に応じた優越・受動のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 50, 150)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;ペニスへのキス
IF MTAR_NUM == 1
		;ペニスへのキス
	CALL KISS_COMMON(MTAR:0, @"%ANAME(MPLY:0)%のペニス", GET_SITUATION_BY_TRAIN_MODE())
ELSE
	CALL KISS_COMMON(MTAR:0, @"%ANAME(MPLY:0)%たちのペニス", (GET_SITUATION_BY_TRAIN_MODE() == "和姦" ? "乱交" # "輪姦"))
ENDIF

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 3

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP90
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP90(ARG:0)
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●プレイヤーについて判定
FOR LOCAL, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):LOCAL
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):嗜虐 += 25
	SOURCE:(LOCAL:2):性行動 += 60
	EXP:(LOCAL:2):嗜虐経験値 += 1

	LOCAL:4 = SENSE_HOUSHI_P(LOCAL:3, LOCAL:2, 200)
	IF TALENT:(LOCAL:3):舌頭霊活
		TIMES LOCAL:4, 1.50
	ENDIF

	SOURCE:(LOCAL:2):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:2, 250) + LOCAL:4


	SOURCE:(LOCAL:3):苦痛 += 65 + MIN(ABL:(LOCAL:2):武闘, 90) / 4
NEXT

;●ターゲットについて判定
EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):口淫経験 += 1
EXP:(LOCAL:3):被虐経験値 += 1

SOURCE:(LOCAL:3):奉仕 += SERVE_HOUSHI(LOCAL:3, 30)
SOURCE:(LOCAL:3):接触 += 25
SOURCE:(LOCAL:3):快Ｍ += 300 * COM90_RATE_M(LOCAL:3) / 1000
SOURCE:(LOCAL:3):逸脱 += 50
SOURCE:(LOCAL:3):性行動 += 100

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE90(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%强制深喉中
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%にイラマチオ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP90(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%用%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%蹂躏着%EQUIP_TARGET_ANAME(ARG:0)%的口内…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の口内を%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%で蹂躙している…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP90(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%从%EQUIP_TARGET_ANAME(ARG:0)%的口中拔出了%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の口から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER90(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 100

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"加虐Lv{ABL:(ARG:0):加虐}", ABL:(ARG:0):加虐 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):老実
	CALL COM_ORDER_ELEMENT(ARG:0, "老実", -3)
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "加虐", 15)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE90
IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = 小巧的口
ELSE
	LOCALS:0 = 口
ENDIF
IF IS_PIERCED(MPLY:0, ピアス_ペニス)
	LOCALS:1 = 上著環的
ELSE
	LOCALS:1 = 
ENDIF
;自前の竿
IF HAS_PENIS(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0, MPLY_NUM)%把%LOCALS:1%肉棒塞进%ANAME(MTAR:0)%的%LOCALS:0%内，突进到喉咙深处…
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は%ANAME(MTAR:0)%の%LOCALS:0%に%LOCALS:1%竿を押し込み、喉奥を突い%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%在%ANAME(MPLY:0, MPLY_NUM)%的阴茎前张开嘴，命令%ANAME(MPLY:0, MPLY_NUM)%用这张嘴来自慰
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0, MPLY_NUM)%のペニスの前で口を開けると、その口を使ってオナニーするように%ANAME(MPLY:0, MPLY_NUM)%に命じた
			PRINTFORMW %ANAME(MPLY:0, MPLY_NUM)%就这样把%LOCALS:1%肉棒插进%ANAME(MTAR:0)%的%LOCALS:0%内，拼命地摆动着腰…
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は言われるまま%ANAME(MTAR:0)%の%LOCALS:0%に%LOCALS:1%竿を突き入れ、必死に腰を振った…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0, MPLY_NUM)%，把阴茎插入%ANAME(MTAR:0)%的嘴里，进行侵犯
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0, MPLY_NUM)%に対して、%ANAME(MTAR:0)%の口にペニスを突き入れて犯すように命令した
			PRINTFORMW %ANAME(MPLY:0, MPLY_NUM)%就这样把%LOCALS:1%肉棒插进%ANAME(MTAR:0)%的%LOCALS:0%内，激烈地摆动着腰…
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は言われるまま%ANAME(MTAR:0)%の%LOCALS:0%に%LOCALS:1%竿を突き入れ、激しく腰を振った…
	ENDSELECT

;穿戴式假陰茎
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0, MPLY_NUM)%在%ANAME(MTAR:0)%的%LOCALS:0%里插入戴着的假肉棒，突刺到喉咙深处…
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は%ANAME(MTAR:0)%の%LOCALS:0%に張り型を押し込み、喉奥を突い%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%命令%ANAME(MPLY:0, MPLY_NUM)%把戴着的假肉棒插到%ANAME(MTAR:0)%的嘴里进行侵犯
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0, MPLY_NUM)%に、装着した張り型で%ANAME(MTAR:0)%の口内を犯すように命じた
			PRINTFORMW %ANAME(MPLY:0, MPLY_NUM)%按照被命令的那样插入%ANAME(MTAR:0)%的%LOCALS:0%内，摆动着腰、突刺到喉咙里头…
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は言われるまま%ANAME(MTAR:0)%の%LOCALS:0%に張り型を突き入れ、腰を振って喉奥を突いた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0, MPLY_NUM)%把戴着的假肉棒插到%ANAME(MTAR:0)%的嘴里进行侵犯
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0, MPLY_NUM)%に対して、%ANAME(MTAR:0)%の口に張り型を突き入れて犯すように命令した
			PRINTFORMW %ANAME(MPLY:0, MPLY_NUM)%按照被命令的那样插入%ANAME(MTAR:0)%的%LOCALS:0%内，摆动着腰、突刺到喉咙里头…
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は言われるまま%ANAME(MTAR:0)%の%LOCALS:0%に張り型を突き入れ、腰を振って喉奥を突いた…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST90
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !CFLAG:(LOCAL:1):TEXT_LAST非表示
		LOCAL:2 = CUP:(LOCAL:1):射精 + CUP:(LOCAL:1):快Ｃ

		IF LOCAL:2 < 1000
			PRINTFORML %ANAME(LOCAL:1)%轻松地来回摆动着腰，在%ANAME(MTAR:0)%的嘴里抽动着%BAR_NAME(MPLY:0)%
			;TranslatedMark %ANAME(LOCAL:1)%はひたすら淡々と腰を振って、%ANAME(MTAR:0)%の口に%BAR_NAME(MPLY:0)%を打ち付けている
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %ANAME(LOCAL:1)%一边不时发出感觉不错的声音，一边在%ANAME(MTAR:0)%的嘴里抽动着%BAR_NAME(MPLY:0)%
			;TranslatedMark %ANAME(LOCAL:1)%は時折気持ち良さそうに声を上げながら、%ANAME(MTAR:0)%の口に%BAR_NAME(MPLY:0)%を打ち付けている
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %ANAME(LOCAL:1)%一边发出似乎很舒服的声音，一边摇着腰品尝着%ANAME(MTAR:0)%嘴巴的触感
			;TranslatedMark %ANAME(LOCAL:1)%は気持ち良さそうな声を出しながら、腰を振って%ANAME(MTAR:0)%の口の感触を味わっている
		ELSEIF LOCAL:2 < 10000
			PRINTFORML %ANAME(LOCAL:1)%心情舒畅地一边喘息，一边被快感刺激着激烈地摆动着腰，体味着%ANAME(MTAR:0)%的口的触感
			;TranslatedMark %ANAME(LOCAL:1)%は気持ち良さそうに喘ぎながら、快感に突き動かされるように腰を打ち付けて%ANAME(MTAR:0)%の口の感触を味わっている
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%因为快感而表情变得荡漾起来，像野兽一样摆动着腰，享受着%ANAME(MTAR:0)%的口穴
			;TranslatedMark %ANAME(LOCAL:1)%は快感のあまり表情を蕩けさせながら、獣の様に腰を打ち付けて%ANAME(MTAR:0)%の口マンコを堪能している
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN90
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_90()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_90()
RETURN POWER(2, 性的嗜好_虐げられたい)

@COM_STACK_SPERM_MPLY_TO_MTAR_90()
RETURN RAND:3 ? 射精部位_口 # 射精部位_顔

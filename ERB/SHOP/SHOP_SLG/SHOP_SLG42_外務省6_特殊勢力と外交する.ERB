;-------------------------------------------------
;「特殊勢力と外交する」
;-------------------------------------------------
@Colonial_Office_COMMUNICATE_SP_COUNTRY
#DIM サキュバス
#DIM ホフゴブリン
#DIM 外来人
#DIM 触手
#DIM 外交対象
#DIM 連邦ナンバー
#DIM 連邦構成国, MAX_COUNTRY
VARSET 連邦構成国

FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, MAX_COUNTRY
		IF TREATY_UNI_COUNTRY:(LOCAL:0):(LOCAL:1):0 == CFLAG:MASTER:所属 || TREATY_UNI_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			連邦ナンバー = LOCAL:0
			BREAK
		ENDIF
	NEXT
NEXT

サキュバス = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_サキュバス))
ホフゴブリン = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_ホフゴブリン)
外来人 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_外来人)
触手 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_触手)

SIF IS_SP_COUNTRY_IN_UNION:連邦ナンバー:3 == 1
	IS_RESEARCH_AGREEMENT_WITH_SP:(CFLAG:MASTER:所属) = 1

PRINTFORML 因为通过研究了解到了特殊势力的真实情况，我们可以跟他们进行秘密的外交
PRINTFORML 但是一旦事情败露，就会引起其他势力的高度警惕…
CALL SINGLE_DRAWLINE
PRINTL

FOR LOCAL:0, 0, MAX_COUNTRY
	IF IS_SP_COUNTRY(LOCAL:0) && LOCAL:0 > 0 && COUNTRY_Protectorate:(LOCAL:0) < 1 && IS_TREATY_UNI:(LOCAL:0) != 1
		IF LOCAL:0 == サキュバス && RESEARCH_UNIVERSITY_CAN_COMMUNICATE_SP:(CFLAG:MASTER:所属):6 == 1 && IS_SP_COUNTRY_IN_UNION:連邦ナンバー:6 == 0
			SETCOLOR COUNTRY_COLOR:(LOCAL:0)
			PRINTBUTTON @" {LOCAL:0}[　　　　　保护梦魔]", LOCAL:0
			RESETCOLOR
			PRINTL
		ELSEIF LOCAL:0 == ホフゴブリン && RESEARCH_UNIVERSITY_CAN_COMMUNICATE_SP:(CFLAG:MASTER:所属):2 == 1 && IS_SP_COUNTRY_IN_UNION:連邦ナンバー:2 == 0
			SETCOLOR COUNTRY_COLOR:(LOCAL:0)
			PRINTBUTTON @" {LOCAL:0}[　　　　保护哥布林]", LOCAL:0
			RESETCOLOR
			PRINTL
		ELSEIF LOCAL:0 == 外来人 && RESEARCH_UNIVERSITY_CAN_COMMUNICATE_SP:(CFLAG:MASTER:所属):3 == 1 && IS_SP_COUNTRY_IN_UNION:連邦ナンバー:3 == 0
			SETCOLOR COUNTRY_COLOR:(LOCAL:0)
			PRINTBUTTON @" {LOCAL:0}[　和外来人合作研究]", LOCAL:0
			RESETCOLOR
			PRINTL
		ENDIF
	ENDIF
NEXT
PRINTBUTTON " 0[　　　　　　　取消]", 0

$INPUT_LOOP1
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF IS_SP_COUNTRY(RESULT)
	外交対象 = RESULT
ELSE
	GOTO INPUT_LOOP1
ENDIF

PRINTFORML @"现在的国库资金　　< {MONEY:(CFLAG:MASTER:所属)} >"
PRINTFORML 要进行资金援助的话需要国库资金20,000

IF MONEY:(CFLAG:MASTER:所属) < 20000
	PRINTFORMW 资金不足
	RETURN
ELSE
	PRINTFORML 要支付吗？
	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 1
		RETURN
	ENDIF
ENDIF

IF 外交対象 == サキュバス
	PRINTFORML 魅魔们接受了我国的保护
	PRINTFORMW %ANAME(GET_COUNTRY_BOSS(外交対象))%势力成为了我国的殖民地。
	CALL INIT_Colony(外交対象, (CFLAG:MASTER:所属))
	IS_SP_COUNTRY_IN_UNION:連邦ナンバー:6 = 1
ELSEIF 外交対象 == ホフゴブリン
	PRINTFORML 哥布林们接受了我国的保护
	PRINTFORMW %ANAME(GET_COUNTRY_BOSS(外交対象))%势力成为了我国的殖民地。
	CALL INIT_Colony(外交対象, (CFLAG:MASTER:所属))
	IS_SP_COUNTRY_IN_UNION:連邦ナンバー:2 = 1
ELSEIF 外交対象 == 外来人
	PRINTFORML 外来人们接受了我国的保护
	PRINTFORML %ANAME(GET_COUNTRY_BOSS(外交対象))%势力成为了我国的殖民地。
	PRINTFORMW 和外来人签定了研究协议
	CALL INIT_Colony(外交対象, (CFLAG:MASTER:所属))
	IS_SP_COUNTRY_IN_UNION:連邦ナンバー:3 = 1
	IS_RESEARCH_AGREEMENT_WITH_SP:(CFLAG:MASTER:所属) = 1
ENDIF

MONEY:(CFLAG:MASTER:所属) -= 20000

;連邦構成国を検索
FOR LOCAL:0, 0, MAX_COUNTRY
	IF TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):0 > 0
		連邦構成国:(TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):0) = 1
	ELSEIF TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):1 > 0
		連邦構成国:(TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):1) = 1
	ENDIF
NEXT

;連邦構成国以外の国からの評価を下げる
PRINTL 
PRINTFORMW 所有势力都对这一事态保持高度警惕

FOR LOCAL, 1, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属 || 連邦構成国:(LOCAL) == 1
		CONTINUE
	CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -200, 300)
NEXT
RETURN

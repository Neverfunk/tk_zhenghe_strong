;屈曲位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME39
IF IS_INSERT_SINGLE(MTAR:0, "Ａ") && GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 10, 30) && IS_INSERT_MUTUAL(MPLY:0, MTAR:0) != 2
	LOCALS:0 = 二穴火車便当式
ELSE
	LOCALS:0 = 火車便当式
ENDIF
RESULTS:0 = %LOCALS:0%挿入
RESULTS:1 = %LOCALS:0%挿入(被役)
RESULTS:2 = %LOCALS:0%挿入(使役)
RESULTS:3 = %LOCALS:0%挿入(被动)
RESULTS:4 = %LOCALS:0%挿入(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE39
;共通部分
CALL COM_ABLE_COMMON(39)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
SIF IS_V_HOLD(MTAR:0)
	RETURN 0
;ターゲットのＡが他のキャラの竿で埋まっていて、体位がＡ背面駅弁以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 10, 30)
	RETURN 0
SIF !CAN_FUCK(MPLY:0, MTAR:0, 39)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM39
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 1
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 1
		CASE 2
			TFLAG:17 = 3
		CASE 3
			TFLAG:17 = 2
		CASE 4
			TFLAG:17 = 3
		CASE 5
			TFLAG:17 = 2
		CASE 6
			TFLAG:17 = 3
		CASE 7
			TFLAG:17 = 3
		CASE 8
			TFLAG:17 = 1
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 30
			TFLAG:17 = 1
		CASE 31
			TFLAG:17 = 3
		CASE 32
			TFLAG:17 = 2
		CASE 33
			TFLAG:17 = 3
		CASE 34
			TFLAG:17 = 2
		CASE 35
			TFLAG:17 = 3
		CASE 37
			TFLAG:17 = 3
		CASE 38
			TFLAG:17 = 1
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 160

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:0, MPLY:0, 1500)
SOURCE:(MPLY:0):露出 = 100
SOURCE:(MPLY:0):性行動 = 450

;性交系の共通処理
CALL COM_COMMON_SEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 80)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 120

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ｖ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 1500)
SOURCE:(MTAR:0):露出 = 200
SOURCE:(MTAR:0):性行動 = 450

;性交系の共通処理
CALL COM_COMMON_SEX_V(MTAR:0, MPLY:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 150, 100)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;体位フラグのセット
TCVAR:(MPLY:0):31 = 1
TCVAR:(MTAR:0):33 = 1

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP39
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP39(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:2, 750)
SOURCE:(LOCAL:2):露出 += 50
SOURCE:(LOCAL:2):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_P(LOCAL:2, LOCAL:3)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 -= 1

DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ｖ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 750)
SOURCE:(LOCAL:3):露出 += 100
SOURCE:(LOCAL:3):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_V(LOCAL:3, LOCAL:2)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 -= 1

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE39(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%的小穴以火车便当式挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP39(ARG:0)
SELECTCASE RAND:2
	CASE 0
		PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%的小穴含入了%EQUIP_PLAYER_ANAME(ARG:0)%的%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%…
		;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の膣が%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を咥え込んでいる…
	CASE 1
		LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
		;ターゲットが行動不能
		IF !IS_PLAYABLE(LOCAL:0)
			PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%把筋疲力尽的%EQUIP_TARGET_ANAME(ARG:0)%举起来然后插入，并激烈地扭动着腰…
			;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%はぐったりとしている%EQUIP_TARGET_ANAME(ARG:0)%を持ち上げて挿入したまま、さらに激しく腰を動かしている…
		ELSE
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%插在%EQUIP_TARGET_ANAME(ARG:0)%里，腰更加剧烈地扭动着…
					;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%に挿入したまま、さらに激しく腰を動かしている…
				;ターゲットに主導権
				CASE 1
					PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%在%EQUIP_PLAYER_ANAME(ARG:0)%的腰上缠着脚，要求她更加激烈地扭动腰
					;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%の腰に足を絡めながら、さらに激しく腰を動かすように要求した
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%在%EQUIP_TARGET_ANAME(ARG:0)%的要求下，从下面无数次地撞击臀部…
					;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%に求められるまま、下から何度も腰を打ち付けている…
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%命令%EQUIP_TARGET_ANAME(ARG:0)%以火車便当式插入%EQUIP_PLAYER_ANAME(ARG:0)%的同时更激烈地扭动腰
					;TranslatedMark %ANAME(TFLAG:45)%は%EQUIP_TARGET_ANAME(ARG:0)%に駅弁で挿入している%EQUIP_PLAYER_ANAME(ARG:0)%に対し、もっと激しく腰を動かすように命令した
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%按照指示的那样，从下面无数次地激烈地抽插了%EQUIP_TARGET_ANAME(ARG:0)%的小穴深处…
					;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は言われるがまま、下から激しく何度も%EQUIP_TARGET_ANAME(ARG:0)%の膣奥を突いている…
			ENDSELECT
		ENDIF
ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP39(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%从%EQUIP_TARGET_ANAME(ARG:0)%的小穴内拔出了%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の膣内から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER39(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;膣性交共通
CALL COM_ORDER_VSEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE39
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = 上著環的
ELSE
	LOCALS:0 = 
ENDIF

IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 =未经人事的
;まだ乙女の
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = 湿漉漉的
;濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MTAR:0)
	LOCALS:0 = %LOCALS:0%危険日的
;%LOCALS:0%危険日の
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 =%LOCALS:0%娇小的秘裂
;%LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF
IF SEARCH_EQUIP(20, MPLY:0, MTAR:0) != -1 || SEARCH_EQUIP(20, MTAR:0, MPLY:0) != -1
	LOCALS:1 = 互相交换着唾液的同时
;口づけを交わしながら
ELSE
	LOCALS:1 = 
ENDIF
;ペニスバンド装着中
IF IS_EQUIP_PLAYER((MPLY:0), 50)
	SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%举起%ANAME(MTAR:0)%，%LOCALS:1%将穿戴着的假肉棒刺入%ANAME(MTAR:0)%的%LOCALS:0%中…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を持ち上げると、%LOCALS:1%装着した張り型を%ANAME(MTAR:0)%の%LOCALS:0%に突き入れ%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%让%ANAME(MPLY:0)%抬起身子，%LOCALS:1%把假肉棒引导到自己的%LOCALS:0%处插入…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に自らを持ち上げさせ、%LOCALS:1%自身の%LOCALS:0%へと張り型を導いて挿入させた…
			;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%以火車便当式侵犯%ANAME(MTAR:0)%
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、駅弁で%ANAME(MTAR:0)%を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%举起%ANAME(MTAR:0)%，%LOCALS:1%把穿戴着的假肉棒刺入了%ANAME(MTAR:0)%的%LOCALS:0%…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を持ち上げると、%LOCALS:1%装着した張り型を%ANAME(MTAR:0)%の%LOCALS:0%に突き入れた…
	ENDSELECT
;ペニスバンド不使用
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%举起%ANAME(MTAR:0)%，将肉棒刺入%ANAME(MTAR:0)%的%LOCALS:0%中，%LOCALS:1%开始扭动腰部…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を持ち上げると、%ANAME(MTAR:0)%の%LOCALS:0%にペニスを突き入れ%LOCALS:1%腰を動かし%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW  %ANAME(MTAR:0)%让%ANAME(MPLY:0)%举起自己，%LOCALS:1%引导肉棒插入到自己的%LOCALS:0%…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に自らを持ち上げさせ、%LOCALS:1%ペニスを自身の%LOCALS:0%へと導いて挿入させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%以火車便当式侵犯%ANAME(MTAR:0)%
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、駅弁で%ANAME(MTAR:0)%を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%举起%ANAME(MTAR:0)%，%LOCALS:1%把肉棒刺入%ANAME(MTAR:0)%的%LOCALS:0%，并开始扭动腰部…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を持ち上げると、%LOCALS:1%ペニスを%ANAME(MTAR:0)%の%LOCALS:0%に突き入れて腰を動かした…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST39
SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML 安装在%ANAME(MPLY:0)%肉棒上的铁环，在%ANAME(MTAR:0)%的小穴内用力地剐剜……
	;TranslatedMark %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%の膣内をごりごりと抉っていく……

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN39
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_39()
RETURN POWER(2, 性的嗜好_性交したい)

@COM_PREFERENCE_TARGET_39()
RETURN POWER(2, 性的嗜好_性交されたい)

@COM_STACK_SPERM_MPLY_TO_MTAR_39()
RETURN 射精部位_膣内

;-------------------------------------------------
;醜聞を流す
;-------------------------------------------------
@DIPLOMACY_CREATE_SCANDAL(ARG:0)
#DIM 君主
#DIM 相手君主
#DIM FIRST_LINE
#DIM CONST 支払額リスト = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
#DIM 支払額
#DIM 効果
FIRST_LINE = LINECOUNT

君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(ARG:0)
CALL SINGLE_DRAWLINE
PRINTFORML 消費国庫資金、向其他勢力散播和%ANAME(相手君主)%有関的醜聞
PRINTFORML 其他勢力和%ANAME(相手君主)%的関係会悪化
PRINTFORML 要花多少資金？(手動輸入・按鈕輸入, 輸入0以下取消, 最大50000)
CALL COLOR_PRINTL("現在的国庫:",,@"{MONEY:(CFLAG:MASTER:所属)}", カラー_注意)
PRINTFORML 
FOR LOCAL, 0, VARSIZE("支払額リスト")
	SIF MONEY:(CFLAG:MASTER:所属) < 支払額リスト:LOCAL
		BREAK
	PRINTBUTTON @"[支付{支払額リスト:LOCAL}]", 支払額リスト:LOCAL
	PRINTL
NEXT
PRINTBUTTON @"[支付{MONEY:(CFLAG:MASTER:所属)}]", MONEY:(CFLAG:MASTER:所属)
PRINTL
PRINTFORM [0] 取消
INPUT

IF RESULT <= 0
	RETURN 0
ELSEIF MONEY:(CFLAG:MASTER:所属) < RESULT || RESULT > 50000
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF
支払額 = RESULT

CALL COLOR_PRINTW(@"%ANAME(MASTER)%金",, @"{支払額}", カラー_注意, @"花费了。散播了%ANAME(相手君主)%的醜聞……")

MONEY:(CFLAG:MASTER:所属) -= 支払額
効果 = CALC_SEND_MONEY_EFFECT(支払額)

;好感度上昇・敵対値下降基準値
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:2 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10

効果 = MAX(効果 * (LOCAL:1 + LOCAL:2 + 100) / 100, 5)

FOR LOCAL, 0 ,MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL)
		CONTINUE
	SIF !GROUPMATCH(LOCAL, CFLAG:MASTER:所属, ARG:0)
		CALL CHANGE_RELATION_C_TO_C(LOCAL, ARG:0, -効果, 効果)
NEXT
;跳转信仰事件
TRYCALL SHOP_LIFE_USERSHOP68_SCANDAL(CFLAG:MASTER:所属,ARG:0,支払額)
RETURN 1


;-------------------------------------------------
;醜聞を流す
;-------------------------------------------------
@DIPLOMACY_SABOTAGE(相手勢力)
#DIM 君主
#DIM 相手勢力
#DIM 相手君主
#DIM FIRST_LINE
#DIM CONST 支払額リスト = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
#DIM 支払額
#DIM 対象
#DIM 効果

FIRST_LINE = LINECOUNT
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(相手勢力)

CALL SINGLE_DRAWLINE
PRINTFORML 可以利用所属于%ANAME(相手君主)%、但醉心于%ANAME(MASTER)%的士官、讓其進行内部工作
CALL COLOR_PRINTL("参加的每一人都會消費", GETDEFCOLOR(), "1000", カラー_注意,  "国庫資金。最多3人")
PRINTFORML 要讓誰参加？
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(MIN(MONEY:(CFLAG:MASTER:所属) / 1000, 3), "DIPLOMACY_SABOTAGE", "DIPLOMACY_SABOTAGE")

IF SELECTED_CHARA_NUM <= 0
	PRINTFORML 還是算了
	RETURN 0
ENDIF

MONEY:(CFLAG:MASTER:所属) -= SELECTED_CHARA_NUM * 1000

PRINTFORML 実施工作活動中……

FOR LOCAL, 0, SELECTED_CHARA_NUM
	対象 = SELECTED_CHARA:LOCAL
	SELECTCASE RAND:3
		CASE 0
			PRINTFORML %ANAME(対象)%在%ANAME(相手君主)%勢力中、為了加強%ANAME(君主)%的好形象進行着遊說活動……
			効果 = SQRT(ABL_POWER(ABL:(対象):政治, 対象)) / 2 * (FIND_PASSIVE_SKILL(対象, "职务人员") + 1)
			効果 = SLG_BUILDING_DIPLOMACY_INNERWORK_INCOME(効果)
			CALL DIPLOMACY_IMPROVE_RELATION(相手勢力, 効果)
		CASE 1
			PRINTFORML %ANAME(対象)%暗中倒賣着%ANAME(相手君主)%的財力給%ANAME(君主)%……
			;相手経済に対して(政治 / 50)%。ただし自身の経済の1%を越えない。
			効果 = MIN(GET_SUM_ECONOMY(相手勢力) * ABL:対象:政治 / 50 / 100 * (FIND_PASSIVE_SKILL(対象, "职务人员") + 1), GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100)
			効果 = SLG_BUILDING_DIPLOMACY_INNERWORK_INCOME(効果)
			CALL COLOR_PRINTL("経済力", GETDEFCOLOR(), @"{効果 / 100}", カラー_注意, "夺取了", GETDEFCOLOR())
			CALL SHIFT_ECONOMY(相手勢力, CFLAG:MASTER:所属, 効果)
		CASE 2
			PRINTFORML %ANAME(対象)%在%ANAME(相手君主)%勢力内進行着工作活動……
			効果 = 2
			効果 = SLG_BUILDING_DIPLOMACY_INNERWORK_INCOME(効果)
			CALL SELECT_CHARA_RANDOM("DIPLOMACY_SABOTAGE")
			IF !GROUPMATCH(RESULT, 対象 , -1) || RAND:100 < 50 - FIND_PASSIVE_SKILL(対象, "职务人员") * 25
				CALL ADD_COOLTIME(RESULT, 効果)
			ELSE
				PRINTFORML ……但是進展好像不太順利……
			ENDIF
	ENDSELECT
NEXT
PRINTFORMW
;跳转信仰事件
TRYCALL SHOP_LIFE_USERSHOP68_SABOTAGE(相手勢力,SELECTED_CHARA_NUM)
CALL TMP_CREATE_IS_FREE_MAP
RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_DIPLOMACY_SABOTAGE(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == DIPLOMACY_DIPLOMAT && 対象 != GET_COUNTRY_BOSS(DIPLOMACY_DIPLOMAT) && (IS_SLAVE(対象) || GETBIT(TALENT:対象:淫乱系, 素質_淫乱_傀儡) || GETBIT(TALENT:対象:淫乱系, 素質_淫乱_薬物中毒))

@SELECT_CHARA_LIST_SELECT_LOGIC_DIPLOMACY_SABOTAGE(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == 0

@SELECT_CHARA_RANDOM_LOGIC_DIPLOMACY_SABOTAGE(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == DIPLOMACY_DIPLOMAT && CFLAG:対象:捕虜先 == 0


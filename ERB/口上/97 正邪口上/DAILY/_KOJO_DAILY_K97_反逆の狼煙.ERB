;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K97_COUNTER_ROCKET_RATE(対象)
#DIM 対象
RETURN 50

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K97_COUNTER_ROCKET_DECISION(対象)
#DIM 対象
#DIM 針妙丸

針妙丸 = NAME_TO_CHARA("針妙丸")

;一回きり
SIF KDVAR:対象:正邪_反逆の狼煙
	RETURN 0

;主人公が君主でない
SIF GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
	RETURN 0

SIF !IS_COUNTRY(CFLAG:対象:所属)
	RETURN 0

SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) != 針妙丸
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 0, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K97_COUNTER_ROCKET_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;------------------------------------
;レミリアからの勧誘
;-----------------------------------
@KOJO_DAILY_K97_COUNTER_ROCKET(対象)
#DIM ボス
#DIM 対象
#DIM 紅魔館

CFLAG:対象:面識 = 1

ボス = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

PRINTFORMW 「喲、你就是%ANAME(MASTER)%嗎」
PRINTFORML 在巡邏時，突然聽到有聲音在叫自己
PRINTFORMW 看來不是甚麼正直的人......
PRINTFORMW 「別那麼警戒啦。我是%NAME_FORMAL(対象)%。我是來告訴你一件對你不錯的事的」
PRINTFORMW 「你沒有想過嗎？為什麼弱小的人就一定要被人欺負呢」
PRINTFORMW 「我對此很不滿，我一直很期待，強者和弱者的關係逆轉的一天」
PRINTFORMW 「現在、幻想鄉充滿混亂。要實行計畫的話就要趁現在……」
PRINTFORMW 「……話雖如此，人手實在不足。需要有戰鬥力的人」
PRINTFORMW 「如何？你、要跟隨我們一起戰鬥嗎」
PRINTFORMW 原來如此、是來挖角的
PRINTFORML %PRONOUN(対象)%所屬的勢力最後如果能存活下來的話，跟隨她會是正確的選擇
PRINTFORML 但這樣的話，與現在君主的關係，肯定會變得非常惡劣吧
PRINTFORML 那麼、要怎麼辦……
CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(対象)%的領地数:{GET_OWN_CITY(CFLAG:対象:所属)} %ANAME(ボス)%的領地数:{GET_OWN_CITY(CFLAG:MASTER:所属)}
PRINTFORML 
CALL ASK_MULTI(@"跟隨%ANAME(対象)%", "拒絕", "拘捕")

IF RESULT == 1
	PRINTFORMW 「這樣啊、真是可惜」
	PRINTFORMW 「再見。希望下次見面時我們不是敵人吧」
	PRINTFORMW %ANAME(対象)%離開了……
	KDVAR:対象:正邪_反逆の狼煙 = 2
	RETURN 1
ELSEIF RESULT == 2
	PRINTFORMW 怎可能輕易答應這種事
	PRINTFORMW 而且這傢伙係敵人。必需要拘捕……
	PRINTFORMW 「呵！　這樣呀、那麼接招吧！」
	PRINTFORMW 「『四尺魔法炸彈』！」
	PRINTFORMW ……利用炸彈爆炸製造混亂後、%ANAME(対象)%逃跑了
	PRINTFORMW %ANAME(MASTER)%受傷了……
	CALL ADD_COOLTIME(MASTER, 3)
	KDVAR:対象:正邪_反逆の狼煙 = 2
	RETURN 1
ENDIF

PRINTFORML 「就知道你會這麼說」
PRINTFORMW 「那麼……拜託你了」
PRINTFORMW %ANAME(MASTER)%緊緊地握住了伸向自己的手……

LOCAL:0 = MIN((ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + ABL:MASTER:防衛) * 200, 50000)
MONEY += LOCAL:0

CFLAG:対象:好感度 = MAX(CFLAG:対象:好感度, 800)
CFLAG:対象:依存度 = MAX(CFLAG:対象:依存度, 200)
CALL CHANGE_RELATION_O_TO_O(ボス, MASTER, -500, 500)
CALL CHANGE_RELATION_O_TO_C(ボス, CFLAG:対象:所属, -500, 500)
CALL CHANGE_COUNTRY(MASTER, CFLAG:対象:所属, 1)

CALL COLOR_PRINTW(@"收到{LOCAL:0}金安家費", カラー_注意)
CALL COLOR_PRINTW(@"与%ANAME(ボス)%的外交関係変得非常悪劣了", カラー_警告)


KDVAR:対象:正邪_反逆の狼煙 = 1

RETURN 1

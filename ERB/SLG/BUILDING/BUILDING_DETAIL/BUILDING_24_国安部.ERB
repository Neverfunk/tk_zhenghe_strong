@BUILDING_GET_NAME_国安部()
    #FUNCTIONS
    RETURNF "国安部"

@BUILDING_GET_EXP_国安部()
    #FUNCTIONS
    RETURNF "降低士官叛乱的概率。"

@BUILDING_SET_INFO_ID_24()
    BUILDING_TYPE:建造物_国安部 = 建造物_类型_政治类
    BUILDING_LEVEL:建造物_国安部 = 建造物_等级_三级建筑
    BUILDING_LIMIT_NUM:建造物_国安部 = 建造物_数量限制_1
    BUILDING_LIMIT_UNI:建造物_国安部 = 0
    BUILDING_COUNTRY_ID:建造物_国安部:特殊勢力_普通势力 = 1
    BUILDING_HAVE_SKILL:建造物_国安部 = 1
    BUILDING_TOHO_REQ_ID:建造物_国安部:0 = 0

@CHECK_BUILDING_FUNCTION_国安部(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM POLITICS
    RESULT:1 = TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:建造物_国安部
    IF RESULT:1 > 0
        RESULT:0 = 1
    ENDIF
    RETURNF RESULT:0

@BUILDING_TEXT_国安部_1(COUNTRY_ID,CHARA_ID,STATUS)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM CHARA_ID
    #DIM STATUS
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F()
    PRINTFORML 因为《国安部》的存在，%ANAME(CHARA_ID)%的叛乱企图被发现了
    IF STATUS
        PRINTFORML 阻止成功！
    ELSE
        PRINTFORML 阻止失败！
    ENDIF
    RETURNF 1

@BUILDING_TEXT_国安部_2(COUNTRY_ID,CHARA_ID,STATUS)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM CHARA_ID
    #DIM STATUS
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F()
    PRINTFORML 因为《国安部》的存在，%ANAME(CHARA_ID)%参加暴乱的企图被发现了
    IF STATUS
        PRINTFORML 阻止成功！
    ELSE
        PRINTFORML 阻止失败！
    ENDIF
    RETURNF 1

@BUILDING_SKILL_国安部(COUNTRY_ID)
    #DIM COUNTRY_ID
    PRINTFORML 发动后可以救出一名被捕获的同伴
    PRINTFORML 冷却时间3回合
    PRINTFORML 是否发动
    CALL ASK_YN()
    IF !RESULT
        CALL SINGLE_DRAWLINE
        PRINTFORML 发动了《国安部》的技能
        CALL BUILDING_SKILL_国安部_FUNCTION(COUNTRY_ID)
        IF RESULT
            BUILDING_SKILL_COLDTIME:COUNTRY_ID:建造物_国安部 = 3
            PRINTFORMW 完毕
        ENDIF
    ENDIF

@BUILDING_SKILL_国安部_FUNCTION(COUNTRY_ID)
    #DIM COUNTRY_ID
    #DIM 捕虜番号,MAX_CHARA_NUM
	PRINTFORML 要救出誰？
	CALL SINGLE_DRAWLINE
	FOR LOCAL, 0, CHARANUM
		IF CFLAG:LOCAL:所属 == COUNTRY_ID && CFLAG:LOCAL:捕虜先 != 0 && CFLAG:LOCAL:捕虜先 != COUNTRY_ID
			捕虜番号:(LOCAL:1) = LOCAL
			PRINTFORM [{LOCAL:1}]%ANAME(LOCAL)%
			PRINTL
			LOCAL:1 ++
		ENDIF
	NEXT
	CALL SINGLE_DRAWLINE
	PRINTFORML [1000] 还是算了
    INPUT
    LOCAL = RESULT
    IF LOCAL >= 0 && LOCAL < LOCAL:1
        LOCAL:2 = 捕虜番号:(RESULT)
        ; CFLAG:(LOCAL:2):捕虜先 = 0
        CALL CAPTURE((LOCAL:2),0)
        PRINTFORMW 将%ANAME(LOCAL:2)%救出了
        RETURN 1
    ELSEIF LOCAL == 1000
        RETURN 0
    ELSE
        RESTART
    ENDIF

;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K10_ASK_MASTER_NAME_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K10_ASK_MASTER_NAME_DECISION(対象)
#DIM 対象

;対象口上デイリー入力系使用の設定が「使用」でなければ戻る
SIF レミリア_口上デイリー入力系使用 != 2
	RETURN 0

SIF !TALENT:対象:恋人 && !TALENT:対象:烙印
	RETURN 0

;一回きり
SIF KDVAR:対象:レミリア_愛称を教えて
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K10_ASK_MASTER_NAME_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他



;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K10_ASK_MASTER_NAME(対象)
#DIM 対象
#DIM レミリア_対象
#DIMS NOW_CALLNAME_MASTER_K10
#DIMS NOW_CALLNAME_MASTER_RUBY_K10

;リセット
レミリア_対象 = MASTER




SETCOLOR レミリア_口上カラー

PRINTFORML 「%CALLNAME_K10(レミリア_対象)%」
PRINTFORMDW 一边巡视着据点，从阳台向这边招手%ANAME(対象)%引入眼帘
PRINTL 
PRINTFORML 「过来一下。我有件事想问你」
PRINTFORML 「呐，%CALLNAME_K10(レミリア_対象)%有人给你起过爱称吗？」
PRINTFORMDW %ANAME(対象)%仿佛要从阳台上掉下来似的，探出身子，眼睛闪闪发光。
PRINTL 
PRINTFORML 「如果有喜欢的称呼，也告诉我吧。」

;CALLNAME_K10で現在呼称判定とフリガナの再取得が行われているのでここでリセット
NOW_CALLNAME_MASTER_K10 '= CALLNAME_K10(レミリア_対象)
NOW_CALLNAME_MASTER_RUBY_K10 '= レミリア_主人公への呼称フリガナ

LOCAL:0 = LINECOUNT
$SHOW_LOOP

RESETCOLOR
CALL SINGLE_DRAWLINE

SETCOLOR カラー_選択不可
PRINTFORM 教えたい名称を入力・または決定
PRINTL 
PRINTFORM 現在の%ANAME(対象)%から%ANAME(レミリア_対象)%への呼称         : 
RESETCOLOR

PRINTFORM %NOW_CALLNAME_MASTER_K10, 20, LEFT%
PRINTBUTTON "  [輸入]", "輸入"
PRINTL 

SETCOLOR カラー_選択不可
PRINTFORM 現在の%ANAME(対象)%から%ANAME(レミリア_対象)%への呼称フリガナ : 
RESETCOLOR

PRINTFORM %NOW_CALLNAME_MASTER_RUBY_K10, 20, LEFT%
PRINTBUTTON "  [輸入]", "输入注音"
PRINTBUTTON "  [決定]", "決定"

PRINTL 
CALL SINGLE_DRAWLINE

;入力・初期値は"決定"
INPUTS 決定

IF RESULTS == "決定"
	レミリア_主人公への呼称 '= NOW_CALLNAME_MASTER_K10
	レミリア_主人公への呼称フリガナ '= NOW_CALLNAME_MASTER_RUBY_K10
	CLEARLINE LINECOUNT - LOCAL:0
	PRINTL 

ELSEIF RESULTS == "輸入"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTFORML 呼称入力中
	RESETCOLOR
	IF RESULTS != NOW_CALLNAME_MASTER_K10
		INPUTS %NOW_CALLNAME_MASTER_K10%
		LOCALS:0 '= RESULTS
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_CALLNAME_MASTER_K10 '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSEIF RESULTS == "输入注音"
	CLEARLINE 1
	SETCOLOR カラー_選択不可
	PRINTFORML 呼称フリガナ入力中
	RESETCOLOR
	IF RESULTS != NOW_CALLNAME_MASTER_RUBY_K10
		INPUTS %NOW_CALLNAME_MASTER_RUBY_K10%
		LOCALS:0 '= RESULTS
		;半角を1字と数えてLOCALS:0の0字目から20字目までを切り出す
		SUBSTRING LOCALS:0, 0, 20
		NOW_CALLNAME_MASTER_RUBY_K10 '= LOCALS:0
	ENDIF
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP

ELSE
	CLEARLINE LINECOUNT - LOCAL:0
	GOTO SHOW_LOOP
ENDIF

;※ここでフラグを立てないと下記テキスト内で変更後の呼称に対応できない
KDVAR:対象:レミリア_愛称を教えて = 1

SETCOLOR カラー_選択不可
PRINTFORML ➡%CALLNAME_K10(レミリア_対象)%（%レミリア_主人公への呼称フリガナ%）
PRINTL 

SETCOLOR レミリア_口上カラー

IF レミリア_主人公への呼称 == "" && レミリア_主人公への呼称フリガナ == ""
	PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(対象)%にこれまで通り『%CALLNAME_K10(レミリア_対象)%（%レミリア_主人公への呼称フリガナ%）』と呼ぶように伝えた
	PRINTL 
	PRINTFORML 「没有吗？真无聊啊」
	PRINTFORMW 「但是，%CALLNAME_K10(レミリア_対象)%、我也喜欢这样称呼你。就那样好吧」
	PRINTL 
	PRINTFORML 「那么，再来玩吧。我等你。%CALLNAME_K10(レミリア_対象)%」
	PRINTFORMDL %ANAME(対象)%挥了挥手回到了房间
ELSE
	PRINTFORMDW %ANAME(レミリア_対象)%は%ANAME(対象)%に『%CALLNAME_K10(レミリア_対象)%（%レミリア_主人公への呼称フリガナ%）』と呼ぶように伝えた
	PRINTL 
	PRINTFORML 「%CALLNAME_K10(レミリア_対象)%？　你喜欢吗？」
	PRINTFORMW 「知道了。%CALLNAME_K10(レミリア_対象)%、%CALLNAME_K10(レミリア_対象)%。我记得是」
	PRINTL 
	PRINTFORML 「那么，再来玩吧。我等你。%CALLNAME_K10(レミリア_対象)%」
	PRINTFORMDL %ANAME(対象)%挥了挥手回到了房间
ENDIF
WAIT
RESETCOLOR
CFLAG:対象:好感度 = CFLAG:対象:好感度 + 100
CFLAG:対象:依存度 = CFLAG:対象:依存度 + 100
CFLAG:対象:従属度 = CFLAG:対象:従属度 + 100

RETURN 1


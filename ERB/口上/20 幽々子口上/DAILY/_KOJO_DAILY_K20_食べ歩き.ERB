;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K20_TABEARUKI_RATE(対象)
#DIM 対象
RETURN 30


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K20_TABEARUKI_DECISION(対象)
#DIM 対象

;好感度500が必要
SIF CFLAG:対象:好感度 < 500
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K20_TABEARUKI_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K20_TABEARUKI(対象)
#DIM 対象
PRINTFORMW %ANAME(MASTER)%工作的時候、%ANAME(対象)%来訪了
SELECTCASE KDVAR:対象:幽幽子_食べ歩き
	CASE 0
		PRINTFORMW 「我是准备出门去稍微转一转的，如果您有空的话要不要一起？」
	CASE 1
		PRINTFORMW 「又到了出门的时间了，请问您能够赏个光？」
	CASE 2
		PRINTFORMW 「我准备再出门转一转，不知您这次可有时间？」
ENDSELECT
PRINTFORML 那麼、要怎麼辦……
PRINTL
CALL ASK_YN("奉陪", "現在很忙")

IF RESULT == 1
	PRINTFORMW 「阿拉，很忙吗？」
	PRINTFORMW 「那还真是多有冒犯，不打搅您了。」
	PRINTFORMW %ANAME(対象)%一個人出去了……
	KDVAR:対象:幽幽子_食べ歩き = 2
ELSE
	PRINTFORMW 「啊，多谢您了。」
	PRINTFORMW 「那么咱们就走吧，今天的目标有十家店哦。」
	PRINTFORMW 陪着%ANAME(対象)%去吃飯了……
	CALL COLOR_PRINTW(@"被帯到各種地方、稍微有点過度消耗体力了", カラー_注意)
	CALL ADD_COOLTIME(MASTER, 1)
	CALL PRINT_ADD_EXP(MASTER, "武闘経験値", RAND(1, 3), 1)
	CALL TRAIN_AUTO_ABLUP(MASTER)
	KDVAR:対象:幽幽子_食べ歩き = 1
ENDIF

RETURN 1

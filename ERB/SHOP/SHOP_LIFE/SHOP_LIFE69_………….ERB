;-------------------------------------------------
;「…………」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME69
RESULTS:0 '= "…………"

;-------------------------------------------------
;「…………」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK69
;主人公が捕虜でなければ戻る
SIF !CFLAG:(MASTER):捕虜先
	RETURN 0
RETURN 1

;-------------------------------------------------
;「…………」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY69
CALL TRAIN_IN_PRISON
IF RESULT == 0
	RESULT = 1
	BEGIN TURNEND
ENDIF
RETURN 1

;-------------------------------------------------
;「…………」の右カラムオフ判定
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_COLUMN_RIGHT_OFF_CHECK69

;-------------------------------------------------
;主人公が投獄中のときの逆調教イベント
;0を返すと調教開始せずにターンエンド
;-------------------------------------------------
@TRAIN_IN_PRISON
#DIM CHARA_LIST, MAX_CHARA_NUM

IF IS_SP_COUNTRY(CFLAG:MASTER:捕虜先)
	CALL SP_COUNTRY_REV_TRAIN()
	IF RESULT
		CALL START_TRAIN_COMMON()
		RETURN 1
	ELSE
		RETURN 0
	ENDIF
ENDIF

;特殊勢力以外の捕虜の場合
;全員の調教参加フラグを解除
CVARSET CFLAG, 6, 0

LOCAL:10 = -1
LOCAL:11 = 0
CALL SELECT_REV_TRAINER()
LOCAL:10 = RESULT:0
LOCAL:11 = RESULT:1

;FLAG:逆調教メイン調教者に記録されたIDからメイン調教者のキャラ番号を取得
LOCAL:12 = ID_TO_CHARA(FLAG:逆調教メイン調教者)
IF LOCAL:12 >= 0 && CFLAG:(LOCAL:12):所属 == CFLAG:MASTER:捕虜先 && CFLAG:(LOCAL:12):捕虜先 == 0 && GROUPMATCH(CFLAG:(LOCAL:12):行動不能状態, 0, 2)
	;メイン調教者を調教に参加させる
	CFLAG:(LOCAL:12):調教参加フラグ = 1
	CFLAG:(LOCAL:12):強制友好化 = 1
	LOCAL:10 = LOCAL:12
	LOCAL:11 ++
;メイン調教者がいないようならここで設定しとけしとけ～～～
ELSEIF LOCAL:12 < 0
	FLAG:逆調教メイン調教者 = GET_ID(LOCAL:10)
	LOCAL:12 = LOCAL:10
ENDIF

IF CFLAG:MASTER:行動不能状態 == 行動不能_臨月
	IF LOCAL:10 >= 0
		LOCALS:0 = 
		IF LOCAL:11 >= 2
			LOCALS:0 = 們
		ENDIF
		CALL SINGLE_DRAWLINE
		PRINTFORML %ANAME(LOCAL:10)%%LOCALS:0%看着%ANAME(MASTER)%越来越大的肚子、
		PRINTFORMW 決定在小孩出産之前停止調教
		PRINTFORMW %ANAME(LOCAL:10)%%LOCALS:0%聽到%ANAME(MASTER)%生下了一個健康的孩子、把孩子帯走了…

	ELSE
		CALL SINGLE_DRAWLINE
		PRINTFORMW %ANAME(MASTER)%在牢房中又過了一天…
	ENDIF
	;解放カウンタをリセット
	CFLAG:MASTER:解放カウンタ = 0

;参加者が一人以上いれば逆調教を開始
ELSEIF LOCAL:11 > 0
	IF FLAG:逆調教回数 == 0
		LOCALS:0 = 
		IF LOCAL:11 >= 2
			LOCALS:0 = 們
		ENDIF
		CALL SINGLE_DRAWLINE
		PRINTFORMW %ANAME(LOCAL:10)%%LOCALS:0%来到了%ANAME(MASTER)%的牢房前
		PRINTFORMW %ANAME(LOCAL:10)%%LOCALS:0%対%ANAME(MASTER)%很在意、発出了要把%ANAME(MASTER)%調教成仕官的宣言…
		;説得してからの期間を0にする
		FLAG:勧誘経過期間 = 0
	ENDIF
	;念のため
	FLAG:夜這い = 0
	;捕虜逆調教モード
	FLAG:調教モード = 5
	;ウフフモード
	FLAG:ウフフフラグ = 1
	;回数をカウント
	FLAG:逆調教回数 ++
	;説得してからの期間を増加
	FLAG:勧誘経過期間 ++
	;解放カウンタをリセット
	CFLAG:MASTER:解放カウンタ = 0
	;アイテムを一通り追加
	ITEM:A_跳蛋 = 1
	ITEM:A_振動棒 = 1
	ITEM:A_肛用振動棒 = 1
	ITEM:A_穿戴式假陰茎 = 1
	ITEM:A_飛機杯 = 1
	ITEM:A_陰蒂夾 = 1
	ITEM:A_乳頭夾 = 1
	ITEM:A_鞭 = 1
	ITEM:A_潤滑液 = 99
	;行動開始時の共通処理
	CALL START_TRAIN_COMMON
	RETURN 1

ELSE
	IF RAND:3 == 0
		;恋慕または服従のキャラがいれば時々やってくる
		LOCAL:11 = 0
		FOR LOCAL:0, 0, CHARANUM
			IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:捕虜先 && CFLAG:(LOCAL:0):捕虜先 == 0 && GROUPMATCH(CFLAG:(LOCAL:0):行動不能状態, 0, 2) && (TALENT:(LOCAL:0):恋慕 || TALENT:(LOCAL:0):服従)
				CHARA_LIST:(LOCAL:11) = LOCAL:0
				LOCAL:11 ++
			ENDIF
		NEXT
		IF LOCAL:11 >= 1
			LOCAL:12 = CHARA_LIST:(RAND:(LOCAL:11))
			CFLAG:(LOCAL:12):調教参加フラグ = 1
			CFLAG:(LOCAL:12):強制友好化 = 1
			LOCAL:10 = 1

			CALL SINGLE_DRAWLINE
			PRINTFORMW %ANAME(LOCAL:12)%来到了%ANAME(MASTER)%的牢房前面
			PRINTFORMW %ANAME(LOCAL:12)%進入了%ANAME(MASTER)%空蕩蕩的牢房中、稍微有些話想対你説…

			;捕虜会話モード
			FLAG:調教モード = 6
			;非ウフフモード
			FLAG:ウフフフラグ = 0
			;行動開始時の共通処理
			CALL START_TRAIN_COMMON
			RETURN 1
		ENDIF
	ENDIF
	CALL SINGLE_DRAWLINE
	PRINTFORMW %ANAME(MASTER)%在牢房中又過了一天…
ENDIF
SHOP_TIME = CALC_SHOP_TIME()
RETURN 0

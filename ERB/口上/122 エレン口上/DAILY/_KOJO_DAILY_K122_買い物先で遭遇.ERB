;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K122_SHOPPING_RATE(対象)
#DIM 対象
RETURN 20


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K122_SHOPPING_DECISION(対象)
#DIM 対象
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && !TALENT:対象:服従

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K122_SHOPPING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K122_SHOPPING(対象)
#DIM 対象

PRINTDATA
	DATAFORM 去城下町買晚飯的材料
	DATAFORM 去城下町買夜宵用的団子
	DATAFORM 去城下町找美味的酒
	DATAFORM 去城下町買新兵書
ENDDATA
PRINTFORML 的時候、遇見了同樣是来買東西的%ANAME(対象)%
IF TALENT:対象:正妻
	SELECTCASE RAND:5
	CASE 1
		PRINTFORMW 「我来買晚飯的材料、%ANAME(MASTER)%也一起吧？」
	CASE 2
		PRINTFORMW 「%ANAME(MASTER)%要買什麼可以和我說說麼%UNICODE(0x2665)%」
	CASE 3
		PRINTFORMW 「買完東西後一起去甜品店喝茶吧%UNICODE(0x2665)%」
	CASE 4
		PRINTFORMW 「今天一起做晚飯吧%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「啊%UNICODE(0x2665)% %ANAME(MASTER)%也来買東西麼%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW 和%ANAME(対象)%一起愉快地購物了
	;正妻かつ好感度100万以上なら大幅に好感度が上がる
	IF CFLAG:対象:好感度 >= 1000000
		LOCAL:1 = MIN((CFLAG:対象:好感度 / 300), 10000)
	ELSE
		LOCAL:1 = 500
	ENDIF
ELSEIF TALENT:対象:恋人 && TALENT:対象:恋慕
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMW 「難得遇見、就一起去買東西吧%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「你好%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW 和%ANAME(対象)%一起愉快地購物了
	LOCAL:1 = 200
ELSEIF TALENT:対象:恋慕 || TALENT:対象:親友
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMW 「啊%UNICODE(0x2665)% 這不是%ANAME(MASTER)%麼%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「在這種地方遇見還真是偶然啊%UNICODE(0x2665)%」
	ENDSELECT
	PRINTFORMDW 和%ANAME(対象)%愉快地聊了一會
	LOCAL:1 = 100
ELSEIF CFLAG:対象:好感度 >= 500
	PRINTFORMW 「啊啦、你也在買東西啊？」
	PRINTFORMDW 和%ANAME(対象)%說了會話
	LOCAL:1 = 50
ELSE
	PRINTFORMW 「啊啦、真巧」
	PRINTFORMDW 和%ANAME(対象)%稍微打了個招呼
	LOCAL:1 = 5
ENDIF
PRINTFORML 
CALL COLOR_PRINTW(@"%NAME_FORMAL(対象)%的好感度提高了{LOCAL:1}", カラー_注意)
;そこそこ上昇するので有用か
CFLAG:対象:好感度 += LOCAL:1


RETURN 1


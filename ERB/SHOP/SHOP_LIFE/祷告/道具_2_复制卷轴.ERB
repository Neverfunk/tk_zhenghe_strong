;--------------基本函数----------------
@MIRACLE_ITEM_2_NAME
	RESULTS = 复制卷轴

@MIRACLE_ITEM_2_TEXT
	RESULTS = 可从本势力角色身上复制任意一项不超过Lv4的技能或神术。 PS.专治不可转移

;道具价格
@MIRACLE_ITEM_2_COST
	RETURN 100000

;道具等级
@MIRACLE_ITEM_2_LEVEL
	RETURN 50

;最大持有个数
@MIRACLE_ITEM_2_MAX
	RETURN 5

;显示条件
@MIRACLE_ITEM_2_CHECK_SHOW
	RETURN 1

;其他购买条件(除价格\等级\个数外)
@MIRACLE_ITEM_2_CHECK_BUY
	RETURN 1

;购买效果
@MIRACLE_ITEM_2_BUY

;使用条件
@MIRACLE_ITEM_2_CHECK_USE
	RETURN 1

;使用效果
@MIRACLE_ITEM_2_USE
#DIM CHARA_NUM
#DIM CHARA_LIST,500
#DIM COPY_NUM
#DIMS COPY_NAME_LIST,10
#DIM COPY_ID_LIST,10
#DIM COPY_NO_LIST,10
#DIM COPY_TYPE_LIST,10
#DIM FIRST_LINE
#DIM FIRST_LINEE
#DIM 角色
#DIM SELECT_TYPE
#DIM EMPTY_SLOT
#DIMS 缩写
#DIM 描画行
FIRST_LINE = LINECOUNT
$SHOW_LOOP
SELECT_TYPE = 0
CHARA_NUM = 0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:1 != CFLAG:MASTER:1
		CONTINUE
	SIF LOCAL == MASTER
		CONTINUE
	CHARA_LIST:CHARA_NUM = LOCAL
	CHARA_NUM ++
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML --请选择要进行复制的对象--
CALL SINGLE_DRAWLINE
FOR LOCAL,0,CHARA_NUM,3
	PRINTBUTTON @"[%ANAME(CHARA_LIST:LOCAL), MAX_CHARANAME_LENGTH, LEFT%]",LOCAL
	IF LOCAL + 1 < CHARA_NUM
		PRINTFORM %TOSTR_SPACE(2)%
		PRINTBUTTON @"[%ANAME(CHARA_LIST:(LOCAL+1)), MAX_CHARANAME_LENGTH, LEFT%]",LOCAL+1
	ENDIF
	IF LOCAL + 2 < CHARA_NUM
		PRINTFORM %TOSTR_SPACE(2)%
		PRINTBUTTON @"[%ANAME(CHARA_LIST:(LOCAL+2)), MAX_CHARANAME_LENGTH, LEFT%]",LOCAL+2
	ENDIF
	PRINTL
NEXT
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON "[取消]",99999
PRINTL
INPUT
IF RESULT == 99999
	RETURN -1 ;取消使用
ELSEIF RESULT >= CHARA_NUM || RESULT < 0
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF
角色 = CHARA_LIST:RESULT
FIRST_LINEE = LINECOUNT
$SELECT_LOOP
COPY_NUM = 0
描画行=0
EMPTY_SLOT = GET_EMPTY_SKILL_SLOT(MASTER, SELECT_TYPE)
CALL SINGLE_DRAWLINE
PRINTFORML --请选择要在%ANAME(角色)%上进行复制的技能--限定:LV4--
CALL SINGLE_DRAWLINE
FOR LOCAL, 0, SKILL_GENRE_NUM
	IF SELECT_TYPE == LOCAL
		SETCOLOR カラー_選択中
	ENDIF
	PRINTBUTTON @"[%SKILL_GENRE:LOCAL%]", 4000 + LOCAL
	PRINT  
	RESETCOLOR
NEXT
IF EMPTY_SLOT == -1
	CALL COLOR_PRINTL(@"  该类型技能插槽已满", カラー_注意)
ELSE
	PRINTL
ENDIF
PRINTL
CALL SINGLE_DRAWLINE
LOCAL = SELECT_TYPE
LOCAL:2 = 0 ;技能等级
LOCALS '= "" ;描述
LOCALS:1 '= "" ;发动率
LOCALS:2 '= "" ;不可转移
LOCALS:3 '= "" ;来自谁
FOR LOCAL:1, 0, MAX_SKILL_SLOT
	SIF GROUPMATCH(-1, SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1), SKILL_ID_SLOT:角色:LOCAL:(LOCAL:1))
		CONTINUE
	SIF CHECK_SKILL_DUPLICATE(MASTER, LOCAL, SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1), SKILL_ID_SLOT:角色:LOCAL:(LOCAL:1))
		CONTINUE ;已学会技能忽视
	缩写 = SKILL_{SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1)}_%SKILL_GENRE_ENG:LOCAL%_{SKILL_ID_SLOT:角色:LOCAL:(LOCAL:1)}_
	;确定等级
	CALLFORM %缩写%LEVEL
	LOCAL:2 = RESULT
	SIF LOCAL:2 > 4
		CONTINUE ;不高于LV4
	CALLFORM %缩写%EXPLANATION
	LOCALS '= RESULTS
	;确定发动率
	IF LOCAL:1 == スキル_ジャンル_PASSIVE || LOCAL:1 == スキル_ジャンル_MIRACLE
		LOCALS:1 '= "  --"
	ELSE
		TRYCCALLFORM %缩写%RATE_VARABLE
			LOCALS:1 '= "可変"
		CATCH
			CALLFORM %缩写%RATE
			LOCALS:1 = %DECIMAL_STRING(RESULT, 1)%
		ENDCATCH
	ENDIF
	;确定来源
	IF SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1) == 0
		TRYCCALLFORM  %缩写%FROMWHO
			SELECTCASE RESULT
				CASE 0
					LOCALS:3 = 汎用
				CASE 1
					LOCALS:3 = 暴虐
				CASE 2
					LOCALS:3 = 诡术
				CASE 3
					LOCALS:3 = 腐朽
				CASE 4
					LOCALS:3 = 色欲
			ENDSELECT
		CATCH
			LOCALS:3 = 汎用
		ENDCATCH
	ELSEIF SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1) > 10000
		LOCALS:3 = 特殊
	ELSE
		LOCALS:3 = %SNAME(NO_TO_CHARA(SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1)))%
	ENDIF
	TRYCCALLFORM %缩写%CANT_TELL
		LOCALS:2 '= "伝達不可"
	CATCH
		LOCALS:2 '= ""
	ENDCATCH
	CALLFORM %缩写%NAME
	COPY_NAME_LIST:COPY_NUM '= RESULTS
	COPY_ID_LIST:COPY_NUM = SKILL_ID_SLOT:角色:LOCAL:(LOCAL:1)
	COPY_NO_LIST:COPY_NUM = SKILL_NO_SLOT:角色:LOCAL:(LOCAL:1)
	COPY_TYPE_LIST:COPY_NUM = LOCAL
	CALL COLOR_LINE
	IF EMPTY_SLOT == -1 
		SETCOLOR カラー_選択不可
		PRINTPLAIN [复制]
		RESETCOLOR
	ELSE
		PRINTBUTTON "[复制]",COPY_NUM
	ENDIF
	PRINTFORM %TOSTR_SPACE(2)%
	PRINTFORML %COPY_NAME_LIST:COPY_NUM, 35, LEFT%LV%TOSTR(LOCAL:2), 3, LEFT% 发动率%LOCALS:1, 6, LEFT%%SKILL_GENRE:LOCAL, 6, LEFT%%LOCALS:2, 10, LEFT%%LOCALS:3, MAX_CHARANAME_LENGTH/2, LEFT%
	PRINTFORML %TOSTR_SPACE(8)%%LOCALS%
	COPY_NUM ++
	描画行 ++
NEXT
IF MAX_SKILL_SLOT - 描画行 > 0
	FOR LOCAL, 描画行, MAX_SKILL_SLOT
		PRINTL
		PRINTL
		SIF LOCAL != MAX_SKILL_SLOT - 1
			PRINTL
	NEXT
ENDIF

CALL SINGLE_DRAWLINE
PRINTBUTTON "[返回]",99999
PRINTL
INPUT
LOCAL = RESULT
SELECTCASE RESULT
	CASE 0 TO COPY_NUM
		CALL SKILL_LEARN(MASTER, COPY_TYPE_LIST:LOCAL, COPY_NO_LIST:LOCAL, COPY_ID_LIST:LOCAL)
		;いっぱいならだめ
		IF RESULT == 0
			PRINTFORML (该类型技能已经满了)
			PRINTFORMW (想要学习新的技能、需要先忘记之前的)
		ELSE
			PRINTL
			PRINTFORMW %ANAME(MASTER)%成功习得了技能%COPY_NAME_LIST:LOCAL%!
			RETURN 1 ;成功使用
		ENDIF
	CASE 4000 TO (4000 + SKILL_GENRE_NUM)
		SELECT_TYPE = RESULT - 4000
	CASE 99999
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINEE
GOTO SELECT_LOOP
;-----------------特殊-----------------
@MIRACLE_ITEM_2_CANCEL_USE ;可以取消使用
	

@BUILDING_SKILL
	#DIM BUILDING_ID
	#DIM FIRST_LINE
	FIRST_LINE = LINECOUNT
	WHILE 1
		CLEARLINE (LINECOUNT - FIRST_LINE)
        CALL SINGLE_DRAWLINE
        ALIGNMENT CENTER
        CALL SINGLE_DRAWLINE
		ALIGNMENT LEFT
		FOR BUILDING_ID,BUILDING_START,BUILDING_NUM
			LOCAL = CHECK_BUILDING_SKILL(CFLAG:MASTER:所属,BUILDING_ID)
			SIF LOCAL == 0
				CONTINUE
			IF LOCAL == -1
				IF BUILDING_SKILL_SHOW_CONFIG
					SETCOLOR カラー_選択不可;未建造
				ELSE
					CONTINUE
				ENDIF
			ELSEIF LOCAL == -2
				SETCOLOR カラー_注意;CD中
			ELSEIF LOCAL == -3
				SETCOLOR カラー_注意;角色条件不满足
			ELSE
				SETCOLOR カラー_緑
			ENDIF
			PRINTBUTTON @"%"    [" + GET_BUILDING_NAME(BUILDING_ID) + "]", 11, LEFT%", BUILDING_ID
			RESETCOLOR
			CALL PRINT_SPASER_EXP(BUILDING_ID)
			PRINTFORM %TOSTR_SPACE(30)%
			IF BUILDING_SKILL_BUFF:(CFLAG:MASTER:所属):BUILDING_ID > 0 && LOCAL > 0
				SETCOLOR カラー_緑
				PRINTFORM 已生效，剩余生效回合：
				IF BUILDING_SKILL_BUFF_REMAIN:(CFLAG:MASTER:所属):BUILDING_ID > 999
					PRINTFORM 无限
				ELSE
					PRINTFORM {BUILDING_SKILL_BUFF_REMAIN:(CFLAG:MASTER:所属):BUILDING_ID}
				ENDIF
				RESETCOLOR
			ENDIF
			CALL PRINT_CANNOT_USE_SKILL_REASON(BUILDING_ID,LOCAL)
			PRINTFORML
			CALL SINGLE_DRAWLINE
		NEXT
		IF BUILDING_SKILL_SHOW_CONFIG
			PRINTBUTTON @"%"[显示全部]", 11, LEFT%", -2
		ELSE
			PRINTBUTTON @"%"[仅显示已建造的]", 11, LEFT%", -2
		ENDIF
		PRINTBUTTON @"%"[取消]", 11, LEFT%", -1
		INPUT
		SIF RESULT == -1
			RETURN
		IF RESULT == -2
			BUILDING_SKILL_SHOW_CONFIG = 1 - BUILDING_SKILL_SHOW_CONFIG
		ELSEIF IS_BUILDING(RESULT) && CHECK_BUILDING_SKILL(CFLAG:MASTER:所属,RESULT) > 0
			CALL BUILDING_SKILL_SELECT(CFLAG:MASTER:所属,RESULT)
		ENDIF
	WEND

@PRINT_CANNOT_USE_SKILL_REASON(BUILDING_ID,REASON_ID)
	#DIM BUILDING_ID
	#DIM REASON_ID
	#DIM CHARA_ID
	SETCOLOR カラー_警告
	SELECTCASE REASON_ID
		CASE -1
			PRINTFORM 建筑未建造
		CASE -2
			PRINTFORM 建筑技能冷却中，剩余冷却回合{BUILDING_SKILL_COLDTIME:(CFLAG:MASTER:所属):BUILDING_ID}
		CASE -3
			CHARA_ID = BUILDING_TOHO_REQ_ID:BUILDING_ID:0
            SIF CFLAG:CHARA_ID:所属 != CFLAG:MASTER:所属
                PRINTFORM %ANAME(CHARA_ID)%不在势力中
            SIF CFLAG:CHARA_ID:好感度 < 0
                PRINTFORM %ANAME(CHARA_ID)%好感度低于0
			SIF !CHECK_CHARA_IS_FREE(CHARA_ID)
        		PRINTFORM %ANAME(CHARA_ID)%暂时无法行动
            IF IS_SP_COUNTRY(CFLAG:MASTER:所属)
                SIF !GETBIT(TALENT:CHARA_ID:特殊勢力陥落系, SP_COUNTRY_TO_CONST(CFLAG:MASTER:所属))
                    PRINTFORM %ANAME(CHARA_ID)%未获得当前特殊势力陷落
            ENDIF
	ENDSELECT
	RESETCOLOR

@SP_COUNTRY_SETTING(強制出現 = 0, 出現設定 = 1)
#DIM 強制出現
#DIM 出現設定
#DIM FIRST_LINE
#DIM 勢力定数
#DIM 選択肢

SIF 強制出現
	VARSET SP_COUNTRY_RANK, 1

CALL SINGLE_DRAWLINE
PRINTL 関於特殊勢力的設定
PRINTL 不推薦「出現」設定設定為「最初出現」（難易度的考慮）
PRINTL ※「强度3」・「半途出现」相对比较合理
PRINTL ※性别设定与回合结束时的俘虏训练描述不一致。
;PRINTL ※性別設定は、ターンエンド時の捕虜調教描写は非対応です
CALL SINGLE_DRAWLINE

FIRST_LINE = LINECOUNT

$SHOW_LOOP

FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:LOCAL) == -1
		CALL PRINT_EACH_SP_COUNTRY_SETTING(LOCAL, 強制出現, 出現設定)
NEXT

CALL SINGLE_DRAWLINE
PRINTBUTTON "[結束]", 9999

INPUT

SELECTCASE RESULT
	CASE 0 TO 999
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || 勢力定数 == 特殊勢力_ダミー || !INRANGE(選択肢, 0, 5)
			GOTO SKIPPED
		SP_COUNTRY_RANK:勢力定数 = 選択肢
	CASE 1000 TO 1999
		RESULT = RESULT - 1000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || 勢力定数 == 特殊勢力_ダミー || !INRANGE(選択肢, 0, 3)
			GOTO SKIPPED
		SP_COUNTRY_BOSSNAME:勢力定数 = 選択肢
		IF 選択肢 == 3
			PRINTL 請輸入姓
			PRINTL (如果想像『琪露諾』那樣不需要姓的話、不需要輸入)
			INPUTS
			SP_COUNTRY_BOSSNAME_LAST_NAME:勢力定数  = %RESULTS%
			PRINTL 請輸入名字
			$NAME_INPUT_LOOP
			INPUTS
			IF RESULTS == ""
				PRINTL 必須輸入名字
				PRINTL 請再次輸入
				GOTO NAME_INPUT_LOOP
			ENDIF
			SP_COUNTRY_BOSSNAME_FIRST_NAME:勢力定数 = %RESULTS%
		ENDIF
	CASE 2000 TO 2999
		RESULT = RESULT - 2000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || 勢力定数 == 特殊勢力_ダミー || !INRANGE(選択肢, 0, 1) || !出現設定
			GOTO SKIPPED
		SP_COUNTRY_INITIAL_RISE:勢力定数 = 選択肢
	CASE 3000, 3001
		SIF !出現設定
			GOTO SKIPPED
		勢力定数 = 特殊勢力_野盗
		選択肢 = RESULT - 3000
		BANDIT_HAS_INITIAL_CITIES = 選択肢
	CASE 4000 TO 4999
		RESULT = RESULT - 4000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10 - 1
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || !INRANGE(選択肢, -1, 2)
			GOTO SKIPPED
		SP_COUNTRY_GENDER:勢力定数 = 選択肢
	CASE 9999
		FOR LOCAL, 0, MAX_SP_COUNTRY
			SIF LOCAL != 特殊勢力_野盗 && GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:LOCAL) == -1 && SP_COUNTRY_RANK:LOCAL > 0 && SP_COUNTRY_INITIAL_RISE:LOCAL
				TRYCALLFORM %SP_COUNTRY_NAME_ENG:LOCAL%_RISE()
			IF LOCAL == 特殊勢力_野盗 && GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_野盗) == -1 && SP_COUNTRY_INITIAL_RISE:LOCAL
				CALL BANDIT_INIT(1)
				SIF BANDIT_HAS_INITIAL_CITIES
					CALL BANDIT_RISE()
			ENDIF
		NEXT
		RETURN
ENDSELECT

$SKIPPED

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------------------------------------
;特殊勢力スタート用の設定
;-------------------------------------------------------
@SP_COUNTRY_START_SETTING(定数)
#DIM 定数
#DIM 勢力定数
#DIM 選択肢
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
CALL PRINT_EACH_SP_COUNTRY_SETTING(定数, 1, 0)
PRINTBUTTON "[結束]", 9999

INPUT

SELECTCASE RESULT
	CASE 0 TO 999
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF 勢力定数 != 定数 || !INRANGE(選択肢, 0, 5)
			GOTO SKIPPED
		SP_COUNTRY_RANK:勢力定数 = 選択肢
	CASE 1000 TO 1999
		RESULT = RESULT - 1000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF 勢力定数 != 定数 || !INRANGE(選択肢, 0, 3)
			GOTO SKIPPED
		SP_COUNTRY_BOSSNAME:勢力定数 = 選択肢
		IF 選択肢 == 3
			PRINTL 請輸入姓
			PRINTL (如果想像『琪露諾』那樣不需要姓的話、不需要輸入)
			INPUTS
			SP_COUNTRY_BOSSNAME_LAST_NAME:勢力定数  = %RESULTS%
			PRINTL 请输入名（必須）
			; PRINTL 名前を入力して下さい（必須）
			$NAME_INPUT_LOOP
			INPUTS
			IF RESULTS == ""
				PRINTL 必須輸入名字
				PRINTL 請再次輸入
				GOTO NAME_INPUT_LOOP
			ENDIF
			SP_COUNTRY_BOSSNAME_FIRST_NAME:勢力定数 = %RESULTS%
		ENDIF
	CASE 4000 TO 4999
		RESULT = RESULT - 4000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10 - 1
		SIF 勢力定数 != 定数 || !INRANGE(選択肢, -1, 2)
			GOTO SKIPPED
		SP_COUNTRY_GENDER:勢力定数 = 選択肢
	CASE 9999
		CALL SP_COUNTRY_START_SETTING_SELECT_CITY()
		TRYCALLFORM %SP_COUNTRY_NAME_ENG:定数%_RISE(RESULT)
		RETURN RESULT
ENDSELECT

$SKIPPED

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;旗揚げスタート時用、勢力の設定
;-------------------------------------------------
@SP_COUNTRY_START_SETTING_SELECT_CITY
#DIM FIRST_LINE
#DIM 勢力
#DIM 都市
#DIM 対象
#DIM 消去行
勢力 = GET_NEW_COUNTRY()

CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE
PRINTL 請選択最初要奪走的城市（這座城市的周辺也会被奪走）
PRINTL 為其他勢力所有的城市、如果這個勢力沒有兩個城市以上的話無法選択
CALL SINGLE_DRAWLINE
PRINTL


$INPUT_LOOP_CITY
INPUT

IF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
	LOCAL:5 = RESULT - 1000
	;指定された都市が存在し、無所属である場合
	IF GET_RELAYNAME(LOCAL:5) != "無名" && CITY_TYPE:(LOCAL:5) == 0 && (IS_COUNTRY(CITY_OWNER:(LOCAL:5)) || GET_OWN_CITY(CITY_OWNER:(LOCAL:5)) > 1)
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(LOCAL:5)%開始了
		RETURN LOCAL:5
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF


;-------------------------------------------------------
;特殊勢力スタート時の及びINIT
;-------------------------------------------------------
@SP_COUNTRY_START_INIT
FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF SP_COUNTRY_START:LOCAL == 0
		CONTINUE
	LOCAL:1 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:LOCAL)
	SELECTCASE SP_COUNTRY_START:LOCAL
		CASE 特殊勢力スタート_士官
			CALL CHANGE_COUNTRY(MASTER, LOCAL:1, 1)
		CASE 特殊勢力スタート_奴隷
			CALL CHANGE_COUNTRY(MASTER, LOCAL:1, 1)
			FOR LOCAL:2, 1, CHARANUM
				IF TALENT:(LOCAL:2):特殊勢力素質 == LOCAL
					TALENT:(LOCAL:2):合意 = 1
					CFLAG:(LOCAL:2):好感度 = 800
					CFLAG:(LOCAL:2):依存度 = 300
					CFLAG:(LOCAL:2):面識 = 1
				ENDIF
			NEXT
		CASE 特殊勢力スタート_捕虜
			CALL CAPTURE(MASTER, LOCAL:1)
		CASE 特殊勢力スタート_君主
			;君主を変更した
			IF SP_COUNTRY_START_CHANGE_BOSS
				CFLAG:(GET_COUNTRY_BOSS(LOCAL:1)):所属 = 0
				CFLAG:(GET_COUNTRY_BOSS(LOCAL:1)):特殊状態 = 0
				COUNTRY_BOSS:(LOCAL:1) = GET_ID(MASTER)
			;君主を変更しなかった
			ELSE
				;主人公を君主に置き換える
				;この時点でMASTERは「あなた」になってる
				CALL SWAP_CHARA(MASTER, GET_COUNTRY_BOSS(LOCAL:1))
			ENDIF
			CALL CHANGE_COUNTRY(MASTER, LOCAL:1, 1)
			FOR LOCAL:2, 1, CHARANUM
				;(ホフゴブリン拡張パッチ)ゴブリンスタート時は設定しない
				IF TALENT:(LOCAL:2):特殊勢力素質 == LOCAL && LOCAL != 特殊勢力_ホフゴブリン
					TALENT:(LOCAL:2):服従 = 1
					TALENT:(LOCAL:2):合意 = 1
					CFLAG:(LOCAL:2):従属度 = 1500
					CFLAG:(LOCAL:2):依存度 = 300
					CFLAG:(LOCAL:2):面識 = 1
				ENDIF
			NEXT
	ENDSELECT
	BREAK
NEXT

;-------------------------------------------------------
;個々の勢力の設定ボタンを出力
;-------------------------------------------------------
@PRINT_EACH_SP_COUNTRY_SETTING(定数, 強制出現 = 0, 出現設定 = 1)
#DIM 定数
#DIM 強制出現
#DIM 出現設定

;ダミー勢力は禁止
SIF 定数 == 特殊勢力_ダミー
	RETURN

;定数範囲外を渡されると死ぬので防御
SIF !INRANGE(定数, 0, MAX_SP_COUNTRY - 1)
	RETURN

;既に存在する場合飛ばす(シナリオファイルでRISEしたあとのNEWLOOP側での設定を想定）
SIF GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:定数) != -1
	RETURN

SIF 強制出現 && SP_COUNTRY_RANK:定数 == 0
	SP_COUNTRY_RANK:定数 = 1

PRINTFORM %SP_COUNTRY_NAME:定数, 16, LEFT%
PRINTL

PRINTFORM 强度:
;強さボタンの表示
FOR LOCAL, 0, 6
	SIF 強制出現 && LOCAL == 0
		CONTINUE
	CALL PRINTBUTTON_EX(@"[{LOCAL}]", 定数 * 10 + LOCAL, 1, SP_COUNTRY_RANK:定数 == LOCAL)
NEXT

PRINTL

PRINTFORM 名字:
CALL PRINTBUTTON_EX("[勢力名]",   定数 * 10 + 1000, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 0)
CALL PRINTBUTTON_EX("[固有名]",   定数 * 10 + 1001, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 1)
CALL PRINTBUTTON_EX("[隨機]", 定数 * 10 + 1002, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 2)
CALL PRINTBUTTON_EX("[手動輸入]",   定数 * 10 + 1003, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 3)

PRINTL

PRINTFORM 性別:
CALL PRINTBUTTON_EX("[默认]", 定数 * 10 + 4000, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == -1)
CALL PRINTBUTTON_EX("[男]",         定数 * 10 + 4001, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == 0)
CALL PRINTBUTTON_EX("[女]",         定数 * 10 + 4002, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == 1)
CALL PRINTBUTTON_EX("[女双]",       定数 * 10 + 4003, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == 2)
PRINTL


SIF !出現設定
	RETURN

PRINTFORM 出現:
CALL PRINTBUTTON_EX("[最初出現]", 定数 * 10 + 2001, SP_COUNTRY_INITIAL_RISE:定数 || (SP_COUNTRY_RANK:定数 && GET_COUNTRY_NUM() + MATCH(SP_COUNTRY_INITIAL_RISE, 1) < MAX_COUNTRY - 1), SP_COUNTRY_INITIAL_RISE:定数 == 1)
CALL PRINTBUTTON_EX("[半途出現]", 定数 * 10 + 2000, SP_COUNTRY_RANK:定数, SP_COUNTRY_INITIAL_RISE:定数 == 0)
PRINTL

IF 定数 == 特殊勢力_野盗
	PRINTFORM 出現時領土
	CALL PRINTBUTTON_EX("[会被奪取]" , 3001, SP_COUNTRY_RANK:定数,     BANDIT_HAS_INITIAL_CITIES == 1)
	CALL PRINTBUTTON_EX("[不被奪取]" , 3000, SP_COUNTRY_RANK:定数, BANDIT_HAS_INITIAL_CITIES == 0)
	PRINTL
ENDIF


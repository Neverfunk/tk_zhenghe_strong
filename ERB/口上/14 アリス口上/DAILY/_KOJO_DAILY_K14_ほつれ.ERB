;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K14_FRAY_RATE(対象)
#DIM 対象
RETURN 30


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K14_FRAY_DECISION(対象)
#DIM 対象
SIF KDVAR:対象:アリス_ほつれ
	RETURN 0


RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K14_FRAY_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K14_FRAY(対象)
#DIM 対象

PRINTFORMW 「唔……請等一下」
PRINTFORML 和%ANAME(対象)%擦肩而過時被%ANAME(対象)%叫住了
PRINTFORMW 正當想有什麼事時、上衣就被脫掉了……
PRINTFORMW 「袖子脫線了。真不像話」
PRINTFORMW 「來、我幫你修吧」
PRINTFORML %ANAME(対象)%取出了裁縫箱、雙手靈活地修補着衣服
PRINTFORMW 不久後、衣服的袖子修復好還變得跟全新一樣……
PRINTFORMW 「這樣就可以了。這只是應急手段、所以不能太粗暴對待」
PRINTFORML
CALL ASK_YN("手工真不錯啊", "謝謝你")

IF RESULT == 0
	PRINTFORMW 「你過獎了。我只是為了修補人偶的衣服所以才練出這種手工而已」
	PRINTFORMW 「從零開始製作人的衣服真是好難啊」
ELSE
	PRINTFORMW 「你客氣了」
ENDIF

PRINTFORMW 「那麼、我告辭了」
PRINTFORMW %ANAME(対象)%離開了……

CFLAG:対象:好感度 += 30

IF !CFLAG:対象:面識
	CALL COLOR_PRINTW(@"結識了%ANAME(対象)%", カラー_注意)
	CFLAG:対象:面識 = 1
ENDIF

KDVAR:対象:アリス_ほつれ = 1

RETURN 1

;胸愛撫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME1
IF TALENT:(MTAR:0):母乳体質
	LOCALS:1 = 搾乳
ELSEIF IS_FEMALE(MTAR:0) || (MTAR_NUM == 2 && IS_FEMALE(MTAR:1))
	LOCALS:1 = 胸愛撫
ELSE
	LOCALS:1 = 责弄乳頭
ENDIF

IF MTAR_NUM >= 2
	LOCALS:0 = 同時%LOCALS:1%
ELSE
	LOCALS:0 = %LOCALS:1%
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE1
;共通部分
CALL COM_ABLE_COMMON(1)
SIF RESULT == 0
	RETURN 0
;1+ players
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットはプレイヤーの倍まで
SIF MTAR_NUM <= 0 || MTAR_NUM > MPLY_NUM * 2
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF !CAN_REACH_BODY(MPLY:(LOCAL:0),MTAR:(LOCAL:1)) 
			RETURN 0
	NEXT
NEXT

FOR LOCAL:0, 0, MTAR_NUM
    ;breasts occupied - tentacle B caress
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 204)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM1
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 60
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 100, 40)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100

	NEXT
NEXT

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):露出 = 100
	SOURCE:(LOCAL:1):接触 = 60
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 80, 0)

NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP1
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP1(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)


	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 30

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400) * LOCAL:10 / 100

	NEXT

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 1
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):露出 += 50
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 60


	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 1
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE1(ARG:0)
LOCAL:2 = 0
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	IF TALENT:(MEQUIP_TARGET:(ARG:0):(LOCAL:0)):母乳体質
		LOCAL:2 = 2
		BREAK
	ELSEIF IS_FEMALE(MEQUIP_TARGET:(ARG:0):(LOCAL:0))
		LOCAL:2 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:2 == 1
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%愛撫%EQUIP_TARGET_ANAME(ARG:0)%的胸部中
ELSEIF LOCAL:2 == 2
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%対%EQUIP_TARGET_ANAME(ARG:0)%搾乳中
ELSE
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%责弄%EQUIP_TARGET_ANAME(ARG:0)%的乳頭中
ENDIF

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP1(ARG:0)
LOCAL:2 = 0
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	IF TALENT:(MEQUIP_TARGET:(ARG:0):(LOCAL:0)):母乳体質
		LOCAL:2 = 2
		BREAK
	ELSEIF IS_FEMALE(MEQUIP_TARGET:(ARG:0):(LOCAL:0))
		LOCAL:2 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:2 == 1
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正揉搓著%EQUIP_TARGET_ANAME(ARG:0)%的胸部…
ELSEIF LOCAL:2 == 2
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正搾取著%EQUIP_TARGET_ANAME(ARG:0)%的母乳…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正责弄著%EQUIP_TARGET_ANAME(ARG:0)%的乳頭…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP1(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER1(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 60

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE1
;プレイヤーが２人
IF MPLY_NUM == 2
	LOCALS:0 = %ANAME(MPLY:0)%和%ANAME(MPLY:1)%
;プレイヤーが１人
ELSE
	LOCALS:0 = %ANAME(MPLY:0)%
ENDIF

;ターゲットが２人
IF MTAR_NUM == 2
	LOCALS:1 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;ターゲットが１人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

IF MTAR_NUM == 2 && IS_PIERCED(MTAR:0, ピアス_乳首) && IS_PIERCED(MTAR:0, ピアス_乳首)
	LOCALS:2 = 上著環的
ELSEIF MTAR_NUM == 1 && IS_PIERCED(MTAR:0, ピアス_乳首)
	LOCALS:2 = 上著環的
ELSE
	LOCALS:2 = 
ENDIF

;ターゲットが２人
IF MTAR_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			;片方が男性
			IF IS_MALE(MTAR:0) || IS_MALE(MTAR:1)
				PRINTFORMW %LOCALS:0%捏住了%LOCALS:1%的%LOCALS:2%乳頭、同時责弄著兩人…
			;二人とも貧乳以下
			ELSEIF GET_BUSTSIZE(MTAR:0) <= -1 && GET_BUSTSIZE(MTAR:1) <= -1
				PRINTFORMW %LOCALS:0%用雙手分別仔細撫摸著%LOCALS:1%的貧瘠胸部…
			ELSE
				PRINTFORMW %LOCALS:0%仔細地揉搓著%LOCALS:1%的合計四個%LOCALS:2%乳房…
			ENDIF
		;ターゲットに主導権
		CASE 1
			;片方が男性
			IF IS_MALE(MTAR:0) || IS_MALE(MTAR:1)
				PRINTFORMW %LOCALS:1%讓%LOCALS:0%揉捏著各自的%LOCALS:2%乳頭、命令%LOCALS:0%讓自己舒服…
			;二人とも貧乳以下
			ELSEIF GET_BUSTSIZE(MTAR:0) <= -1 && GET_BUSTSIZE(MTAR:1) <= -1
				PRINTFORMW %LOCALS:1%讓%LOCALS:0%撫摸各自的%LOCALS:2%貧瘠胸部、命令%LOCALS:0%讓自己舒服…
			ELSE
				PRINTFORMW %LOCALS:1%分別把%LOCALS:0%的手摁在了各自的%LOCALS:2%乳房上、命令%LOCALS:0%開始揉搓讓自己舒服…
			ENDIF
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%去愛撫%LOCALS:1%的%LOCALS:2%胸部
			;片方が男性
			IF IS_MALE(MTAR:0) || IS_MALE(MTAR:1)
				PRINTFORMW %LOCALS:0%捏住了%LOCALS:1%的%LOCALS:2%乳頭、同時责弄著兩人…
			;二人とも貧乳以下
			ELSEIF GET_BUSTSIZE(MTAR:0) <= -1 && GET_BUSTSIZE(MTAR:1) <= -1
				PRINTFORMW %LOCALS:0%分別用手撫摸著%LOCALS:1%的%LOCALS:2%貧瘠胸部…
			ELSE
				PRINTFORMW %LOCALS:0%揉搓著%LOCALS:1%合計四個的%LOCALS:2%乳房…
			ENDIF
	ENDSELECT

;ターゲットが１人
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %LOCALS:0%
			IF IS_MALE(MTAR:0)
				PRINTFORMW 用指尖反復责弄著%LOCALS:1%的%LOCALS:2%柔軟膨脹的乳頭…
			ELSE
				IF GET_BUSTSIZE(MTAR:0) == -2
					PRINTFORMW 慢慢撫摸著%LOCALS:1%的%STR_BODY("胸：長：愛撫", MTAR:0)%…
				ELSEIF GET_BUSTSIZE(MTAR:0) == -1
					PRINTFORMW 仔細责弄著%LOCALS:1%的%STR_BODY("胸：長：愛撫", MTAR:0)%…
				ELSE
					PRINTFORMW 仔細揉搓著%LOCALS:1%的%STR_BODY("胸：長：愛撫", MTAR:0)%…
				ENDIF
			ENDIF
		;ターゲットに主導権
		CASE 1
			IF IS_MALE(MTAR:0)
				PRINTFORMW %LOCALS:1%讓%LOCALS:0%揉捏著自己的%LOCALS:2%乳頭、命令%LOCALS:0%讓自己舒服…
			ELSE
				PRINTFORM %LOCALS:1%把%LOCALS:0%的手按在了自身的
				IF GET_BUSTSIZE(MTAR:0) == -2
					PRINTFORMW %STR_BODY("胸：長：接触", MTAR:0)%上、命令%LOCALS:0%開始撫摸讓自己舒服…
				ELSEIF GET_BUSTSIZE(MTAR:0) == -1
					PRINTFORMW %STR_BODY("胸：長：接触", MTAR:0)%上、命令%LOCALS:0%開始撫摸讓自己舒服…
				ELSE
					PRINTFORMW %STR_BODY("胸：長：愛撫", MTAR:0)%上、命令%LOCALS:0%開始揉搓讓自己舒服…
				ENDIF
			ENDIF
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%去愛撫%LOCALS:1%的胸部
			PRINTFORM %LOCALS:0%
			IF IS_MALE(MTAR:0)
				PRINTFORMW 用指尖反復责弄著%LOCALS:1%的%LOCALS:2%柔軟膨脹的乳頭…
			ELSE
				IF GET_BUSTSIZE(MTAR:0) == -2
					PRINTFORMW 慢慢撫摸著%STR_BODY("胸：長：愛撫", MTAR:0)%…
				ELSEIF GET_BUSTSIZE(MTAR:0) == -1
					PRINTFORMW 仔細责弄著%STR_BODY("胸：長：愛撫", MTAR:0)%…
				ELSE
					PRINTFORMW 仔細揉搓著%STR_BODY("胸：長：愛撫", MTAR:0)%…
				ENDIF
			ENDIF
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST1
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	IF (LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):快Ｂ

		IF IS_MALE(LOCAL:1)
			IF TCVAR:(LOCAL:1):52
				;気絶中
				PRINTFORML 失去意識的%ANAME(LOCAL:1)%因為乳頭傳來的刺激微顫著身體
			ELSEIF LOCAL:2 < 300
				PRINTFORML %ANAME(LOCAL:1)%好像並沒有從乳頭那感到快感的樣子
			ELSEIF LOCAL:2 < 1000
				PRINTFORML %ANAME(LOCAL:1)%開始能感覺到一點點快感、因為瘙癢扭動著身子
			ELSEIF LOCAL:2 < 3000
				PRINTFORML %ANAME(LOCAL:1)%能感覺到從乳頭傳來的刺激、開始発出了火熱的吐息
			ELSEIF LOCAL:2 < 6000
				PRINTFORML %ANAME(LOCAL:1)%因為從乳頭傳來的快感而表情舒緩、任憑擺佈
			ELSEIF LOCAL:2 < 10000
				PRINTFORML %ANAME(LOCAL:1)%感覺太過敏感、每次被责弄乳頭時會感受到激烈難耐的快感
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%完全成為了乳頭傳來的快感的俘虜、每次被揉捏著乳頭時都會因為難耐的快感讓身體弾起
			ENDIF
		ELSE
			IF TCVAR:(LOCAL:1):52
				;気絶中
				PRINTFORML 失去意識的%ANAME(LOCAL:1)%因為胸部傳來的刺激微顫著身體
			ELSEIF LOCAL:2 < 300
				PRINTFORML %ANAME(LOCAL:1)%好像並沒有從胸部那感到快感的樣子
			ELSEIF LOCAL:2 < 1000
				PRINTFORML %ANAME(LOCAL:1)%開始能感覺到一點點快感、因為瘙癢扭動著身子
			ELSEIF LOCAL:2 < 3000
				PRINTFORML %ANAME(LOCAL:1)%被持續揉搓著胸部讓乳頭漸漸硬了起來、開始発出了火熱的吐息
			ELSEIF LOCAL:2 < 6000
				PRINTFORML %ANAME(LOCAL:1)%僅僅被愛撫著胸部就會露出滿足的表情
			ELSEIF LOCAL:2 < 10000
				PRINTFORML %ANAME(LOCAL:1)%感覺太過敏感、光是胸部的刺激就會陷入到極致的愉悅之中
			ELSE
				PRINTFORM %ANAME(LOCAL:1)%僅僅因為胸部被持續揉搓而產生的快感就讓身體不住的跳動、
				IF GET_BUSTSIZE(LOCAL:1) == 1
					PRINTL 看著在眼前弾動著的巨乳真是大飽眼福
				ELSEIF GET_BUSTSIZE(LOCAL:1) == 2
					PRINTL 看著在眼前劇烈弾動著的爆乳真是大飽眼福
				ELSE
					PRINTL 露出舒緩的表情委身于快感
				ENDIF
			ENDIF
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN1
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_1()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_1()
RETURN POWER(2, 性的嗜好_愛撫されたい)


;亀頭責め手コキ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME106
IF MPLY_NUM == 2
	LOCALS:0 = Ｗ手交责弄亀頭
ELSE
	LOCALS:0 = 手交责弄亀頭
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE106
;共通部分
CALL COM_ABLE_COMMON(106)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
SIF !HAS_PENIS(MTAR:0)
	RETURN 0
FOR LOCAL:0, 0, MPLY_NUM
	SIF !P_STACKABLE(MPLY:(LOCAL:0), MTAR:0, 106)
		RETURN 0
	SIF !CAN_REACH_GROIN(MPLY:(LOCAL:0), MTAR:0)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM106
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 250)
	SOURCE:(LOCAL:2):接触 = 100
	SOURCE:(LOCAL:2):性行動 = 210

	IF IS_INITIATIVE(LOCAL:2)
		SOURCE:(LOCAL:2):嗜虐 = 50
	ENDIF

	SELECTCASE ABL:(LOCAL:2):奉仕
		CASE 0
			TIMES SOURCE:(LOCAL:2):不潔, 4.00
		CASE 1
			TIMES SOURCE:(LOCAL:2):不潔, 2.50
		CASE 2
			TIMES SOURCE:(LOCAL:2):不潔, 1.50
		CASE 3
			TIMES SOURCE:(LOCAL:2):不潔, 1.00
		CASE 4
			TIMES SOURCE:(LOCAL:2):不潔, 0.50
		CASEELSE
			TIMES SOURCE:(LOCAL:2):不潔, 0.10
	ENDSELECT

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 180, 80)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
;		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 110)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP106
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP106(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 70)
	SOURCE:(LOCAL:2):接触 += 50
	SOURCE:(LOCAL:2):性行動 += 70

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
;		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	SOURCE:(LOCAL:1):接触 += 25
	SOURCE:(LOCAL:1):性行動 += 60
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE106(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%责备龟头手交中
;RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%に亀頭責め手コキ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP106(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%用手掌循环抚摸着%EQUIP_TARGET_ANAME(ARG:0)%的龟头…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%の亀頭を掌で撫で回している…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP106(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER106(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF
IF TALENT:(ARG:0):討厭男人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭男人", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE106
;プレイヤーが２人
IF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%分别将手掌压在%ANAME(MTAR:0)%的龟头上，一边挤压着龟头一边轻轻的擦拭着…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、それぞれ手のひらを%ANAME(MTAR:0)%の亀頭に押し付けると、亀頭を押し潰すようにしながら擦り上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%对%ANAME(MPLY:0)%和%ANAME(MPLY:1)%，要求用手责备龟头来进行服侍
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、亀頭責めで気持ち良くするように要求した
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%两个人分担着，用各自的手指抚摸着%ANAME(MTAR:0)%的龟头服务着…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は二人で分担しながら、それぞれの指で%ANAME(MTAR:0)%の亀頭を撫でまわして奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MPLY:1)%用手指责备着%ANAME(MTAR:0)%的龟头
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に対して、%ANAME(MTAR:0)%の亀頭を指で責めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%两个人分担着，用各自的手指抚摸着%ANAME(MTAR:0)%的龟头…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は二人で分担しながら、それぞれの指で%ANAME(MTAR:0)%の亀頭を撫でまわした…
	ENDSELECT

;プレイヤーが１人
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			IF ABL:(MPLY:0):加虐 >= 3 && MPLY:0 != MASTER
				PRINTFORMW %ANAME(MPLY:0)%用力握着%ANAME(MTAR:0)%的龟头，激烈地摩擦着责备…
				;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の亀頭を強く握ると、激しく擦り上げて責め立て%TARGET_STR("た", "てきた")%…
			ELSE
				PRINTFORMW %ANAME(MPLY:0)%用手掌包住%ANAME(MTAR:0)%的龟头，激烈地擦拭责备着…
				;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の亀頭を手のひらで包み込むと、激しく擦り上げて責め立て%TARGET_STR("た", "てきた")%…
			ENDIF
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%向%ANAME(MPLY:0)%提出了用手来责备龟头使其获得快乐的要求
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に、亀頭責めで気持ち良くするように要求した
			PRINTFORMW %ANAME(MPLY:0)%用手掌包裹着%ANAME(MTAR:0)%的龟头，温柔地抚摸着它，为它服务…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の亀頭を手のひらで包み込み、優しく撫で回して奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用手指责备%ANAME(MTAR:0)%的龟头
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%の亀頭を指で責めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%用手掌将%ANAME(MTAR:0)%的龟头包住，激烈地摩擦责备着…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の亀頭を手のひらで包み込むと、激しく擦り上げて責め立てた…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN106
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_106()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_106()
RETURN POWER(2, 性的嗜好_奉仕されたい)

@COM_STACK_SPERM_MTAR_TO_MPLY_106()
RETURN 射精部位_手

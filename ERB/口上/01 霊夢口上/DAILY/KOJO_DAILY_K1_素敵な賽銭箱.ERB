;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K1_LOVELY_SAISENBAKO_RATE(対象)
#DIM 対象
RETURN 60


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K1_LOVELY_SAISENBAKO_DECISION(対象)
#DIM 対象
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K1_LOVELY_SAISENBAKO_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K1_LOVELY_SAISENBAKO(対象)
#DIM 対象
#DIM 賽銭
#DIM 御利益種類
#DIM 御利益量

IF MASTER == NAME_TO_CHARA("霖之助")
	LOCALS = %ANAME(MASTER)%桑
ELSE
	LOCALS = %ANAME(MASTER)%
ENDIF

SELECTCASE RAND:100
	CASE IS < 40
		賽銭 = 10
	CASE IS < 60
		賽銭 = 100
	CASE IS < 80
		賽銭 = 500
	CASE IS < 90
		賽銭 = 1000
	CASEELSE
		賽銭 = 10000
ENDSELECT

IF KDVAR:対象:霊夢_素敵な賽銭箱 == 0
	PRINTFORMW 「嗯」
	PRINTFORML 工作的時候、%ANAME(対象)%突然出現在面前、伸出了一個小小的木箱
	PRINTFORMW 看上去像是募金箱……
	PRINTFORMW 「楽園的絶妙的賽銭箱、攜帯版。把錢放進去的話説不定会降福哦」
	PRINTFORM 「想実現的話、就投入賽銭吧。対了、投入
	CALL COLOR_PRINT(@"{賽銭}", カラー_注意)
	PRINTFORMW 金大概就差不多了吧」
	PRINTFORMW ……直接伸到面前的賽銭箱還是第一次看見
	PRINTFORML 另外、賽銭的金額是可以指定的東西麼……
ELSEIF KDVAR:対象:霊夢_素敵な賽銭箱 == 1
	PRINTFORML 「我又来了」
	PRINTFORML 工作的時候、%ANAME(対象)%来了、伸出了賽錢箱
	PRINTFORM 「想実現的話、就投入賽銭吧。対了、投入
	CALL COLOR_PRINT(@"{賽銭}", カラー_注意)
	PRINTFORML 金大概就差不多了吧」
	PRINTFORML 和以前一樣指定了金額……
ELSE
	PRINTFORML 「這次的錢、也会給吧？」
	PRINTFORML 工作的時候、%ANAME(対象)%来了、伸出了賽錢箱
	PRINTFORM 「想実現的話、就投入賽銭吧。対了、投入
	CALL COLOR_PRINT(@"{賽銭}", カラー_注意)
	PRINTFORML 金大概就差不多了吧」
	PRINTFORML 和以前一樣指定了金額……
ENDIF
PRINTFORML
CALL ASK_MULTI_JUDGE("把錢放進去", MONEY >= 賽銭, "放棄", 1)
IF RESULT == 1
	PRINTFORMW 「什麼、小気！」
	PRINTFORMW %ANAME(対象)%生気的回去了……
	KDVAR:対象:霊夢_素敵な賽銭箱 = 2
ELSE
	PRINTFORMW 「嗚呼呼、謝謝♪」
	PRINTFORMW %ANAME(対象)%露出財迷的表情離開了……
	CFLAG:対象:好感度 += MIN(賽銭 / 10, 500)
	MONEY -= 賽銭
	IF RAND:1000 < LIMIT(賽銭, 200, 800)
		SELECTCASE RAND:100
			CASE IS < 40
				御利益種類 = GETNUM(EXP, "武闘経験値")
			CASE IS < 80
				御利益種類 = GETNUM(EXP, "知略経験値")
			CASEELSE
				御利益種類 = GETNUM(EXP, "政治経験値")
		ENDSELECT
		御利益量 = LIMIT(賽銭 / 50, 5, 50)
		IF RAND:2
			CALL COLOR_PRINTW("好像真的有点霊験……", カラー_注意)
			CALL PRINT_ADD_EXP(MASTER, EXPNAME:御利益種類, 御利益量, 1)
			CALL TRAIN_AUTO_ABLUP(MASTER)
		ELSE
			CALL COLOR_PRINTW(@"好像真的有点霊験……%ANAME(対象)%", カラー_注意)
			CALL PRINT_ADD_EXP(対象, EXPNAME:御利益種類, 御利益量, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
		ENDIF
	ENDIF
	KDVAR:対象:霊夢_素敵な賽銭箱 = 1
ENDIF



RETURN 1

;武術訓練

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME301
RESULTS:0 = 邀請訓練武術
RESULTS:1 = 陪著訓練武術

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE301
;共通部分
CALL COM_ABLE_COMMON(301)
SIF RESULT == 0
	RETURN 0
;捕虜会話では不可
SIF FLAG:調教モード == 6
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;デート中で場所が川辺、花畑、森林以外なら無理
SIF TFLAG:54 && !GROUPMATCH(GET_PLACENAME(TFLAG:54), "川辺", "花畑", "森林")
	RETURN 0
;臨月or怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月 || CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1


;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM301
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "武闘"), 0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE301
;主導権が主人公側
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%一起進行了武術訓練
ELSE
	;相手より武闘が10以上劣っている
	IF ABL:(MPLY:0):武闘 + 10 <= ABL:(MTAR:0):武闘
		PRINTFORMW %ANAME(MPLY:0)%被%ANAME(MTAR:0)%認真地教授了武術
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%陪著%ANAME(MTAR:0)%進行了訓練
	ENDIF
ENDIF

IF TFLAG:18 == 1
	PRINTFORML 比試比想象中做的還要好。獲得了很好的経験…
ELSEIF TFLAG:18 == -1
	PRINTFORML ……但是、%ANAME(MPLY:0)%乾淨利落地就被擊敗了
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST301
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN301
RETURN コマンド_日常

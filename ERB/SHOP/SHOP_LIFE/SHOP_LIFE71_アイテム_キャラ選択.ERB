;-------------------------------------------------
;対象キャラを選択するアイテムの使用条件
;-------------------------------------------------
;●双性人薬
@IS_ITEM_USE280(ARG:0)
SIF IS_ANIMAL(ARG:0)
	RETURN 0
IF ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	RETURN 1
ENDIF
RETURN 0

;●性転換薬
@IS_ITEM_USE281(ARG:0)
IF !IS_ANIMAL(ARG:0) && (((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)
	RETURN 1
ENDIF
RETURN 0
;●獣変化の秘薬
@IS_ITEM_USE283(ARG:0)
SIF ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	RETURN 1
RETURN 0

;●再生の秘薬
@IS_ITEM_USE284(ARG:0)
IF (((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属) && ARG:0 != NAME_TO_CHARA("あなた")
	RETURN 1
ENDIF
RETURN 0

;●太平要術の書
@IS_ITEM_USE141(ARG:0)
IF ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF !TALENT:(ARG:0):妖術知識
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●淫技大全
@IS_ITEM_USE142(ARG:0)
IF ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF ABL:(ARG:0):性知識 == 4 && ABL:(ARG:0):性技 >= 6
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●永久脱毛剤
@IS_ITEM_USE287(ARG:0)
SIF TALENT:(ARG:0):陰毛現在値 != 陰毛_パイパン
	RETURN ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
RETURN 0

;●入れ墨除去手術
@IS_ITEM_USE288(ARG:0)
FOR LOCAL, 0, VARSIZE("TATTOO")
	SIF TATTOO:(ARG:0):LOCAL != ""
		RETURN ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
NEXT
RETURN 0

;●穴トレ
@IS_ITEM_USE289(ARG:0)
RETURN ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属

;●美容
@IS_ITEM_USE290(ARG:0)
RETURN (((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;●危険日ずらし
@IS_ITEM_USE291(ARG:0)
RETURN (((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属) && HAS_VAGINA(ARG:0)


;避妊薬
@IS_ITEM_USE292(ARG:0)
RETURN !CFLAG:(ARG:0):妊娠期間 && IS_FEMALE(ARG:0) && CFLAG:(ARG:0):避妊薬残ターン == 0 && (((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;堕胎薬
@IS_ITEM_USE293(ARG:0)
RETURN TALENT:(ARG:0):妊娠  && IS_FEMALE(ARG:0) && (((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;●能動受動
@IS_ITEM_USE294(ARG:0)
RETURN ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 || GROUPMATCH(TMP_COUNTRY_RELATION:(CFLAG:(ARG:0):所属):(CFLAG:(MASTER):所属), 3, 4)) && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属

;-------------------------------------------------
;アイテムの対象キャラを選択してその場で使用
;ARG:0=アイテム番号
;-------------------------------------------------
@SELECT_ITEM_USER(ARG:0)
#DIM FIRST_LINE

CLEARLINE 3

;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0

;アイテムを使用可能なキャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:0)
	IF RESULT
		LOCAL:5 ++
	ENDIF
NEXT

;ページ数の計算
LOCAL:10 = (LOCAL:5 - 1) / 44 + 1
LOCAL:11 = 1
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTFORM %TOSTR_SPACE(6)%
PRINTFORML 要対誰使用%ITEMNAME:(ARG:0)%？
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
FIRST_LINE = LINECOUNT

$SHOW_LOOP

;キャラリストの表示
LOCAL:12 = (LOCAL:11 - 1) * 44
LOCAL:13 = LOCAL:11* 44
LOCAL:14 = 0
LOCAL:15 = 0
LOCAL:16 = 4
PRINTFORM %TOSTR_SPACE(6)%
FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:0)
	IF RESULT
		IF LOCAL:14 >= LOCAL:12 && LOCAL:14 < LOCAL:13
			IF LOCAL:15 % 2 != 0
				PRINTPLAINFORM %TOSTR_SPACE(2)%
			ELSEIF LOCAL:15 >= 1
				PRINTL
				PRINTFORM %TOSTR_SPACE(6)%
				LOCAL:16 ++
			ENDIF
			;CALL PRINT_PARTNER_DATA(LOCAL:0)
			CALL COLUMN_RIGHT_CHARALIST_BUTTON(LOCAL:0, 0, , , 1)
			LOCAL:15 ++
		ENDIF
		LOCAL:14 ++
	ENDIF
NEXT
PRINTL
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR

IF LOCAL:10 >= 2
	LOCAL:16 += 2
	IF LOCAL:11 > 1
		PRINTBUTTON @"[%"察看前一頁", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_BACK
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [%"察看前一頁", MAX_CHARANAME_LENGTH, LEFT%]
		RESETCOLOR
	ENDIF
	LOCALS:0 = page{LOCAL:11}/{LOCAL:10}
	PRINTPLAINFORM %TOSTR_SPACE(10)%
	PRINTPLAINFORM %LOCALS:0, 16, LEFT%
	IF LOCAL:11 < LOCAL:10
		PRINTBUTTON @"[%"察看下一頁", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_NEXT
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [%"察看下一頁", MAX_CHARANAME_LENGTH, LEFT%]
		RESETCOLOR
	ENDIF
	PRINTL
	SETCOLOR カラー_灰色
	CALL SINGLE_DRAWLINE
	RESETCOLOR
ENDIF

PRINTPLAIN
PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", 0
PRINTFORML

REDRAW 0

$INPUT_LOOP
INPUT 0

CLEARLINE 2

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == SHOP_LIFE_LIST1_PAGE_BACK
	LOCAL:11 --
	IF LOCAL:11 < 0
		LOCAL:11 = LOCAL:10
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == SHOP_LIFE_LIST1_PAGE_NEXT
	LOCAL:11 ++
	IF LOCAL:11 > LOCAL:10
		LOCAL:11 = 0
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSE
	LOCAL:20 = RESULT - 100
	LOCAL:21 = NO_TO_CHARA(LOCAL:20)
	IF LOCAL:21 >= 0
		RESULT = 0
		TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:21)
		IF RESULT
			CALLFORM ITEM_EFFECT{ARG:0}(LOCAL:21)
			REDRAW 1
			RETURN 1
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;対象キャラを選択してその場で使用アイテムの効果
;-------------------------------------------------
;●双性人薬
@ITEM_EFFECT280(ARG:0)
SELECTCASE TALENT:(ARG:0):性別
	CASE 0
		TALENT:(ARG:0):性別 = 3
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成扶她了
	CASE 1
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成扶她了
	CASE 2
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成女性了
	CASE 3
		TALENT:(ARG:0):性別 = 0
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成男性了
	CASE 4
		TALENT:(ARG:0):性別 = 5
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成扶她了
	CASE 5
		TALENT:(ARG:0):性別 = 4
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成偽娘了
ENDSELECT
;穿孔穴を塞ぐ
CALL CLEAR_PIERCE_HOLE_SEX_CHANGE(ARG:0)

;女ふたなりになった場合(あたらしくチンポがついた場合)
IF TALENT:(ARG:0):性別 == 2
	PRINTFORML %TOSTR_SPACE(6)%肉棒的尺寸是……
	CALL ASK_MULTI("超短小", "短小", "普通", "巨根", "超巨根")
	CALL SET_PENISSIZE(ARG:0, RESULT -2)
ENDIF

IF GROUPMATCH(TALENT:(ARG:0):性別, 2, 3, 5)
	PRINTFORML %TOSTR_SPACE(6)%要讓%ANAME(ARG:0)%恢復童貞麼？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変回童貞了
		CALL VIRGINIZE(ARG:0, 初体験_童貞)
	ENDIF
ENDIF

;●性転換薬
@ITEM_EFFECT281(ARG:0)
SELECTCASE TALENT:(ARG:0):性別
	CASE 0
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成女性了
	CASE 1
		IF CFLAG:(ARG:0):元男の娘フラグ
			TALENT:(ARG:0):性別 = 4
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成偽娘了
			CFLAG:(ARG:0):元男の娘フラグ = 0
		ELSE
			TALENT:(ARG:0):性別 = 0
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成男性了
		ENDIF
	CASE 2
		IF CFLAG:(ARG:0):元男の娘フラグ
			TALENT:(ARG:0):性別 = 5
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成偽娘扶她了
			CFLAG:(ARG:0):元男の娘フラグ = 0
		ELSE
			TALENT:(ARG:0):性別 = 3
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成了男性扶她了
		ENDIF
	CASE 3
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成了女性扶她了
	CASE 4
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成女性了
		;元男の娘フラグ
		CFLAG:(ARG:0):元男の娘フラグ = 1
	CASE 5
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成了女性扶她了
		;元男の娘フラグ
		CFLAG:(ARG:0):元男の娘フラグ = 1
ENDSELECT
;穿孔穴を塞ぐ
CALL CLEAR_PIERCE_HOLE_SEX_CHANGE(ARG:0)
CALL SET_BODYSIZE(ARG:0)


;●獣変化の秘薬
@ITEM_EFFECT283(ARG:0)
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT

CALL PRINT_TOGGLE_BUTTON("[動物耳]", 0, TALENT:(ARG:0):動物耳)
CALL PRINT_TOGGLE_BUTTON("[尾巴]", 1, TALENT:(ARG:0):尾巴)
PRINTL
PRINTBUTTON "[取消]", 100

INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0
	TALENT:(ARG:0):動物耳 = !TALENT:(ARG:0):動物耳
ELSEIF RESULT == 1
	TALENT:(ARG:0):尾巴 = !TALENT:(ARG:0):尾巴
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;●再生の秘薬
@ITEM_EFFECT284(ARG:0)
#DIM FIRST_LINE
IF !EXISTCSV(NO:(ARG:0))
	CALL OLD_ITEM_EFFECT_284(ARG:0)
	RETURN
ENDIF
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%喝下了再生之秘薬……
PRINTFORML
FIRST_LINE = LINECOUNT
$SHOW_LOOP_SET
LOCAL:1 = CFLAG:(ARG:0):所属
LOCAL:2 = CFLAG:(ARG:0):捕虜先
CALL CLEAR_ALL_PASS_SETTINGS()
CALL PASS_CHARA_UI(ARG)
CALL PASS_CHARA(ARG)
CFLAG:(ARG:0):所属 = LOCAL:1
;CFLAG:(ARG:0):捕虜先 = LOCAL:2
CALL CAPTURE(ARG:0,LOCAL:2)
;デフォルトで処女でないキャラは、再生させるか確認する
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "処女"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%默認不是処女、所以之前選択的処女初期化要変成非処么
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%要変回処女么？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成了処女
		CALL VIRGINIZE(ARG:0, 初体験_処女)
	ENDIF
ENDIF
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "後庭処女"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%默認後庭不是処、所以之前選択的後庭処女初期化要変成非処么
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%要変回後庭処女么？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%変回了後庭処女
		CALL VIRGINIZE(ARG:0, 初体験_アナル処女)
	ENDIF
ENDIF
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "童貞"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%默認不是童貞、所以之前選択的童貞初期化要変成非童貞么
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%要変回童貞么？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%変回了童貞
		CALL VIRGINIZE(ARG:0, 初体験_童貞)
	ENDIF
ENDIF
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "無接吻経験"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%默認擁有接吻経験、所以之前選択的無接吻経験的初期化默認是有接吻経験
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%要変回無接吻経験么？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%変回了無接吻経験
		CALL VIRGINIZE(ARG:0, 初体験_キス)
	ENDIF
ENDIF


PRINTFORML %TOSTR_SPACE(6)%要留下在日常事件中得到的素質么？
CALL ASK_YN("保留", "消去")
SIF RESULT == 1
	TALENT:(ARG:0):日常系 = 0

PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%的肉体再生了
WAIT
RETURN

;東方キャラ以外に再生の秘薬を飲ませた場合
@OLD_ITEM_EFFECT_284(ARG:0)
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%的肉体再生了
FOR LOCAL:0, 0, 5
	ABL:(ARG:0):(LOCAL:0) = 0
	EXP:(ARG:0):(LOCAL:0) = 0
NEXT
IF TALENT:(ARG:0):処女 != 1
	CALL VIRGINIZE(ARG:0, 初体験_処女)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%的<処女>素質再生了
ENDIF
IF TALENT:(ARG:0):無接吻経験 != 1
	CALL VIRGINIZE(ARG:0, 初体験_キス)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の<無接吻経験>が再生されました
ENDIF
IF TALENT:(ARG:0):童貞 != 1
	CALL VIRGINIZE(ARG:0, 初体験_童貞)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%的<童貞>素質再生了
ENDIF
IF TALENT:(ARG:0):後庭処女 != 1
	CALL VIRGINIZE(ARG:0, 初体験_アナル処女)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の<後庭処女>が再生されました
ENDIF

IF TALENT:(ARG:0):烙印
	TALENT:(ARG:0):烙印 = 0
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%身体的<烙印>消失了
ENDIF
TALENT:(ARG:0):特殊勢力陥落系 = 0
TALENT:(ARG:0):日常系 = 0
WAIT


;●永久脱毛剤
@ITEM_EFFECT287(ARG:0)
PRINTFORML %TOSTR_SPACE(6)%在%ANAME(ARG:0)%的胯下塗抹永久脱毛剤
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%変成了<白虎>
TALENT:(ARG:0):陰毛目標値 = 陰毛_パイパン
TALENT:(ARG:0):陰毛現在値 = 陰毛_パイパン

;●入れ墨除去手術
@ITEM_EFFECT288(ARG:0)
PRINTFORML %TOSTR_SPACE(6)%要除去%ANAME(ARG:0)%身上的紋身么？
FOR LOCAL, 0, VARSIZE("TATTOO")
	SIF TATTOO:(ARG:0):(LOCAL) != ""
		PRINTFORML %TOSTR_SPACE(6)%[{LOCAL}] %GET_TATTOO_NAME(LOCAL)% - 「%TATTOO:(ARG:0):(LOCAL)%」
NEXT
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTPLAIN
PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", 99
PRINTL
$INPUT_LOOP
INPUT 99
IF RESULT == 99
	CALL GET_PRICE288
	MONEY += RESULT
	RETURN
ELSEIF 0 <= RESULT && RESULT < VARSIZE("TATTOO") && TATTOO:(ARG:0):RESULT != ""
	PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%の%GET_TATTOO_NAME(RESULT)%の文身を除去しました……
	TATTOO:(ARG:0):RESULT =
ELSE
	GOTO INPUT_LOOP
ENDIF

;●穴トレ
@ITEM_EFFECT289(ARG:0)
#DIMS 変化前
#DIMS 変化後
PRINTFORML %TOSTR_SPACE(6)%要鍛煉%ANAME(ARG:0)%哪一個穴？
CALL ASK_MULTI_JUDGE("膣", HAS_VAGINA(ARG:0), "臀部", 1, @"%"取消", MAX_CHARANAME_LENGTH, LEFT%", 1)
SELECTCASE RESULT
	CASE 0
		変化前 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ緊緻度)%
		TALENT:(ARG:0):Ｖ緊緻度 = LIMIT(TALENT:(ARG:0):Ｖ緊緻度 + 100, 締まり_最低値, 締まり_最高値)
		変化後 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ緊緻度)%
		CALL COLOR_PRINTW(@"%TOSTR_SPACE(6)%%ANAME(ARG:0)%的膣穴得到了鍛煉！", カラー_注意)
		CALL COLOR_PRINTW(@"変化前:%変化前%　→　変化後:%変化後%", カラー_注意)
	CASE 1
		変化前 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ緊緻度)%
		TALENT:(ARG:0):Ａ緊緻度 = LIMIT(TALENT:(ARG:0):Ａ緊緻度 + 100, 締まり_最低値, 締まり_最高値)
		変化後 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ緊緻度)%
		CALL COLOR_PRINTW(@"%TOSTR_SPACE(6)%%ANAME(ARG:0)%的菊穴得到了鍛煉！", カラー_注意)
		CALL COLOR_PRINTW(@"変化前:%変化前%　→　変化後:%変化後%", カラー_注意)
	CASE 2
		CALL GET_PRICE289
		MONEY += RESULT
		RETURN
ENDSELECT
RETURN

;●美容手術
@ITEM_EFFECT290(ARG:0)
CALL BEAUTIFY(ARG:0)
CALL COLOR_PRINTW(@"%ANAME(ARG:0)%的美容手術結束了！", カラー_注意)

@BEAUTIFY(ARG:0)
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
PRINTFORML 要怎麼処理%ANAME(ARG:0)%的身体？
CALL SHOW_BODYSIZE(ARG:0)
CALL SINGLE_DRAWLINE()
PRINTL

IF HAS_PENIS(ARG:0)
	PRINTFORM 竿:
	CALL PRINT_SELECT_BUTTON("{超短小}", 0, GET_PENISSIZE(ARG:0) == -2)
	CALL PRINT_SELECT_BUTTON("{短小}",   1, GET_PENISSIZE(ARG:0) == -1)
	CALL PRINT_SELECT_BUTTON("{並根}",   2, GET_PENISSIZE(ARG:0) ==  0)
	CALL PRINT_SELECT_BUTTON("{巨根}",   3, GET_PENISSIZE(ARG:0) ==  1)
	CALL PRINT_SELECT_BUTTON("{超巨根}", 4, GET_PENISSIZE(ARG:0) ==  2)
	PRINTL
ENDIF
IF IS_FEMALE(ARG:0)
	PRINTFORM 胸:
	CALL PRINT_SELECT_BUTTON("{絶壁}", 10, GET_BUSTSIZE(ARG:0) == -2)
	CALL PRINT_SELECT_BUTTON("{貧乳}", 11, GET_BUSTSIZE(ARG:0) == -1)
	CALL PRINT_SELECT_BUTTON("{並乳}", 12, GET_BUSTSIZE(ARG:0) == 0)
	CALL PRINT_SELECT_BUTTON("{巨乳}", 13, GET_BUSTSIZE(ARG:0) == 1)
	CALL PRINT_SELECT_BUTTON("{爆乳}", 14, GET_BUSTSIZE(ARG:0) == 2)
	PRINTL
ENDIF

PRINTFORM 尻:
CALL PRINT_SELECT_BUTTON("{壁尻}", 100, GET_HIPSIZE(ARG:0) == -2)
CALL PRINT_SELECT_BUTTON("{薄尻}", 101, GET_HIPSIZE(ARG:0) == -1)
CALL PRINT_SELECT_BUTTON("{普尻}", 102, GET_HIPSIZE(ARG:0) == 0)
CALL PRINT_SELECT_BUTTON("{巨尻}", 103, GET_HIPSIZE(ARG:0) == 1)
CALL PRINT_SELECT_BUTTON("{爆尻}", 104, GET_HIPSIZE(ARG:0) == 2)
PRINTL


IF HAS_VAGINA(ARG:0)
	PRINTFORM Ｖ:
	LOCALS = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ緊緻度)%
	;翻译者注：グロマン Guroman，是怪诞的阴部 "的意思，指的是视觉上肮脏的女性阴道。
	CALL PRINT_SELECT_BUTTON("{不堪入目}", 65, LOCALS == TOSTR_TIGHTNESS(締まり_最低値))
	CALL PRINT_SELECT_BUTTON("{崩壊}",     40, LOCALS == TOSTR_TIGHTNESS(締まり_崩壊))
	CALL PRINT_SELECT_BUTTON("{擴張}", 41, LOCALS == TOSTR_TIGHTNESS(締まり_がばがば))
	CALL PRINT_SELECT_BUTTON("{張開}", 42, LOCALS == TOSTR_TIGHTNESS(締まり_ぽっかり))
	CALL PRINT_SELECT_BUTTON("{略張}", 43, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるゆる))
	CALL PRINT_SELECT_BUTTON("{微張}",   44, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるめ))
	CALL PRINT_SELECT_BUTTON("{普通}",     45, LOCALS == TOSTR_TIGHTNESS(締まり_普通))
	CALL PRINT_SELECT_BUTTON("{名器}",     46, LOCALS == TOSTR_TIGHTNESS(締まり_名器))
	CALL PRINT_SELECT_BUTTON("{微閉}", 47, LOCALS == TOSTR_TIGHTNESS(締まり_きゅっきゅっ))
	CALL PRINT_SELECT_BUTTON("{閉合}", 48, LOCALS == TOSTR_TIGHTNESS(締まり_きつきつ))
	CALL PRINT_SELECT_BUTTON("{緊閉}", 49, LOCALS == TOSTR_TIGHTNESS(締まり_ぎちぎち))
	PRINTL
ENDIF

LOCALS =  %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ緊緻度)%
PRINTFORM Ａ:
CALL PRINT_SELECT_BUTTON("{不堪入目}", 66, LOCALS == TOSTR_TIGHTNESS(締まり_最低値))
CALL PRINT_SELECT_BUTTON("{崩壊}",     50, LOCALS == TOSTR_TIGHTNESS(締まり_崩壊))
CALL PRINT_SELECT_BUTTON("{擴張}", 51, LOCALS == TOSTR_TIGHTNESS(締まり_がばがば))
CALL PRINT_SELECT_BUTTON("{張開}", 52, LOCALS == TOSTR_TIGHTNESS(締まり_ぽっかり))
CALL PRINT_SELECT_BUTTON("{略張}", 53, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるゆる))
CALL PRINT_SELECT_BUTTON("{微張}",   54, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるめ))
CALL PRINT_SELECT_BUTTON("{普通}",     55, LOCALS == TOSTR_TIGHTNESS(締まり_普通))
CALL PRINT_SELECT_BUTTON("{名器}",     56, LOCALS == TOSTR_TIGHTNESS(締まり_名器))
CALL PRINT_SELECT_BUTTON("{微閉}", 57, LOCALS == TOSTR_TIGHTNESS(締まり_きゅっきゅっ))
CALL PRINT_SELECT_BUTTON("{閉合}", 58, LOCALS == TOSTR_TIGHTNESS(締まり_きつきつ))
CALL PRINT_SELECT_BUTTON("{緊閉}", 59, LOCALS == TOSTR_TIGHTNESS(締まり_ぎちぎち))
PRINTL
PRINTL

PRINT 体格:
CALL PRINT_SELECT_BUTTON("{小孩}", 70, TALENT:(ARG:0):体格 == 体格_子供)
CALL PRINT_SELECT_BUTTON("{小柄}", 71, TALENT:(ARG:0):体格 == 体格_小柄)
CALL PRINT_SELECT_BUTTON("{普通}", 72, TALENT:(ARG:0):体格 == 体格_普通)
CALL PRINT_SELECT_BUTTON("{長身}", 73, TALENT:(ARG:0):体格 == 体格_長身)
CALL PRINT_SELECT_BUTTON("{大柄}", 74, TALENT:(ARG:0):体格 == 体格_大柄)
CALL PRINT_SELECT_BUTTON("{巨大}", 75, TALENT:(ARG:0):体格 == 体格_巨大)
PRINTL
PRINT 体型:
CALL PRINT_SELECT_BUTTON("{过瘦}", 80, TALENT:(ARG:0):体型 == 体型_痩せすぎ)
CALL PRINT_SELECT_BUTTON("{纤细}", 81, TALENT:(ARG:0):体型 == 体型_細身)
CALL PRINT_SELECT_BUTTON("{修长}", 82, TALENT:(ARG:0):体型 == 体型_スレンダー)
CALL PRINT_SELECT_BUTTON("{普通}", 83, TALENT:(ARG:0):体型 == 体型_普通)
CALL PRINT_SELECT_BUTTON("{丰盈}", 84, TALENT:(ARG:0):体型 == 体型_グラマー)
CALL PRINT_SELECT_BUTTON("{豊満}", 85, TALENT:(ARG:0):体型 == 体型_豊満)
CALL PRINT_SELECT_BUTTON("{肥満}", 86, TALENT:(ARG:0):体型 == 体型_肥満)
PRINTL
PRINTBUTTON "[三圍尺寸再生成]", 90
PRINTL

PRINTBUTTON "[頭髮長度]", 60
PRINTFORM 現在:%TOSTR_HAIR_LENGTH(TALENT:(ARG:0):頭髮長度)%
PRINTFORM
PRINTBUTTON "[染髪]", 61
PRINTFORM 現在:
CALL PRINT_HAIR_COLOR(TALENT:(ARG:0):髪色)
PRINTL

CALL PRINT_TOGGLE_BUTTON("{美脚}", 20, TALENT:(ARG:0):美脚)
CALL PRINT_TOGGLE_BUTTON("{美臀}", 31, TALENT:(ARG:0):美臀)
PRINTL

PRINTBUTTON "[99] 完了", 99

INPUT
IF INRANGE(RESULT, 0, 4) && HAS_PENIS(ARG:0)
	CALL SET_PENISSIZE(ARG:0, RESULT - 2)
	CALL SET_BODYSIZE(ARG:0)
ELSEIF INRANGE(RESULT, 10, 14) && IS_FEMALE(ARG:0)
	CALL SET_BUSTSIZE(ARG:0, RESULT - 12)
	CFLAG:(ARG:0):妊娠時バストサイズ増加 = 0
ELSEIF INRANGE(RESULT, 100, 104)
	CALL SET_HIPSIZE(ARG:0, RESULT - 100 - 2)
	CALL SET_BODYSIZE(ARG:0)
ELSEIF RESULT == 20
	TALENT:(ARG:0):美脚 = !TALENT:(ARG:0):美脚
	CALL SET_BODYSIZE(ARG:0)
ELSEIF RESULT == 30
	TALENT:(ARG:0):美臀 = 0
	TALENT:(ARG:0):美脚 = 0
	CALL SET_BODYSIZE(ARG:0)
ELSEIF RESULT == 31
	TALENT:(ARG:0):美臀 = !TALENT:(ARG:0):美臀
	CALL SET_BODYSIZE(ARG:0)
ELSEIF INRANGE(RESULT, 40, 49) && HAS_VAGINA(ARG:0)
	TALENT:(ARG:0):Ｖ緊緻度 = (RESULT - 40) * 100 + 50
ELSEIF INRANGE(RESULT, 50, 59)
	TALENT:(ARG:0):Ａ緊緻度 = (RESULT - 50) * 100 + 50
ELSEIF RESULT == 65
	TALENT:(ARG:0):Ｖ緊緻度 = 締まり_最低値
ELSEIF RESULT == 66
	TALENT:(ARG:0):Ａ緊緻度 = 締まり_最低値
ELSEIF RESULT == 60
	CALL CHOOSE_HAIR_LENGTH
	TALENT:(ARG:0):頭髮長度 = RESULT
ELSEIF RESULT == 61
	CALL EDIT_HAIR_COLOR((ARG:0))
ELSEIF INRANGE(RESULT, 70, 75)
	TALENT:(ARG:0):体格 = RESULT - 70 - 2
	CALL SET_BODYSIZE(ARG:0)
ELSEIF INRANGE(RESULT, 80, 86)
	TALENT:(ARG:0):体型 = RESULT - 80 - 3
	CALL SET_BODYSIZE(ARG:0)
ELSEIF RESULT == 90
	CALL SET_BODYSIZE(ARG:0)
ELSEIF RESULT == 99
	RETURN
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;●危険日ずらし
@ITEM_EFFECT291(ARG:0)
PRINTFORML %ANAME(ARG:0)%的危険日是什麼時候呢……
PRINTFORML 現在的危険日:{TALENT:(ARG:0):危険日}
PRINTFORML （輸入0-4、每5回合来一次）
$INPUT_LOOP
INPUT

IF !INRANGE(RESULT, 0, 4)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

LOCAL = RESULT
TALENT:(ARG:0):危険日 = LOCAL

CALL COLOR_PRINTW(@"%ANAME(ARG:0)%的危険日変成了{LOCAL}！", カラー_注意)


;●避妊薬
@ITEM_EFFECT292(ARG:0)
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%暫時無法妊娠了……
CFLAG:(ARG:0):避妊薬残ターン += 5
CALL CLEAR_CREAMPIE_HISTORY(LOCAL:0)

;●堕胎手術
@ITEM_EFFECT293(ARG:0)
IF IS_LOVER(ARG:0) && CFLAG:(ARG:0):子の父親 == GET_ID(MASTER)
	PRINTFORML %ANAME(ARG:0)%非常抵觸打掉和%ANAME(MASTER)%之間的孩子的行為……
	PRINTFORML 真的要進行手術麼？
	CALL ASK_YN()
	IF RESULT == 1
		CALL GET_PRICE293
		MONEY += RESULT
		RETURN 0
	ENDIF
ENDIF

PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%接受了堕胎手術……
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%失去了＜妊娠＞
;状況に応じて崩壊値が増えたりする処理をいれたい

;妊娠関係のフラグをクリア
SIF CFLAG:(ARG:0):行動不能状態 == 行動不能_臨月
	CFLAG:(ARG:0):行動不能状態 = 0
CFLAG:(ARG:0):妊娠期間 = 0
CFLAG:(ARG:0):子の父親 = 0
TALENT:(ARG:0):妊娠 = 0

;中出し履歴をクリア
CALL CLEAR_CREAMPIE_HISTORY(ARG:0)
IF CFLAG:(ARG:0):育児期間 == 0
	CALL RESET_MOTHER_STATE(ARG:0, 0)
ENDIF

IF IS_LOVER(ARG:0)
	PRINTFORML %ANAME(ARG:0)%受到了很大的打撃……
	CFLAG:(ARG:0):好感度 -= CFLAG:(ARG:0):好感度 / 10
	CFLAG:(ARG:0):依存度 -= CFLAG:(ARG:0):依存度 / 10
ENDIF

;●主導操作の法
@ITEM_EFFECT294(ARG:0)
#DIM FIRST_LINE
#DIM 操作項目
#DIM 操作後値
FIRST_LINE = LINECOUNT
PRINTFORML 進行%ANAME(ARG:0)%的主導操作……
PRINTFORML [0] 昼主導度
PRINTFORML [1] 夜倒錯度
PRINTFORML [2] 夜主導度

INPUT

IF !INRANGE(RESULT, 0, 2)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

操作項目 = RESULT

PRINTFORML 要設為多少……

IF GROUPMATCH(操作項目, 0, 2)
	PRINTFORML (請輸入-1000 ～ 1000、範囲外数字為取消)
ELSE
	PRINTFORML (請輸入0 ～ 1000、範囲外数字為取消)
ENDIF

INPUT

IF (GROUPMATCH(操作項目, 0, 2) && !INRANGE(RESULT, -1000, 1000)) || (操作項目 == 1 && !INRANGE(RESULT, 0, 1000))
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

操作後値 = RESULT

SELECTCASE 操作項目
	CASE 0
		PRINTFORMW %ANAME(ARG:0)%的昼主導度変成了{操作後値}
		ABL:(ARG:0):主導度Ｎ = 操作後値
	CASE 1
		PRINTFORMW %ANAME(ARG:0)%的夜倒錯度変成了{操作後値}
		ABL:(ARG:0):倒錯度 = 操作後値
	CASE 2
		PRINTFORMW %ANAME(ARG:0)%的夜主導度変成了{操作後値}
		ABL:(ARG:0):主導度Ｕ = 操作後値
ENDSELECT

;●太平要術の書
@ITEM_EFFECT141(ARG:0)
TALENT:(ARG:0):妖術知識 = 1
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%掌握了<妖術知識>

;●淫技大全
@ITEM_EFFECT142(ARG:0)
ABL:(ARG:0):性知識 = 5
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%的性知識上昇到了(Lv5)


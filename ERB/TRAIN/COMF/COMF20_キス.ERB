;キス

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME20
IF MTAR_NUM >= 2 || MPLY_NUM >= 2
	LOCALS:0 = Ｗ接吻
ELSE
	LOCALS:0 = 接吻
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE20
;共通部分
CALL COM_ABLE_COMMON(20)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;2-2は不可
SIF MPLY_NUM == 2 && MTAR_NUM == 2
	RETURN 0
FOR LOCAL:0, 0, MPLY_NUM
	SIF IS_M_HOLD(MPLY:(LOCAL:0))
		RETURN 0
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	SIF IS_M_HOLD(MTAR:(LOCAL:0))
		RETURN 0
NEXT
FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
        ;unconscious
		SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
			RETURN 0
        ;both bound
		SIF (IS_BIND(MPLY:(LOCAL:0)) && IS_BIND(MTAR:(LOCAL:0)))
			RETURN 0
        ;giving/receiving tribadism, doubleidlo, trampling, footlicking, footjob)
		SIF SEARCH_EQUIP_IC_M(MPLY:(LOCAL:0), MTAR:(LOCAL:0), 21, 22, 103, 104, 14) >= 0
			RETURN 0
        ;one participant grovelling, but not the other
		SIF (IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110) + IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 110)) == 1
			RETURN 0
	NEXT
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM20
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 60

	EXP:(LOCAL:2):性技経験値 += MTAR_NUM / 2 + 1
	EXP:(LOCAL:2):接吻経験 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 30)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
	IF MTAR_NUM == 1
		;ペニスへのキス
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%的唇", GET_SITUATION_BY_TRAIN_MODE())
	ELSE
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%们的唇", (GET_SITUATION_BY_TRAIN_MODE() == "和姦" ? "乱交" # "輪姦"))
	ENDIF	

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1000) * LOCAL:10 / 100
	NEXT
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 60

	EXP:(LOCAL:2):性技経験値 += MPLY_NUM / 2 + 1
	EXP:(LOCAL:2):接吻経験 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 30)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	CALL KISS_COMMON(LOCAL:2, @"%ANAME(MPLY:0)%的唇", GET_SITUATION_BY_TRAIN_MODE())

	;全プレイヤーに与える快感系ソースを計算
	FOR LOCAL:1, 0, MPLY_NUM
		LOCAL:3 = MPLY:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1000) * LOCAL:10 / 100
	NEXT
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -2

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP20
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP20(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):接吻経験 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 40)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):性行動 += 30

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 2

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_PLAYER_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):接吻経験 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 40)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):性行動 += 30

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 2

	;全プレイヤーに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_PLAYER_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600) * LOCAL:10 / 100

	NEXT
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE20(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%和%EQUIP_TARGET_ANAME(ARG:0)%接吻中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP20(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%用舌头和%EQUIP_TARGET_ANAME(ARG:0)%接吻…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%と舌を絡ませてキスをしている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP20(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER20(ARG:0)
CALL COM_ORDER_COMMON20(ARG:0)
RETURN 1

@COM_ORDER_TARGET20(ARG:0)
CALL COM_ORDER_COMMON20(ARG:0)
RETURN 1

@COM_ORDER_COMMON20(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 60

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"Ｍ感Lv{ABL:(ARG:0):Ｍ感}", ABL:(ARG:0):Ｍ感 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -4)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

IF IS_MPLY(ARG:0)
	IF TALENT:(ARG:0):討厭男人
		FOR LOCAL:0, 0, MTAR_NUM
			IF IS_MALE(MTAR:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "討厭男人", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
	IF TALENT:(ARG:0):討厭女人
		FOR LOCAL:0, 0, MTAR_NUM
			IF IS_FEMALE(MTAR:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "討厭女人", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSE
	IF TALENT:(ARG:0):討厭男人
		FOR LOCAL:0, 0, MPLY_NUM
			IF IS_MALE(MPLY:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "討厭男人", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
	IF TALENT:(ARG:0):討厭女人
		FOR LOCAL:0, 0, MPLY_NUM
			IF IS_FEMALE(MPLY:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "討厭女人", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF

IF TALENT:(ARG:0):無接吻経験
	IF TALENT:(ARG:0):難以越過的底線
		CALL COM_ORDER_ELEMENT(ARG:0, "無接吻経験", -45)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "無接吻経験", -30)
	ENDIF
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF

;好感度
LOCAL:0 = 0
IF CFLAG:(ARG:0):2 < 500
	IF CFLAG:(ARG:0):2 < 100
		LOCAL:0 = -30
	ELSEIF CFLAG:(ARG:0):2 < 300
		LOCAL:0 = -10
	ELSE
		LOCAL:0 = -5
	ENDIF

	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度不足", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 80
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE20
;ターゲットが２人
IF MTAR_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%将%ANAME(MTAR:0)%、%ANAME(MTAR:1)%两人紧紧抱在一起，交替用舌头缠绕着交换着唾液%TARGET_STR("た", "てきた")%…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%、%ANAME(MTAR:1)%の二人を抱き寄せ、交互に舌を絡ませながら唾液を交換し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%和%ANAME(MTAR:1)%要求%ANAME(MPLY:0)%接吻
			;TranslatedMark %ANAME(MTAR:0)%と%ANAME(MTAR:1)%は、%ANAME(MPLY:0)%に対してキスをするように要求した
			PRINTFORMW %ANAME(MPLY:0)%按照所说的那样与%ANAME(MTAR:0)%和%ANAME(MTAR:1)%接吻，交替地一边缠绕舌头一边交换了唾液…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%と%ANAME(MTAR:1)%に口付け、交互に舌を絡ませながら唾液を交換した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MTAR:0)%、%ANAME(MTAR:1)%两个人接吻
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%、%ANAME(MTAR:1)%の二人とキスするように命令した
			PRINTFORMW %ANAME(MPLY:0)%按照所说的那样与%ANAME(MTAR:0)%和%ANAME(MTAR:1)%接吻，交替地一边缠绕舌头一边交换了唾液…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%と%ANAME(MTAR:1)%に口付け、交互に舌を絡ませながら唾液を交換した…
	ENDSELECT

;ターゲットが１人
ELSE
	;プレイヤーが２人
	IF MPLY_NUM == 2
		LOCALS:0 = %ANAME(MPLY:0)%和%ANAME(MPLY:1)%
		LOCALS:1 = 二人
	;プレイヤーが１人
	ELSE
		LOCALS:0 = %ANAME(MPLY:0)%
		LOCALS:1 = %ANAME(MPLY:0)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %LOCALS:0%舔舐遍了%ANAME(MTAR:0)%的口腔，粘糊糊的舌头相互缠绕着交换着唾液…
			;TranslatedMark %LOCALS:0%は%ANAME(MTAR:0)%の口腔を隅々まで舐め回し、ねっとりと舌を絡め合いながら唾液を交換し%TARGET_STR("た", "てきた")%…
			IF MTAR:0 != MASTER && MPLY:0 == MASTER && IS_LOVER(MTAR:0)
				PRINTFORMW %ANAME(MTAR:0)%脸上浮现出痴迷的表情，自己也积极地用舌头缠绕着，使%LOCALS:1%感到愉悦…
				;TranslatedMark %ANAME(MTAR:0)%はうっとりとした表情を浮かべながら、自らも積極的に舌を絡めて%LOCALS:1%を悦ばせた…
			ENDIF
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%要求%LOCALS:0%亲吻她
			;TranslatedMark %ANAME(MTAR:0)%は%LOCALS:0%に対してキスをするように要求した
			PRINTFORMW %LOCALS:0%按照他说的那样吻着%ANAME(MTAR:0)%，一边舔舐着口腔一边交换了唾液…
			;TranslatedMark %LOCALS:0%は言われるまま%ANAME(MTAR:0)%に口付け、口腔を舐め回しながら唾液を交換した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%和%ANAME(MTAR:0)%接吻
			;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:0%に%ANAME(MTAR:0)%とキスするように命令した
			PRINTFORMW %LOCALS:0%按照他说的那样吻着%ANAME(MTAR:0)%，一边舔舐着口腔一边交换了唾液…
			;TranslatedMark %LOCALS:0%は言われるまま%ANAME(MTAR:0)%に口付け、口腔を舐め回しながら唾液を交換した…
			IF MTAR:0 != MASTER && MPLY:0 == MASTER && IS_LOVER(MTAR:0)
				PRINTFORMW %ANAME(MTAR:0)%脸上浮现出痴迷的表情，自己也积极地用舌头缠绕着，使%LOCALS:1%感到愉悦…
				;TranslatedMark %ANAME(MTAR:0)%はうっとりとした表情を浮かべながら、自らも積極的に舌を絡めて%LOCALS:1%を悦ばせた…
			ENDIF
	ENDSELECT
	;snowballing - restricted for simplicity to the 1-1 case
	IF CUM_CUR_AMOUNT:(MPLY:0):射精部位_口
		IF ABL:(MTAR:0):精愛 >= 6
			PRINTFORMW %ANAME(MTAR:0)%疯狂地舔舐着积在%ANAME(MPLY:0)%口中的精液…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の口に溜まった精液を夢中になって舐め取っている…
		ELSEIF ABL:(MTAR:0):精愛 >= 4
			PRINTFORMW %ANAME(MTAR:0)%积极地舔舐着%ANAME(MPLY:0)%口中积存的精液…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の口に溜まった精液を積極的に舐め取っている…
		ELSE
			PRINTFORMW %ANAME(MTAR:0)%讨厌%ANAME(MPLY:0)%口中积存的精液…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の口に溜まった精液を嫌がっている…
		ENDIF
		EXP:(MPLY:0):精愛経験値 += 4
		EXP:(MTAR:0):精愛経験値 += 4
	ELSEIF CUM_CUR_AMOUNT:(MPLY:0):射精部位_口
		IF ABL:(MPLY:0):精愛 >= 6
			PRINTFORMW %ANAME(MPLY:0)%疯狂地舔舐着积在%ANAME(MTAR:0)%口中的精液…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の口に溜まった精液を夢中になって舐め取っている…
		ELSEIF ABL:(MPLY:0):精愛 >= 4
			PRINTFORMW %ANAME(MPLY:0)%积极地舔舐着%ANAME(MTAR:0)%口中积存的精液…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の口に溜まった精液を積極的に舐め取っている…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%讨厌%ANAME(MTAR:0)%口中积存的精液…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の口に溜まった精液を嫌がっている…
		ENDIF

		EXP:(MPLY:0):精愛経験値 += 4
		EXP:(MTAR:0):精愛経験値 += 4
	ENDIF
	;Each partner gets half the total cum
	LOCAL = CUM_CUR_AMOUNT:(MPLY:0):射精部位_口 + CUM_CUR_AMOUNT:(MTAR:0):射精部位_口
	CUM_CUR_AMOUNT:(MPLY:0):射精部位_口 = LOCAL/2
	CUM_CUR_AMOUNT:(MTAR:0):射精部位_口 = LOCAL/2
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN20
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_20()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_20()
RETURN POWER(2, 性的嗜好_奉仕されたい)


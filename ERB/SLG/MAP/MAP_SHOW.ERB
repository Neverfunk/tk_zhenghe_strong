@INIT_MAP_SHOW_CONFIG_BUTTON_NAME
MAP_SHOW_CONFIG_BUTTON_NAME:0 '= "[势力]"
MAP_SHOW_CONFIG_BUTTON_NAME:1 '= "[联盟]"
MAP_SHOW_CONFIG_BUTTON_NAME:2 '= "[战线]"
MAP_SHOW_CONFIG_BUTTON_NAME:3 '= "[关系]"
MAP_SHOW_CONFIG_BUTTON_NAME:4 '= "[建筑]"
MAP_SHOW_CONFIG_BUTTON_NAME:5 '= "[经济]"
MAP_SHOW_CONFIG_BUTTON_NAME:6 '= "[等级]"

@DRAW_MAP_SHOW_CONFIG_BUTTON()
PRINTFORM 选择地图显示模式：
CALL INIT_MAP_SHOW_CONFIG_BUTTON_NAME
SIF !INRANGE(MAP_SHOW_CONFIG,0,MAP_SHOW_CONFIG_BUTTON_NUM-1)
	MAP_SHOW_CONFIG = 0
FOR LOCAL:0,0,MAP_SHOW_CONFIG_BUTTON_NUM
	SIF MAP_SHOW_CONFIG == LOCAL:0
		SETCOLOR 0xee7800
	PRINTBUTTON MAP_SHOW_CONFIG_BUTTON_NAME:(LOCAL:0),(10000+LOCAL:0)
	RESETCOLOR
	PRINTFORM %TOSTR_SPACE(2)%
NEXT
PRINTFORML

@CHANGE_MAP_SHOW(ARG:0)
	MAP_SHOW_CONFIG = ARG:0 - 10000

@GET_CITY_COUNTRY(CITY_ID)
	#DIM CITY_ID
	SETCOLOR CITY_COLOR:(CITY_ID)
	RETURN
@GET_CITY_ALLY(CITY_ID)
	#DIM CITY_ID
	IF CITY_TYPE:(CITY_ID) ==1
		RESETCOLOR
		RETURN
	ENDIF
	IF !IS_COUNTRY(CITY_OWNER:CITY_ID)
		SETCOLOR カラー_選択不可
		RETURN
	ENDIF
	SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, CITY_OWNER:CITY_ID)
		CASE 0
			SETCOLOR カラー_警告
		CASE 1
			SETCOLOR カラー_停戦
		CASE 2
			SETCOLOR カラー_連合
		CASE 3
			SETCOLOR カラー_同盟
		CASE 4
			SETCOLOR カラー_永久同盟
		CASE 5
			SETCOLOR カラー_シアン
		CASE 6
			SETCOLOR カラー_青
		CASE 7
			SETCOLOR カラー_青
		CASE 8
			SETCOLOR カラー_青
		CASE 9
			SETCOLOR カラー_青
	ENDSELECT
	RETURN
@GET_CITY_DANGER(CITY_ID)
	#DIM CITY_ID
	IF CITY_TYPE:(CITY_ID) == 1
		SETCOLOR CITY_COLOR:(CITY_ID)
		RETURN
	ENDIF
	IF !IS_COUNTRY(CITY_OWNER:CITY_ID)
		RESETCOLOR
		RETURN
	ENDIF
	; IF CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, CITY_OWNER:CITY_ID) == 0
	; 	SETCOLOR カラー_選択不可
	; 	RETURN
	; ENDIF
	IF INRANGE(CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, CITY_OWNER:CITY_ID),0,3)
		SETCOLOR カラー_選択不可
		RETURN
	ENDIF
	SELECTCASE GET_CITY_RISK(CITY_ID)
			CASE 0
				SETCOLOR カラー_緑
			CASE 1
				SETCOLOR カラー_永久同盟
			CASE 2
				SETCOLOR カラー_永久同盟
			CASE 3
				SETCOLOR カラー_永久同盟
			CASE 4
				SETCOLOR カラー_黄
			CASE 5
				SETCOLOR カラー_警告
			CASE 6
				SETCOLOR カラー_警告
		ENDSELECT
	RETURN
@GET_CITY_RELATION(CITY_ID)
	#DIM CITY_ID
	#DIM MASTER_COUNTRY
	#DIM TARGET_COUNTRY
	IF CITY_TYPE:(CITY_ID) == 1
		RESETCOLOR
		RETURN
	ENDIF
	IF !IS_COUNTRY(CITY_OWNER:CITY_ID)
		SETCOLOR カラー_選択不可
		RETURN
	ENDIF
	MASTER_COUNTRY = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
	TARGET_COUNTRY = GET_COUNTRY_BOSS(CITY_OWNER:CITY_ID)
	LOCAL:10 = REL_LIKE:(TARGET_COUNTRY):(MASTER_COUNTRY)
	LOCAL:11 = REL_HATE:(TARGET_COUNTRY):(MASTER_COUNTRY)
	LOCAL:12 = (LOCAL:10 >= 800) + (LOCAL:10 >= 300)
	LOCAL:13 = (LOCAL:11 >= 800) + (LOCAL:11 >= 300)


	IF MASTER_COUNTRY == TARGET_COUNTRY
		SETCOLOR カラー_緑
	ELSEIF CFLAG:(TARGET_COUNTRY):所属 == CFLAG:MASTER:所属
		SETCOLOR カラー_緑
	ELSEIF LOCAL:12 == 2 && LOCAL:13 == 2
		SETCOLOR 0x482889
	ELSEIF LOCAL:12 == 2 && LOCAL:13 == 1
		SETCOLOR 0x482889
	ELSEIF LOCAL:12 == 2 && LOCAL:13 == 0
		SETCOLOR 0x12eca3
	ELSEIF LOCAL:12 == 1 && LOCAL:13 == 2
		SETCOLOR 0x8600c8
	ELSEIF LOCAL:12 == 1 && LOCAL:13 == 1
		SETCOLOR 0x9528cc
	ELSEIF LOCAL:12 == 1 && LOCAL:13 == 0
		SETCOLOR 0x23b1b1
	ELSEIF LOCAL:12 == 0 && LOCAL:13 == 2
		SETCOLOR 0xff0000
	ELSEIF LOCAL:12 == 0 && LOCAL:13 == 1
		SETCOLOR 0x842c2c
	ELSE
		SETCOLOR カラー_白
	ENDIF
	RETURN
@GET_CITY_DEVELOP(CITY_ID)
	#DIM CITY_ID
	#DIM EXIST_BUILDING_FLAG
	#DIM SLOT_PTR
	EXIST_BUILDING_FLAG = 0
	IF CITY_TYPE:(CITY_ID) == 1
		RESETCOLOR
		RETURN
	ENDIF
	IF CFLAG:MASTER:所属 != CITY_OWNER:CITY_ID
		SETCOLOR カラー_選択不可
		RETURN
	ENDIF
	FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
		IF !BUILDING_SLOT_AVAIL_CHECK(CFLAG:MASTER:所属,SLOT_PTR)
			EXIST_BUILDING_FLAG++
			CONTINUE
		ENDIF
		SIF CITY_BUILDING:CITY_ID:SLOT_PTR >= 1
			EXIST_BUILDING_FLAG++

	NEXT
	IF EXIST_BUILDING_FLAG == MAX_BUILDING_SLOT_NUM
		RESETCOLOR
		RETURN
	ELSEIF EXIST_BUILDING_FLAG > 0 && EXIST_BUILDING_FLAG < MAX_BUILDING_SLOT_NUM
		SETCOLOR カラー_黄
	ELSE
		SETCOLOR カラー_緑
	ENDIF
	RETURN
@GET_CITY_ECONOMY(CITY_ID)
	#DIM CITY_ID
	IF CITY_TYPE:(CITY_ID) == 1
		RESETCOLOR
		RETURN
	ENDIF
	IF CFLAG:MASTER:所属 != CITY_OWNER:CITY_ID
		SETCOLOR カラー_選択不可
		RETURN
	ENDIF
	IF CITY_ECONOMY_LIMIT:CITY_ID >= 999900
		RESETCOLOR
		RETURN
	ELSEIF CITY_ECONOMY_LIMIT:CITY_ID == CITY_ECONOMY:CITY_ID
		SETCOLOR カラー_黄
		RETURN
	ELSE
		SETCOLOR カラー_緑
		RETURN
	ENDIF

@GET_CITY_LEVEL(CITY_ID)
	#DIM CITY_ID
	IF CITY_TYPE:(CITY_ID) == 1
		RESETCOLOR
		RETURN
	ENDIF
	IF CFLAG:MASTER:所属 != CITY_OWNER:CITY_ID
		SETCOLOR カラー_選択不可
		RETURN
	ENDIF
	IF (CITY_ECONOMY:CITY_ID) / 100 >= GET_CITY_ECONOMY_REQ(CFLAG:MASTER:所属,建造物_等级_三级建筑)
		SETCOLOR カラー_赤
	ELSEIF (CITY_ECONOMY:CITY_ID) / 100 >= GET_CITY_ECONOMY_REQ(CFLAG:MASTER:所属,建造物_等级_二级建筑)
		SETCOLOR カラー_黄
	ELSE
		RESETCOLOR
	ENDIF
	RETURN
@CITY_COLOR_SET(CITY_ID)
	#DIM CITY_ID
	SELECTCASE MAP_SHOW_CONFIG
		CASE 0
			CALL GET_CITY_COUNTRY(CITY_ID)
		CASE 1
			CALL GET_CITY_ALLY(CITY_ID)
		CASE 2
			CALL GET_CITY_DANGER(CITY_ID)
		CASE 3
		 	CALL GET_CITY_RELATION(CITY_ID)
		CASE 4
		 	CALL GET_CITY_DEVELOP(CITY_ID)
		CASE 5
		 	CALL GET_CITY_ECONOMY(CITY_ID)
		CASE 6
			CALL GET_CITY_LEVEL(CITY_ID)
		CASEELSE
			CALL GET_CITY_COUNTRY(CITY_ID)
	ENDSELECT
	RETURN

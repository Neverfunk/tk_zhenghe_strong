;触手搾乳

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME207
LOCALS:0 = 触手搾乳

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE207
;共通部分
CALL COM_ABLE_COMMON(207)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0
;プレイヤーが触手召喚中でないなら不可
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;ターゲットに<母乳体質>が必要
	SIF !TALENT:(MTAR:(LOCAL:0)):母乳体質
		RETURN 0
	;nipples occupied
	SIF IS_N_HOLD(MTAR:LOCAL)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM207
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●プレイヤーについて処理
EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):妖術経験値 += 1
EXP:(MPLY:0):触手経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):嗜虐 = 50
SOURCE:(MPLY:0):逸脱 = 100
SOURCE:(MPLY:0):触手 = 30
SOURCE:(MPLY:0):性行動 = 90

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 140, 30)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):触手経験値 += 1

	LOCAL:3 = 1000 + ABL:(MPLY:0):妖術 * 8
	SOURCE:(LOCAL:2):快Ｂ = SENSE_HOUSHI(MPLY:0, LOCAL:2, LOCAL:3 * 3 / 5) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 500)
	SOURCE:(LOCAL:2):搾乳 = SENSE_HOUSHI(MPLY:0, LOCAL:2, LOCAL:3 * 3 / 5) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 500)
	SOURCE:(LOCAL:2):苦痛 = 100 
	SOURCE:(LOCAL:2):逸脱 = 500 - (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 250)
	SOURCE:(LOCAL:2):触手 = 400 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
	SOURCE:(LOCAL:2):性行動 = 180 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 90)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP207
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP207(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):性行動 += 30

;全てのターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	;ソースを退避
	CALL PUTOUT_SOURCE(LOCAL:3)

	EXP:(LOCAL:3):触手経験値 += 1

	LOCAL:4 = 800 + ABL:(LOCAL:2):妖術 * 6
	SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, LOCAL:4 * 3 / 5) + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 400)
	SOURCE:(LOCAL:3):搾乳 += SENSE_HOUSHI(LOCAL:2, LOCAL:3, LOCAL:4 * 3 / 5) + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 400)
	SOURCE:(LOCAL:3):苦痛 += 50
	SOURCE:(LOCAL:3):逸脱 += 150 - (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 75)
	SOURCE:(LOCAL:3):触手 += 200 + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 100)
	SOURCE:(LOCAL:3):性行動 += 60 + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 30)

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 2

	;退避したソースを加算
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE207(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%命令触手对%EQUIP_TARGET_ANAME(ARG:0)%触手榨乳中
;RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%に対して%EQUIP_PLAYER_ANAME(ARG:0)%の触手が搾乳中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP207(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%操纵着触手吸着%EQUIP_TARGET_ANAME(ARG:0)%的乳头，想要榨出母乳…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%の操る触手が%EQUIP_TARGET_ANAME(ARG:0)%の乳首に吸い付き母乳を搾り出そうとしている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP207(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%解除了对%EQUIP_TARGET_ANAME(ARG:0)%的触手榨乳

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER207(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"触手Lv{ABL:(ARG:0):触手}", ABL:(ARG:0):触手 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 2)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -7)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 4)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -2)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド) || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ) || ABL:(ARG:0):倒錯度 >= 800
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 32)
ELSEIF ABL:(ARG:0):倒錯度 >= 500
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 24)
ELSEIF ABL:(ARG:0):倒錯度 >= 300
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 16)
ELSEIF ABL:(ARG:0):倒錯度 >= 100
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 8)
ELSE
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
ENDIF


IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_雌牛)
	CALL COM_ORDER_ELEMENT(ARG:0, "雌牛", 10)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE207
;ターゲットが2人以上
IF MTAR_NUM >= 2
	IF MTAR_NUM >= 3
		LOCALS:1 = %ANAME(MTAR:0)%们
;%ANAME(MTAR:0)%たち
	ELSEIF MTAR_NUM == 2
		LOCALS:1 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;%ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%用稍粗的触手吸着%LOCALS:1%的胸部，用会感觉到疼痛的力道强力的吸引着榨取着母乳…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の胸に吸い付かせると、痛みを感じるほど強く吸引し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:1%命令%ANAME(MPLY:0)%用触手榨出母乳
			;TranslatedMark %LOCALS:1%は%ANAME(MPLY:0)%に、触手で母乳を吸い上げるように命令した
			PRINTFORMW %ANAME(MPLY:0)%让一个稍粗的触手吸着%LOCALS:1%的胸部，就这样使用很强的力道吮吸着乳头榨取着母乳…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の胸に吸い付かせると、言われるまま乳首を強く吸い上げた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用触手榨取%LOCALS:1%的母乳
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、触手で%LOCALS:1%の母乳を搾るように命令した
			PRINTFORMW %ANAME(MPLY:0)%让一个稍粗的触手吸着%LOCALS:1%的胸部，就这样用很强的力道不断吸吮着榨取着母乳…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の胸に吸い付かせると、言われるまま乳首を強く吸い上げた…
	ENDSELECT

;ターゲットが1人
ELSE
	IF GET_BUSTSIZE(MTAR:0) < 0
		LOCALS:0 = 扁平的胸部
		LOCALS:1 = %ANAME(MTAR:0)%
;平たい胸
	ELSEIF GET_BUSTSIZE(MTAR:0) > 0
		LOCALS:0 = 丰满的胸部
		LOCALS:1 = %ANAME(MTAR:0)%
;豊満な胸
	ELSE
		LOCALS:0 = 的胸部
		LOCALS:1 = %ANAME(MTAR:0)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%用稍粗的触手吸着%LOCALS:1%%LOCALS:0%，用会感觉到疼痛的力道强力的吸引着榨取着母乳…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の胸に吸い付かせると、痛みを感じるほど強く吸引し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:1%命令%ANAME(MPLY:0)%用触手吸吮母乳
			;TranslatedMark %LOCALS:1%は%ANAME(MPLY:0)%に、触手で母乳を吸い上げるように命令した
			PRINTFORMW %ANAME(MPLY:0)%让一个稍粗的触手吸着%LOCALS:1%%LOCALS:0%，就这样使用很强的力道吮吸着乳头榨取着母乳…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の胸に吸い付かせると、言われるまま乳首を強く吸い上げた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用触手榨取%LOCALS:1%的母乳
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、触手で%LOCALS:1%の母乳を搾るように命令した
			PRINTFORMW %ANAME(MPLY:0)%让一个稍粗的触手吸着%LOCALS:1%%LOCALS:0%，就这样用很强的力道不断吸吮着榨取着母乳…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の胸に吸い付かせると、言われるまま乳首を強く吸い上げた…
	ENDSELECT
ENDIF

IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_雌牛)
	PRINTFORML 已经变成为母牛的%ANAME(MTAR:0)%，自己将乳房主动向前挺出，陶醉在被榨取母乳的快乐中…
	;TranslatedMark 雌牛に成り下がった%ANAME(MTAR:0)%は、自ら乳房を突き出して搾乳の快楽に酔いしれている…
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST207

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN207
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_207()
RETURN POWER(2, 性的嗜好_触手で犯したい) + POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_207()
RETURN POWER(2, 性的嗜好_触手で犯されたい) + POWER(2, 性的嗜好_虐げられたい)


;浣腸

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME141
LOCALS:0 = 浣腸

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE141
;共通部分
CALL COM_ABLE_COMMON(141)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;浣腸が必要
SIF ITEM:浣腸 <= 0 && ITEM:A_浣腸 <= 0
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	LOCAL:1 = MTAR:LOCAL
;ケツ塞がってたらダメ
	SIF IS_A_HOLD(LOCAL:1)
		RETURN 0
	SIF !CAN_REACH_GROIN(MPLY:0, LOCAL:1)
		RETURN 0
	RETURN 1
NEXT
;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM141
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;主導権の所在を取得
LOCAL:11 = GET_COM_INITIATIVE()

;●プレイヤー側の処理
SOURCE:(MPLY:0):性行動 = 90

;プレイヤーに主導権
IF LOCAL:11 == 0 || LOCAL:11 == 2
	SOURCE:(MPLY:0):嗜虐 = 200
	SOURCE:(MPLY:0):逸脱 = 300
;プレイヤーに主導権がない
ELSE
	SOURCE:(MPLY:0):逸脱 = 600
	SOURCE:(MPLY:0):恐怖 = MAX(150 - 40 * ABL:(MPLY:0):欲望, 0)
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 100, 80)

FOR LOCAL, 0, MTAR_NUM
	LOCAL:1 = MTAR:LOCAL
;●ターゲット側の処理
	EXP:(LOCAL:1):排泄経験値 += 2

	LOCAL:3 = 1000
	LOCAL:4 = 600

	SELECTCASE ABL:(LOCAL:1):排泄
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:1):排泄 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:1):露出 = 1400
	SOURCE:(LOCAL:1):逸脱 = 600
	SOURCE:(LOCAL:1):中毒充足 = LOCAL:3
	SOURCE:(LOCAL:1):反感 = LOCAL:4
	SOURCE:(LOCAL:1):性行動 = 150

;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 80, 170)

;屁残量をゼロにする
	TCVAR:(LOCAL:1):55 = 0
NEXT

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 6

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER141(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"排泄Lv{ABL:(ARG:0):排泄}", ABL:(ARG:0):排泄 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):倒錯的
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯的", 8)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -8)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE141
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORML %ANAME(MPLY:0)%在%ANAME(MTAR:0, MTAR_NUM)%的肛门里注入灌肠液
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0, MTAR_NUM)%的尻穴浣腸液を注入し%TARGET_STR("た", "てきた")%
		PRINTFORMW 从%ANAME(MTAR:0, MTAR_NUM)%的尻穴里，和粗俗的声音一起吐出了茶色浑浊的液体…
		;TranslatedMark %ANAME(MTAR:0, MTAR_NUM)%の尻穴から、下品な音と共に茶色く濁った液体が吐き出された…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0, MTAR_NUM)%把屁股朝向%ANAME(MPLY:0)%，要求灌肠
		;TranslatedMark %ANAME(MTAR:0, MTAR_NUM)%は%ANAME(MPLY:0)%に尻を向けて、浣腸するように要求した
		PRINTFORMW %ANAME(MPLY:0)%注入灌肠液后，从%ANAME(MTAR:0, MTAR_NUM)%的尻穴里和粗俗的声音一起吐出了茶色浑浊的液体…
		;TranslatedMark %ANAME(MPLY:0)%が浣腸液を注入すると、%ANAME(MTAR:0, MTAR_NUM)%の尻穴から下品な音と共に茶色く濁った液体が吐き出された…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%灌肠%ANAME(MTAR:0, MTAR_NUM)%
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0, MTAR_NUM)%に浣腸するように命令した
		PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0, MTAR_NUM)%的肛门里注入灌肠液后，从%ANAME(MTAR:0, MTAR_NUM)%的尻穴里和粗俗的声音一起吐出了茶色浑浊的液体…
		;TranslatedMark %ANAME(MPLY:0)%が%ANAME(MTAR:0, MTAR_NUM)%的尻穴浣腸液を注入すると、%ANAME(MTAR:0, MTAR_NUM)%の尻穴から下品な音と共に茶色く濁った液体が吐き出された…
ENDSELECT

IF CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル
	PRINTFORMW %ANAME(MTAR:0, MTAR_NUM)%的%STR_BODY("アナル", MTAR:0)%被洗净了…
	;TranslatedMark %ANAME(MTAR:0, MTAR_NUM)%の%STR_BODY("アナル", MTAR:0)%が清められた…
	CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル = 0
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST141
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):怒主 + CUP:(LOCAL:1):哀主

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):欲情 >= 500
			PRINTFORMW %ANAME(LOCAL:1)%陶醉于排泄物的臭味，脸上浮现出妖艳的笑容
			;TranslatedMark %ANAME(LOCAL:1)%は排泄物の臭いにうっとりしながら、妖しく笑みを浮かべている
		ELSEIF LOCAL:2 < 0
			PRINTFORMW %ANAME(LOCAL:1)%红着脸，盯着排泄的样子
			;TranslatedMark %ANAME(LOCAL:1)%は顔を赤らめながら排泄の様子をじっと見つめている
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%因排泄物的臭味而表情扭曲，显露出不快感
			;TranslatedMark %ANAME(LOCAL:1)%は排泄物の臭いに顔を歪め、不快感を露わにした
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):排泄 >= 2
			PRINTFORMW 也许是因为对羞耻玩法而兴奋，%ANAME(LOCAL:1)%的脸都荡漾了起来
			;TranslatedMark %ANAME(LOCAL:1)%はあまりの恥ずかしさに興奮したのか、すっかり顔を蕩けさせている
		ELSEIF ABL:(LOCAL:1):排泄 >= 1
			PRINTFORMW %ANAME(LOCAL:1)%因过于羞耻而把脸伏在地上，但总觉得有些兴奋的样子，气喘吁吁
			;TranslatedMark %ANAME(LOCAL:1)%は羞恥のあまり顔を伏せているが、どこか興奮した様子で息を荒くしている
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%因过于羞耻而把脸朝下，眼看就要哭出来的样子
			;TranslatedMark %ANAME(LOCAL:1)%は羞恥のあまり顔を伏せ、今にも泣き出しそうな様子だ
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN141
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_141()
RETURN POWER(2, 性的嗜好_辱めたい) + POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_141()
RETURN POWER(2, 性的嗜好_辱められたい) + POWER(2, 性的嗜好_虐げられたい)


;外交関係の基本部分

;-------------------------------------------------
;「外交」の名称
;-------------------------------------------------
@SHOP_SLG_NAME41
RESULTS:0 '= "外交"

;-------------------------------------------------
;「外交」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK41
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
; SIF DAY < SLG_PP:0
; 	RETURN 0
; SIF COUNTRY_IS_CLOSED:(CFLAG:MASTER:所属)
; 	RETURN 0
RETURN 1

;-------------------------------------------------
;「外交」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY41
CALL DIPLOMACY
RETURN 1

;-------------------------------------------------
;「外交」本体
;-------------------------------------------------
@DIPLOMACY
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
IF DAY < SLG_PP:0
	PRINTFORMW 到第{SLG_PP:0}期前不能進行外交
	;TranslatedMark {SLG_PP:0}期までは外交を行うことができません
	RETURN
ENDIF

IF COUNTRY_NOTDIPLOMACY_TERM:(CFLAG:MASTER:所属)
	PRINTFORMW 現在我方勢力不能和其他勢力進行外交（還剩{COUNTRY_NOTDIPLOMACY_TERM:(CFLAG:MASTER:所属),2}ターン）
	;TranslatedMark 現在わが勢力と外交を行おうとする勢力がありません（残{COUNTRY_NOTDIPLOMACY_TERM:(CFLAG:MASTER:所属),2}ターン）
	RETURN
ENDIF

IF COUNTRY_IS_CLOSED:(CFLAG:MASTER:所属)
	PRINTFORMW 我方勢力不能和其他勢力進行外交
	RETURN
ENDIF

;国家間の隣接関係マップの作成(処理の高速化)
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
;勢力関係マップを作成
CALL TMP_CREATE_RELATION_MAP
CALL TMP_CREATE_IS_FREE_MAP
CALL TMP_CREATE_POLITICS_POWER_MAP
CALL TMP_CREATE_SUM_SOLDIER_MAP

CALL SINGLE_DRAWLINE
PRINTL 請選択需要進行外交的勢力
;TranslatedMark 外交を行う勢力を選択して下さい
PRINTFORML 現在の外交警戒値:{DIPLOMACY_HATE:(CFLAG:MASTER:所属)}
CALL SINGLE_DRAWLINE
PRINTFORM %"君主",10,LEFT%
PRINTFORM %TOSTR_SPACE(6)%
PRINTFORM %"関係",10, RIGHT%
PRINTFORM %"隣接",6, RIGHT%
PRINTFORM %"印象",6, RIGHT%
PRINTFORM %"友好", 7, RIGHT%
PRINTFORM %"敵対", 7, RIGHT%
PRINTFORM %"俘虜", 26, RIGHT%
;TranslatedMark %"囚えている", 26, RIGHT%
PRINTFORML %"被俘的角色", 26, RIGHT%
;TranslatedMark %"囚われている", 26, RIGHT%

;他勢力一覧の表示
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:所属

		LOCALS:0 =%SNAME(GET_COUNTRY_BOSS(LOCAL:0))%
		;外交禁止フラグが立っているならグレー表示
		IF COUNTRY_IS_CLOSED:(LOCAL:0)
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM [%LOCALS:0, MAX_CHARANAME_LENGTH / 2, LEFT%]
			PRINTBUTTON "[情報]", LOCAL:0 + 1000
			RESETCOLOR
		ELSE
			SETCOLOR COUNTRY_COLOR:(LOCAL:0)
			PRINTBUTTON @"[%LOCALS:0, MAX_CHARANAME_LENGTH / 2, LEFT%]", LOCAL:0
			PRINTBUTTON "[情報]", LOCAL:0 + 1000
			RESETCOLOR
		ENDIF

		CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:所属, LOCAL:0)
		PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:所属, LOCAL:0, 1), 10, RIGHT%
		RESETCOLOR
		PRINTFORM %TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:所属):LOCAL == 1 ? "〇" # "×", 6, RIGHT%
		LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
		LOCAL:5 = GET_COUNTRY_BOSS(LOCAL:0)
		PRINT
		CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:4)

;		PRINTFORM
		PRINTFORM {REL_LIKE:(LOCAL:5):(LOCAL:4), 7, RIGHT}
		PRINTFORM {REL_HATE:(LOCAL:5):(LOCAL:4), 7, RIGHT}

		;囚えている捕虜の表示
		LOCALS:1 =
		FOR LOCAL:1, 0, CHARANUM
			IF CFLAG:(LOCAL:1):所属 == LOCAL:0 && CFLAG:(LOCAL:1):捕虜先 == CFLAG:MASTER:所属
				IF LOCALS:1 == ""
					LOCALS:1 = %ANAME(LOCAL:1)%
				ELSE
					IF STRLENS(@"%LOCALS:1%、%ANAME(LOCAL:1)%") >= 24
						LOCALS:1 = %LOCALS:1%、他
						BREAK
					ELSE
						LOCALS:1 = %LOCALS:1%、%ANAME(LOCAL:1)%
					ENDIF
				ENDIF
			ENDIF
		NEXT
		SETCOLOR カラー_オレンジ
		PRINTFORM %LOCALS:1, 26, RIGHT%
		RESETCOLOR

		;囚えられている捕虜の表示
		LOCALS:1 =
		FOR LOCAL:1, 0, CHARANUM
			IF CFLAG:(LOCAL:1):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:1):捕虜先 == LOCAL:0
				IF LOCALS:1 == ""
					LOCALS:1 = %ANAME(LOCAL:1)%
				ELSE
					IF STRLENS(@"%LOCALS:1%、%ANAME(LOCAL:1)%") >= 24
						LOCALS:1 = %LOCALS:1%、他
						BREAK
					ELSE
						LOCALS:1 = %LOCALS:1%、%ANAME(LOCAL:1)%
					ENDIF
				ENDIF
			ENDIF
		NEXT
		SETCOLOR カラー_シアン
		PRINTFORM %LOCALS:1, 26, RIGHT%
		RESETCOLOR
		PRINTL
	ENDIF
NEXT
CALL SINGLE_DRAWLINE
PRINTBUTTON "98[自動送金設定]",98
PRINTL
PRINTBUTTON "99[取消]", 99
;TranslatedMark 99[戻る]
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT
IF INRANGE(RESULT, 1, MAX_COUNTRY - 1) && RESULT != CFLAG:MASTER:所属 && IS_COUNTRY(RESULT) && !COUNTRY_IS_CLOSED:RESULT
	REDRAW 1
	CALL DIPLOMACY_MAIN(RESULT)

	;外交対象の勢力が一つもなければ戻る
	LOCAL:1 = 0
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:所属
			LOCAL:1 ++
		ENDIF
	NEXT
	IF LOCAL:1 >= 1
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ELSEIF INRANGE(RESULT, 1001, 1000 + MAX_COUNTRY - 1) && IS_COUNTRY(RESULT - 1000)
	CALL SHOW_COUNTRY_INFO(RESULT - 1000)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF RESULT == 98
	CALL AUTO_SENDMONEY_SETTING
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF RESULT != 99
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

REDRAW 1

;-------------------------------------------------
;ARG:0勢力との外交メニュー
;-------------------------------------------------
@DIPLOMACY_MAIN(ARG:0)
#DIMS 選択項目
#DIM REST_COUNTRY
#DIM 停戦可能数
#DIM 同盟可能数
#DIM 連邦ナンバー
#DIM 連邦構成a
#DIM 連邦構成b
VARSET 選択項目
;降伏勧告で国が無くなったときの対策
IF !IS_COUNTRY(ARG:0)
	RETURN
ENDIF



DIPLOMACY_DIPLOMAT = ARG:0


;残りの勢力数を計算
REST_COUNTRY = (MAX_COUNTRY - 1) - MATCH(COUNTRY_BOSS, 0)

;停戦・同盟可能数の設定
停戦可能数 = MAX(REST_COUNTRY / 3 ,1)
同盟可能数 = MAX(REST_COUNTRY / 3 ,1)

;研究による停戦・同盟数の増加判定
SIF TECHNOLOGY_STATUS:(CFLAG:MASTER:所属):TECHNOLOGY_DIPLOMACY >= 5
	停戦可能数 ++
SIF TECHNOLOGY_STATUS:(CFLAG:MASTER:所属):TECHNOLOGY_DIPLOMACY >= 6
	同盟可能数 ++

;自国の君主のキャラ番号を取得
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

;相手勢力の君主のキャラ番号を取得
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)
LOCAL:8 = MAX(LOCAL:6, LOCAL:7) * 10 / MAX(MIN(LOCAL:6, LOCAL:7), 1)

CALL SINGLE_DRAWLINE
PRINTFORM 現在的関係:
;TranslatedMark 現在の関係:
CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:所属, ARG:0)
PRINTFORML %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:所属, ARG:0, 1)%
RESETCOLOR

PRINTFORM 現在的印象:
;TranslatedMark 現在の印象:
CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:4)
PRINTL

LOCAL:11 = MAX(STRLENS(NAME:(LOCAL:5)), STRLENS(NAME:(LOCAL:4)))
LOCAL:12 = MAX(GET_DIGIT(LOCAL:6 / 100), GET_DIGIT(LOCAL:7 / 100))

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(LOCAL:4), LOCAL:11%的経済力:{LOCAL:6 / 100, LOCAL:12}
;TranslatedMark %ANAME(LOCAL:4), LOCAL:11%の経済力:{LOCAL:6 / 100, LOCAL:12}
PRINTFORML %ANAME(LOCAL:5), LOCAL:11%的経済力:{LOCAL:7 / 100, LOCAL:12}
;TranslatedMark %ANAME(LOCAL:5), LOCAL:11%の経済力:{LOCAL:7 / 100, LOCAL:12}
PRINTFORM %ANAME(LOCAL:4)% %TOSTR_COMPARE(LOCAL:6, LOCAL:7)% %ANAME(LOCAL:5)%  %DECIMAL_STRING(LOCAL:8, 1)%倍

IF LOCAL:8 < 15
	SETCOLOR 0xFF40FF
	PRINTL 対等
ELSEIF LOCAL:6 > LOCAL:7
	IF LOCAL:8 >= 60
		SETCOLOR 0x40FFFF
		PRINTL 圧倒
	ELSEIF LOCAL:8 >= 30
		SETCOLOR 0x40A0FF
		PRINTL 有利
	ELSE
		SETCOLOR 0x4040FF
		PRINTL 稍微有利
		;TranslatedMark やや有利
	ENDIF
ELSE
	IF LOCAL:8 >= 60
		SETCOLOR 0xA02020
		PRINTL 圧倒的不利
	ELSEIF LOCAL:8 >= 30
		SETCOLOR 0xFF4040
		PRINTL 不利
	ELSE
		SETCOLOR 0xFF4080
		PRINTL 稍微不利
		;TranslatedMark やや不利
	ENDIF
ENDIF

RESETCOLOR

LOCAL:13 = TMP_SUM_POLITICS_POWER:(CFLAG:MASTER:所属)
LOCAL:14 = TMP_SUM_POLITICS_POWER:(ARG:0)
LOCAL:15 = MAX(LOCAL:13, LOCAL:14) * 10 / MAX(MIN(LOCAL:13, LOCAL:14), 1)
LOCAL:16 = MAX(GET_DIGIT(LOCAL:13), GET_DIGIT(LOCAL:14))

CALL SINGLE_DRAWLINE

PRINTFORML %ANAME(LOCAL:4), LOCAL:11%の政治力:{LOCAL:13, LOCAL:16}
PRINTFORML %ANAME(LOCAL:5), LOCAL:11%の政治力:{LOCAL:14, LOCAL:16}
PRINTFORM %ANAME(LOCAL:4)% %TOSTR_COMPARE(LOCAL:13, LOCAL:14)% %ANAME(LOCAL:5)%  %DECIMAL_STRING(LOCAL:15, 1)%倍

IF LOCAL:15 < 15
	SETCOLOR 0xFF40FF
	PRINTL 対等
ELSEIF LOCAL:13 > LOCAL:14
	IF LOCAL:15 >= 60
		SETCOLOR 0x40FFFF
		PRINTL 圧倒
	ELSEIF LOCAL:15 >= 30
		SETCOLOR 0x40A0FF
		PRINTL 有利
	ELSE
		SETCOLOR 0x4040FF
		PRINTL 稍微有利
		;TranslatedMark やや有利
	ENDIF
ELSE
	IF LOCAL:15 >= 60
		SETCOLOR 0xA02020
		PRINTL 圧倒的不利
	ELSEIF LOCAL:15 >= 30
		SETCOLOR 0xFF4040
		PRINTL 不利
	ELSE
		SETCOLOR 0xFF4080
		PRINTL 稍微不利
		;TranslatedMark やや不利
	ENDIF
ENDIF

RESETCOLOR


LOCAL:17 = TMP_SUM_SOLDIER:(CFLAG:MASTER:所属)
LOCAL:18 = TMP_SUM_SOLDIER:(ARG:0)
LOCAL:19 = MAX(LOCAL:17, LOCAL:18) * 10 / MAX(MIN(LOCAL:17, LOCAL:18), 1)
LOCAL:20 = MAX(GET_DIGIT(LOCAL:17), GET_DIGIT(LOCAL:18))

CALL SINGLE_DRAWLINE

PRINTFORML   %ANAME(LOCAL:4), LOCAL:11%の兵力:{LOCAL:17, LOCAL:16}
PRINTFORML   %ANAME(LOCAL:5), LOCAL:11%の兵力:{LOCAL:18, LOCAL:16}
PRINTFORM %ANAME(LOCAL:4)% %TOSTR_COMPARE(LOCAL:17, LOCAL:18)% %ANAME(LOCAL:5)%  %DECIMAL_STRING(LOCAL:19, 1)%倍

IF LOCAL:19 < 15
	SETCOLOR 0xFF40FF
	PRINTL 対等
ELSEIF LOCAL:17 > LOCAL:18
	IF LOCAL:19 >= 60
		SETCOLOR 0x40FFFF
		PRINTL 圧倒
	ELSEIF LOCAL:19 >= 30
		SETCOLOR 0x40A0FF
		PRINTL 有利
	ELSE
		SETCOLOR 0x4040FF
		PRINTL 稍微有利
		;TranslatedMark やや有利
	ENDIF
ELSE
	IF LOCAL:19 >= 60
		SETCOLOR 0xA02020
		PRINTL 圧倒的不利
	ELSEIF LOCAL:19 >= 30
		SETCOLOR 0xFF4040
		PRINTL 不利
	ELSE
		SETCOLOR 0xFF4080
		PRINTL 稍微不利
		;TranslatedMark やや不利
	ENDIF
ENDIF

RESETCOLOR

CALL SINGLE_DRAWLINE

PRINT 条約締結可能数
PRINTFORM  停戦:{COUNTRY_TREATY_NO:(CFLAG:MASTER:所属):1, 2}/{停戦可能数},
PRINTFORML 同盟:{COUNTRY_TREATY_NO:(CFLAG:MASTER:所属):0, 2}/{同盟可能数}

CALL SINGLE_DRAWLINE

LOCALS:0 = 提出停戦
;TranslatedMark 停戦を申し込む
LOCALS:1 = 提出同盟
;TranslatedMark 同盟を申し込む
LOCALS:2 = 提出永久同盟
;TranslatedMark 永久同盟を申し込む
LOCALS:3 = 提出外交合併
;TranslatedMark 外交併合を申し込む
LOCALS:4 = 破棄同盟(停戦)協定
;TranslatedMark 同盟(停戦)破棄
LOCALS:5 = 流傳醜聞
;TranslatedMark 醜聞を流す
LOCALS:6 = 内部工作
LOCALS:7 = 寻求保护
;TranslatedMark 保護を求める
LOCALS:10 = 会談
;TranslatedMark 会談する
LOCALS:11 = 転讓士兵
;TranslatedMark 兵を譲り渡す
LOCALS:12 = 転讓経済力
;TranslatedMark 経済力を譲り渡す
LOCALS:13 = 士官移籍
;TranslatedMark 士官を移籍させる
LOCALS:14 = 割譲領土
;TranslatedMark 領土を割譲する
LOCALS:20 = 進行勸降
;TranslatedMark 降伏勧告を行う
LOCALS:21 = 要求提供士兵
;TranslatedMark 兵を要求する
LOCALS:22 = 要求提供経済力
;TranslatedMark 経済力を要求する
LOCALS:23 = 要求提供人質
;TranslatedMark 人質を要求する
LOCALS:24 = 要求釋放人質
;TranslatedMark 人質解放を要求する
LOCALS:25 = 調教
;TranslatedMark 調教する
LOCALS:26 = 調教部下
;TranslatedMark 部下を調教する
LOCALS:27 = 进行従属勧告
;TranslatedMark 従属勧告を行う
LOCALS:30 = 捕虜返還交渉
;(従属国パッチ)従属勧告を追加
;(従属国パッチ)保護を求めるを追加

VARSET LOCAL, 0, 100, 130
;(従属国パッチ)(TMP_COUNTRY_RELATION:宗主国=6 辺境伯=7 植民地=8 傀儡国=9 COUNTRY_Protectorate:辺境伯=2 植民地=3 傀儡国=4)
;(従属国パッチ)辺境伯か植民地,傀儡国であるときは停戦・同盟・永久同盟・外交併合・同盟破棄・降伏勧告・従属勧告ができない
IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:所属):(ARG:0) >= 1 || COUNTRY_TREATY_NO:(CFLAG:MASTER:所属):1 >= 停戦可能数 || COUNTRY_Protectorate:(CFLAG:MASTER:所属) >= 2
	LOCAL:100 = 1
ENDIF
IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:所属):(ARG:0) >= 3 || COUNTRY_TREATY_NO:(CFLAG:MASTER:所属):0 >= 同盟可能数 || COUNTRY_Protectorate:(CFLAG:MASTER:所属) >= 2
	LOCAL:101 = 1
ENDIF
IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:所属):(ARG:0) >= 4 || COUNTRY_Protectorate:(CFLAG:MASTER:所属) >= 2
	LOCAL:102 = 1
ENDIF
IF COUNTRY_Protectorate:(CFLAG:MASTER:所属) >= 2
	LOCAL:103 = 1
	LOCAL:104 = 1
	LOCAL:120 = 1
	LOCAL:127 = 1
ENDIF
;(従属国パッチ)連邦を形成していたり宗主国,辺境伯,植民地,傀儡国であるときは「保護を求める」ができない
;(従属国パッチ)相手国が従属国であってもならない
SIF COUNTRY_Protectorate:(CFLAG:MASTER:所属) >= 1 || IS_TREATY_UNI:(CFLAG:MASTER:所属) >= 1 || COUNTRY_Protectorate:(ARG:0) >= 2
	LOCAL:107 = 1

;(従属国パッチ)宗主国は従属国・永久同盟国に対して同盟破棄・降伏勧告・従属勧告できない
IF IS_TREATY_UNI:(CFLAG:MASTER:所属) == 1
	FOR 連邦ナンバー, 0, MAX_COUNTRY
		FOR 連邦構成a, 0, MAX_COUNTRY
			IF TREATY_UNI_COUNTRY:連邦ナンバー:連邦構成a:1 == CFLAG:MASTER:所属
				FOR 連邦構成b, 0, MAX_COUNTRY
					IF TREATY_UNI_COUNTRY:連邦ナンバー:連邦構成b:0 == ARG:0
						LOCAL:104 = 1
						LOCAL:120 = 1
						LOCAL:127 = 1
						BREAK
					ENDIF
				NEXT
			ELSEIF TREATY_UNI_COUNTRY:連邦ナンバー:連邦構成a:0 == CFLAG:MASTER:所属
				FOR 連邦構成b, 0, MAX_COUNTRY
					IF TREATY_UNI_COUNTRY:連邦ナンバー:連邦構成b:1 == ARG:0
						LOCAL:104 = 1
						LOCAL:120 = 1
						LOCAL:127 = 1
						BREAK
					ENDIF
				NEXT
			ELSEIF TREATY_UNI_COUNTRY:連邦ナンバー:連邦構成a:0 == CFLAG:MASTER:所属
				FOR 連邦構成b, 0, MAX_COUNTRY
					IF TREATY_UNI_COUNTRY:連邦ナンバー:連邦構成b:0 == ARG:0
						LOCAL:104 = 1
						LOCAL:120 = 1
						LOCAL:127 = 1
						BREAK
					ENDIF
				NEXT
			ENDIF
		NEXT
	NEXT
ENDIF

;(従属国パッチ)従属国に対しては停戦・同盟・永久同盟・外交併合・同盟破棄・従属勧告ができない
IF COUNTRY_Protectorate:(ARG:0) >= 2
	LOCAL:100 = 1
	LOCAL:101 = 1
	LOCAL:102 = 1
	LOCAL:103 = 1
	LOCAL:104 = 1
	LOCAL:127 = 1
ENDIF

LOCAL:124 = 1
LOCAL:130 = 1
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
		LOCAL:124 = 0
		LOCAL:130 = 0
		BREAK
	ENDIF
NEXT
IF LOCAL:130
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			LOCAL:130 = 0
		ENDIF
	NEXT
ENDIF

IF CFLAG:(LOCAL:5):外交調教中
	LOCAL:125 = 1
ENDIF

IF DONE_DIPLOMACY_SCANDAL:(ARG:0)
	LOCAL:105  = 1
ENDIF

LOCAL:1 = 1
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:所属 == ARG:0 && LOCAL != GET_COUNTRY_BOSS(ARG:0) && (IS_SLAVE(LOCAL) || GETBIT(TALENT:LOCAL:淫乱系, 素質_淫乱_傀儡) || GETBIT(TALENT:LOCAL:淫乱系, 素質_淫乱_薬物中毒)) && CFLAG:LOCAL:捕虜先 == 0
		LOCAL:1 = 0
		BREAK
	ENDIF
NEXT

LOCAL:106 = LOCAL:1

IF DONE_DIPLOMACY_SABOTAGE:(ARG:0) || MONEY:(CFLAG:MASTER:所属) < 1000
	LOCAL:106 = 1
ENDIF

IF DONE_DIPLOMACY_FRIENDSHIP:(ARG:0)
	VARSET LOCAL, 1, 110, 120
ENDIF

IF DONE_DIPLOMACY_REQUEST:(ARG:0)
	VARSET LOCAL, 1, 120, 130
ENDIF

PRINT ◇締結関係◇
;TranslatedMark ◇関係を結ぶ◇
LOCAL:1 = 0
FOR LOCAL:0, 0, 10
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL
		ENDIF
		LOCAL:1 ++
		PRINT
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
CALL PRINT_DIPLO_IMAGE(LOCAL:5, 800, GetDiplomacyMood(LOCAL:5, LOCAL:4), 200)
PRINTL
PRINTL

PRINT ◇加深友好◇
;TranslatedMark ◇友好を深める◇
LOCAL:1 = 0
FOR LOCAL:0, 10, 20
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL
		ENDIF
		LOCAL:1 ++
		PRINT
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL
PRINTL

PRINT ◇提出要求◇
;TranslatedMark ◇要求を行う◇
LOCAL:1 = 0
FOR LOCAL:0, 20, 30
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL
		ENDIF
		LOCAL:1 ++
		PRINT
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL
PRINTL

PRINT ◇進行交渉◇
;TranslatedMark ◇交渉する◇
LOCAL:1 = 0
FOR LOCAL:0, 30, 40
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL
		ENDIF
		LOCAL:1 ++
		PRINT
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 18%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL
CALL SINGLE_DRAWLINE
PRINT
PRINTBUTTON "99[              取消]", 99
;TranslatedMark 99[              戻る]
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 99
	REDRAW 1
	RETURN 0

ELSEIF RESULT >= 0 && RESULT < 40 && !LOCAL:(RESULT + 100)
	IF RESULT >= 0 && RESULT < 10
		選択項目 = 関係
	ELSEIF RESULT >= 10 && RESULT < 20
		選択項目 = 友好
	ELSEIF RESULT >= 20 && RESULT < 30
		選択項目 = 要求
	ELSEIF RESULT >= 30 && RESULT < 40
		選択項目 = 交渉
	ENDIF
	;停戦を申し込む
	IF RESULT == 0
		CALL DIPLOMACY_CEASEFIRE(ARG:0)

	;同盟を申し込む
	ELSEIF RESULT == 1
		CALL DIPLOMACY_ALLIANCE(ARG:0)

	;永久同盟を申し込む
	ELSEIF RESULT == 2
		CALL DIPLOMACY_ALLIANCE_INDIFINITE(ARG:0)

	;外交併合を申し込む
	ELSEIF RESULT == 3
		CALL DIPLOMACY_PERSONAL_UNION(ARG:0)

	;同盟を破棄する
	ELSEIF RESULT == 4
		CALL DIPLOMACY_ALLIANCE_RENOUNCE(ARG:0)
	;醜聞を流す
	ELSEIF RESULT == 5
		CALL DIPLOMACY_CREATE_SCANDAL(ARG:0)
		DONE_DIPLOMACY_SCANDAL:(ARG:0) = RESULT

	;内部工作
	ELSEIF RESULT == 6
		CALL DIPLOMACY_SABOTAGE(ARG:0)
		DONE_DIPLOMACY_SABOTAGE:(ARG:0) = RESULT
	;保護を求める
	ELSEIF RESULT == 7
		CALL DIPLOMACY_Protectorate(ARG:0)
	;会談する
	ELSEIF RESULT == 10
		CALL DIPLOMACY_TALK(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT
	;兵を譲り渡す
	ELSEIF RESULT == 11
		CALL DIPLOMACY_GIVE_SOLDIER(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT

	;経済力を譲り渡す
	ELSEIF RESULT == 12
		CALL DIPLOMACY_GIVE_ECONOMY(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT

	;士官を移籍させる
	ELSEIF RESULT == 13
		CALL DIPLOMACY_GIVE_CHARA(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT

	;領土を割譲する
	ELSEIF RESULT == 14
		CALL DIPLOMACY_GIVE_CITY(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT

	;降伏勧告(开始)
	ELSEIF RESULT == 20
		CALL DIPLOMACY_SURRENDER(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;兵を要求する
	ELSEIF RESULT == 21
		CALL DIPLOMACY_REQUEST_SOLDIER(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;経済力を要求する
	ELSEIF RESULT == 22
		CALL DIPLOMACY_REQUEST_ECONOMY(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;人質を要求する
	ELSEIF RESULT == 23
		CALL DIPLOMACY_REQUEST_HOSTAGE(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;人質解放を要求する
	ELSEIF RESULT == 24
		CALL DIPLOMACY_REQUEST_RELEASE(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;調教する
	ELSEIF RESULT == 25
		CALL DIPLOMACY_REQUEST_TRAIN(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;部下を調教
	ELSEIF RESULT == 26
		CALL DIPLOMACY_REQUEST_TRAIN_BUKA(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;(従属国パッチ)従属勧告を行う
	ELSEIF RESULT == 27
		CALL DIPLOMACY_PUPPET(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;人質交渉
	ELSEIF RESULT == 30
		CALL DIPLOMACY_PRISONER_TRADE(ARG:0)
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
[SKIPSTART]
;DIPLOMACY_REQUEST_SUCCESS_COUNTをDIPLOMACY_HATEに統合するためスキップ処理
	IF 選択項目 == "要求" && RESULT == 1 && DIPLOMACY_REQUEST_SUCCESS_COUNT % 3 == 0 && DIPLOMACY_REQUEST_SUCCESS_COUNT
		SETCOLOR カラー_警告
		PRINTFORMW 多次向其他勢力提出要求、各国開始警戒起%ANAME(LOCAL:4)%的動作！
		;TranslatedMark 度重なる他勢力への要求により、%ANAME(LOCAL:4)%を警戒する動きが各国で出ているようです！
		PRINTFORMW %ANAME(LOCAL:4)%和其他国家的関係悪化了
		;TranslatedMark %ANAME(LOCAL:4)%と他国との関係が悪化しました
		FOR LOCAL:0, 0, MAX_COUNTRY

			SIF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:所属
				CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -1 * RAND(DIPLOMACY_REQUEST_SUCCESS_COUNT * 10, DIPLOMACY_REQUEST_SUCCESS_COUNT * 15),  RAND(DIPLOMACY_REQUEST_SUCCESS_COUNT * 10, DIPLOMACY_REQUEST_SUCCESS_COUNT * 15))
		NEXT
		RESETCOLOR
	ENDIF
[SKIPEND]
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RESTART

;-------------------------------------------------
;ARG:0勢力が、全ての勢力の中で最大の経済規模を持っているかどうかを判定
;-------------------------------------------------
@CHECK_IS_BEST_COUNTRY(ARG:0)
LOCAL:1 = -1
LOCAL:2 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:3 = GET_SUM_ECONOMY(LOCAL:0)
		IF LOCAL:3 > LOCAL:1
			LOCAL:1 = LOCAL:3
			LOCAL:2 = LOCAL:0
		ENDIF
	ENDIF
NEXT
IF LOCAL:2 == ARG:0
	RETURN 1
ENDIF
RETURN 0

;-------------------------------------------------
;ARG:0勢力が、他の敵対的で対等(=経済規模がARG:0勢力の2/3以上)な勢力と隣接しているかを判定する
;-------------------------------------------------
@CHECK_IS_STRONG_NEIBOR(ARG:0)
;勢力関係マップを作成
CALL TMP_CREATE_RELATION_MAP

;勢力間の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

LOCAL:1 = GET_SUM_ECONOMY(ARG:0) * 2 / 3
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && TMP_COUNTRY_IS_NEIBORING:(ARG:0):(LOCAL:0) && TMP_COUNTRY_RELATION:(ARG:0):(LOCAL:0) <= 1
		IF GET_SUM_ECONOMY(LOCAL:0) >= LOCAL:1
			RETURN 1
		ENDIF
	ENDIF
NEXT
RETURN 0

;-------------------------------------------------
;ARG:0勢力からARG:1勢力に、ARG:2だけ経済力を移動させる処理
;ARG:2は経済力の内部値(表示値の100倍)なので注意
;-------------------------------------------------
@SHIFT_ECONOMY(ARG:0, ARG:1, ARG:2)
LOCAL:2 = GET_SUM_ECONOMY(ARG:0)
LOCAL:3 = GET_SUM_ECONOMY(ARG:1)

;ARG:0勢力の経済規模をARG:2分だけ減らす(上限値も減少値の1/5だけ減少)
FOR LOCAL:0, 1, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == ARG:0
		LOCAL:5 = CITY_ECONOMY:(LOCAL:0) * ARG:2 / LOCAL:2
		CITY_ECONOMY:(LOCAL:0) = MAX(CITY_ECONOMY:(LOCAL:0) - LOCAL:5, 5000)
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MAX(CITY_ECONOMY_LIMIT:(LOCAL:0) - LOCAL:5 / 5, 5000)
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MIN(CITY_ECONOMY_LIMIT:(LOCAL:0), 999900)
		CITY_ECONOMY:(LOCAL:0) = MIN(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
	ENDIF
NEXT

LOCAL:5 = GET_SUM_ECONOMY(ARG:0) + ARG:2 - LOCAL:2
CALL FISHER_YATES_SHAFFLE(MAX_CITY)
FOR LOCAL:1, 0, MAX_CITY
	LOCAL:0 = SHAFFLE_ARRAY:(LOCAL:1)
	IF CITY_OWNER:(LOCAL:0) == ARG:0
		CITY_ECONOMY:(LOCAL:0) -= LOCAL:5
		IF CITY_ECONOMY:(LOCAL:0) >= 5000
			BREAK
		ENDIF
		LOCAL:5 = 5000 - CITY_ECONOMY:(LOCAL:0)
		CITY_ECONOMY:(LOCAL:0) = 5000
	ENDIF
NEXT

;ARG:1勢力の経済規模をARG:2分だけ増やす(上限値を上回ると、その分だけ上限値も上昇)
LOCAL:0 = 0
WHILE 1
	LOCAL:0 = FINDELEMENT(CITY_OWNER, ARG:1, LOCAL:0)
	SIF LOCAL:0 < 0
		BREAK
	CITY_ECONOMY:(LOCAL:0) += CITY_ECONOMY:(LOCAL:0) * ARG:2 / LOCAL:3
	;上限を超えた分の1%が上限を引き上げる
	IF CITY_ECONOMY:(LOCAL:0) > CITY_ECONOMY_LIMIT:(LOCAL:0)
		CITY_ECONOMY_LIMIT:(LOCAL:0) += (CITY_ECONOMY:(LOCAL:0) - CITY_ECONOMY_LIMIT:(LOCAL:0)) / 100
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MIN(CITY_ECONOMY_LIMIT:(LOCAL:0), 999900)
		CITY_ECONOMY:(LOCAL:0) = MIN(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
	ENDIF
	LOCAL:0 ++
WEND

LOCAL:5 = LOCAL:3 + ARG:2 - GET_SUM_ECONOMY(ARG:1)

CALL FISHER_YATES_SHAFFLE(MAX_CITY)
FOR LOCAL:1, 0, MAX_CITY
	LOCAL:0 = SHAFFLE_ARRAY:(LOCAL:1)
	IF CITY_OWNER:(LOCAL:0) == ARG:1
		CITY_ECONOMY:(LOCAL:0) += LOCAL:5
		IF CITY_ECONOMY:(LOCAL:0) <= 999900
			CITY_ECONOMY_LIMIT:(LOCAL:0) = MAX(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
			BREAK
		ENDIF
		LOCAL:5 = CITY_ECONOMY:(LOCAL:0) - 999900
		CITY_ECONOMY:(LOCAL:0) = 999900
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MAX(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
	ENDIF
NEXT

;-------------------------------------------------
;ARG:1の値分だけ勢力ARG:0との好感度を上昇させ敵対値を低下させる処理
;-------------------------------------------------
@DIPLOMACY_IMPROVE_RELATION(ARG:0, ARG:1)
#DIM 君主
#DIM 自勢力君主
#DIM 好感度
#DIM 敵対度
君主  = GET_COUNTRY_BOSS(ARG:0)
自勢力君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
好感度 = REL_LIKE:君主:自勢力君主
敵対度 = REL_HATE:君主:自勢力君主

CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:所属, ARG:0, ARG:1, -ARG:1)
CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:所属, ARG:1, -ARG:1)

SETCOLOR カラー_注意
PRINTFORML 好感度:{好感度, 4, RIGHT} → {REL_LIKE:君主:自勢力君主, 4, RIGHT}
PRINTFORML 敵対度:{敵対度, 4, RIGHT} → {REL_HATE:君主:自勢力君主, 4, RIGHT}
RESETCOLOR

;-------------------------------------------------
;(従属国パッチ)ARG:1の値分だけ勢力ARG:0との敵対値を上昇させ好感度を低下させる処理
;-------------------------------------------------
@DIPLOMACY_WORSEN_RELATION(ARG:0, ARG:1)
#DIM 君主
#DIM 自勢力君主
#DIM 好感度
#DIM 敵対度
君主  = GET_COUNTRY_BOSS(ARG:0)
自勢力君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
好感度 = REL_LIKE:君主:自勢力君主
敵対度 = REL_HATE:君主:自勢力君主

CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:所属, ARG:0, -ARG:1, ARG:1)
CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:所属, -ARG:1, ARG:1)

SETCOLOR カラー_注意
PRINTFORML 好感度:{好感度, 4, RIGHT} → {REL_LIKE:君主:自勢力君主, 4, RIGHT}
PRINTFORML 敵対度:{敵対度, 4, RIGHT} → {REL_HATE:君主:自勢力君主, 4, RIGHT}
RESETCOLOR


;-------------------------------------------------
;「仕官する」の名称
;-------------------------------------------------
@SHOP_SLG_NAME76
RESULTS:0 '= "成為士官"

;-------------------------------------------------
;「仕官する」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK76
SIF CFLAG:MASTER:所属 != 0
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF FLAG:クリアフラグ
	RETURN 0
RETURN 1

;-------------------------------------------------
;「仕官する」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY76
CALL SHOP_SERVE_COUNTRY
RETURN 1

;-------------------------------------------------
;仕官する
;-------------------------------------------------
@SHOP_SERVE_COUNTRY
LOCAL:10 = 0

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTL 請選択要成為哪個国家的士官
PRINT 一旦任職之後、
SETCOLOR カラー_注意
PRINT 5
RESETCOLOR
PRINTL 回合内、不能選択下野
CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

LOCAL:2 = ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:10))

IF LOCAL:2 < 0
	PRINTL 点撃城市、能得到那個勢力的情報
	PRINTL 
	PRINTBUTTON " 0[取消]", 0
	PRINTL 
ELSE
	PRINT 這座都市処在 
	SETCOLOR COUNTRY_COLOR:(LOCAL:10)
	PRINTFORM %NAME_FORMAL(LOCAL:2)%
	RESETCOLOR
	PRINTL  勢力的統治下
	IF COUNTRY_IS_CLOSED:(LOCAL:10)
		SETCOLOR カラー_選択不可
		PRINTFORM  X[不能成為這個勢力的士官]
		RESETCOLOR
	ELSEIF FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL:10) != -1
		SETCOLOR カラー_選択不可
		PRINTFORM  X[不能脱離剛剛加入的勢力]
		RESETCOLOR
	ELSE
		PRINTBUTTON " 1[在這個勢力做官]", 1
	ENDIF
	PRINTL 
	PRINTBUTTON " 0[取消]", 0
	PRINTL 
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 1 && !COUNTRY_IS_CLOSED:(LOCAL:10) && FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL:10) == -1
	IF LOCAL:2 >= 0
		CALL CHANGE_COUNTRY(MASTER, LOCAL:10)
		TIME = 0
		PRINTFORMW 成為了%ANAME(LOCAL:2)%勢力的士官
		PLAYER_HIRED_COUNTER  = 5
		RETURN
	ENDIF
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		LOCAL:10 = LOCAL:4
		CLEARLINE LINECOUNT - LINES_SHOP
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		LOCAL:10 = 0
		CLEARLINE LINECOUNT - LINES_SHOP
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP


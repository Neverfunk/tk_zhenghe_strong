;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K61_THINKING_RATE(対象)
#DIM 対象
RETURN 30


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K61_THINKING_DECISION(対象)
#DIM 対象

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("空"), NAME_TO_CHARA("燐"), NAME_TO_CHARA("こいし"))
	RETURN 0

SIF KDVAR:対象:さとり_考え事
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K61_THINKING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K61_THINKING(対象)
#DIM 対象

PRINTFORML 在走廊和%ANAME(対象)%擦肩而过……
;TranslatedMark 廊下で%ANAME(対象)%とすれ違った……

CALL ASK_MULTI("打招呼", "思考快乐的事情", "考虑痛苦的事情", "想下流的事")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「……是的，你好」
		;TranslatedMark 「……ええ、こんにちは」
		PRINTFORMW 没什么特别的事就擦肩而过了……
		;TranslatedMark 特になにごともなくすれ違った……
		CFLAG:対象:好感度 += 10
	CASE 1
		PRINTFORMW 「……噗噗」
		;TranslatedMark 「……ふふ」
		PRINTFORMW 「很开心哦」
		;TranslatedMark 「楽しめましたよ」
		PRINTFORMW 看来她很喜欢呢……
		;TranslatedMark 気に入ってもらえたようだ……
		CFLAG:対象:好感度 += 50
	CASE 2
		PRINTFORMW 「……为什么要那样？」
		;TranslatedMark 「……なんでそんなことを？」
		PRINTFORMW 「是因为我路过吗？」
		;TranslatedMark 「私が通りがかったからですか？」
		PRINTFORMW 「即使我能读你的心，也很难理解你」
		;TranslatedMark 「心を読めても、理解しかねますよ」
		PRINTFORMW 看来被她讨厌了呢……
		;TranslatedMark 気に入られなかったようだ……
		CFLAG:対象:好感度 -= 100
	CASE 3
		IF ABL:対象:欲望 >= ランク閾値:ランク_その他:ランク_B
			PRINTFORMW 「……嗯」
			;TranslatedMark 「……ええと」
			PRINTFORMW 「是想和我做H的事情吗？」
			;TranslatedMark 「それは、私と淫らなことをしたいということでしょうか？」
			PRINTFORMW 「这样的话，晚上也请去我的房间……♥」
			;TranslatedMark 「そういうことでしたら、夜にでも私の部屋にどうぞ……♥」
			PRINTFORMW %ANAME(対象)%露出了色气的笑容……
			;TranslatedMark %ANAME(対象)%は好色な笑みを浮かべていた……
			IF !TALENT:対象:合意
				CALL COLOR_PRINTW(@"%ANAME(対象)%の合意を得た", カラー_注意)
				TALENT:対象:合意 = 1
			ENDIF
			CFLAG:対象:好感度 += 100
		ELSEIF IS_LOVER(対象)
			PRINTFORMW 「……嗯」
			;TranslatedMark 「……ええと」
			PRINTFORMW 「……怎么能从白天开始就想这种事情呢」
			;TranslatedMark 「……昼間からそういう思考を垂れ流すのはいかがなものかと」
			PRINTFORMW 「那个，如果你想那样做的话，晚上」
			;TranslatedMark 「その、そういうことがしたいのでしたら、夜に」
			PRINTFORMW %ANAME(対象)%的脸颊涨红了……
			;TranslatedMark %ANAME(対象)%の頬は紅潮していた……
			CFLAG:対象:好感度 += 50
			CFLAG:対象:依存度 += 50
		ELSEIF IS_SLAVE(対象)
			PRINTFORMW 「……嗯」
			;TranslatedMark 「……ええと」
			PRINTFORMW 「……我明白了你想用我发泄欲望，但是从白天开始就想这种事果然还是不太…」
			;TranslatedMark 「……私を使いたいというのはわかりましたが、昼間からそういう思考を垂れ流すのはいかがなものかと」
			PRINTFORMW 「那个，如果你想那样做的话，晚上」
			;TranslatedMark 「その、そういうことがしたいのでしたら、夜に」
			PRINTFORMW %ANAME(対象)%的脸颊涨红了……
			;TranslatedMark %ANAME(対象)%の頬は紅潮していた……
			CFLAG:対象:従属度 += 50
			CFLAG:対象:依存度 += 50
		ELSE
			PRINTFORMW 「……哈哈」
			;TranslatedMark 「……はあ」
			PRINTFORMW 「大白天就开始思考这种东西。下流。鄙视你」
			;TranslatedMark 「真昼間からそんなこと。不潔ですよ。軽蔑します」
			PRINTFORMW 感受到了冷漠的视线……
			;TranslatedMark 冷ややかな視線を向けられた……
			CFLAG:対象:好感度 -= 100
			CFLAG:対象:従属度 -= 100
			CFLAG:対象:依存度 -= 100
		ENDIF
ENDSELECT
KDVAR:対象:さとり_考え事 = 1

RETURN 1

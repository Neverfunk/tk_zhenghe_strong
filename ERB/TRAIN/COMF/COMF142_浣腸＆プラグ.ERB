;浣腸＆プラグ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME142
LOCALS:0 = 浣腸＆肛塞

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE142
;共通部分
CALL COM_ABLE_COMMON(142)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは一人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0
;浣腸が必要
SIF ITEM:浣腸 <= 0 && ITEM:A_浣腸 <= 0
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0) && !FLAG:RECHECKING
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	LOCAL:1 = MTAR:LOCAL
	;ケツ塞がってたらダメ
	SIF IS_A_HOLD(LOCAL:1)
		RETURN 0
	SIF IS_EQUIP_TARGET(LOCAL:1, 142)
		RETURN 0
	SIF !CAN_REACH_GROIN(MPLY:0, LOCAL:1) && !FLAG:RECHECKING
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM142
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;主導権の所在を取得
LOCAL:11 = GET_COM_INITIATIVE()

;●プレイヤー側の処理
SOURCE:(MPLY:0):性行動 = 90

;プレイヤーに主導権
IF LOCAL:11 == 0 || LOCAL:11 == 2
	SOURCE:(MPLY:0):嗜虐 = 200
	SOURCE:(MPLY:0):逸脱 = 300
;プレイヤーに主導権がない
ELSE
	SOURCE:(MPLY:0):逸脱 = 600
	SOURCE:(MPLY:0):恐怖 = MAX(150 - 40 * ABL:(MPLY:0):欲望, 0)
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 100, 80)

;●ターゲット側の処理
FOR LOCAL, 0, MTAR_NUM
	LOCAL:1 = MTAR:LOCAL
	LOCAL:3 = 1000
	LOCAL:4 = 600

	SELECTCASE ABL:(LOCAL:1):排泄
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:1):排泄 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:1):露出 = 1400
	SOURCE:(LOCAL:1):逸脱 = 600
	SOURCE:(LOCAL:1):中毒充足 = LOCAL:3
	SOURCE:(LOCAL:1):反感 = LOCAL:4
	SOURCE:(LOCAL:1):性行動 = 150

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 80, 170)

	;屁残量をゼロにする
	TCVAR:(LOCAL:1):55 = 0
NEXT

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 6

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP142
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP142(ARG:0)
FOR LOCAL, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):LOCAL

	LOCAL:5 = 500
	LOCAL:6 = 300

	SELECTCASE ABL:(LOCAL:3):排泄
		CASE 0
			TIMES LOCAL:5, 0.00
			TIMES LOCAL:6, 1.00
		CASE 1
			TIMES LOCAL:5, 0.10
			TIMES LOCAL:6, 0.60
		CASE 2
			TIMES LOCAL:5, 0.20
			TIMES LOCAL:6, 0.25
		CASE 3
			TIMES LOCAL:5, 0.40
			TIMES LOCAL:6, 0.05
		CASE 4
			TIMES LOCAL:5, 0.70
			TIMES LOCAL:6, 0.00
		CASEELSE
			LOCAL:5 = LOCAL:5 * (100 + (ABL:(MTAR:0):排泄 - 5) * 2) / 100
			LOCAL:6 = 0
	ENDSELECT

	SOURCE:(LOCAL:3):苦痛 += 80
	SOURCE:(LOCAL:3):逸脱 += 200
	SOURCE:(LOCAL:3):中毒充足 += LOCAL:5
	SOURCE:(LOCAL:3):反感 += LOCAL:6

	IF GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_マゾ)
		SOURCE:(LOCAL:3):中毒充足 += 300
	ENDIF

;倒錯度変化
	TCVAR:(LOCAL:3):50 += 4
NEXT
;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE142(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%的尻穴肛门塞挿入中
;RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%的尻穴プラグを挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP142(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%被肛塞堵住肛门，看起来很痛苦…
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%はプラグで肛門をせき止められ、苦しそうにしている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP142(ARG:0)

LOCAL:2 = MEQUIP_TURN:(ARG:0) / 2 + 2

PRINTFORMW 从%ANAME(MEQUIP_TARGET:(ARG:0):0, MEQUIP_TARGET_NUM:(ARG:0))%的肛门拔出塞子
;TranslatedMark %ANAME(MEQUIP_TARGET:(ARG:0):0, MEQUIP_TARGET_NUM:(ARG:0))%の肛門からプラグを引き抜いた

;イベント口上の表示
CALL KOJO_EVENT(LOCAL:0, 151)

PRINTL 
FOR LOCAL, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):LOCAL
	PRINTFORML 排泄经验值+{LOCAL:2}
	;TranslatedMark %ANAME(LOCAL:1), 8%:排泄経験値+{LOCAL:2}

	IF CUM_CUR_AMOUNT:(LOCAL:1):射精部位_アナル
		PRINTFORML %ANAME(LOCAL:1)%的%STR_BODY("アナル", LOCAL:1)%被净化了
		;TranslatedMark %ANAME(LOCAL:1)%の%STR_BODY("アナル", LOCAL:1)%が清められた
		CUM_CUR_AMOUNT:(LOCAL:1):射精部位_アナル = 0
	ENDIF

	EXP:(LOCAL:1):排泄経験値 += LOCAL:2
	CALL TRAIN_AUTO_ABLUP(LOCAL:1)
NEXT
WAIT

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER142(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"排泄Lv{ABL:(ARG:0):排泄}", ABL:(ARG:0):排泄 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):倒錯的
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯的", 8)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -8)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE142
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORML %ANAME(MPLY:0)%在%ANAME(MTAR:0, MTAR_NUM)%的肛门里注入灌肠液
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0, MTAR_NUM)%的尻穴浣腸液を注入し%TARGET_STR("た", "てきた")%
		PRINTFORMW 而且%ANAME(MPLY:0)%还用肛塞塞住了%ANAME(MTAR:0, MTAR_NUM)%的尻穴…
		;TranslatedMark さらに%ANAME(MPLY:0)%は%ANAME(MTAR:0, MTAR_NUM)%の尻穴にプラグで栓をした…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0, MTAR_NUM)%把%STR_BODY("尻：外見：長", MTAR:0)%朝向%ANAME(MPLY:0)%，要求灌肠
		;TranslatedMark %ANAME(MTAR:0, MTAR_NUM)%は%ANAME(MPLY:0)%に%STR_BODY("尻：外見：長", MTAR:0)%を向けて、浣腸するように要求した
		PRINTFORMW %ANAME(MPLY:0)%按照所说的那样把灌肠液注入%ANAME(MTAR:0, MTAR_NUM)%的肛门，用肛塞把肛门塞住…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま浣腸液を%ANAME(MTAR:0, MTAR_NUM)%的尻穴注入し、肛門にプラグで栓をした…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%灌肠插上肛塞
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0, MTAR_NUM)%に浣腸してプラグを差すように命令した
		PRINTFORMW %ANAME(MPLY:0)%按照所说的那样把灌肠液注入%ANAME(MTAR:0, MTAR_NUM)%的肛门，并用肛塞把肛门塞住…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま浣腸液を%ANAME(MTAR:0, MTAR_NUM)%的尻穴注入し、肛門にプラグで栓をした…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST142
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):怒主

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):排泄 >= 2
			PRINTFORMW %ANAME(LOCAL:1)%为了寻求排泄的解放感，用谄媚的视线盯着%ANAME(MPLY:0)%
			;TranslatedMark %ANAME(LOCAL:1)%は排泄の解放感を求めて、媚びるような視線で%ANAME(MPLY:0)%を見つめている
		ELSEIF LOCAL:2 < 100
			PRINTFORMW %ANAME(LOCAL:1)%被肛塞堵住肛门，脸上露出苦闷的表情
			;TranslatedMark %ANAME(LOCAL:1)%はプラグで肛門をせき止められ、苦悶の表情を浮かべている
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%痛苦地按着肚子，怨恨地盯着%ANAME(MPLY:0)%
			;TranslatedMark %ANAME(LOCAL:1)%は苦しそうに腹を押さえながら、恨めしそうに%ANAME(MPLY:0)%を睨んでいる
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN142
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_142()
RETURN POWER(2, 性的嗜好_辱めたい) + POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_142()
RETURN POWER(2, 性的嗜好_辱められたい) + POWER(2, 性的嗜好_虐げられたい)


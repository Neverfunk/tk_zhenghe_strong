;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K51_GOOD_MORNING_RATE(対象)
#DIM 対象
RETURN 30

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K51_GOOD_MORNING_DECISION(対象)
#DIM 対象
SIF !(IS_LOVER(対象) && (TALENT:対象:正妻 || TALENT:対象:妾))
	RETURN 0

SIF KDVAR:対象:衣玖_朝の挨拶
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K51_GOOD_MORNING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K51_GOOD_MORNING(対象)
#DIM 対象

PRINTFORMW 「早上好、老爺」
PRINTFORMW ……正睡着的時候、被%ANAME(対象)%叫醒了
PRINTFORMW 「因為沒有按时起床、所以我来叫醒你了」
PRINTFORML 
CALL ASK_MULTI("起床", "把她拉進被窩")

IF RESULT == 0
	PRINTFORMW 「今天也要努力一整天喲？」
	PRINTFORMW 在%ANAME(対象)%的催促下、從床上爬了起來……
ELSEIF RESULT == 1
	PRINTFORMW 「啊……稍微、等一下」
	PRINTFORMW 「……今天決定休息麼？」
	PRINTFORMW 在床上和%ANAME(対象)%開始親熱了……
ENDIF
KDVAR:対象:衣玖_朝の挨拶 = 1

RETURN 1

;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K132_ODANGO_RATE(対象)
#DIM 対象
RETURN 50

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K132_ODANGO_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:清蘭_商品開発
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("豊姫"), NAME_TO_CHARA("サグメ"), NAME_TO_CHARA("依姫"), NAME_TO_CHARA("鈴瑚"))
	RETURN 0

;好感度500が必要
SIF CFLAG:対象:好感度 < 500
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K132_ODANGO_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K132_ODANGO(対象)
#DIM 対象

PRINTFORMW %ANAME(MASTER)%工作的時候、%ANAME(対象)%来訪了
PRINTFORMW 「吶%ANAME(MASTER)%、能過來幫忙一下嗎？」
PRINTFORMW 看來是正在開発団子屋的新商品
PRINTFORML 那麼、要怎麼辦……
PRINTL
CALL ASK_YN("奉陪", "現在很忙")

IF RESULT == 1
	PRINTFORMW 「啊ー、這樣呀？」
	PRINTFORMW 「真沒辦法呢、那我就一個人做吧」
	PRINTFORMW %ANAME(対象)%自己走向廚房了……
ELSE
	PRINTFORMW 「真的嗎！？那麼事不延遲…」
	PRINTFORMW 兩人走向廚房了…
	IF ABL:MASTER:料理 + ABL:対象:料理 < 70
		PRINTFORMW 結果到最後都沒有什麼好主意…
		CALL PRINT_ADD_EXP(MASTER, "料理経験値", RAND(1, 3), 1)
		CALL PRINT_ADD_EXP(対象, "料理経験値", RAND(1, 3), 1)
		CALL TRAIN_AUTO_ABLUP(MASTER)
		CALL TRAIN_AUTO_ABLUP(対象)
	ELSE
		PRINTFORMW 成功開発商品了！
		PRINTFORMW 団子變得流行、領民和士官們都來購買了
		PRINTFORMW 「謝謝你%ANAME(MASTER)%！多得你現在生意興隆了！」
		PRINTFORMW 從清蘭分到一部分營業額了…
		CALL COLOR_PRINTW(@"獲得了50000金", カラー_注意)
		MONEY += 50000
		CALL COLOR_PRINTW(@"%ANAME(対象)%和%ANAME(MASTER)%料理的實力提升了", カラー_注意)
		ABL:MASTER:料理 += 3
		ABL:対象:料理 += 3
	ENDIF
	KDVAR:対象:清蘭_商品開発 = 1
ENDIF

RETURN 1

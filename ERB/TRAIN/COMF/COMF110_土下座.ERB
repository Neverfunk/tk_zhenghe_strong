;足を舐める

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME110
RESULTS:0 = 土下座
RESULTS:1 = 土下座(被役)
RESULTS:2 = 土下座(使役)
RESULTS:3 = 土下座(被动)
RESULTS:4 = 土下座(使役)
RESULTS:5 = 土下座(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE110
;共通部分
CALL COM_ABLE_COMMON(110)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で5人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 5
	RETURN 0
;ターゲットは最大で5人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 5
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
		
	;Ｖ挿入中かつ体位が後背位以外なら不可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "Ｖ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MPLY:0, "Ｖ"), 2)
		RETURN 0
	;Ａ挿入中かつ体位が後背位以外なら不可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MPLY:0, "Ａ"), 2)
		RETURN 0
	;拘束されていたらダメ
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
    ;receiving nipple sucking, boobjob, buttjob
    SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 9, 12, 15)
		RETURN 0
		
    ;exisiting action requires mobility
    ;There is no good way to do this. We just manually list every action that is restricted
    SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 0, 1, 3, 4, 5, 9, 10)
		RETURN 0
    SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 12, 14, 15, 16, 18, 23)
        RETURN 0
    SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 87, 88, 90, 94)
        RETURN 0
    SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 100, 101, 102, 103, 105, 106, 116)
        RETURN 0
    ;fucking actions. Can't use INSERT_SINGLE because it includes cowgirl etc.
    SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 13, 30, 31, 34, 35, 36, 37, 38, 39, 40, 41, 44, 45)
		RETURN 0
    SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 46, 47, 48, 49, 52, 53, 55, 56, 57, 58, 160, 161)
        RETURN 0
    ;giving/receiving tribadism, doubledildo, frotting
    SIF IS_EQUIP(MPLY:(LOCAL:0), 21, 22, 23)
        RETURN 0
        
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM110
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 50

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 150)
	SOURCE:(LOCAL:2):逸脱 = 80
	SOURCE:(LOCAL:2):接触 = 30
	SOURCE:(LOCAL:2):露出 = 600
	EXP:(LOCAL:2):被虐経験値 += 1

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(MPLY:0):4 = 1

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 200)
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 40

	SOURCE:(LOCAL:2):嗜虐 = 50
	SOURCE:(LOCAL:2):逸脱 = 20
	SOURCE:(LOCAL:2):接触 = 30
	EXP:(LOCAL:2):嗜虐経験値 += 1

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 160, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP110
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP110(ARG:0)
;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 50)
	SOURCE:(LOCAL:2):逸脱 += 40
	SOURCE:(LOCAL:2):接触 += 15
	SOURCE:(LOCAL:2):露出 += 400
	EXP:(LOCAL:2):被虐経験値 += 1

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 10, 50)

	;奉仕経験値を得られるコマンドのフラグ     TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 3
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):嗜虐 += 30
	SOURCE:(LOCAL:2):逸脱 += 10
	SOURCE:(LOCAL:2):接触 += 15
	EXP:(LOCAL:2):嗜虐経験値 += 1

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 20)

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 3
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE110(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%在屈辱地下跪中
;RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が土下座中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP110(ARG:0)
LOCAL:2 = 1
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	IF !IS_INITIATIVE(MEQUIP_TARGET:(ARG:0):(LOCAL:0))
		LOCAL:2 = 0
	ENDIF
NEXT
IF LOCAL:2
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%屈辱的跪在地上…
	;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%が土下座している…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%屈辱的跪在地上…
	;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%が土下座している…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP110(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER110(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 100

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"被虐Lv{ABL:(ARG:0):被虐}", ABL:(ARG:0):被虐 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -4)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF


RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE110

;プレイヤーが2人以上
IF MPLY_NUM >= 2
	LOCALS:0 = %ANAME(MPLY:0)%们
;%ANAME(MPLY:0)%たち
	 LOCALS:2 = 并排
	;LOCALS:2 = 並んで
;プレイヤーが１人
ELSE
	LOCALS:0 = %ANAME(MPLY:0)%
	LOCALS:2 = 
ENDIF
;ターゲットが2人以上
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%们
;%ANAME(MTAR:0)%たち
;ターゲットが1人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE RAND:8
	CASE 0
		LOCALS:3 = 央求侵犯自己
;犯して下さいとねだりながら
	CASE 1
		LOCALS:3 = 恳求把自己变成奴隶
;奴隷にして下さいと懇願しながら
	CASE 2
		LOCALS:3 = 请求让自己舒服起来
;気持ちよくして下さいと言いながら
	CASE 3
		LOCALS:3 = 发出自己是奴隶的宣言
;私はあなたの奴隷ですと宣言しながら
	CASE 4
		LOCALS:3 = 央求小鸡鸡
;おチンポを下さいとねだりながら
	CASEELSE
		LOCALS:3 = 
ENDSELECT


SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:0%在%LOCALS:1%面前一边%LOCALS:3%，一边屈辱地%LOCALS:2%下跪…
		;TranslatedMark %LOCALS:0%は%LOCALS:1%の目の前で%LOCALS:2%、%LOCALS:3%土下座し%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		IF MTAR_NUM == 1 && ABL:(MTAR:0):加虐 >= 3
			PRINTFORMW %LOCALS:1%带着嗜虐的笑容命令%LOCALS:0%，强迫其一边%LOCALS:3%一边屈辱地下跪…
			;TranslatedMark %LOCALS:1%は嗜虐的な笑みを浮かべ%LOCALS:0%に命令し、%LOCALS:3%無理やり%LOCALS:2%土下座させ%PLAYER_STR("た", "てきた")%…
		ELSEIF MTAR_NUM == 1 && ABL:(MTAR:0):主導度Ｕ < 100
			PRINTFORMW %LOCALS:1%稍微有些紧张地命令%LOCALS:0%，让其一边%LOCALS:3%一边屈辱地%LOCALS:2%下跪…
			;TranslatedMark %LOCALS:1%は少し緊張した様子で%LOCALS:0%に命令し、%LOCALS:3%%LOCALS:2%土下座させた…
		ELSE
			PRINTFORMW %LOCALS:1%命令%LOCALS:0%，强行让其一边%LOCALS:3%一边屈辱地下跪…
			;TranslatedMark %LOCALS:1%は%LOCALS:0%に命令し、%LOCALS:3%無理やり%LOCALS:2%土下座させた…
		ENDIF
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%在%LOCALS:1%前%LOCALS:3%的同时屈辱地下跪
		;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%に%LOCALS:3%土下座するように命令した
		PRINTFORMW %LOCALS:0%就这样%LOCALS:2%向%LOCALS:1%屈辱地低头了…
		;TranslatedMark %LOCALS:0%は言われるまま%LOCALS:2%%LOCALS:1%に頭を下げた…
ENDSELECT

@COM_TEXT_LAST110
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)
	SIF LOCAL:1 == MASTER
		CONTINUE
	IF GETBIT(TALENT:(LOCAL:1):淫乱系, 素質_淫乱_精液便女)
		PRINTFORML 变成精液便女的%ANAME(LOCAL:1)%，在屈辱地下跪中发现了快乐…
		;TranslatedMark 精液便女に成り下がった%ANAME(LOCAL:1)%は、土下座することに快楽を見いだしている…
	ELSEIF IS_SLAVE(LOCAL:1)
		PRINTFORML 不屈不挠的%ANAME(LOCAL:1)%，发现了跪在地上的喜悦…
		;TranslatedMark 屈服しきった%ANAME(LOCAL:1)%は、土下座することに悦びを見いだしている…
	ELSEIF ABL:(LOCAL:1):被虐 >= 5
		PRINTFORML 完全被虐的%ANAME(LOCAL:1)%，在本应是屈辱地下跪中发现了快乐…
		;TranslatedMark すっかり被虐が身についた%ANAME(LOCAL:1)%は、土下座することに快楽を見いだしている…
	ELSEIF ABL:(LOCAL:1):被虐 >= 3
		PRINTFORML %ANAME(LOCAL:1)%对下跪感到羞辱，同时也渐渐记住了快乐…
		;TranslatedMark %ANAME(LOCAL:1)%は土下座に対し屈辱と、同時に快楽をも覚えつつあるようだ…
	ELSE
		PRINTFORML %ANAME(LOCAL:1)%为下跪的屈辱而咬紧牙关…
		;TranslatedMark %ANAME(LOCAL:1)%は土下座することの屈辱に歯ぎしりしている…
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN110
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_110()
RETURN POWER(2, 性的嗜好_辱めたい)

@COM_PREFERENCE_TARGET_110()
RETURN POWER(2, 性的嗜好_辱められたい)


@DIPLOMACY_BATCH_PROCESSING
    #DIM FIRST_LINE
    FIRST_LINE = LINECOUNT
	WHILE 1
        CALL SINGLE_DRAWLINE
        ALIGNMENT CENTER
        PRINTFORML
        PRINTFORML 欢迎来到外交管理中心！尊敬的%ANAME(MASTER)%
        PRINTFORML 请选择你要进行的操作
        PRINTFORML
        CALL SINGLE_DRAWLINE
		PRINTBUTTON "[批量內部工作]",1
		PRINTL
        PRINTL
		PRINTBUTTON "[批量索取兵力]",2
		PRINTL
        PRINTL
        PRINTBUTTON "[批量索取经济力]",3
		PRINTL
        PRINTL
		PRINTBUTTON "[返回]",0
		PRINTL
        ALIGNMENT LEFT
		INPUT
		IF RESULT == 0
			RETURN
        ENDIF
		IF RESULT == 1
			CALL DIPLOMACY_SABOTAGE_BATCH_PROCESSING
        ENDIF
		IF RESULT == 2
			CALL DIPLOMACY_REQUEST_SOLDIER_BATCH_PROCESSING
        ENDIF
        IF RESULT == 3
			CALL DIPLOMACY_REQUEST_ECONOMY_BATCH_PROCESSING
        ENDIF
        CLEARLINE (LINECOUNT - FIRST_LINE)
	WEND
@DIPLOMACY_SABOTAGE_BATCH_PROCESSING
    #DIM COUNTRY_ID
    FOR COUNTRY_ID,0,MAX_COUNTRY
        SIF !IS_COUNTRY(COUNTRY_ID)
            CONTINUE
        SIF IS_SP_COUNTRY(COUNTRY_ID)
            CONTINUE
        SIF COUNTRY_ID == CFLAG:MASTER:所属
            CONTINUE
        DIPLOMACY_DIPLOMAT = COUNTRY_ID
        LOCAL:1 = 1
        FOR LOCAL, 0, CHARANUM
            IF CFLAG:LOCAL:所属 == COUNTRY_ID && LOCAL != GET_COUNTRY_BOSS(COUNTRY_ID) && (IS_SLAVE(LOCAL) || GETBIT(TALENT:LOCAL:淫乱系, 素質_淫乱_傀儡) || GETBIT(TALENT:LOCAL:淫乱系, 素質_淫乱_薬物中毒)) && CFLAG:LOCAL:捕虜先 == 0
                LOCAL:1 = 0
                BREAK
            ENDIF
        NEXT
        IF LOCAL:1
            PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%势力没用可以利用的角色
            CONTINUE
        ENDIF
        IF DONE_DIPLOMACY_SABOTAGE:(COUNTRY_ID)
            PRINTFORML 本轮已经在%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%势力进行了内部工作
            CONTINUE
        ENDIF
        PRINTFORML 剩余国库资金数 {MONEY:(CFLAG:MASTER:所属)}
        IF MONEY:(CFLAG:MASTER:所属) < 1000
            PRINTFORMW 资金不足，停止内部工作
            RETURN
        ELSE
            PRINTFORML 准备在%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%势力进行内部工作
            PRINTFORML 是否继续？
            CALL ASK_YN()
            IF RESULT
                PRINTFORMW 终止内部工作
                RETURN
            ENDIF
        ENDIF
        CALL DIPLOMACY_SABOTAGE(COUNTRY_ID)
		DONE_DIPLOMACY_SABOTAGE:(COUNTRY_ID) = RESULT
    NEXT
    PRINTFORMW 批量内部工作完成

@DIPLOMACY_REQUEST_SOLDIER_BATCH_PROCESSING
    #DIM COUNTRY_ID
    #DIM SOLDIER_CNT
    #DIM ONLY_FALLEN
    VARSET SOLDIER_CNT
    PRINTFORML 是否只向已经陷落的君主进行索要？
    CALL ASK_YN("是（需要先前成功索要过）","否")
    ONLY_FALLEN = !RESULT
    FOR COUNTRY_ID,0,MAX_COUNTRY
        SIF !IS_COUNTRY(COUNTRY_ID)
            CONTINUE
        SIF IS_SP_COUNTRY(COUNTRY_ID)
            CONTINUE
        SIF COUNTRY_ID == CFLAG:MASTER:所属
            CONTINUE

        IF ONLY_FALLEN
            IF !CHECK_FALLEN(GET_COUNTRY_BOSS(COUNTRY_ID))
                PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%尚未陷落
                CONTINUE
            ENDIF
        ENDIF
        IF DONE_DIPLOMACY_REQUEST:(COUNTRY_ID)
            PRINTFORMW 本回合已经向%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%进行过外交索取了
            CONTINUE
        ELSE
            PRINTFORML 准备向%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%索取士兵
            PRINTFORML 是否继续？
            CALL ASK_YN()
            IF RESULT
                PRINTFORMW 终止索取士兵
                BREAK
            ENDIF
        ENDIF
        SOLDIER_CNT -= COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
        CALL DIPLOMACY_REQUEST_SOLDIER(COUNTRY_ID)
		DONE_DIPLOMACY_REQUEST:(COUNTRY_ID) = RESULT
        SOLDIER_CNT += COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
    NEXT
    PRINTFORMW 一共索取了{SOLDIER_CNT}士兵
    PRINTFORMW 批量索取士兵完成
@DIPLOMACY_REQUEST_ECONOMY_BATCH_PROCESSING
    #DIM COUNTRY_ID
    #DIM ECONOMY_CNT
    #DIM ONLY_FALLEN
    VARSET ECONOMY_CNT
    PRINTFORML 是否只向已经陷落的君主进行索要？
    CALL ASK_YN("是（需要先前成功索要过）","否")
    ONLY_FALLEN = !RESULT
    FOR COUNTRY_ID,0,MAX_COUNTRY
        SIF !IS_COUNTRY(COUNTRY_ID)
            CONTINUE
        SIF IS_SP_COUNTRY(COUNTRY_ID)
            CONTINUE
        SIF COUNTRY_ID == CFLAG:MASTER:所属
            CONTINUE
        IF ONLY_FALLEN
            IF !CHECK_FALLEN(GET_COUNTRY_BOSS(COUNTRY_ID))
                PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%尚未陷落
                CONTINUE
            ENDIF
        ENDIF
        IF DONE_DIPLOMACY_REQUEST:(COUNTRY_ID)
            PRINTFORMW 本回合已经向%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%进行过外交索取了
            CONTINUE
        ELSE
            PRINTFORML 准备向%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%索取经济力
            PRINTFORML 是否继续？
            CALL ASK_YN()
            IF RESULT
                PRINTFORMW 终止索取经济力
                BREAK
            ENDIF
        ENDIF
        ECONOMY_CNT -= GET_SUM_ECONOMY(CFLAG:MASTER:所属)
        CALL DIPLOMACY_REQUEST_ECONOMY(COUNTRY_ID)
		DONE_DIPLOMACY_REQUEST:(COUNTRY_ID) = RESULT
        ECONOMY_CNT += GET_SUM_ECONOMY(CFLAG:MASTER:所属)
    NEXT
    PRINTFORMW 一共索取了{ECONOMY_CNT/100}经济力
    PRINTFORMW 批量索取经济力完成

@CHECK_FALLEN(CHARA_ID)
    #FUNCTION
    #DIM CHARA_ID
    SIF (((CFLAG:(CHARA_ID):外交要求成功フラグ == 2 && (TALENT:(CHARA_ID):烙印 || TALENT:(CHARA_ID):服従 || TALENT:(CHARA_ID):隷属 ||(GETBIT(TALENT:(CHARA_ID):淫乱系, 素質_淫乱_マゾ) && TALENT:(CHARA_ID):恋慕)))) ||(CFLAG:(CHARA_ID):外交要求成功フラグ == 3 && GETBIT(TALENT:(CHARA_ID):淫乱系, 素質_淫乱_薬物中毒)))
        RETURNF 1
    RETURNF 0

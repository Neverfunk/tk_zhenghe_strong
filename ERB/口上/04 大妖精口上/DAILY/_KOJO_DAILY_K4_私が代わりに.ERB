;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_DECISION(対象)
#DIM 対象
#DIM チルノ
チルノ = NAME_TO_CHARA("チルノ")

SIF CFLAG:対象:所属 != CFLAG:チルノ:所属
	RETURN 0

SIF CFLAG:チルノ:所属 == CFLAG:チルノ:捕虜先
	RETURN 0

SIF CFLAG:対象:所属 == CFLAG:チルノ:捕虜先
	RETURN 0

SIF KDVAR:対象:大妖精_私が代わりに
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, -1, 0) && CHECK_KOJO_DAILY_HAPPEN(チルノ, -1, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER(対象)
#DIM 対象
#DIM チルノ
チルノ = NAME_TO_CHARA("チルノ")

PRINTFORMW 「啊……%ANAME(チルノ)%醬啊……！」
PRINTFORML %ANAME(対象)%顯得很狼狽。因為%ANAME(チルノ)%被抓起来了
PRINTFORML 対%PRONOUN(対象)%来説、%ANAME(チルノ)%是比親友、還要重要的存在
PRINTFORMW 出了什麼事的話……会這麼在意也是理所当然的
PRINTFORMW 「總会有辦法的、吧、我……我」
PRINTFORML 自己。
PRINTFORML 対本質上非常懦弱的%ANAME(対象)%来説、要自己戦闘是一件需要相当高的决断力的事
PRINTFORML 但是、%ANAME(チルノ)%現在陷入了危機。這種程度的勇気、無論如何也要強行打起
PRINTFORMW 「%ANAME(チルノ)%醬稍等一会……我馬上就会去救你的！」
CALL COLOR_PRINTW(@"%ANAME(対象)%已経擁有了覚悟", カラー_注意)
CALL COLOR_PRINTW(@"%ANAME(対象)%的能力上昇了", カラー_注意)

ABL:対象:武闘 = MAX(ABL:対象:武闘 + 5, 60)
ABL:対象:防衛 = MAX(ABL:対象:防衛 + 5, 60)
ABL:対象:知略 = MAX(ABL:対象:知略 + 5, 60)

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_PERSONAL, NO:対象, "強力応援")
SIF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%習得了技能＜強力応援＞", カラー_注意)

KDVAR:対象:大妖精_私が代わりに = 1

RETURN 1

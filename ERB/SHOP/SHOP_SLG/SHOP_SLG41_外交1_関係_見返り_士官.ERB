;-------------------------------------------------
;士官の要求の処理
;ARG:0は相手の勢力番号、ARG:1は要求の重さ。要求を飲むと1、断ると0を返す
;-------------------------------------------------
@DIPLOMACTY_REQUESTED_CHARA(ARG:0, ARG:1)
;自国・相手勢力の君主のキャラ番号を取得
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;軽度・中度の場合、一定能力以上の士官から選択
IF ARG:1 == 0 || ARG:1 == 1
	PRINTFORMW %ANAME(LOCAL:5)%、提出了要一個有才能的士官的要求
	IF ARG:1 == 0
		CALL DIPLOMACY_REQUESTED_CHARA_SELECT(ARG:0, 65)
	ELSE
		CALL DIPLOMACY_REQUESTED_CHARA_SELECT(ARG:0, 80)
	ENDIF

	IF RESULT
		RETURN 1
	ENDIF

;重度の場合、士官を指定
ELSE
	LOCAL:10 = -1
	LOCAL:11 = 0

	;1/4の確率で最も主人公への好感度が高いキャラを指定
	IF RAND:4 == 0
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4
				LOCAL:12 = CFLAG:(LOCAL:0):2
				IF LOCAL:12 >= LOCAL:11
					LOCAL:10 = LOCAL:0
					LOCAL:11 = LOCAL:12
				ENDIF
			ENDIF
		NEXT
	;3/4の確率で最も能力が高いキャラを指定
	ELSE
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4
				LOCAL:12 = ABL_POWER(ABL:(LOCAL:0):武闘, -1) + ABL_POWER(ABL:(LOCAL:0):防衛, -1) + ABL_POWER(ABL:(LOCAL:0):知略, -1) + ABL_POWER(ABL:(LOCAL:0):政治, -1)
				LOCAL:12 += ABL_POWER(ABL:(LOCAL:0):歌唱, -1) + ABL_POWER(ABL:(LOCAL:0):料理, -1)
				IF LOCAL:12 >= LOCAL:11
					LOCAL:10 = LOCAL:0
					LOCAL:11 = LOCAL:12
				ENDIF
			ENDIF
		NEXT
	ENDIF

	IF LOCAL:10 < 0
		PRINTW 錯誤:不存在満足条件的士官 @DIPLOMACTY_REQUESTED_CHARA 要求(重)
	ELSE
		PRINTFORMW %ANAME(LOCAL:5)%、提出了讓%ANAME(LOCAL:10)%移籍過去的要求
		CALL ASK_YN("接受", "拒绝")
		IF RESULT == 0
			PRINTFORML %ANAME(LOCAL:10)%成為了%ANAME(LOCAL:5)%勢力的士官…
			CALL CHANGE_COUNTRY(LOCAL:10, ARG:0, 1)
			RETURN 1
		ENDIF
	ENDIF
ENDIF

RETURN 0

;-------------------------------------------------
;ARG:0勢力に引き渡す士官を選択する関数 ARG:1は能力値の下限
;-------------------------------------------------
@DIPLOMACY_REQUESTED_CHARA_SELECT(ARG:0, ARG:1)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;対象となる士官の数を数える
LOCAL:6 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):捕虜先
		IF ABL:(LOCAL:0):武闘 >= ARG:1 || ABL:(LOCAL:0):防衛 >= ARG:1 || ABL:(LOCAL:0):知略 >= ARG:1 || ABL:(LOCAL:0):政治 >= ARG:1 || ABL:(LOCAL:0):歌唱 >= ARG:1 || ABL:(LOCAL:0):料理 >= ARG:1
			LOCAL:6 ++
		ENDIF
	ENDIF
NEXT

LOCAL:20 = 1

;1ページに表示する最大キャラ数の設定
LOCAL:7 = 44

;ページ数を計算
LOCAL:8 = (LOCAL:6 - 1) / LOCAL:7 + 1

$SHOW_LOOP1

CALL SINGLE_DRAWLINE
PRINTFORML ★★請選択要引渡的士官★★
PRINTFORML (要從最大能力在{ARG:1}以上的士官中選択)
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

LOCAL:10 = LOCAL:7 * (LOCAL:20 - 1)
LOCAL:11 = LOCAL:7 * LOCAL:20
LOCAL:12 = 0
LOCAL:13 = 0

FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):捕虜先
		IF ABL:(LOCAL:0):武闘 >= ARG:1 || ABL:(LOCAL:0):防衛 >= ARG:1 || ABL:(LOCAL:0):知略 >= ARG:1 || ABL:(LOCAL:0):政治 >= ARG:1 || ABL:(LOCAL:0):歌唱 >= ARG:1 || ABL:(LOCAL:0):料理 >= ARG:1
			IF LOCAL:13 >= LOCAL:10 && LOCAL:13 < LOCAL:11
				IF LOCAL:12 % 2 != 0
					PRINTPLAIN   
				ELSE
					IF LOCAL:12 >= 1
						PRINTL 
					ENDIF
					PRINTPLAIN   
				ENDIF
				CALL PRINT_PARTNER_DATA(LOCAL:0)
				LOCAL:12 ++
			ENDIF
			LOCAL:13 ++
		ENDIF
	ENDIF
NEXT
IF LOCAL:12 >= 1
	PRINTL 
ENDIF

IF LOCAL:8 >= 2
	CALL SINGLE_DRAWLINE
	PRINTPLAIN   
	IF LOCAL:20 >= 2
		PRINTBUTTON "  54[察看前一頁    ]", 54
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM   54[察看前一頁    ]
		RESETCOLOR
	ENDIF

	PRINTPLAINFORM         page{LOCAL:20, 2, RIGHT}/{LOCAL:8, 2, LEFT}     
	IF LOCAL:20 < LOCAL:8
		PRINTBUTTON "  56[察看下一頁    ]", 56
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM   56[察看下一頁    ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

CALL SINGLE_DRAWLINE
PRINTPLAIN   
PRINTBUTTON "   0[拒絶          ]", 0

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 54 && LOCAL:20 >= 2
	LOCAL:20 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56 && LOCAL:20 < LOCAL:8
	LOCAL:20 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ENDIF

LOCAL:15 = NO_TO_CHARA(RESULT - 100)

IF LOCAL:15 >= 0 && CFLAG:(LOCAL:15):所属 == CFLAG:MASTER:所属 && LOCAL:15 != MASTER && LOCAL:15 != LOCAL:4 && !CFLAG:(LOCAL:15):捕虜先
	IF ABL:(LOCAL:15):武闘 >= ARG:1 || ABL:(LOCAL:15):防衛 >= ARG:1 || ABL:(LOCAL:15):知略 >= ARG:1 || ABL:(LOCAL:15):政治 >= ARG:1 || ABL:(LOCAL:15):歌唱 >= ARG:1 || ABL:(LOCAL:15):料理 >= ARG:1
		REDRAW 1

		;対象キャラの情報を表示
		CALL SINGLE_DRAWLINE
		CALL SHOW_INFO(LOCAL:15)
		CALL SINGLE_DRAWLINE
		PRINTFORML 要讓這個角色移籍麼？

		;はい／いいえ入力処理
		CALL ASK_YN()
		IF RESULT == 0
			PRINTFORML %ANAME(LOCAL:15)%成為了%ANAME(LOCAL:5)%勢力的士官…
			CALL CHANGE_COUNTRY(LOCAL:15, ARG:0, 1)
			RETURN 1
		ENDIF
	ENDIF
	GOTO SHOW_LOOP1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP


;背中を流す

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME358
RESULTS:0 = 擦背
RESULTS:1 = 被要求擦背
RESULTS:2 = 讓対方擦背
RESULTS:3 = 被擦拭背部

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE358
;共通部分
CALL COM_ABLE_COMMON(358)
SIF RESULT == 0
	RETURN 0
;主人公以外が実行する場合、好感度が500以上必要
SIF MPLY:0 != MASTER && CFLAG:(MPLY:0):2 < 500
	RETURN 0
;温泉でのデート中限定
SIF GET_PLACENAME(TFLAG:54) != "温泉"
	RETURN 0
;怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM358
;実行できるかの判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MTAR:0, 5, 10)
ELSE
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MPLY:0, 5, 10)
ENDIF

;固定で入るソース
SOURCE:(MPLY:0):接触 = 300
SOURCE:(MTAR:0):接触 = 300
SOURCE:(MPLY:0):露出 = 40
SOURCE:(MTAR:0):露出 = 80

;親密に応じた歓楽のソース追加
SOURCE:(MPLY:0):歓楽 = -30
SOURCE:(MTAR:0):歓楽 = -30
CALL ADD_SOURCE_KANRAKU(MPLY:0, 165)
CALL ADD_SOURCE_KANRAKU(MTAR:0, 165)
SOURCE:(MPLY:0):歓楽 = MAX(SOURCE:(MPLY:0):歓楽, 0)
SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

;親密に応じた愛情のソース追加
CALL ADD_SOURCE_AIZYOU(MPLY:0, 120)
CALL ADD_SOURCE_AIZYOU(MTAR:0, 120)

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 70, 70)
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 70, 70)

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

;失敗
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):歓楽, 0.20
	TIMES SOURCE:(MPLY:0):愛情, 0.20
	TIMES SOURCE:(MTAR:0):歓楽, 0.20
	TIMES SOURCE:(MTAR:0):愛情, 0.20
	SOURCE:(MPLY:0):不満 += 500
	SOURCE:(MTAR:0):不満 += 500
	TFLAG:37 -= 5
;成功
ELSEIF TFLAG:18 == 0

;大成功
ELSE
	TIMES SOURCE:(MPLY:0):歓楽, 2.00
	TIMES SOURCE:(MPLY:0):愛情, 2.00
	TIMES SOURCE:(MTAR:0):歓楽, 2.00
	TIMES SOURCE:(MTAR:0):愛情, 2.00
	TFLAG:37 += 5
ENDIF

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER358(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 30

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)

;好感度
LOCAL:0 = CFLAG:(ARG:0):2 / 75
CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):母性
	CALL COM_ORDER_ELEMENT(ARG:0, "母性", 3)
ENDIF
IF TFLAG:54
	CALL COM_ORDER_ELEMENT(ARG:0, "约会中", 3)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE358
;プレイヤーが主人公側
IF MPLY:0 == MASTER
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORMW %ANAME(MPLY:0)%用手巾為%ANAME(MTAR:0)%擦拭著背部
	;主導権が相手側
	ELSE
		PRINTFORMW %ANAME(MTAR:0)%讓%ANAME(MPLY:0)%幫自己擦洗背部
	ENDIF

	IF TFLAG:18 == 1
		PRINTFORML %ANAME(MTAR:0)%好像很舒服…
	ELSEIF TFLAG:18 == -1
		PRINTFORML 但是、好像用的力氣太大了、感到痛的%ANAME(MTAR:0)%発出了抗議聲…
	ENDIF

;プレイヤーが相手側
ELSE
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORMW %ANAME(MTAR:0)%讓%ANAME(MPLY:0)%幫自己擦洗背部
	;主導権が相手側
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%強行讓%ANAME(MTAR:0)%背対自己、用手巾為其擦拭著背部
	ENDIF

	IF TFLAG:18 == 1
		PRINTFORML %ANAME(MPLY:0)%看起來很開心…
	ELSEIF TFLAG:18 == -1
		PRINTFORML 但是、可能是太用力了、%ANAME(MTAR:0)%被過度摩擦過的背部陣陣発痛…
	ENDIF
ENDIF

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST358
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN358
RETURN コマンド_日常

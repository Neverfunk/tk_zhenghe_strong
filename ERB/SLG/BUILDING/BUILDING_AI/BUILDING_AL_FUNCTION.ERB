@AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
	#DIM CITY_ID
	#DIM SLOT_PTR
	IF IS_SP_COUNTRY(CITY_OWNER:CITY_ID)
		SIF !CHECK_SP_BUILDING_SLOT(SLOT_PTR)
			RETURN 0
	ENDIF
	MONEY:(CITY_OWNER:CITY_ID) += GET_BUILDING_COST(CITY_OWNER:CITY_ID,BUILDING_LEVEL:(CITY_BUILDING:CITY_ID:SLOT_PTR),1)
	[IF_DEBUG]
		SIF CITY_BUILDING:CITY_ID:SLOT_PTR > 0
			PRINTFORML AI 在%CITY_NAME:CITY_ID% 槽位{SLOT_PTR+1}拆除了%GET_BUILDING_NAME(CITY_BUILDING:CITY_ID:SLOT_PTR)%
	[ENDIF]
	CITY_BUILDING:CITY_ID:SLOT_PTR = 0

@AI_BUILD_BUILDING(CITY_ID,SLOT_PTR,BUILDING_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
	#DIM BUILDING_ID
	IF CAN_BUILD_BUILDING(CITY_ID,BUILDING_ID,1) > 0
		CITY_BUILDING:CITY_ID:SLOT_PTR = BUILDING_ID
		TRYCALL SHOP_LIFE_USERSHOP68_BOSSBUILD(GET_COUNTRY_BOSS(CITY_OWNER:CITY_ID),BUILDING_ID)
        [IF_DEBUG]
            SIF CITY_BUILDING:CITY_ID:SLOT_PTR > 0
                PRINTFORML AI 在%CITY_NAME:CITY_ID% 槽位{SLOT_PTR+1}建造了%GET_BUILDING_NAME(CITY_BUILDING:CITY_ID:SLOT_PTR)%
        [ENDIF]
		TMP_BUILDING_EXIST_FLAG:(CITY_OWNER:(CITY_ID)):(CITY_BUILDING:CITY_ID:SLOT_PTR) = 1
        TMP_BUILDING_CNT:(CITY_OWNER:(CITY_ID)):(CITY_BUILDING:CITY_ID:SLOT_PTR)++
		MONEY:(CITY_OWNER:CITY_ID) -= GET_BUILDING_COST(CITY_OWNER:CITY_ID,BUILDING_LEVEL:(CITY_BUILDING:CITY_ID:SLOT_PTR),)
		RETURN 1
	ELSE
		RETURN 0
	ENDIF

@CHECK_SP_BUILDING_SLOT(SLOT_PTR)
	#FUNCTION
	#DIM SLOT_PTR
	IF SLOT_PTR == 2
		SIF DAY < GET_SP_AI_BUILDING_TIME() +20
				RETURNF 0
	ELSEIF SLOT_PTR == 1
		SIF DAY < GET_SP_AI_BUILDING_TIME() +10
				RETURNF 0
	ELSE
		SIF DAY < GET_SP_AI_BUILDING_TIME()
				RETURNF 0
	ENDIF
	RETURNF 1
@CHECK_SP_BUILDING_LEVEL(BUILDING_ID)
	#FUNCTION
	#DIM BUILDING_ID
	IF BUILDING_LEVEL:BUILDING_ID == 建造物_等级_三级建筑
		SIF DAY < GET_SP_AI_BUILDING_TIME() +20
				RETURNF 0
	ELSEIF 	BUILDING_LEVEL:BUILDING_ID == 建造物_等级_二级建筑
		SIF DAY < GET_SP_AI_BUILDING_TIME() +10
				RETURNF 0
	ELSE
		SIF DAY < GET_SP_AI_BUILDING_TIME()
				RETURNF 0
	ENDIF
	RETURNF 1
@SP_AI_BUILD_BUILDING(CITY_ID,SLOT_PTR,BUILDING_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
	#DIM BUILDING_ID
	SIF !CHECK_SP_BUILDING_LEVEL(BUILDING_ID)
		RETURN 0
	SIF !CHECK_SP_BUILDING_SLOT(SLOT_PTR)
		RETURN 0
	SIF CITY_BUILDING:CITY_ID:SLOT_PTR == BUILDING_ID
		RETURN 0
	IF CAN_BUILD_BUILDING(CITY_ID,BUILDING_ID,1) > 0
		SIF CITY_BUILDING:CITY_ID:SLOT_PTR > 0
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		CITY_BUILDING:CITY_ID:SLOT_PTR = BUILDING_ID
        [IF_DEBUG]
            SIF CITY_BUILDING:CITY_ID:SLOT_PTR > 0
                PRINTFORML AI 在%CITY_NAME:CITY_ID% 槽位{SLOT_PTR+1}建造了%GET_BUILDING_NAME(CITY_BUILDING:CITY_ID:SLOT_PTR)%
        [ENDIF]
		TMP_BUILDING_EXIST_FLAG:(CITY_OWNER:(CITY_ID)):(CITY_BUILDING:CITY_ID:SLOT_PTR) = 1
        TMP_BUILDING_CNT:(CITY_OWNER:(CITY_ID)):(CITY_BUILDING:CITY_ID:SLOT_PTR)++
		MONEY:(CITY_OWNER:CITY_ID) -= GET_BUILDING_COST(CITY_OWNER:CITY_ID,BUILDING_LEVEL:(CITY_BUILDING:CITY_ID:SLOT_PTR),)
		RETURN 1
	ELSE
		RETURN 0
	ENDIF

@GET_SP_AI_BUILDING_TIME()
	#FUNCTION
	SELECTCASE CONFIG:314
		CASE 0
			RETURNF 15
		CASE 1
			RETURNF 12
		CASE 2
			RETURNF 9
		CASE 3
			RETURNF 6
		CASE 4
			RETURNF 3
	ENDSELECT

@AL_BUILDING_TOHO_BUILDING(COUNTRY_ID)
	#DIM COUNTRY_ID
    #DIM CITY_ID
    #DIM SLOT_PTR
	#DIM BUILDING_ID
    FOR CITY_ID, 0, CITY_NUM + 1
		SIF CITY_OWNER:COUNTRY_ID != COUNTRY_ID
			CONTINUE
        FOR SLOT_PTR, 0,MAX_BUILDING_SLOT_NUM
			SIF !BUILDING_SLOT_AVAIL_CHECK(CITY_OWNER:CITY_ID,SLOT_PTR)
				RETURN
			SIF CITY_BUILDING:CITY_ID:SLOT_PTR > 0
				CONTINUE
			FOR BUILDING_ID,BUILDING_START,BUILDING_NUM
				IF BUILDING_TYPE:BUILDING_ID == 建造物_类型_幻想类
					CALL AI_BUILD_BUILDING(CITY_ID,SLOT_PTR,BUILDING_ID)
					SIF RESULT:0
						BREAK
				ENDIF
			NEXT
		NEXT
	NEXT
@CLEAR_SAFE_CITY_UNNECESSERY_BUILDING(CITY_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
	SIF TMP_CITY_RISK:(CITY_OWNER:CITY_ID):CITY_ID != 0
		RETURN
	FOR SLOT_PTR, 0,MAX_BUILDING_SLOT_NUM
		IF GROUPMATCH(CITY_BUILDING:CITY_ID:SLOT_PTR,建造物_弓櫓,建造物_守卫炮台,建造物_前线指挥所,建造物_野战基地,建造物_伤兵医院)
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		ENDIF
	NEXT

@CLEAR_DANGER_CITY_UNNECESSERY_BUILDING(CITY_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
	SIF TMP_CITY_RISK:(CITY_OWNER:CITY_ID):CITY_ID < 4
		RETURN
	FOR SLOT_PTR, 0,MAX_BUILDING_SLOT_NUM
		IF GROUPMATCH(CITY_BUILDING:CITY_ID:SLOT_PTR,建造物_独占市场,建造物_基础工业园区,建造物_地方政府)
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		ENDIF
	NEXT

@CLEAR_HIGH_ECONOMY_CIYT_BUILDING(CITY_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
	FOR SLOT_PTR, 0,MAX_BUILDING_SLOT_NUM
		IF GROUPMATCH(CITY_BUILDING:CITY_ID:SLOT_PTR,建造物_基础工业园区)
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		ENDIF
	NEXT

@CLEAR_CITY_TECH_BUILDING(CITY_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
	FOR SLOT_PTR, 0,MAX_BUILDING_SLOT_NUM
		IF GROUPMATCH(CITY_BUILDING:CITY_ID:SLOT_PTR,建造物_研究所,建造物_忍者之里,建造物_科研大楼,建造物_定向实验室)
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		ENDIF
	NEXT


@CLEAR_CITY_MASTER_BUILDING(CITY_ID)
	#DIM CITY_ID
	#DIM SLOT_PTR
    ;防止在委托行动时删除重要建筑
    SIF CITY_OWNER:CITY_ID == CFLAG:MASTER:所属
        RETURN
	FOR SLOT_PTR, 0,MAX_BUILDING_SLOT_NUM
		IF GROUPMATCH(BUILDING_TYPE:(CITY_BUILDING:CITY_ID:SLOT_PTR),建造物_类型_色情类)
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		ENDIF
        IF GROUPMATCH(CITY_BUILDING:CITY_ID:SLOT_PTR,建造物_空军基地,建造物_议会大厅,建造物_外交部)
			CALL AI_DEL_BUILDING(CITY_ID,SLOT_PTR)
		ENDIF
	NEXT
@COUNTRY_TECH_IS_FULL(COUNTRY_ID)
	#FUNCTION
	#DIM COUNTRY_ID
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_BATTLE_SKILL < 7
		RETURNF 0
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_BATTLE_PASSIVE < 6
		RETURNF 0
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_DIPLOMACY < 6
		RETURNF 0
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_CONSCRIPTION < 6
		RETURNF 0
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_TAX < 6
		RETURNF 0
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_BUILDINGS < 6
		RETURNF 0
	SIF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_EMPLOYMENT < 4
		RETURNF 0
	RETURNF 1

;-------------------------------------------------
;「デート」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME61
RESULTS:0 '= "約会"

;-------------------------------------------------
;「デート」の選択可否判定
;-------------------------------------------------
@SHOP_LIFE_CHECK61
;デート代不足
SIF MONEY < 3000
	RETURN 0
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
RETURN 1

;-------------------------------------------------
;「デート」の選択可能キャラ存在判定（誘えるキャラ）
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA61(ARG:0)
;非主人公、同一勢力、捕虜でない、動物でない、通常状態または育児中
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0 && !IS_ANIMAL(ARG:0) && GROUPMATCH(CFLAG:(ARG:0):行動不能状態, 0, 2)

;-------------------------------------------------
;「デート」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY61
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「デート」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW61
;タイトル
CALL COLUMN_RIGHT_TITLE("対象者選択", "0", "1", "1～全", "3,000～", "只允许自己的势力")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
;右カラムの標準的なキャラリストとページ移動呼び出し
CALL COLUMN_RIGHT_CHARALIST
RETURN

;-------------------------------------------------
;「デート」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP61(対象)
#DIM 対象, 1
#DIM 結果, 1
#DIM 全消費, 1
#DIM 内容, 1
#DIM 能力, 1
#DIM イベント発生, 1
#DIM 随机结果
;対象者とデートする
;お金消費,1回10000
;好感度+ 200+RAND:100
;行先は湖、森、人里etc

FLAG:能力表示モード = 0

イベント発生 = 0
LOCAL:5 = 0
CALL SINGLE_DRAWLINE
PRINTFORML 消費行動回数和金錢、与%ANAME(対象)%約会。
PRINTFORML 每次約会消費3,000金。
PRINTFORML 要邀請誰？
PRINTFORML 
PRINTFORML 残行動:{CALC_SHOP_TIME() - SHOP_TIME}
PRINTFORML 所持金:%NUM_FORMAT(MONEY)%
CALL SINGLE_DRAWLINE
PRINTBUTTON @"%"[湖]", 12, LEFT%", 1
PRINTBUTTON "[行動全消費]", 11
PRINTL   邀請去湖辺。武闘値越高給与対象的印象越好。
PRINTBUTTON @"%"[山]", 12, LEFT%", 2
PRINTBUTTON "[行動全消費]", 12
PRINTL   邀請去山裡。防衛値越高給与対象的印象越好。
PRINTBUTTON @"%"[森]", 12, LEFT%", 3
PRINTBUTTON "[行動全消費]", 13
PRINTL   邀請去森林。知略値越高給与対象的印象越好。
PRINTBUTTON @"%"[人里]", 12, LEFT%", 4
PRINTBUTTON "[行動全消費]", 14
PRINTL   邀請去村莊。政治値越高給与対象的印象越好。
PRINTBUTTON @"%"[花畑]", 12, LEFT%", 5
PRINTBUTTON "[行動全消費]", 15
PRINTL   邀請去花田。歌唱値越高給与対象的印象越好。
PRINTBUTTON @"%"[自宅]", 12, LEFT%", 6
PRINTBUTTON "[行動全消費]", 16
PRINTL   邀請去自家。料理値越高給与対象的印象越好。
CALL SINGLE_DRAWLINE
PRINTBUTTON "[0] - 取消", 0
$INPUT_LOOP
INPUT 0

内容 = RESULT
;0だったら戻す
IF 内容 == 0
	RETURN 0
ENDIF

IF GROUPMATCH(内容, 11, 12, 13, 14, 15, 16)
	IF MONEY < 3000 * (CALC_SHOP_TIME() - SHOP_TIME)
		PRINTFORMW 金錢不足。
		GOTO INPUT_LOOP
	ELSE
		全消費 = 1
		内容 -= 10
		LOCAL:5 = 1
	ENDIF
ELSEIF GROUPMATCH(内容, 1, 2, 3, 4 ,5, 6)
	IF MONEY < 3000
		PRINTFORMW 金錢不足。
		GOTO INPUT_LOOP
	ELSE
		全消費 = 0
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

;全消費の場合ここに戻ってくる
$DATE_LOOP

SHOP_TIME += 1
SHOP_WORK_COUNT += 1


;結果の計算
SELECTCASE RAND:100 * (1 + TALENT:MASTER:謎之魅力)
	CASE 0 TO 19
		随机结果 = -1
		SIF TALENT:MASTER:上天的使者
			随机结果 = 1
	CASE 20 TO 79
		随机结果 = 1
	CASEELSE
		随机结果 = 2
ENDSELECT
;------------------------------------
;イベント発生タイミングを前に変更
IF !イベント発生
	CALL KOJO_DATE_EVENT(対象, 内容)
	SIF RESULT
		イベント発生 = 1
ENDIF
;------------------------------------
SELECTCASE 内容
	CASE 1
		IF LOCAL:5 == 0
			PRINTFORML 邀請%ANAME(対象)%去了湖辺。
		ELSE
			PRINTFORML 持續的在湖辺与%ANAME(対象)%開心約会。
		ENDIF
		能力 = ABL:MASTER:武闘
	CASE 2
		IF LOCAL:5 == 0
			PRINTFORML 邀請%ANAME(対象)%去了山裡。
		ELSE
			PRINTFORML 持續的在山裡与%ANAME(対象)%開心約会。
		ENDIF
		能力 = ABL:MASTER:防衛
	CASE 3
		IF LOCAL:5 == 0
			PRINTFORML 邀請%ANAME(対象)%去了森林。
		ELSE
			PRINTFORML 持續的在森林与%ANAME(対象)%開心約会。
		ENDIF
		能力 = ABL:MASTER:知略
	CASE 4
		IF LOCAL:5 == 0
			PRINTFORML 邀請%ANAME(対象)%去了村莊。
		ELSE
			PRINTFORML 持續的在村莊与%ANAME(対象)%開心約会。
		ENDIF
		能力 = ABL:MASTER:政治
	CASE 5
		IF LOCAL:5 == 0
			PRINTFORML 邀請%ANAME(対象)%去了花田。
		ELSE
			PRINTFORML 持續的在花田与%ANAME(対象)%開心約会。
		ENDIF
		能力 = ABL:MASTER:歌唱
	CASE 6
		IF LOCAL:5 == 0
			PRINTFORML 邀請%ANAME(対象)%去了自宅。
		ELSE
			PRINTFORML 持續的在自宅与%ANAME(対象)%開心約会。
		ENDIF
		能力 = ABL:MASTER:料理
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

;口上でキャラ毎に別のセリフを言わせられるようにしたい
SELECTCASE RAND:5
	CASE 0
		PRINTFORMW %PRONOUN(対象)%玩得很開心的樣子。
	CASE 1
		PRINTFORMW 二人一起度過了快楽的時光。
	CASE 2
		PRINTFORMW 与%PRONOUN(対象)%聊得十分起勁。
	CASE 3
		PRINTFORMW 被%PRONOUN(対象)%嬉鬧着。
	CASE 4
		PRINTFORMW 愉快的時間一眨眼就過去了。
ENDSELECT

PRINTFORML
結果 = 100 + (RAND:100 * 随机结果) + (能力 * ABS(随机结果))
CFLAG:対象:好感度 += 結果
MONEY -= 3000
SIF CFLAG:対象:面識 == 0
	CFLAG:対象:面識 = 1
PRINTFORMW ……
SELECTCASE 随机结果
	CASE -1 ;失败
		PRINTFORML 莫名的平平无奇……
	CASE 1  ;普通
		PRINTFORML 真是愉快啊～
	CASE 2  ;大成功
		PRINTFORML 看起来%PRONOUN(対象)%还意犹未尽啊～
ENDSELECT
PRINTFORML %ANAME(対象)%的好感度上昇了{結果}
PRINTFORML 消費了3,000金



PRINTFORMW 
IF SHOP_TIME < CALC_SHOP_TIME()
	SIF 全消費
		GOTO DATE_LOOP
	RESTART
ENDIF

;デートイベントが発生していなければ、キスと告白を受けるかもしれない
IF !イベント発生
	;デート後、一定条件でキスされる
	IF CFLAG:対象:好感度 >= 1000 && RAND:3 == 0
		PRINTFORML 
		PRINTFORML 約会後回家的路上、突然被%ANAME(対象)%亲了一下
		PRINTFORMW 驚訝的%ANAME(MASTER)%愣住了、%PRONOUN(対象)%発出了悪作劇成功的笑声加快脚步離去了
		CALL KISS_COMMON(対象, @"%ANAME(MASTER)%的嘴唇", "約會後的和姦", 1)
		LOCAL:6 = 1
	ENDIF

	;デート後、一定条件で告白される
	IF CFLAG:対象:好感度 >= 1500 && !IS_SLAVE(対象) && !IS_SLAVED_BY(対象) && !TALENT:対象:恋人 && LOCAL:6 != 1 && RAND:2 == 0
		PRINTFORML 
		PRINTFORML 約会後回家的路上、突然被%ANAME(対象)%告白了
		PRINTFORML %PRONOUN(対象)%微微湿潤的眼睛緊緊的盯着%ANAME(MASTER)%
		PRINTFORML 怎麼辦？
		CALL ASK_YN("接受", "拒绝")
		IF RESULT == 1
			PRINTFORML 非常抱歉的拒絶了
			PRINTFORMW %PRONOUN(対象)%一瞬間露出了寂寞的表情、但是很快就当做什麼都沒有発生一樣笑着疾步離去了
		ELSE
			PRINTFORML %ANAME(MASTER)%握着%ANAME(対象)%的手、眼神直視地認真答応了
			PRINTFORML 於是%PRONOUN(対象)%露出了開心的笑容、一鼓作気的抱住了%ANAME(MASTER)%吻了上去
			PRINTFORMW 短暫的甜蜜的接吻之後感覚空気中充満了害羞的気息、緩解着害羞手牽手一起回去了
			CALL KISS_COMMON(対象, @"%ANAME(MASTER)%的嘴唇", "約會後的和姦", 1)
			CALL GET_LOVERS(対象)
			TALENT:対象:合意 = 1
			PRINTFORMW 得到了%ANAME(対象)%的[合意]
		ENDIF
	ENDIF
ENDIF

RESULT = 1

LINES_SHOP = LINECOUNT
BEGIN TURNEND

RETURN 1


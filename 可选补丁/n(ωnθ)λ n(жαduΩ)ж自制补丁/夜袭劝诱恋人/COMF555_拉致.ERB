;綁劫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME555
IF IS_SLAVE(MTAR:0) || IS_LOVER(MTAR:0)
RESULTS:0 = 士官劝诱
ELSE
RESULTS:0 = 綁劫
ENDIF

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE555
;共通部分
CALL COM_ABLE_COMMON(555)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;自国の仕官は不可
SIF CFLAG:(MTAR:0):所属 == CFLAG:(MPLY:0):所属
	RETURN 0
;夜這い中限定
SIF !FLAG:夜這い
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;放浪中なら駄目
SIF !CFLAG:MASTER:所属
	RETURN 0
;相手が君主なら不可
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF COUNTRY_BOSS:(LOCAL:0) == GET_ID(MTAR:0)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM555
;●実行値の計算
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(MTAR:0):25 = 120

CALL COM_ORDER_BASE(MTAR:0)

SIF TCVAR:(MTAR:0):51
	CALL COM_ORDER_ELEMENT(MTAR:0, "疲労", 10)
SIF TCVAR:(MTAR:0):52
	CALL COM_ORDER_ELEMENT(MTAR:0, "失神", 10)

IF TALENT:(MTAR:0):反抗的
	CALL COM_ORDER_ELEMENT(MTAR:0, "反抗的", -5)
ENDIF
IF TALENT:(MTAR:0):堅強
	CALL COM_ORDER_ELEMENT(MTAR:0, "堅強", -5)
ENDIF
IF TALENT:(MTAR:0):坦率
	CALL COM_ORDER_ELEMENT(MTAR:0, "坦率", 5)
ENDIF
IF TALENT:(MTAR:0):自尊心高
	CALL COM_ORDER_ELEMENT(MTAR:0, "自尊心高", -15)
ENDIF
IF TALENT:(MTAR:0):自尊心低
	CALL COM_ORDER_ELEMENT(MTAR:0, "自尊心低", 5)
ENDIF
IF TALENT:(MTAR:0):好奇心
	CALL COM_ORDER_ELEMENT(MTAR:0, "好奇心", 5)
ENDIF
IF TALENT:(MTAR:0):保守的
	CALL COM_ORDER_ELEMENT(MTAR:0, "保守的", -8)
ENDIF
IF TALENT:(MTAR:0):孤高
	CALL COM_ORDER_ELEMENT(MTAR:0, "孤高", -(20 * KOKOU_RATE(MTAR:0) / 100))
ENDIF
IF TALENT:(MTAR:0):難以越過的底線
	CALL COM_ORDER_ELEMENT(MTAR:0, "難以越過的底線", -15)
ENDIF
IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱) && FLAG:ウフフフラグ
	CALL COM_ORDER_ELEMENT(MTAR:0, "淫乱", 5)
ENDIF

IF TALENT:(MTAR:0):親愛
	CALL COM_ORDER_ELEMENT(MTAR:0, "親愛", 50)
ELSEIF TALENT:(MTAR:0):恋慕
	CALL COM_ORDER_ELEMENT(MTAR:0, "恋慕", 20)
ELSEIF TALENT:(MTAR:0):親友
	CALL COM_ORDER_ELEMENT(MTAR:0, "親友", 20)
ENDIF

IF TALENT:(MTAR:0):恋人
	CALL COM_ORDER_ELEMENT(MTAR:0, "恋人", 10)
ENDIF

IF TALENT:(MTAR:0):隷属
	CALL COM_ORDER_ELEMENT(MTAR:0, "隷属", 60)
ELSEIF TALENT:(MTAR:0):服従
	CALL COM_ORDER_ELEMENT(MTAR:0, "服従", 30)
ENDIF

IF TALENT:(MTAR:0):烙印
	CALL COM_ORDER_ELEMENT(MTAR:0, "烙印", 10)
ENDIF

IF TALENT:(MTAR:0):所有者
	CALL COM_ORDER_ELEMENT(MTAR:0, "所有者", 25)
ELSEIF TALENT:(MTAR:0):主人
	CALL COM_ORDER_ELEMENT(MTAR:0, "主人", 10)
ENDIF

SELECTCASE CONFIG:314
	CASE 2
		CALL COM_ORDER_ELEMENT(MTAR:0, "難易度補正", -10)
	CASE 3
		CALL COM_ORDER_ELEMENT(MTAR:0, "難易度補正", -20)
	CASE 4
		CALL COM_ORDER_ELEMENT(MTAR:0, "難易度補正", -50)
ENDSELECT

LOCAL:0 = GET_PALAMLV(PALAM:(MTAR:0):欲情)
IF LOCAL:0 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 30))
ENDIF

IF TALENT:(MTAR:0):自尊心高
	LOCAL:0 = 4
ELSEIF TALENT:(MTAR:0):自尊心低
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF

IF MARK:(MTAR:0):既成事実 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"既成事実Lv{MARK:(MTAR:0):既成事実}", MARK:(MTAR:0):既成事実 * 3 * LOCAL:0)
ENDIF

IF FLAG:調教モード == 6
	CALL COM_ORDER_ELEMENT(MTAR:0, "捕虜会話", -180)
ELSEIF CFLAG:(MTAR:0):所属 != 0
	;CALL COM_ORDER_ELEMENT(MTAR:0, "仕えている君主がいる", -45)
	;今の君主への好印象-悪印象を10で割ったもの
	LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属))) / 10
	IF LOCAL:0 >= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"対現在的君主%ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属))%的好印象", -(LOCAL:0))
	ELSEIF LOCAL:0 <= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"対現在的君主%ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属))%的壞印象", -(LOCAL:0))
	ENDIF
ENDIF

;誘われた勢力の君主への好印象-悪印象を10で割ったもの
LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:所属)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))) / 10

SIF LOCAL <= 0 && (IS_SLAVE(MTAR:0) || IS_LOVER(MTAR:0) || IS_SLAVED_BY(MTAR:0))
	LOCAL /= 2
IF !CFLAG:(MTAR:0):所属
	IF LOCAL:0 > 0
		LOCAL /= 2
	ELSE
		LOCAL *= 2
	ENDIF
ENDIF

IF LOCAL:0 >= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"対君主%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%的好印象", LOCAL:0)
ELSEIF LOCAL:0 <= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"対君主%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%的壞印象", LOCAL:0)
ENDIF

;IF CFLAG:(MTAR:0):2 < 300
;	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -20)
;ELSEIF CFLAG:(MTAR:0):2 < 500
;	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -10)
;ELSEIF CFLAG:(MTAR:0):2 < 800
;	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -5)
;ENDIF

;判定結果を表示
PRINTFORMW  = {TCVAR:(MTAR:0):24} %TOSTR_COMPARE(TCVAR:(MTAR:0):24, TCVAR:(MTAR:0):25)% 実行値{TCVAR:(MTAR:0):25}

;判定成功
IF TCVAR:(MTAR:0):24 >= TCVAR:(MTAR:0):25
	CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:所属, CFLAG:(MTAR:0):所属, -300, 300)
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 3
	PRINTL 
	; IF !IS_SLAVE(MTAR:0)
	; 	PRINTFORML %ANAME(MASTER)%把掙扎的%ANAME(MTAR:0)%装進袋子里綁劫了……
	; 	PRINTFORMW ……%ANAME(MTAR:0)%成為了我國的俘虜
	; 	;所属国家の変更
	; 	CALL CAPTURE(MTAR:0, CFLAG:MASTER:所属)
	; ELSE
	; 	PRINTFORML %ANAME(MASTER)%対%ANAME(MTAR:0)%命令道跟隨自己
	; 	PRINTFORMW ……%ANAME(MTAR:0)%接受了這個命令
	; 	CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, 1)
	; ENDIF
	IF IS_SLAVE(MTAR:0)
		PRINTFORML %ANAME(MASTER)%対%ANAME(MTAR:0)%命令道跟隨自己
		PRINTFORMW ……%ANAME(MTAR:0)%接受了這個命令
		CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, 1)
	ELSEIF IS_LOVER(MTAR:0)
		PRINTFORML %ANAME(MASTER)%让%ANAME(MTAR:0)%跟随自己
		PRINTFORMW ……%ANAME(MTAR:0)%点了点头
		CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, 1)
	ELSE
		PRINTFORML %ANAME(MASTER)%把掙扎的%ANAME(MTAR:0)%装進袋子里綁劫了……
		PRINTFORMW ……%ANAME(MTAR:0)%成為了我國的俘虜
		;所属国家の変更
		CALL CAPTURE(MTAR:0, CFLAG:MASTER:所属)
	ENDIF
	;強制終了フラグを立てる
	TFLAG:38 = 1
;判定失敗
ELSE
	SIF IS_SLAVE(MTAR:0) || IS_LOVER(MTAR:0)
		PRINT 见劝诱不成，
	PRINTFORML %ANAME(MASTER)%試圖把%ANAME(MTAR:0)%装進袋子里綁劫……
	PRINTFORMW ……被%ANAME(MTAR:0)%抵抗了
	IF CFLAG:(MTAR:0):捕虜先 == CFLAG:MASTER:所属
		SOURCE:(MTAR:0):反感 = 300
	ELSE
		SOURCE:(MTAR:0):反感 = 3000
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN555
RETURN コマンド_共通

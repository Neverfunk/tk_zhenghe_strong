;泡踊り

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME5
IF MPLY_NUM >= 2
	LOCALS:0 = 三明治泡泡浴
ELSE
	LOCALS:0 = 泡泡浴
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE5
;共通部分
CALL COM_ABLE_COMMON(5)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;ローションが必要
SIF !ITEM:潤滑液 && !ITEM:A_潤滑液
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	SIF !CAN_REACH_BODY(MPLY:(LOCAL:0) , MTAR:0)
		RETURN 0
	;fucking someone other than the target
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0)) && IS_INSERT_MUTUAL(MPLY:(LOCAL:0), MTAR:0) == 0
		RETURN 0
	SIF IS_RIDE(MPLY:(LOCAL:0)) || IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
NEXT
SIF IS_RIDE(MTAR:0) || IS_RIDDEN(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM5
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

SIF !ITEM:A_潤滑液
	ITEM:潤滑液 -= 1

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 400)
	SOURCE:(LOCAL:2):露出 = 50
	SOURCE:(LOCAL:2):接触 = 250
	SOURCE:(LOCAL:2):性行動 = 360

	IF HAS_PENIS(LOCAL:2)
		SOURCE:(LOCAL:2):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:2, 600)
	ELSE
		SOURCE:(LOCAL:2):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:2, 600)
	ENDIF
	SOURCE:(LOCAL:2):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:2, 450)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 120, 100)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
NEXT

;●ターゲットについて処理
SOURCE:(MTAR:0):露出 = 50
SOURCE:(MTAR:0):接触 = 250
SOURCE:(MTAR:0):性行動 = 360

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 60)

FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF HAS_PENIS(MTAR:0)
		SOURCE:(MTAR:0):快Ｐ += SENSE_HOUSHI_P(LOCAL:1, MTAR:0, 1200) * LOCAL:10 / 100
	ELSE
		SOURCE:(MTAR:0):快Ｃ += SENSE_HOUSHI(LOCAL:1, MTAR:0, 1200) * LOCAL:10 / 100
	ENDIF
	SOURCE:(MTAR:0):快Ｂ += SENSE_HOUSHI(LOCAL:1, MTAR:0, 900) * LOCAL:10 / 100

NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 8

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP5
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP5(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):露出 += 20
	SOURCE:(LOCAL:2):接触 += 150
	SOURCE:(LOCAL:2):性行動 += 120

	IF HAS_PENIS(LOCAL:2)
		SOURCE:(LOCAL:2):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:2, 200)
	ELSE
		SOURCE:(LOCAL:2):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:2, 200)
	ENDIF
	SOURCE:(LOCAL:2):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:2, 150)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
NEXT

;●ターゲットについて処理
LOCAL:2 = MEQUIP_TARGET:(ARG:0):0

SOURCE:(LOCAL:2):露出 += 20
SOURCE:(LOCAL:2):接触 += 150
SOURCE:(LOCAL:2):性行動 += 120

FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	IF HAS_PENIS(LOCAL:2)
		SOURCE:(LOCAL:2):快Ｐ += SENSE_HOUSHI_P(LOCAL:1, LOCAL:2, 400) * LOCAL:10 / 100
	ELSE
		SOURCE:(LOCAL:2):快Ｃ += SENSE_HOUSHI(LOCAL:1, LOCAL:2, 400) * LOCAL:10 / 100
	ENDIF
	SOURCE:(LOCAL:2):快Ｂ += SENSE_HOUSHI(LOCAL:1, LOCAL:2, 300) * LOCAL:10 / 100
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE5(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%対%EQUIP_TARGET_ANAME(ARG:0)%做泡泡浴中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP5(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正摩擦著%EQUIP_TARGET_ANAME(ARG:0)%的全身…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP5(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER5(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 80

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -5)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE5
;プレイヤーが２人
IF MPLY_NUM == 2
	IF GET_INMOU_LEVEL(MPLY:0) >= 1 && GET_INMOU_LEVEL(MPLY:1) >= 1
		LOCALS:1 = 像刷子般的陰毛
	ELSE
		LOCALS:1 = 肌膚
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%把潤滑液塗滿全身、前後夾住了%ANAME(MTAR:0)%、用%LOCALS:1%摩擦刺激著%ANAME(MTAR:0)%的身体…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%讓%ANAME(MPLY:0)%和%ANAME(MPLY:1)%把潤滑液塗滿全身、二人一前一後用%LOCALS:1%摩擦自己的身體進行侍奉…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MPLY:1)%把潤滑液塗滿全身、為%ANAME(MTAR:0)%進行泡泡浴
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%前後夾住了%ANAME(MTAR:0)%、用沾滿潤滑液的滑溜的%LOCALS:1%摩擦著%ANAME(MTAR:0)%的身体
	ENDSELECT

;プレイヤーが１人
ELSE
	IF GET_INMOU_LEVEL(MPLY:0) >= 1
		LOCALS:1 = 像刷子般的陰毛
	ELSE
		LOCALS:1 = 肌膚
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%把潤滑液塗滿全身、用%LOCALS:1%摩擦刺激著%ANAME(MTAR:0)%的身体…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%讓%ANAME(MPLY:0)%把潤滑液塗滿全身、用%LOCALS:1%摩擦自己的身體進行泡泡浴侍奉…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%把潤滑液塗滿全身、為%ANAME(MTAR:0)%進行泡泡浴
			PRINTFORMW %ANAME(MPLY:0)%緊貼著%ANAME(MTAR:0)%、用沾滿潤滑液的滑溜的%LOCALS:1%摩擦著%ANAME(MTAR:0)%的身体
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST5
;FOR LOCAL:0, 0, MTAR_NUM
;	LOCAL:1 = MTAR:(LOCAL:0)
;
;	IF LOCAL:1 != MASTER
;		LOCAL:2 = CUP:(LOCAL:1):快Ｐ + CUP:(LOCAL:1):快Ｃ + CUP:(LOCAL:1):快Ｂ
;
;		IF TCVAR:(LOCAL:1):52
;			;気絶中
;			PRINTFORML %ANAME(LOCAL:1)%失去了意識身體不住地顫抖
;		ELSEIF LOCAL:2 < 300
;			PRINTFORML %ANAME(LOCAL:1)%好像並沒有從胸部那感到快感的樣子
;		ELSEIF LOCAL:2 < 1000
;			PRINTFORML %ANAME(LOCAL:1)%開始能感覺到一點點快感、因為瘙癢扭動著身子
;		ELSEIF LOCAL:2 < 3000
;			PRINTFORML %ANAME(LOCAL:1)%被持續揉搓著胸部讓乳頭漸漸硬了起來、、開始発出了火熱的吐息
;		ELSEIF LOCAL:2 < 6000
;			PRINTFORML %ANAME(LOCAL:1)%僅僅被愛撫著胸部就會露出滿足的表情
;		ELSEIF LOCAL:2 < 10000
;			PRINTFORML %ANAME(LOCAL:1)%感覺太過敏感、光是胸部的刺激就會陷入到極致的愉悅之中
;		ELSE
;			PRINTFORML 緩んだ表情で快楽に身を任せていた
;		ENDIF
;	ENDIF
;NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN5
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_5()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_5()
RETURN POWER(2, 性的嗜好_愛撫されたい)


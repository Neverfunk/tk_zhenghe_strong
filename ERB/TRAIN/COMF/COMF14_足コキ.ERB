;足コキ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME14
IF MTAR_NUM == 2
	LOCALS:0 = 同時足交
ELSEIF MPLY_NUM == 3
	LOCALS:0 = Ｔ足交
ELSEIF MPLY_NUM == 2
	LOCALS:0 = Ｗ足交
ELSE
	LOCALS:0 = 足交
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE14
;共通部分
CALL COM_ABLE_COMMON(14)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で3人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;複数-複数は不可
SIF MPLY_NUM >= 2 && MTAR_NUM >= 2
	RETURN 0
;全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	SIF IS_RIDDEN(MPLY:(LOCAL:0), MTAR:0)
		RETURN 0
	;プレイヤーが土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
	;ターゲットからプレイヤーに足舐め中なら不可
	SIF SEARCH_EQUIP(104, MTAR:0, MPLY:(LOCAL:0)) >= 0
		RETURN 0
NEXT
;全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;竿が必要
	SIF !HAS_PENIS(MTAR:(LOCAL:0))
		RETURN 0
	SIF !P_STACKABLE(MPLY:0, MTAR:(LOCAL:0), 14)
		RETURN 0
	;ターゲットのＶが他のキャラの竿で埋まっていて、体位が後背位・背面座位・騎乗位・背面騎乗位以外なら不可
	SIF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｖ"), 2, 4, 5, 6)
		RETURN 0
	;ターゲットのＡが他のキャラの竿で埋まっていて、体位がＡ後背位・Ａ背面座位・Ａ騎乗位・Ａ背面騎乗位以外なら不可
	SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 2, 4, 5, 6)
		RETURN 0
	;土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 110)
		RETURN 0
NEXT


FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF IS_INSERT_MUTUAL(MPLY:(LOCAL:0), MTAR:(LOCAL:1))
			RETURN 0
		SIF REACHING_BODY(MPLY:(LOCAL:0), MTAR:(LOCAL:1)) || REACHING_BODY(MTAR:(LOCAL:1), MPLY:(LOCAL:0))
			RETURN 0
		SIF REACHING_GROIN(MPLY:(LOCAL:0), MTAR:(LOCAL:1)) || REACHING_GROIN(MTAR:(LOCAL:1), MPLY:(LOCAL:0))
			RETURN 0
		SIF LICKING_GROIN(MPLY:(LOCAL:0), MTAR:(LOCAL:1)) || LICKING_GROIN(MTAR:(LOCAL:1), MPLY:(LOCAL:0))
			RETURN 0
		SIF LICKING_BODY(MPLY:(LOCAL:0), MTAR:(LOCAL:1)) || LICKING_BODY(MTAR:(LOCAL:1), MPLY:(LOCAL:0))
			RETURN 0
        ;buttjob (giving or receiving)
		SIF SEARCH_EQUIP_IC(15, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
	NEXT
NEXT


RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM14
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):逸脱 = 30
	SOURCE:(LOCAL:2):性行動 = 210

	;プレイヤー主導の場合
	IF GET_COM_INITIATIVE() == 0
		SOURCE:(LOCAL:2):嗜虐 = 80
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 170, 90)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1100 + TALENT:(LOCAL:2):美脚 * 300) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):逸脱 = 30
	SOURCE:(LOCAL:1):苦痛 = 20
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 120)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP14
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP14(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):逸脱 += 15
	SOURCE:(LOCAL:2):性行動 += 70

	;プレイヤー主導の場合
	IF IS_INITIATIVE(LOCAL:2)
		SOURCE:(LOCAL:2):嗜虐 = 80
	ENDIF

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 370 + TALENT:(LOCAL:2):美脚 * 100) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 10

	SOURCE:(LOCAL:1):接触 += 25
	SOURCE:(LOCAL:1):逸脱 += 15
	SOURCE:(LOCAL:1):苦痛 += 10
	SOURCE:(LOCAL:1):性行動 += 60

	;倒錯度変化基準値
	TCVAR:(LOCAL:1):50 += 1
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE14(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%足交中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP14(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%用脚把%EQUIP_TARGET_ANAME(ARG:0)%的肉棒抬起来…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%のペニスを足でシゴき上げている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP14(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER14(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 80

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF
IF TALENT:(ARG:0):討厭男人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭男人", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE14
;ターゲットが２人
IF TALENT:(MPLY:0):美脚
	LOCALS:1 = 一边展示着修长的美腿、
;すらりとした美しい脚を見せつけつつ、
ELSE
	LOCALS:1 =
ENDIF
IF MTAR_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0)%
			;TranslatedMark %ANAME(MPLY:0)%は
			IF MPLY:0 != MASTER && ABL:(MPLY:0):加虐 >= 3
				PRINT 一边露出虐待狂的笑容，一边说、
				;TranslatedMark サディスティックな笑みを浮かべながら、
			ELSE
				PRINT 一边困惑着一边说、
				;TranslatedMark 戸惑うようにしながら、
			ENDIF
			PRINTFORML %LOCALS:1%
			PRINTFORMW 用左右脚踩着%ANAME(MTAR:0)%和%ANAME(MTAR:1)%的肉棒摩擦着…
			;TranslatedMark %ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを左右の足で踏みつけ擦りあげ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%张开双腿，用左右脚摩擦%LOCALS:1%%ANAME(MTAR:0)%和%ANAME(MTAR:1)%的肉棒侍奉着…
			;TranslatedMark %ANAME(MPLY:0)%は足を開き、%LOCALS:1%%ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを左右の足で擦りあげて侍奉した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用脚来踩%ANAME(MTAR:0)%和%ANAME(MTAR:1)%的肉棒
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを足でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%张开双腿，用左右脚摩擦%LOCALS:1%%ANAME(MTAR:0)%和%ANAME(MTAR:1)%的肉棒…
			;TranslatedMark %ANAME(MPLY:0)%は足を開き、%LOCALS:1%%ANAME(MTAR:0)%と%ANAME(MTAR:1)%のペニスを左右の足で擦りあげた…
	ENDSELECT

;プレイヤーが３人
ELSEIF MPLY_NUM == 3
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%三人，%LOCALS:1%一起踩上了%ANAME(MTAR:0)%的肉棒，摩擦着…
			;TranslatedMark %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、%LOCALS:1%一斉に%ANAME(MTAR:0)%のペニスを踏みつけ擦り上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%三人，在%LOCALS:1%的协助下，用脚摩擦%ANAME(MTAR:0)%的肉棒侍奉着…
			;TranslatedMark %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、%LOCALS:1%協力しながら%ANAME(MTAR:0)%のペニスを足で擦り上げて侍奉した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%三人用脚来踩%ANAME(MTAR:0)%肉棒
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人に、%ANAME(MTAR:0)%のペニスを足でシゴくように命令した
			PRINTFORMW 三人按照说的那样，%LOCALS:1%一起用脚摩擦%ANAME(MTAR:0)%的肉棒…
			;TranslatedMark 三人は言われた通り、%LOCALS:1%一斉に%ANAME(MTAR:0)%のペニスを足で擦り上げた…
	ENDSELECT

;プレイヤーが２人
ELSEIF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%二人%LOCALS:1%踩上了%ANAME(MTAR:0)%的肉棒摩擦着…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、%LOCALS:1%二人で%ANAME(MTAR:0)%のペニスを踏みつけ擦り上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%二人合作，%LOCALS:1%用脚摩擦侍奉着%ANAME(MTAR:0)%的肉棒…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、%LOCALS:1%二人で協力しながら%ANAME(MTAR:0)%のペニスを足で擦り上げて侍奉した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MPLY:1)%用脚来踩%ANAME(MTAR:0)%的肉棒
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%ANAME(MTAR:0)%のペニスを足でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%两个人用脚摩擦了%ANAME(MTAR:0)%的肉棒…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、%LOCALS:1%二人で%ANAME(MTAR:0)%のペニスを足で擦り上げた…
	ENDSELECT

;プレイヤーが１人
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0)%
			;TranslatedMark %ANAME(MPLY:0)%は
			PRINTFORM %LOCALS:1%
			IF MPLY:0 != MASTER && ABL:(MPLY:0):加虐 >= 3
				PRINT 一边露出虐待狂的笑容，一边用两脚激烈地
				;TranslatedMark サディスティックな笑みを浮かべながら、両足で激しく
			ELSE
				PRINT 小心翼翼地用双脚
				;TranslatedMark 遠慮がちに、両足で
			ENDIF
			PRINTFORMW 将%ANAME(MTAR:0)%的肉棒摩擦着…
			;TranslatedMark %ANAME(MTAR:0)%のペニスをシゴきあげ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0)%面前张开脚，用两脚摩擦侍奉着%LOCALS:1%肉棒…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の前で足を開き、%LOCALS:1%ペニスを両足で擦りあげて侍奉した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用脚来踩%ANAME(MTAR:0)%的肉棒
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%のペニスを足でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0)%面前张开脚，用两脚摩擦%LOCALS:1%肉棒…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の前で足を開き、%LOCALS:1%ペニスを両足で擦り上げた…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN14
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_14()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_14()
RETURN POWER(2, 性的嗜好_奉仕されたい)

@COM_STACK_SPERM_MTAR_TO_MPLY_14()
RETURN 射精部位_足

;見つめ合う

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME325
RESULTS:0 = 互相凝視
RESULTS:1 = 互相凝視
RESULTS:2 = 互相凝視
RESULTS:3 = 互相凝視

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE325
;共通部分
CALL COM_ABLE_COMMON(325)
SIF RESULT == 0
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM325
;実行判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

;コマンドの成否をTFLAG:18にセット
CALL JUDGE_COM_RESULT(MTAR:0, 5, 10)

;好感度500未満なら確実に失敗
IF MPLY:0 == MASTER && CFLAG:(MTAR:0):2 < 500
	TFLAG:18 = -1
ENDIF

;主導権に応じた優越・受動のソース追加
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 50, 50)
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 50, 50)

;親密に応じた歓楽のソース追加
SOURCE:(MPLY:0):歓楽 = -50
SOURCE:(MTAR:0):歓楽 = -50
CALL ADD_SOURCE_KANRAKU(MPLY:0, 70)
CALL ADD_SOURCE_KANRAKU(MTAR:0, 70)
SOURCE:(MPLY:0):歓楽 = MAX(SOURCE:(MPLY:0):歓楽, 0)
SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

;親密に応じた愛情のソース追加
CALL ADD_SOURCE_AIZYOU(MPLY:0, 165)
CALL ADD_SOURCE_AIZYOU(MTAR:0, 165)

;失敗
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):歓楽, 0.10
	TIMES SOURCE:(MPLY:0):優越, 0.50
	TIMES SOURCE:(MPLY:0):愛情, 0.10
	TIMES SOURCE:(MTAR:0):歓楽, 0.10
	TIMES SOURCE:(MTAR:0):優越, 0.50
	TIMES SOURCE:(MTAR:0):愛情, 0.10
	SOURCE:(MPLY:0):反感 += 60
	SOURCE:(MTAR:0):反感 += 60
	SOURCE:(MPLY:0):不満 += 500
	SOURCE:(MTAR:0):不満 += 500
	TFLAG:37 -= 5
;成功
ELSEIF TFLAG:18 == 0

;大成功
ELSE
	TIMES SOURCE:(MPLY:0):歓楽, 2.00
	TIMES SOURCE:(MPLY:0):優越, 2.00
	TIMES SOURCE:(MPLY:0):愛情, 2.00
	TIMES SOURCE:(MTAR:0):歓楽, 2.00
	TIMES SOURCE:(MTAR:0):優越, 2.00
	TIMES SOURCE:(MTAR:0):愛情, 2.00
	TFLAG:37 += 5
ENDIF

;主導度変化基準値
TFLAG:49 = 0

;倒錯度変化基準値
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE325
IF TFLAG:18 == 1
	PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%目不轉睛地互相凝視著
	PRINTFORML 兩人之間流淌著甜蜜的空気…
ELSEIF TFLAG:18 == 0
	PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%目不轉睛地互相凝視著
	IF TALENT:(MTAR:0):反抗的 || TALENT:(MTAR:0):傲慢 || TALENT:(MTAR:0):保守的
		PRINTFORML 沒過多久、%ANAME(MTAR:0)%就害羞得移開了眼睛…
	ELSE
		PRINTFORML %ANAME(MTAR:0)%対%ANAME(MPLY:0)%温柔地微笑著…
	ENDIF
ELSEIF TFLAG:18 == -1
	PRINTFORMW %ANAME(MPLY:0)%想要和%ANAME(MTAR:0)%互相凝視
	IF TALENT:(MTAR:0):反抗的 || TALENT:(MTAR:0):傲慢 || TALENT:(MTAR:0):保守的
		PRINTFORML ……但是、被凝視的%ANAME(MTAR:0)%露出了討厭的神色
	ELSE
		PRINTFORML ……但是、不太明白為什麼的%ANAME(MTAR:0)%露出了困惑的表情
	ENDIF
ENDIF

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST325
;日常コマンドに共通の地の文
;CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN325
RETURN コマンド_日常

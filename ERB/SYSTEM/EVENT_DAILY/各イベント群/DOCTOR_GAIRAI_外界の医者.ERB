;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI_RATE()
RETURN 35


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI_DECISION()
RETURN 1

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI_GENRE()
RETURN デイリー_ジャンル_トレーナー

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI()
;外界の医者による治疗
IF CFLAG:MASTER:所属 != 0 && CFLAG:MASTER:捕虜先 == 0
	;[空虚][崩壊]が付いたキャラの数を調べる(自国限定)
	LOCAL:2 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF (CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属) && (TALENT:(LOCAL:0):空虚 || TALENT:(LOCAL:0):崩壊)
			LOCAL:2 ++
		ENDIF
	NEXT

	;[空虚][崩壊]のキャラがいる
	IF LOCAL:2

		PRINTFORML 有外界的医生來訪
		PRINTFORML

		PRINTFORMW 「…聽說這裡有心靈受創的人」
		PRINTFORMW 「治疗心傷也是醫生的工作。能否讓我進行治疗呢」
		PRINTL 
		PRINTFORML ★★可以治疗一位有[空虚]或[崩壊]的角色★★
		PRINTFORML ★★請選擇要治疗的角色★★
		CALL SINGLE_DRAWLINE

		;全員の調教参加フラグをクリア
		CVARSET CFLAG, 6, 0

		;ページ数の計算
		LOCAL:6 = (LOCAL:2 - 1) / 44 + 1
		LOCAL:7 = 1

		$SHOW_LOOP

		;キャラリストの表示
		LOCAL:8 = 0
		LOCAL:9 = 0
		LOCAL:10 = (LOCAL:7 - 1) * 44
		LOCAL:11 = LOCAL:7 * 44
		LOCAL:12 = 4
		FOR LOCAL:0, 0, CHARANUM
			IF (CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属) && (TALENT:(LOCAL:0):空虚 || TALENT:(LOCAL:0):崩壊)
				IF LOCAL:8 >= LOCAL:10 && LOCAL:8 < LOCAL:11
					IF LOCAL:9 % 2 != 0
						PRINTPLAIN 　
					ELSEIF LOCAL:9 >= 1
						PRINTL 
						LOCAL:12 ++
					ENDIF
					CALL PRINT_PARTNER_DATA(LOCAL:0)
					LOCAL:9 ++
				ENDIF
				LOCAL:8 ++
			ENDIF
		NEXT
		PRINTL 
		CALL SINGLE_DRAWLINE

		IF LOCAL:6 >= 2
			LOCAL:12 += 2
			IF LOCAL:7 > 1
				PRINT [  8] 前一頁            
			ELSE
				PRINT                             
			ENDIF
			LOCALS:0 = page{LOCAL:7}/{LOCAL:6}
			PRINTPLAINFORM %LOCALS:0, 28, LEFT%
			IF LOCAL:7 < LOCAL:6
				PRINT [  9] 後一頁
			ENDIF
			PRINTL 
			CALL SINGLE_DRAWLINE
		ENDIF

		PRINTL [  0] 不治疗

		REDRAW 0

		$INPUT_LOOP
		INPUT

		;治疗しない
		IF RESULT == 0
			REDRAW 1
			PRINTFORML %ANAME(MASTER)%拒絕了治疗
			PRINTFORMW 「是、是麼…。就算是有什麼特殊情況、我也不會說出去的…」
		;察看前一頁
		ELSEIF RESULT == 8 && LOCAL:7 > 1
			LOCAL:7 = MAX(1, LOCAL:7 - 1)
			CLEARLINE LOCAL:12
			GOTO SHOW_LOOP
		;後のページ
		ELSEIF RESULT == 9 && LOCAL:7 < LOCAL:6
			LOCAL:7 = MIN(LOCAL:6, LOCAL:7 + 1)
			CLEARLINE LOCAL:12
			GOTO SHOW_LOOP
		ELSE
			LOCAL:5 = NO_TO_CHARA(RESULT - 100)
			IF LOCAL:5 >= 0 && (CFLAG:(LOCAL:5):所属 == CFLAG:MASTER:所属 || CFLAG:(LOCAL:5):捕虜先 == CFLAG:MASTER:所属) && (TALENT:(LOCAL:5):空虚 || TALENT:(LOCAL:5):崩壊)
				REDRAW 1
				PRINTFORMW 「是要治疗%ANAME(LOCAL:5)%麼？　好、交給我吧！」
				PRINTFORMW 「我的身体、和鍼合而為一！　一鍼同体！　阻疾傷去！　元・氣・恢・復～～～～～～！」
				SETCOLOR カラー_注意
				IF TALENT:(LOCAL:5):崩壊
					PRINTFORMW %ANAME(LOCAL:5)%失去了<崩壊>
				ELSE
					PRINTFORMW %ANAME(LOCAL:5)%失去了<空虚>
				ENDIF
				RESETCOLOR
				TALENT:(LOCAL:5):崩壊 = 0
				TALENT:(LOCAL:5):空虚 = 0
				CFLAG:(LOCAL:5):崩壊 = MIN(CFLAG:(LOCAL:5):崩壊, 1000)
				PRINTFORMW 「這樣就沒事了。但是、短時間內還是不要讓其太過勉強」
			ELSE
				CLEARLINE 1
				GOTO INPUT_LOOP
			ENDIF
		ENDIF

	;[空虚][崩壊]のキャラがいない場合(クリア済みなら何も起きない)
	ELSEIF !FLAG:クリアフラグ
		SETCOLOR カラー_注意
		PRINTFORMW 有一位外界的医生來拜訪了
		RESETCOLOR

		PRINTFORMW 「聽說這裡有人受傷了。作為医生希望能讓我来治疗他們」
		PRINTFORMW ………………
		;徴兵限界を超えて兵の確保が可能
		LOCAL:0 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 200, 2000)
		COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += LOCAL:0
		SETCOLOR カラー_注意
		PRINTFORMW 我軍的士兵数増加了{LOCAL:0}
		RESETCOLOR
		LOCAL:2 = 1
	ENDIF
ENDIF

RETURN LOCAL:2 > 0

;クンニ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME8
LOCAL:0 = IS_RIDE(MPLY:0, MTAR:0)

;男性プレイヤーがターゲットに顔面騎乗中
IF LOCAL:0 && !HAS_VAGINA(MPLY:0)
	LOCALS:0 = 顔騎舐肛
;非男性プレイヤーがターゲットに顔面騎乗中 or ターゲットからプレイヤーにフェラ・クンニ・アナル舐め中
ELSEIF LOCAL:0 || SEARCH_EQUIP(11, MTAR:0, MPLY:0) >= 0 || SEARCH_EQUIP(2, MTAR:0, MPLY:0) >= 0 || SEARCH_EQUIP(8, MTAR:0, MPLY:0) >= 0
	LOCALS:0 = Ａ六九舐肛
ELSE
	LOCALS:0 = 舐肛
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE8
;共通部分
CALL COM_ABLE_COMMON(8)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
SIF !CAN_LICK_GROIN(MPLY:0 ,MTAR:0)
	RETURN 0
;target already getting a rimjob
SIF IS_EQUIP_TARGET(MTAR:0, 8)
	RETURN 0
;vaginal facesitting is a special case
SIF IS_EQUIP_PLAYER(MTAR:0, 101)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM8
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 120

EXP:(MPLY:0):性技経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 300)
SOURCE:(MPLY:0):接触 = 50
SOURCE:(MPLY:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 100)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;膣口へのキス
CALL KISS_COMMON(MPLY:0, @"%ANAME(MTAR:0)%的肛门", GET_SITUATION_BY_TRAIN_MODE())

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 60

SOURCE:(MTAR:0):液体追加 = 300
SOURCE:(MTAR:0):露出 = 150
SOURCE:(MTAR:0):逸脱 = 50
SOURCE:(MTAR:0):接触 = 50
SOURCE:(MTAR:0):快Ａ = SENSE_HOUSHI(MPLY:0, MTAR:0, 1500)
SOURCE:(MTAR:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 150, 0)

IF TALENT:(MPLY:0):舌頭霊活
	TIMES SOURCE:(MTAR:0):快Ａ, 1.50
ENDIF

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP8
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP8(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●プレイヤーの処理
DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
SOURCE:(LOCAL:2):接触 += 25
SOURCE:(LOCAL:2):性行動 += 100

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;●ターゲットの処理
DOWNBASE:(LOCAL:3):体力 += 10

SOURCE:(LOCAL:3):液体追加 += 150
SOURCE:(LOCAL:3):露出 += 75
SOURCE:(LOCAL:3):逸脱 += 25
SOURCE:(LOCAL:3):接触 += 25
SOURCE:(LOCAL:3):性行動 += 100

IF TALENT:(LOCAL:2):舌頭霊活
	SOURCE:(LOCAL:3):快Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1125)
ELSE
	SOURCE:(LOCAL:3):快Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 750)
ENDIF

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE8(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%舐肛中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP8(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%用舌头舔舐着%EQUIP_TARGET_ANAME(ARG:0)%的肛门…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%のアナルを舌で舐めている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP8(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER8(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF
;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE8
LOCAL:5 = SEARCH_EQUIP(101, MPLY:0, MTAR:0)
LOCAL:6 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
LOCAL:7 = SEARCH_EQUIP(11, MTAR:0, MPLY:0)
LOCAL:8 = SEARCH_EQUIP(2, MTAR:0, MPLY:0)
LOCAL:9 = SEARCH_EQUIP(101, MTAR:0, MPLY:0)

IF TALENT:(MTAR:0):美臀
	LOCALS:2 = 掰开%STR_BODY("尻：尻肉：長：接触", MTAR:0)%、
;%STR_BODY("尻：尻肉：長：接触", MTAR:0)%を割り開き、
ELSEIF GET_HIPSIZE(MTAR:0) >= 1
	LOCALS:2 = 推开%STR_BODY("尻肉：長：接触", MTAR:0)%、
;%STR_BODY("尻肉：長：接触", MTAR:0)%をかきわけ、
ELSE
	LOCALS:2 = 
ENDIF

IF HAS_PENIS(MTAR:0)
	LOCALS:1 = 从肉棒的底部周围
;竿の付け根のあたりから
ELSEIF TALENT:(MTAR:0):体格 == 体格_子供
	LOCALS:1 = 没有色素沉淀
;色素も沈着していない
ELSEIF ABL:(MTAR:0):Ａ感 <= 1
	LOCALS:1 = 还不习惯
;未だ不慣れな
ELSE
	LOCALS:1 = 
ENDIF

IF GET_INMOU_LEVEL(MTAR:0) >= 2
	LOCALS:3 = 到菊花附近都长满了毛的
;菊座近くまで毛の繁った
ELSE
	LOCALS:3 = 
ENDIF

IF CUM_GET_AMOUNT:(MTAR:0):射精部位_アナル > 0
	LOCALS:4 = 正溢出白濁的
ELSE
	LOCALS:4 = 
ENDIF


;プレイヤーがターゲットに顔面騎乗中かつ男の場合
IF LOCAL:5 >= 0 && !HAS_VAGINA(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%一边让%ANAME(MTAR:0)%舔舐自己的睾丸，一边%LOCALS:2%舔舐%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の玉を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舐めほぐし%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%舔舐着%ANAME(MPLY:0)%的睾丸的同时，让%ANAME(MPLY:0)%%LOCALS:2%舔舐了自己的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の玉を舐めながら、%LOCALS:2%%ANAME(MPLY:0)%に自身の%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%舔舐%ANAME(MTAR:0)%的菊穴
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に%ANAME(MTAR:0)%の菊穴を舐めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%一边让%ANAME(MTAR:0)%舔舐自己的睾丸，一边%LOCALS:2%舔舐%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の玉を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舐めほぐした…
	ENDSELECT
	;顔面騎乗の地の文をカット
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF

;非男性プレイヤーがターゲットに顔面騎乗中 or ターゲットからプレイヤーにフェラ・クンニ中
ELSEIF LOCAL:5 >= 0 || LOCAL:6 >= 0 || LOCAL:7 >= 0 || LOCAL:8 >= 0
	IF LOCAL:6 >= 0
		LOCALS:0 = 尻穴
	ELSEIF LOCAL:7 >= 0
		LOCALS:0 = 肉棒
	ELSE
		LOCALS:0 = 秘唇
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%一边让%ANAME(MTAR:0)%舔舐自己的%LOCALS:0%，一边%LOCALS:2%舔舐%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门，一边解开%TARGET_STR("た", "てきた")%…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:0%を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舐めほぐし%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%舔舐着%ANAME(MPLY:0)%的%LOCALS:0%的同时，让%ANAME(MPLY:0)%%LOCALS:2%舔舐了自己的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の%LOCALS:0%を舐めながら、%LOCALS:2%%ANAME(MPLY:0)%に自身の%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MTAR:0)%用六九式互相口交
			PRINTFORMW %ANAME(MPLY:0)%一边让%ANAME(MTAR:0)%舔舐自己的%LOCALS:0%，一边%LOCALS:2%舔舐%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:0%を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舐めほぐした…
	ENDSELECT
	;顔面騎乗・フェラ・クンニの地の文をカット
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF
	IF LOCAL:6 >= 0
		MEQUIP_CUT:(LOCAL:6) = 1
	ENDIF
	IF LOCAL:7 >= 0
		MEQUIP_CUT:(LOCAL:7) = 1
	ENDIF
	IF LOCAL:8 >= 0
		MEQUIP_CUT:(LOCAL:8) = 1
	ENDIF

;ターゲットからプレイヤーに顔面騎乗中
ELSEIF LOCAL:8 >= 0
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%让%ANAME(MTAR:0)%跨在脸上，从下面%LOCALS:2%贪婪地舔舐%ANAME(MTAR:0)%的%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を顔の上に跨がらせたまま、下から%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:4%アナルを貪るように舐め上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%跨在%ANAME(MPLY:0)%的脸上，%LOCALS:2%用腰用力向下压住，让她舔舐自己的%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の顔に跨ったまま、%LOCALS:2%グリグリと腰を押し付けて自身の%LOCALS:3%%LOCALS:4%アナルを舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%舔舐%ANAME(MTAR:0)%的菊穴
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に%ANAME(MTAR:0)%の菊穴を舐めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%让%ANAME(MTAR:0)%跨在脸上，从下面%LOCALS:2%舔舐%ANAME(MTAR:0)%的%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を顔の上に跨がらせたまま、下から%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:4%アナルを舐め上げた…
	ENDSELECT
	;顔面騎乗の地の文をカットする
	MEQUIP_CUT:(LOCAL:8) = 1

;プレイヤーが拘束中
ELSEIF IS_BIND(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%把%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门贴在脸上，%LOCALS:2%用舌头激烈地来回舔舐着…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを顔に押し当てさせると、%LOCALS:2%舌で激しく舐め回し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%把%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门压在动弹不得的%ANAME(MPLY:0)%的嘴上，让其舔舐侍奉…
			;TranslatedMark %ANAME(MTAR:0)%は%LOCALS:2%身動きの取れない%ANAME(MPLY:0)%の口に%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを押し付け、舐めさせ侍奉させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MTAR:0)%舔舐菊穴
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対して、%ANAME(MPLY:0)%に菊穴を舐めさせるように命令した
			PRINTFORMW %ANAME(MTAR:0)%把%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门压在动弹不得的%ANAME(MPLY:0)%的嘴上，让其舔舐…
			;TranslatedMark %ANAME(MTAR:0)%は%LOCALS:2%身動きの取れない%ANAME(MPLY:0)%の口に%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを押し付け、舐めさせた…
	ENDSELECT

ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:2%用舌头舔舐%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舌で舐め上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_サド)
				LOCALS:2 = 露出虐待狂般的笑容
;サディステックな笑みを浮かべながら、
			ELSEIF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱)
				LOCALS:2 = 露出恍惚的神情
;恍惚とした表情を浮かべながら、
			ENDIF
			PRINTFORMW %ANAME(MTAR:0)%%LOCALS:2%把%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门按在%ANAME(MPLY:0)%的口上，让它舔舐…
			;TranslatedMark %ANAME(MTAR:0)%は%LOCALS:2%%ANAME(MPLY:0)%の口に%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを押し付け、舐めさせ侍奉させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%舔舐%ANAME(MTAR:0)%的菊穴
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に%ANAME(MTAR:0)%の菊穴を舐めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:2%用舌头舔舐了%ANAME(MTAR:0)%的%LOCALS:1%%LOCALS:3%%LOCALS:4%肛门…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:1%%LOCALS:3%%LOCALS:4%アナルを舌で舐め上げた…
	ENDSELECT
ENDIF

IF CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル
	PRINTFORM %SPLIT_RAND("热热的/粘粘稠的/濃厚的/新鮮的", 1)%精液
	;TranslatedMark %SPLIT_RAND("熱い/粘つく/濃厚な/新鮮な", 1)%精液が

	IF ABL:(MPLY:0):精愛 >= 4
		PRINTFORML 一滴到%SPLIT_RAND("喜欢精液好的/获得精愛味覚的/渴望精液的", 1)%%ANAME(MPLY:0)%的嘴里，%PRONOUN(MPLY:0)%就发出了喜悦的呻吟声…
		;TranslatedMark %SPLIT_RAND("精液好きの/精愛味覚を得た/精液に飢えた", 1)%%ANAME(MPLY:0)%の口に滴ると、%PRONOUN(MPLY:0)%は悦びの呻き声を漏らした…
	ELSE
		PRINTFORML 虽然滴到了%ANAME(MPLY:0)%的口中，%PRONOUN(MPLY:0)%却不得不为之服务…
		;TranslatedMark %ANAME(MPLY:0)%の口に滴るが、%PRONOUN(MPLY:0)%は侍奉を余儀なくされた…
	ENDIF
	;target eats 1/4th + 3 cum, up to 20
	LOCAL = LIMIT(MIN(((CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル/4)+3),CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル),0,20)
	;TranslatedMark LIMIT(MIN(((CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル / 4) + 3), CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル) , 0, 20)
	CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル -= LOCAL
	CUM_CUR_AMOUNT:(MPLY:0):射精部位_口 += LOCAL
	SIF CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル == 0
		PRINTFORML %ANAME(MTAR:0)%%STR_BODY("アナル", MPLY:0)%的精液全部被%ANAME(MPLY:0)%喝完了
		;TranslatedMark %ANAME(MTAR:0)%の%STR_BODY("アナル", MPLY:0)%の精液は全て%ANAME(MPLY:0)%が飲み干した
	;Cumlove exp increases by amount of cum
	EXP:(MPLY:0):精愛経験値 += LOCAL
	;Swallowing exp
	SIF ABL:(MPLY:0):精愛 >= 4
		EXP:(MPLY:0):口内射精経験 ++
	WAIT
ENDIF
;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST8
IF (MTAR:0 != MASTER || CONFIG:441) && !IS_ANIMAL(MTAR:0)
	LOCAL:2 = CUP:(MTAR:0):快Ａ

	IF TCVAR:(MTAR:0):52
		;気絶中
		PRINTFORML 每次被舔舐肛门，%ANAME(MTAR:0)%都会战战兢兢地颤抖着身体
		;TranslatedMark %ANAME(MTAR:0)%はアナルを舐め上げられるたびに、ビクビクと体を震わせている
	ELSEIF LOCAL:2 < 300
		PRINTFORML %ANAME(MTAR:0)%的肛门一直紧闭着。感觉快乐也许还需要一点时间
		;TranslatedMark %ANAME(MTAR:0)%のアナルは硬く閉じたままだ。快楽を感じるにはもう少しかかるかもしれない
	ELSEIF LOCAL:2 < 1000
		PRINTFORML %ANAME(MTAR:0)%虽然一直忍受着舔舐肛门的刺激，但确实感觉到了
		;TranslatedMark %ANAME(MTAR:0)%はアナル舐めの刺激にじっと耐えているものの、確実に感じてきているようだ
	ELSEIF LOCAL:2 < 3000
		PRINTFORM %ANAME(MTAR:0)%感觉到自己敏感的洞被舔舐了
		;TranslatedMark 敏感な穴を舐められると感じるのか、%ANAME(MTAR:0)%は
		IF TALENT:(MTAR:0):害羞
			PRINT 羞得滿臉通紅、用手捂著嘴想要圧住自己的呻吟聲、
		ELSEIF TALENT:(MTAR:0):解放 || TALENT:(MTAR:0):接受快感
			PRINTL 発出灼熱的吐息把身体交給了快感
		ELSEIF TALENT:(MTAR:0):圧抑 || TALENT:(MTAR:0):孤高 || TALENT:(MTAR:0):否定快感
			PRINTL 像是在否定快感一般搖頭忍耐著
		ELSE
			PRINTL 扭动着腰微微吐了一口气
			;TranslatedMark 腰をくねらせながらわずかに小さく吐息を漏らした
		ENDIF
	ELSEIF LOCAL:2 < 6000
		PRINTFORML 用舌头在%ANAME(MTAR:0)%的菊穴里来回舔舐，%ANAME(MTAR:0)%一边发出可爱的声音一边轻轻摇动着身体
		;TranslatedMark %ANAME(MTAR:0)%の菊穴を舌でねぶり回してやると%ANAME(MTAR:0)%は可愛らしい声を漏らしながらがくがくと身体を揺らした
	ELSEIF LOCAL:2 < 10000
		PRINTFORML %ANAME(MTAR:0)%发出明显的快乐之声，下意识地用手捂住了自己的嘴，想把那个部分声音压下去
		;TranslatedMark %ANAME(MTAR:0)%は明らかな快楽の声をあげ、無意識にこちらの頭を手で押さえてさらにその部分を押し付けてきた
	ELSE
		IF TALENT:(MTAR:0):Ａ敏感
			PRINTFORML %ANAME(MTAR:0)%激烈地扭动着被责弄到敏感的肛门，连声音都不能顺利发出
			;TranslatedMark %ANAME(MTAR:0)%は敏感なアナルへの責めに激しく身もだえし、あられも無い声を張り上げ続けた
		ELSE
			PRINTFORML %ANAME(MTAR:0)%在肛门的刺激下，继续剧烈的扭动和呻吟。
			;TranslatedMark %ANAME(MTAR:0)%はアナルへの刺激に激しい喘ぎ声を上げながら悶え続けた
		ENDIF
	ENDIF
	IF TALENT:(MTAR:0):美臀
		PRINTFORML %STR_BODY("尻肉：長：外見", MTAR:0)%不停顫抖帯起陣陣臀浪……
		;TranslatedMark %STR_BODY("尻肉：長：外見", MTAR:0)%がふるふると震えている……
	ELSEIF GET_HIPSIZE(LOCAL:1) >= 1
		PRINTFORML %STR_BODY("尻肉：長：外見", MTAR:0)%不停顫抖帯起陣陣臀浪……
		;TranslatedMark %STR_BODY("尻肉：長：外見", MTAR:0)%がふるふると震えている……
	ENDIF
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN8
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_8()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_8()
RETURN POWER(2, 性的嗜好_奉仕されたい)

@COM_STACK_SPERM_MTAR_TO_MPLY_8()
RETURN 射精部位_顔

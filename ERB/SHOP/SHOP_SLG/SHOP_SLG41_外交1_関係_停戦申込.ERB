;-------------------------------------------------
;停戦を申し込む
;-------------------------------------------------
@DIPLOMACY_CEASEFIRE(相手勢力)
#DIM 勢力
#DIM 相手勢力
#DIM 君主
#DIM 相手君主
#DIM 相手外交計画
#DIM 判定値
#DIM 期間
#DIM 勢力経済
#DIM 相手勢力経済

IF IS_SP_COUNTRY(相手勢力)
	PRINTFORMW 面対停戦的要求、対面不屑的笑了出来
	RETURN 0
ENDIF
勢力 = CFLAG:MASTER:所属
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(相手勢力)

相手外交計画 = CHECK_AI_DIPLOMACY_PLAN(相手勢力, CFLAG:MASTER:所属)

;相手の外交計画内ならば、無条件で成立
IF GROUPMATCH(相手外交計画, AI_外交計画_停戦, AI_外交計画_同盟)
	PRINTFORMW %ANAME(MASTER)%積極的要求停戦、%ANAME(LOCAL:5)%爽快的答応了…
	GOTO SUCCESS
ENDIF

勢力経済 = GET_SUM_ECONOMY(勢力)
相手勢力経済 = GET_SUM_ECONOMY(相手勢力)


判定値 = (REL_LIKE:相手君主:君主 - REL_HATE:相手君主:君主) * 100 / 1500
判定値 = 判定値 * MAX(勢力経済 * 100 / 相手勢力経済, 100) / 100
判定値 += MAX((勢力経済 - 相手勢力経済) / 5000, 0)

;DEBUGPRINTFORML 停戦のベース判定値:{判定値}

;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

SIF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
	判定値 /= 2

SIF TECHNOLOGY_STATUS:勢力:TECHNOLOGY_DIPLOMACY >= 1
	判定値 += 5
SIF TECHNOLOGY_STATUS:勢力:TECHNOLOGY_DIPLOMACY >= 3
	判定値 += 5
IF CHECK_BUILDING_FUNCTION_SELECT(-1,勢力,建造物_外交部) > 0
	判定値 += RESULT:1
	BUILDING_TEXT = BUILDING_TEXT_外交部(勢力)
ENDIF
SELECTCASE 判定値
	CASE IS >= 30
		PRINTFORMW %ANAME(君主)%提出了停戦要求、%ANAME(相手君主)%爽快的答応了…
	CASE 15 TO 29
		CALL DIPLOMACY_REQUESTED(相手勢力)
		;要求できるものなし
		IF RESULT == -1
			IF INRANGE(判定値, 25, 30)
				PRINTFORMW %ANAME(君主)%提出了停戦要求、%ANAME(相手君主)%苦悩的答応了…
			ELSE
				PRINTFORMW %ANAME(君主)%提出了停戦要求、%ANAME(相手君主)%苦悩的拒絶了…
				RETURN
			ENDIF
		ELSEIF RESULT == 0
			RETURN
		ENDIF
	CASEELSE
		PRINTFORMW %ANAME(君主)%的停戦提案、被%ANAME(相手君主)%拒絶了…
		SIF !TMP_COUNTRY_IS_NEIBORING:勢力:相手勢力
			PRINTFORMW 領地不相鄰的状態下要求停戦、比通常情況更難…
		RETURN
ENDSELECT

$SUCCESS
期間 = MAX((REL_LIKE:相手君主:君主 * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100 -  REL_HATE:相手君主:君主 * AI_RELATION:(COUNTRY_AI_TYPE:相手勢力):(COUNTRY_AI_TYPE:勢力) / 100) / 100, 3)

CALL INIT_CEASEFIRE(勢力, 相手勢力, 期間)

PRINTL
SETCOLOR カラー_注意
PRINTFORMW 和%ANAME(相手君主)%達成了{期間}期間内的停戦協議
RESETCOLOR
;跳转信仰事件
TRYCALL SHOP_LIFE_USERSHOP68_CEASE_FIRE(勢力,相手勢力,期間)

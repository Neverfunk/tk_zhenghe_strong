;素股

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME13

IF MPLY_NUM == 2
	LOCALS:0 = Ｗ
ELSE
	LOCALS:0 = 
ENDIF

IF !HAS_VAGINA(MPLY:0)
	LOCALS:0 += "睾丸性交"
ELSE
	LOCALS:0 += "股間性交"
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE13
;共通部分
CALL COM_ABLE_COMMON(13)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;潤滑が高くないとダメ
FOR LOCAL, 0, MPLY_NUM
	SIF PALAM:(MPLY:LOCAL):潤滑 < 1500
		RETURN 0
	SIF !HAS_PENIS(MTAR:0)
		RETURN 0
	SIF !CAN_FUCK(MTAR:0, MPLY:LOCAL, 13)
		RETURN 0
	SIF !P_STACKABLE(MPLY:LOCAL, MTAR:0, 13)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;快Ｃソースの倍率を取得する関数 ARG:0=PLAYERのキャラ番号
;-------------------------------------------------
@COM13_RATE_C(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):奉仕
	CASE 0
		TIMES LOCAL:5, 0.50
	CASE 1
		TIMES LOCAL:5, 0.80
	CASE 2
		TIMES LOCAL:5, 1.00
	CASE 3
		TIMES LOCAL:5, 1.20
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(MPLY:0):奉仕 - 5) * 2 + 150) / 100
ENDSELECT

SELECTCASE ABL:(ARG:0):性技
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(MPLY:0):性技 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;素股
;-------------------------------------------------
@COM13
;実行できるかの判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;対Ｐ侍奉系コマンドの継続フラグを解除
CALL RELEASE_SERVE_P

;●プレイヤーの潤滑に応じた快感倍率の設定
IF PALAM:(MPLY:0):潤滑 < PALAMLV:5
	LOCAL:10 = 500
ELSEIF PALAM:(MPLY:0):潤滑 < PALAMLV:6
	LOCAL:10 = 700
ELSEIF PALAM:(MPLY:0):潤滑 < PALAMLV:7
	LOCAL:10 = 800
ELSEIF PALAM:(MPLY:0):潤滑 < PALAMLV:8
	LOCAL:10 = 900
ELSE
	LOCAL:10 = 1000
ENDIF

FOR LOCAL, 0, MPLY_NUM
	;●プレイヤーについて処理
	DOWNBASE:(MPLY:LOCAL):体力 += 120

	EXP:(MPLY:LOCAL):性技経験値 += 1

	SOURCE:(MPLY:LOCAL):奉仕 = SERVE_HOUSHI(MPLY:LOCAL, 350)
	SOURCE:(MPLY:LOCAL):露出 = 50
	SOURCE:(MPLY:LOCAL):接触 = 100
	SOURCE:(MPLY:LOCAL):性行動 = 300
	IF HAS_VAGINA(MPLY:LOCAL)
		SOURCE:(MPLY:LOCAL):快Ｃ = 500 * COM13_RATE_C(MPLY:LOCAL) / 1000 * LOCAL:10 / 1000
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:LOCAL, 130, 100)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(MPLY:LOCAL):4 = 1
NEXT

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 60

SOURCE:(MTAR:0):露出 = 50
SOURCE:(MTAR:0):接触 = 100
SOURCE:(MTAR:0):快Ｐ = SENSE_HOUSHI_P(MPLY:0, MTAR:0, 1400 * LOCAL:10 / 1000)
SOURCE:(MTAR:0):性行動 = 180

;主導権に応じた優越・受動のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 130, 50)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP13
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP13(ARG:0)

LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

FOR LOCAL, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):LOCAL

	;●プレイヤーの潤滑に応じた快感倍率の設定
	IF PALAM:(LOCAL:2):潤滑 < PALAMLV:5
		LOCAL:10 = 500
	ELSEIF PALAM:(LOCAL:2):潤滑 < PALAMLV:6
		LOCAL:10 = 700
	ELSEIF PALAM:(LOCAL:2):潤滑 < PALAMLV:7
		LOCAL:10 = 800
	ELSEIF PALAM:(LOCAL:2):潤滑 < PALAMLV:8
		LOCAL:10 = 900
	ELSE
		LOCAL:10 = 1000
	ENDIF

	;●プレイヤーについて処理
	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 130)
	SOURCE:(LOCAL:2):露出 += 25
	SOURCE:(LOCAL:2):接触 += 50
	SOURCE:(LOCAL:2):性行動 += 100
	IF HAS_VAGINA(LOCAL:2)
		SOURCE:(LOCAL:2):快Ｃ += 250 * COM13_RATE_C(LOCAL:2) / 1000 * LOCAL:10 / 1000
	ENDIF

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
NEXT

;●ターゲットについて処理
DOWNBASE:(LOCAL:3):体力 += 10

SOURCE:(LOCAL:3):露出 += 25
SOURCE:(LOCAL:3):接触 += 50
SOURCE:(LOCAL:3):性行動 += 60
SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 450 * LOCAL:10 / 1000)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE13(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%素股中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP13(ARG:0)
LOCAL:0 = MEQUIP_PLAYER:(ARG:0):0
IF IS_PLAYABLE(LOCAL:0)
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%把%EQUIP_TARGET_ANAME(ARG:0)%的肉棒夹在股间摩擦…
	;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%のペニスを股で挟んで擦り上げている…
ELSE
	PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%把肉棒夹在%EQUIP_PLAYER_ANAME(ARG:0)%的股间摩擦着…
	;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%が%EQUIP_PLAYER_ANAME(ARG:0)%の股にペニスを挟んで擦り上げている…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP13(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER13(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 85

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -3)
ENDIF
IF TALENT:(ARG:0):討厭男人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭男人", -12)
ENDIF
IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 4)
ENDIF
IF TFLAG:69
	CALL COM_ORDER_ELEMENT(ARG:0, "桃源香", 3)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE13
IF !HAS_VAGINA(MPLY:0)
	LOCALS:0 = 睾丸
;玉袋
ELSE
	LOCALS:0 = 小穴
;ワレメ
ENDIF
IF IS_PIERCED(MPLY:0, ピアス_ラビア)
	LOCALS:1 = 上著環的
ELSE
	LOCALS:1 = 
ENDIF
IF IS_PIERCED(MTAR:0, ピアス_ペニス)
	LOCALS:2 = 上著環的
ELSE
	LOCALS:2 = 
ENDIF
IF IS_PLAYABLE(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0, MPLY_NUM)%把%ANAME(MTAR:0)%的%LOCALS:2%肉棒夹在胯下、
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は%ANAME(MTAR:0)%の%LOCALS:2%ペニスを股に挟み込むと、
			IF MPLY:0 != MASTER && TALENT:(MPLY:0):恋慕 && GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_淫乱) && PALAM:(MPLY:0):欲情 > PALAMLV:8
				IF !HAS_VAGINA(MPLY:0)
					PRINTFORMW 把因欲望而变硬的睾丸压在上面，贪图快乐似的摆动着腰，不断摩擦着肉棒…
					;TranslatedMark 欲情で固くなった玉袋を押し付けながら、快楽を貪るように腰を振って竿を擦り上げ%TARGET_STR("た", "てきた")%…
				ELSE
					PRINTFORMW 把因欲望而被烫湿的%LOCALS:1%秘唇压在上面，贪图快乐似的摆动着腰，不断摩擦着肉棒…
					;TranslatedMark 欲情で熱く濡れた%LOCALS:1%秘唇を押し付けながら、快楽を貪るように腰を振って竿を擦り上げ%TARGET_STR("た", "てきた")%…
				ENDIF
			ELSEIF MPLY:0 != MASTER && (TALENT:(MPLY:0):恋慕 || GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_淫乱))
				PRINTFORMW 为了享受肉棒的触感，开始摆动腰部，摩擦着%LOCALS:2%肉棒…
				;TranslatedMark 肉棒の感触を楽しむように腰を振って%LOCALS:2%竿を擦り上げ%TARGET_STR("た", "てきた")%…
			ELSE
				IF !HAS_VAGINA(MPLY:0)
					PRINTFORMW 把睾丸压在上面，开始摇动腰部摩擦%LOCALS:2%肉棒…
					;TranslatedMark 玉袋を押し付けながら腰を振って%LOCALS:2%竿を擦り上げ%TARGET_STR("た", "てきた")%…
				ELSE
					PRINTFORMW 把%LOCALS:1%秘唇压在上面，开始摇动腰部摩擦%LOCALS:2%肉棒…
					;TranslatedMark %LOCALS:1%秘唇を押し付けながら腰を振って%LOCALS:2%竿を擦り上げ%TARGET_STR("た", "てきた")%…
				ENDIF
			ENDIF

		;ターゲットに主導権
		CASE 1
			IF !HAS_VAGINA(MPLY:0)
				PRINTFORMW %ANAME(MTAR:0)%把%LOCALS:2%肉棒夹在%ANAME(MPLY:0, MPLY_NUM)%的两腿之间，摆动腰在%ANAME(MPLY:0, MPLY_NUM)%的睾丸上磨蹭肉棒
				;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0, MPLY_NUM)%の股の間に%LOCALS:2%ペニスを挟みこむと、腰を振って%ANAME(MPLY:0, MPLY_NUM)%の玉袋に肉棒を擦り付けた
			ELSE
				PRINTFORMW %ANAME(MTAR:0)%把%LOCALS:2%肉棒夹在%ANAME(MPLY:0, MPLY_NUM)%的两腿之间，摆动腰在%ANAME(MPLY:0, MPLY_NUM)%的%LOCALS:1%秘唇上磨蹭肉棒
				;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0, MPLY_NUM)%の股の間に%LOCALS:2%ペニスを挟みこむと、腰を振って%ANAME(MPLY:0, MPLY_NUM)%の%LOCALS:1%秘唇に肉棒を擦り付けた
			ENDIF

		;第三者に主導権
		CASEELSE
			PRINTFORMW %ANAME(TFLAG:45)%命令%ANAME(MPLY:0, MPLY_NUM)%把%ANAME(MTAR:0)%的%BAR_NAME(MTAR:0)%夹在胯下摩擦…
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0, MPLY_NUM)%に対し、%ANAME(MTAR:0)%の%BAR_NAME(MTAR:0)%を股で挟んで擦るように命令した…
			PRINTFORM %ANAME(MPLY:0, MPLY_NUM)%把%ANAME(MTAR:0)%的%LOCALS:2%肉棒夹在胯下、
			;TranslatedMark %ANAME(MPLY:0, MPLY_NUM)%は%ANAME(MTAR:0)%の%LOCALS:2%ペニスを股に挟み込み、
			IF !HAS_VAGINA(MPLY:0)
				PRINTFORMW 把睾丸压在上面，开始摇动腰部摩擦肉棒…
				;TranslatedMark 玉袋を押し付けながら腰を振って竿を擦り上げた…
			ELSE
				PRINTFORMW 把%LOCALS:1%秘唇压在上面，开始摇动腰部摩擦肉棒…
				;TranslatedMark %LOCALS:1%秘唇を押し付けながら腰を振って竿を擦り上げた…
			ENDIF
	ENDSELECT
ELSE
	IF !HAS_VAGINA(MPLY:0)
		PRINTFORMW %ANAME(MTAR:0)%把%LOCALS:2%肉棒夹在%ANAME(MPLY:0, MPLY_NUM)%的大腿之间，摆动腰在%ANAME(MPLY:0, MPLY_NUM)%的睾丸上磨蹭肉棒
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0, MPLY_NUM)%の股の間に%LOCALS:2%ペニスを挟みこむと、腰を振って%ANAME(MPLY:0, MPLY_NUM)%の玉袋に肉棒を擦り付けた
	ELSE
		PRINTFORMW %ANAME(MTAR:0)%把%LOCALS:2%肉棒夹在%ANAME(MPLY:0, MPLY_NUM)%的大腿之间，摆动腰在%ANAME(MPLY:0, MPLY_NUM)%的%LOCALS:1%秘唇上磨蹭肉棒
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0, MPLY_NUM)%の股の間に%LOCALS:2%ペニスを挟みこむと、腰を振って%ANAME(MPLY:0, MPLY_NUM)%の%LOCALS:1%秘唇に肉棒を擦り付けた
	ENDIF
ENDIF
;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST13

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN13
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_13()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_13()
RETURN POWER(2, 性的嗜好_奉仕されたい)


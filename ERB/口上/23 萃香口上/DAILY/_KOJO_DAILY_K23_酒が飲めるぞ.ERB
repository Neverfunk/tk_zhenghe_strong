;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K23_ALCOHOL_RATE(対象)
#DIM 対象
RETURN 35

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K23_ALCOHOL_DECISION(対象)
#DIM 対象

SIF CFLAG:対象:特殊状態 != 特殊状態_放浪
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("霊夢"), NAME_TO_CHARA("華扇"), NAME_TO_CHARA("紫"), NAME_TO_CHARA("天子"), NAME_TO_CHARA("勇儀"))
	RETURN 0

;一度しか発生しないイベントならこんな感じ
SIF KDVAR:対象:萃香_アル中
	RETURN 0

;面識があると駄目
SIF CFLAG:対象:面識 == 1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K23_ALCOHOL_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K23_ALCOHOL(対象)
#DIM 対象

KDVAR:対象:萃香_アル中 = 1


PRINTFORMW 「喂，那边的人」
;TranslatedMark 「ちょっと、そこのアンタ」
PRINTFORML 当%ANAME(MASTER)%在街上巡回的时候，突然有人向我打招呼
;TranslatedMark %ANAME(MASTER)%が街を巡回していると、不意に声をかけられた
PRINTFORMW 一看，一位少女正盯着这边
;TranslatedMark 見ると、少女がこちらを見つめていた
CALL ICPRINT(@"%PRONOUN(対象)%は……確か<%NAME_FORMAL(対象)%>、幻想郷縁起にも掲載される有名な鬼だ。なぜこんなところに？", "W", カラー_注意)
PRINTFORMW 「呀，其实呢，现在我在流浪中，到处闲逛」
;TranslatedMark 「や、実はさ、今私は放浪中の身でさ、そこらへんぶらついてるんだけどさぁ」
PRINTFORMW 「已经没有钱了，酒也已经见底了……而且葫芦里的酒已经喝腻了，所以能请我喝酒吗？」
;TranslatedMark 「路銀もないのに酒が底をつきちゃって……瓢箪から出る酒は飽きちゃったし、そういうわけで、お酒持ってない？」
CALL ICPRINT("「そうだなぁ、<日本酒みっつ>、くれたらお礼するよ。嘘じゃないよ、なんせ私は鬼だからさ", "W", カラー_注意)
PRINTFORML ……我没想到会有被要求请喝酒的时候。而且理由是"没有钱"，真是太无情了
;TranslatedMark ……まさか、酒をたかられる日がこようとは思っていなかった。それも理由が「路銀がないから」とは、だいぶ情けない
PRINTFORML 但是，%PRONOUN(対象)%是鬼。是不会撒谎的
;TranslatedMark しかし、%PRONOUN(対象)%は鬼だ。嘘はつくまい
PRINTFORML 那么，怎么办呢……
;TranslatedMark さて、どうするか……

CALL ASK_MULTI_JUDGE("请她喝酒", ITEM:日本酒 >= 3, "不行", 1)

IF RESULT == 1
	PRINTFORMW 「是…是吗……嘛，没办法啊」
	;TranslatedMark 「そうか、そうかぁ……まぁ、仕方ないかね」
	PRINTFORMW 「那就拜托别人吧，不好意思打扰了」
	;TranslatedMark 「それじゃ、他の誰かに頼むとしよう、邪魔して悪かったよ」
	PRINTFORMW 只说了一句，%ANAME(対象)%便像雾一样消失了……
	;TranslatedMark 言うだけ言って、%ANAME(対象)%は霧のように消えてしまった……
	RETURN 1
ENDIF

PRINTFORMW 「真的吗！那我就不客气了」
;TranslatedMark 「さっすが！　それじゃあ早速いただくよ」
PRINTFORMW 说起来，%PRONOUN(対象)%还是第一次被请喝酒……
;TranslatedMark 言うなり、%PRONOUN(対象)%は酒盛りを初めてしまった……
PRINTFORML ・
PRINTFORML ・
PRINTFORML ・
PRINTFORML 茅台酒喝三瓶的话应该已经是相当高的酒量，但是%ANAME(対象)%却一口喝干了
;TranslatedMark 日本酒も三瓶ともなればなかなかのアルコール量になるはずだが、%ANAME(対象)%はあっさり飲み干してしまった
PRINTFORMW 果然是鬼啊……
;TranslatedMark やはり鬼ということか……
PRINTFORMW 「呼，喝了。还是茅台酒好啊」
;TranslatedMark 「ふう、飲んだ飲んだ。やっぱり日本酒はいいよ」
PRINTFORMW 「那么，我给你还礼吧。你想要什么？」
;TranslatedMark 「それじゃ、礼をしてやろうかな。何にする？」
PRINTFORML 那么，怎么办呢……
;TranslatedMark さて、どうしよう……
ITEM:日本酒 -= 3
CALL ASK_MULTI_JUDGE("想要力量", 1, "教我技巧", GET_EMPTY_SKILL_SLOT(MASTER, スキル_ジャンル_PERSONAL) != -1, "都说了没有钱啊！", 0, "成为伙伴", 1, "帮我国宣传", 1, "无欲无求", 1)
;TranslatedMark "想要力量", 1, "教我技巧", GET_EMPTY_SKILL_SLOT(MASTER, スキル_ジャンル_PERSONAL) != -1, "金は無いと言っていたな……", 0, "仲間になって", 1, "噂を広めて", 1, "いらない", 1

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「那样可以吗？这样是不是占了你的便宜…」
		;TranslatedMark 「そんなんでいいのかい？　お安い御用だよ」
		PRINTFORMW 被%ANAME(対象)%训练了……
		;TranslatedMark %ANAME(対象)%に訓練してもらった……
		CALL COLOR_PRINTW(@"%ANAME(MASTER)%的武斗上升了3", カラー_注意)
		;TranslatedMark @"%ANAME(MASTER)%の武闘が3上昇した！", カラー_注意
		ABL:MASTER:武闘 += 3
	CASE 1
		PRINTFORMW 「嗯？不需要吗？」
		;TranslatedMark 「ふうん？　まぁいいよ」
		PRINTFORMW 「那么，我来教你我的必杀技吧」
		;TranslatedMark 「それじゃ、私の必殺技を教えてあげるとしよう」
		PRINTFORMW 「你可要勤学苦练争取早日运用自如啊」
		;TranslatedMark 「うまいこと使いこなしなよ」
		CALL SKILL_LEARN_BY_NAME(MASTER, スキル_ジャンル_PERSONAL, NO:対象, "户隐山之投")
		CALL COLOR_PRINTW(@"%ANAME(MASTER)%学会了技能「户隐山之投」！", カラー_注意)
		;TranslatedMark @"%ANAME(MASTER)%はスキル「户隐山之投」を習得した！", カラー_注意
	CASE 2
		;死に分岐
	CASE 3
		PRINTFORMW 「啊？成为你的伙伴？」
		;TranslatedMark 「あん？　あんたの仲間？」
		PRINTFORMW 「嗯，算了，你是个好人，希望你有好事发生」
		;TranslatedMark 「ふーん、まぁいいよ、あんた親切だし、いいことがありそうだ」
		PRINTFORMW 「我的力量，可以充分使用哦」
		;TranslatedMark 「私の力、存分に使うといいよ」
		CALL COLOR_PRINTW(@"%ANAME(対象)%成为伙伴了", カラー_注意)
		;TranslatedMark @"%ANAME(対象)%が仲間になりました", カラー_注意
		CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
		CFLAG:対象:好感度 += 500
		CALL COLOR_PRINTL(@"%ANAME(対象)%的好感度上升了500", カラー_シアン)
		RETURN 1
	CASE 4
		PRINTFORMW 「传言？啊，也就是说想获得外交地位」
		;TranslatedMark 「噂？　ああ、つまり外交的地位を得たいわけだ」
		PRINTFORMW 「我会变成雾，我很擅长那样。等一下」
		;TranslatedMark 「私は霧になれるし、そういうのは大得意だよ。ま、ちょっと待ってな」
		CALL COLOR_PRINTW(@"%ANAME(対象)%传播了谣言、%ANAME(MASTER)%所属国的评价上升了", カラー_注意)
		;TranslatedMark @"%ANAME(対象)%が噂を広め、%ANAME(MASTER)%の評価が上昇しました", カラー_注意
		FOR LOCAL, 0, MAX_COUNTRY
			SIF IS_COUNTRY(LOCAL)
				CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, 100, -100)
		NEXT
	CASE 5
		PRINTFORMW 「啊？啊，是吗，那样的话还可以」
		;TranslatedMark 「へっ？　あ、そう、それならまぁいいけど」
		PRINTFORMW 「无欲无求吗？好奇怪啊」
		;TranslatedMark 「無欲だねぇ、変なの」
ENDSELECT
PRINTFORMW 「那么，我就用这个」
;TranslatedMark 「じゃ、私はこれで」
PRINTFORMW %ANAME(対象)%离开了……
;TranslatedMark %ANAME(対象)%は去っていった……
CFLAG:対象:面識 = 1

RETURN 1

;--------------基本函数----------------
@MIRACLE_ITEM_1_NAME
	RESULTS = 期货

@MIRACLE_ITEM_1_TEXT
	RESULTS = 诡术制作的小玩意,每期价格都会改变,使用则获得当前价位的资金。 PS.诡术的信徒也许会有优待？

;道具价格
@MIRACLE_ITEM_1_COST
	SIF MIRACLE_ITEM_FUTURES <= 0
		MIRACLE_ITEM_FUTURES = 1500
	RETURN MIRACLE_ITEM_FUTURES

;道具等级
@MIRACLE_ITEM_1_LEVEL
	RETURN 0

;最大持有个数
@MIRACLE_ITEM_1_MAX
	RETURN 999

;显示条件
@MIRACLE_ITEM_1_CHECK_SHOW
	RETURN 1

;其他购买条件(除价格\等级\个数外)
@MIRACLE_ITEM_1_CHECK_BUY
	RETURN 1

;购买效果
@MIRACLE_ITEM_1_BUY

;使用条件
@MIRACLE_ITEM_1_CHECK_USE
	RETURN 1

;使用效果
@MIRACLE_ITEM_1_USE
	SIF MIRACLE_ITEM_FUTURES <= 0
		MIRACLE_ITEM_FUTURES = 1500
	MONEY += MIRACLE_ITEM_FUTURES

;-----------------特殊-----------------
@MIRACLE_ITEM_1_TURNEND ;回合结束时调用函数
#DIM 随机数
	SIF MIRACLE_ITEM_FUTURES <= 0
		MIRACLE_ITEM_FUTURES = 1500
	随机数 = 100 * MIN((30 + DAY * 3),100) / 100
	随机数 = RAND:随机数
	IF RAND:100 + (MIRACLE_ITEM_FUTURES - 1500) / 30 - (CFLAG:MASTER:信仰神 == 2 && MIRACLE_ITEM_NUM:1) * 5 < 50
		IF MIRACLE_ITEM_FUTURES + 随机数 >= 3500
			MIRACLE_ITEM_FUTURES = 3500
			PRINTFORML 期货价格涨停了,当前价格{MIRACLE_ITEM_FUTURES}
		ELSE
			MIRACLE_ITEM_FUTURES += 随机数
			PRINTFORML 期货价格上涨了{随机数},当前价格{MIRACLE_ITEM_FUTURES}
		ENDIF
	ELSE
		IF MIRACLE_ITEM_FUTURES - 随机数 <= 300
			MIRACLE_ITEM_FUTURES = 300
			PRINTFORML 期货价格跌停了,当前价格{MIRACLE_ITEM_FUTURES}
		ELSE
			MIRACLE_ITEM_FUTURES -= 随机数
			PRINTFORML 期货价格下跌了{随机数},当前价格{MIRACLE_ITEM_FUTURES}
		ENDIF
	ENDIF
	

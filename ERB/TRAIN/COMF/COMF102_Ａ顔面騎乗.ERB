;Ａ顔面騎乗

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME102
LOCALS:0 = Ａ顔面騎乗

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE102
;共通部分
CALL COM_ABLE_COMMON(102)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;ターゲットがＰ挿入中かつ体位が騎乗位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ｐ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｐ"), 5)
	RETURN 0
;ターゲットがＶ挿入中かつ体位が正常位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｖ"), 1)
	RETURN 0
;ターゲットがＶ挿入中かつ体位が正常位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 1)
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;挿入中は不可
SIF IS_INSERT_SINGLE(MPLY:0, "全")
	RETURN 0
;尻コキ中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 15)
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが既に他の相手に顔面騎乗しているなら不可
SIF IS_RIDE(MPLY:0) && !(SEARCH_EQUIP(101, MPLY:0, MTAR:0) >= 0 || SEARCH_EQUIP(102, MPLY:0, MTAR:0) >= 0 || SEARCH_EQUIP(105, MPLY:0, MTAR:0) >= 0)
	RETURN 0
;ターゲットが既に他の相手に顔面騎乗されているなら不可
SIF IS_RIDDEN(MTAR:0) && !(SEARCH_EQUIP(101, MPLY:0, MTAR:0) >= 0 || SEARCH_EQUIP(102, MPLY:0, MTAR:0) >= 0 || SEARCH_EQUIP(105, MPLY:0, MTAR:0) >= 0)
	RETURN 0
;プレイヤーからターゲットに足舐め中なら不可
SIF SEARCH_EQUIP(104, MPLY:0, MTAR:0) >= 0
	RETURN 0
;ターゲットが顔面騎乗中なら不可
SIF IS_RIDE(MTAR:0)
	RETURN 0
;プレイヤーが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;ターゲットが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
;ターゲットがキス中なら不可
SIF IS_EQUIP(MTAR:0, 20)
	RETURN 0
;ターゲットが足舐め中なら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 104)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM102
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;クンニ、顔面騎乗、Ｗ顔面騎乗のフラグを解除
FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		CALL RELEASE_EQUIP_EX(2, MTAR:(LOCAL:1), -1)
		CALL RELEASE_EQUIP_EX(101, MPLY:(LOCAL:0), -1)
		CALL RELEASE_EQUIP_EX(105, MPLY:(LOCAL:0), -1)
	NEXT
NEXT

;●プレイヤーについて処理
SOURCE:(MPLY:0):液体追加 = 100
SOURCE:(MPLY:0):露出 = 300
SOURCE:(MPLY:0):逸脱 = 200
SOURCE:(MPLY:0):接触 = 60
SOURCE:(MPLY:0):性行動 = 150
SOURCE:(MPLY:0):快Ａ = SENSE_HOUSHI(MTAR:0, MPLY:0, 1000)
EXP:(MPLY:0):嗜虐経験値 += 1

;主導権がプレイヤー側にある場合
IF GET_COM_INITIATIVE() == 0
	SOURCE:(MPLY:0):嗜虐 = 130
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 120)

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 100

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):被虐経験値 += 1

SOURCE:(MTAR:0):奉仕 = SERVE_HOUSHI(MTAR:0, 300 + (GET_HIPSIZE(MPLY:0) >= 1 || TALENT:(MPLY:0):美臀) * 100)
SOURCE:(MTAR:0):逸脱 = 150
SOURCE:(MTAR:0):接触 = 60
SOURCE:(MTAR:0):性行動 = 150

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 140)

IF TALENT:(MTAR:0):舌頭霊活
	TIMES SOURCE:(MPLY:0):快Ａ, 1.50
ENDIF

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;アナルへのキス
CALL KISS_COMMON(MTAR:0, @"%ANAME(MPLY:0)%的尻穴", GET_SITUATION_BY_TRAIN_MODE())

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP102
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP102(ARG:0)
;●プレイヤーについて処理
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):液体追加 += 100
SOURCE:(LOCAL:2):露出 += 150
SOURCE:(LOCAL:2):逸脱 += 100
SOURCE:(LOCAL:2):接触 += 30
SOURCE:(LOCAL:2):性行動 += 50
EXP:(LOCAL:2):嗜虐経験値 += 1
IF HAS_VAGINA(LOCAL:2)
	SOURCE:(LOCAL:2):快Ａ += SENSE_HOUSHI(LOCAL:2, MEQUIP_TARGET:(ARG:0):0, 500)
ENDIF

;倒錯度変化
TCVAR:(LOCAL:2):50 += 2

;●ターゲットについて処理
LOCAL:2 = MEQUIP_TARGET:(ARG:0):0

DOWNBASE:(LOCAL:2):体力 += 10

EXP:(LOCAL:2):性技経験値 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
SOURCE:(LOCAL:2):逸脱 += 80
SOURCE:(LOCAL:2):接触 += 30
SOURCE:(LOCAL:2):性行動 += 50
EXP:(LOCAL:2):被虐経験値 += 1

;倒錯度変化
TCVAR:(LOCAL:2):50 += 2

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE102(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%顔面騎乗中(Ａ)
;RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%に顔面騎乗中(Ａ)

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP102(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%跨在了%EQUIP_TARGET_ANAME(ARG:0)%的脸上…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%の顔の上に跨っている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP102(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%把腰从%EQUIP_TARGET_ANAME(ARG:0)%的脸上抬了起来…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の顔から腰を上げた…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER102(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF
IF TALENT:(ARG:0):討厭女人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭女人", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE102
LOCALS:0 =
SELECTCASE CUM_CUR_AMOUNT:(MPLY:0):射精部位_アナル
	CASE IS >= 15
	    LOCALS:0 = 精液%SPLIT_R("沾满的：充满的：溢出的：不断流出的")%
		;LOCALS:0 = 精液%SPLIT_R("まみれの：の詰まった：の溢れる：が留処なくこぼれる")%
	CASE IS >= 1
	    LOCALS:0 = 精液%SPLIT_R("垂下来的：漏出来的")%
		;LOCALS:0 = 精液%SPLIT_R("を垂らす：の漏れる")%
ENDSELECT
LOCALS:0 = %LOCALS:0%%STR_BODY("アナル", MPLY:0)%



SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF MPLY:0 == MASTER
			PRINTFORMW %ANAME(MPLY:0)%跨坐在%ANAME(MTAR:0)%的脸上，慢慢地把%LOCALS:0%尻穴了压了上去…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の顔に跨り、%LOCALS:0%をグリグリと押し付けた…
		ELSEIF ABL:(MPLY:0):加虐 >= 3
			PRINTFORMW %ANAME(MPLY:0)%带着淫乱的笑容跨在%ANAME(MTAR:0)%的脸上，将%LOCALS:0%尻穴用力的压到%ANAME(MTAR:0)%的脸上…
			;TranslatedMark %ANAME(MPLY:0)%はサディスティックな笑みを浮かべながら%ANAME(MTAR:0)%の顔に跨り、%LOCALS:0%をグリグリと押し付け%TARGET_STR("た", "てきた")%…
		ELSEIF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_淫乱)
			PRINTFORMW %ANAME(MPLY:0)%跨在%ANAME(MTAR:0)%的脸上，为了贪图快感而将%LOCALS:0%尻穴不断的压向%ANAME(MTAR:0)%的嘴边…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の顔に跨り、快感を貪るように%LOCALS:0%をグリグリと押し付け%TARGET_STR("た", "てきた")%…
		ELSEIF !TALENT:(MPLY:0):不知羞恥 && ABL:(MPLY:0):露出 < 3
			PRINTFORMW %ANAME(MPLY:0)%在感到羞耻同时，跨上%ANAME(MTAR:0)%的脸，将%LOCALS:0%尻穴压到了%ANAME(MTAR:0)%的嘴边…
			;TranslatedMark %ANAME(MPLY:0)%は恥じらうようにしながらも、%ANAME(MTAR:0)%の顔に跨って%LOCALS:0%を押し付け%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%跨在%ANAME(MTAR:0)%的脸上，慢慢地把%LOCALS:0%尻穴推向了%ANAME(MTAR:0)%的嘴边…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の顔に跨り、%LOCALS:0%をグリグリと押し付け%TARGET_STR("た", "てきた")%…
		ENDIF
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %ANAME(MTAR:0)%让%ANAME(MPLY:0)%跨在脸上，舌头伸进肛门，在%ANAME(MTAR:0)%%LOCALS:0%尻穴里面咕噜咕噜地挖着…
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を顔の上に跨らせると、アナルに舌を入れて内部をグリグリと抉っ%PLAYER_STR("た", "てきた")%…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%跨在%ANAME(MTAR:0)%的脸上强行压上%LOCALS:0%尻穴
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%の顔に跨って%LOCALS:0%を押し付けるように命令した
		IF !TALENT:(MPLY:0):不知羞恥 && ABL:(MPLY:0):露出 < 3
			PRINTFORMW 虽然%ANAME(MPLY:0)%很害羞，但还是按照她说的那样跨上了%ANAME(MTAR:0)%的脸，将%LOCALS:0%尻穴强行压给了%ANAME(MTAR:0)%…
			;TranslatedMark %ANAME(MPLY:0)%は恥ずかしがりながらも、言われるまま%ANAME(MTAR:0)%の顔に跨り%LOCALS:0%を押し付けた…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%就这样，跨上%ANAME(MTAR:0)%的脸，把%LOCALS:0%尻穴强压给了她…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%の顔に跨り%LOCALS:0%を押し付けた…
		ENDIF
ENDSELECT

IF CUM_CUR_AMOUNT:(MPLY:0):射精部位_アナル
	PRINTFORM %SPLIT_RAND("炙热的/粘稠的/浓厚的/新鲜的", 1)%精液
	;TranslatedMark %SPLIT_RAND("熱い/粘つく/濃厚な/新鮮な", 1)%精液が

	IF ABL:(MTAR:0):精愛 >= 4
		PRINTFORML 一滴到%SPLIT_RAND("喜欢精液的/爱上精液的/渴求精液的", 1)%%ANAME(MTAR:0)%嘴里，%PRONOUN(MTAR:0)%就发出了喜悦的呻吟声…
		;TranslatedMark %SPLIT_RAND("精液好きの/精愛味覚を得た/精液に飢えた", 1)%%ANAME(MTAR:0)%の口に滴ると、%PRONOUN(MTAR:0)%は悦びの呻き声を漏らした…
	ELSE
		PRINTFORML 虽然滴到了%ANAME(MTAR:0)%的口中，%PRONOUN(MTAR:0)%却不得不继续为之服务…
		;TranslatedMark %ANAME(MTAR:0)%の口に滴るが、%PRONOUN(MTAR:0)%は奉仕を余儀なくされた…
	ENDIF
	;target eats 1/4th + 3 cum, up to 20
	LOCAL = LIMIT(MIN(((CUM_CUR_AMOUNT:(MPLY:0):射精部位_アナル / 4) + 3), CUM_CUR_AMOUNT:(MPLY:0):射精部位_アナル) , 0, 20)
	CUM_CUR_AMOUNT:(MPLY:0):射精部位_アナル -= LOCAL
	CUM_CUR_AMOUNT:(MTAR:0):射精部位_口 += LOCAL
	SIF CUM_CUR_AMOUNT:(MPLY:0):射精部位_アナル == 0
		PRINTFORML %ANAME(MPLY:0)%%STR_BODY("アナル", MPLY:0)%的精液全部被%ANAME(MTAR:0)%喝干了
		;TranslatedMark %ANAME(MPLY:0)%の%STR_BODY("アナル", MPLY:0)%の精液は全て%ANAME(MTAR:0)%が飲み干した
	;Cumlove exp increases by amount of cum
	EXP:(MTAR:0):精愛経験値 += LOCAL
	;Swallowing exp
	SIF ABL:(MTAR:0):精愛 >= 4
		EXP:(MTAR:0):口内射精経験 ++
	WAIT
ENDIF
;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST102

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN102
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_102()
RETURN POWER(2, 性的嗜好_辱めたい) + POWER(2, 性的嗜好_虐げられたい)

@COM_PREFERENCE_TARGET_102()
RETURN POWER(2, 性的嗜好_辱められたい) + POWER(2, 性的嗜好_虐げられたい)


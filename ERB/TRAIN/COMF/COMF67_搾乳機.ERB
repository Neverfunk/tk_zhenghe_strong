;搾乳機

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME67
LOCALS:0 = 搾乳機

RESULTS:0 = %LOCALS:0%责弄
;RESULTS:0 = %LOCALS:0%で責める
RESULTS:1 = %LOCALS:0%奉仕
;RESULTS:1 = %LOCALS:0%で奉仕する
RESULTS:2 = %LOCALS:0%奉仕(使役)
;RESULTS:2 = %LOCALS:0%で奉仕させる
RESULTS:3 = %LOCALS:0%责弄(被动)
;RESULTS:3 = %LOCALS:0%で責められる
RESULTS:4 = %LOCALS:0%责弄(使役)
;RESULTS:4 = %LOCALS:0%で責めさせる
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE67
;共通部分
CALL COM_ABLE_COMMON(67)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;搾乳機が必要
SIF !ITEM:搾乳機 && !ITEM:A_搾乳機
	RETURN 0

FOR LOCAL, 0, MTAR_NUM
	;母乳体質が必要
	SIF !TALENT:(MTAR:LOCAL):母乳体質
		RETURN 0
	SIF IS_N_HOLD(MTAR:LOCAL)
		RETURN 0
	SIF !CAN_REACH_BODY(MPLY:0, MTAR:0) && !FLAG:RECHECKING
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM67
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 60

EXP:(MPLY:0):性技経験値 += MTAR_NUM / 2 + 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):接触 = 10
SOURCE:(MPLY:0):性行動 = 90

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 20)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

FOR LOCAL, 0, MTAR_NUM
	LOCAL:2 = MTAR:LOCAL
	;●ターゲットについて処理
	DOWNBASE:(LOCAL:2):体力 += 60

	SOURCE:(LOCAL:2):露出 = 50
	SOURCE:(LOCAL:2):逸脱 = 120
	SOURCE:(LOCAL:2):接触 = 10
	SOURCE:(LOCAL:2):性行動 = 180
	IF TALENT:(MPLY:0):技師
		SOURCE:(LOCAL:2):快Ｂ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 900)
		SOURCE:(LOCAL:2):搾乳 = SENSE_HOUSHI(MPLY:0, LOCAL:2, 1000)
	ELSE
		SOURCE:(LOCAL:2):快Ｂ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 600)
		SOURCE:(LOCAL:2):搾乳 = SENSE_HOUSHI(MPLY:0, LOCAL:2, 700)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 20)

NEXT
;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP67
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP67(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
SOURCE:(LOCAL:2):性行動 += 30

FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)
	IF TALENT:(LOCAL:2):技師
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600)
		SOURCE:(LOCAL:3):搾乳 += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600)
	ELSE
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400)
		SOURCE:(LOCAL:3):搾乳 += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400)
	ENDIF
	SOURCE:(LOCAL:3):露出 += 20
	SOURCE:(LOCAL:3):逸脱 += 50
	SOURCE:(LOCAL:3):性行動 += 60
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE67(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%吸附着搾乳机中
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%に搾乳機を装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP67(ARG:0)
PRINTFORML 榨乳机从%EQUIP_TARGET_ANAME(ARG:0)%身体里榨取母乳…
;TranslatedMark 搾乳機が%EQUIP_TARGET_ANAME(ARG:0)%から母乳を吸い上げている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP67(ARG:0)
PRINTFORMW 取下了挤取%EQUIP_TARGET_ANAME(ARG:0)%母乳的榨乳机
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の母乳を搾り取る搾乳機を外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER67(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF


IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_雌牛)
	CALL COM_ORDER_ELEMENT(ARG:0, "雌牛", 10)
ENDIF


RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE67
IF IS_PIERCED(MTAR:0, ピアス_乳首)
	LOCALS:0 = 上著環的
ELSE
	LOCALS:0 = 
ENDIF

SELECTCASE GET_BUSTSIZE(MTAR:0)
	CASE -2, -1
		LOCALS:0 = 虽然小巧但充满奶水的乳房
		;TranslatedMark 小ぶりながらもミルクを蓄えた乳房
	CASE 0
		LOCALS:0 = 大小适中且充满了奶水的乳房
		;TranslatedMark 程よいサイズのミルクを蓄えた乳房
	CASE 1
		LOCALS:0 = 充满着实在分量奶水的丰盈乳房
		;TranslatedMark たわわに実るミルクを蓄えた乳房
	CASE 2
		LOCALS:0 = 蕴含着都从乳头溢出奶水的丰满乳房
		;TranslatedMark はち切れんばかりのミルクを蓄えた乳房
ENDSELECT

IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%们
	;TranslatedMark %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%在%LOCALS:1%%LOCALS:0%上安装了榨乳机…
		;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:1%の%LOCALS:0%に搾乳機を取り付け%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:1%让%ANAME(MPLY:0)%握住榨乳机，安装在自己%LOCALS:0%上…
		;TranslatedMark %LOCALS:1%は%ANAME(MPLY:0)%に搾乳機を握らせ、自身の%LOCALS:0%に取り付けさせた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%给%LOCALS:1%安装榨乳机
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:1%に搾乳機を取り付けるように命令した
		PRINTFORMW %ANAME(MPLY:0)%在%LOCALS:1%的%LOCALS:0%上安装了榨乳机…
		;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:1%の%LOCALS:0%に搾乳機を取り付けた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST67

FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	SIF LOCAL:1 == MASTER && !CONFIG:441
		CONTINUE
	IF GETBIT(TALENT:(LOCAL:1):淫乱系, 素質_淫乱_雌牛)
		PRINTFORML  已经变成了母牛的%ANAME(LOCAL:1)%，只是刚被安装了挤奶机就从乳头滴下了母乳…
		;TranslatedMark 雌牛に成り下がった%ANAME(LOCAL:1)%は、搾乳機を取り付けられただけで乳首から母乳を垂らしている…
	ENDIF
NEXT

;-------------------------------------------------
;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN67
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_67()
RETURN POWER(2, 性的嗜好_道具を使いたい) + POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_67()
RETURN POWER(2, 性的嗜好_道具を使われたい) + POWER(2, 性的嗜好_虐げられたい)


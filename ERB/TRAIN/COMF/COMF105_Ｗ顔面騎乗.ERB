;Ｗ顔面騎乗

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME105
LOCALS:0 = Ｗ顔面騎乗

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE105
;共通部分
CALL COM_ABLE_COMMON(105)
SIF RESULT == 0
	RETURN 0
;プレイヤーが2人のとき限定
SIF MPLY_NUM != 2
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;ターゲットがＰ挿入中かつ体位が騎乗位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ｐ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｐ"), 5)
	RETURN 0
;ターゲットがＶ挿入中かつ体位が正常位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｖ"), 1)
	RETURN 0
;ターゲットがＶ挿入中かつ体位が正常位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 1)
	RETURN 0
;target ridden or riding
SIF IS_RIDE(MTAR:0) || IS_RIDDEN(MTAR:0)
	RETURN 0
;target's mouth occupied
SIF IS_M_HOLD(MTAR:0)
	RETURN 0

;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;挿入中は不可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "全")
		RETURN 0
	;尻コキ中なら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 15)
		RETURN 0
    ;foam play with target
    SIF SEARCH_EQUIP_IC(5, MPLY:(LOCAL:0), MTAR:0) >= 0
		RETURN 0
	;player ridden or already riding
	SIF IS_RIDDEN(MPLY:(LOCAL:0)) || IS_RIDE(MPLY:(LOCAL:0))
			RETURN 0
	;プレイヤーからターゲットに足舐め中なら不可
	SIF SEARCH_EQUIP(104, MPLY:(LOCAL:0), MTAR:0) >= 0
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM105
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	SOURCE:(LOCAL:2):露出 = 200
	SOURCE:(LOCAL:2):逸脱 = 150
	SOURCE:(LOCAL:2):接触 = 40
	SOURCE:(LOCAL:2):性行動 = 150
	EXP:(LOCAL:2):嗜虐経験値 += 1

	;主導権がプレイヤー側にある場合
	IF GET_COM_INITIATIVE() == 0
		SOURCE:(MPLY:0):嗜虐 = 130
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 120)
NEXT

;●ターゲットについて処理
EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):被虐経験値 += 1

SOURCE:(MTAR:0):奉仕 = SERVE_HOUSHI(MTAR:0, 300)
SOURCE:(MTAR:0):逸脱 = 150
SOURCE:(MTAR:0):接触 = 60
SOURCE:(MTAR:0):性行動 = 150

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 140)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP105
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP105(ARG:0)
;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	SOURCE:(LOCAL:2):露出 += 100
	SOURCE:(LOCAL:2):逸脱 += 80
	SOURCE:(LOCAL:2):接触 += 20
	SOURCE:(LOCAL:2):性行動 += 50
	EXP:(LOCAL:2):嗜虐経験値 += 1

	;倒錯度変化
	TCVAR:(LOCAL:2):50 += 2
NEXT

;●ターゲットについて判定
LOCAL:2 = MEQUIP_TARGET:(ARG:0):0

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):被虐経験値 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
SOURCE:(LOCAL:2):逸脱 += 80
SOURCE:(LOCAL:2):接触 += 30
SOURCE:(LOCAL:2):性行動 += 50

;倒錯度変化
TCVAR:(LOCAL:2):50 += 2

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE105(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%顔面騎乗中(尻)

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP105(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%跨在了%EQUIP_TARGET_ANAME(ARG:0)%的脸上…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%の顔の上に跨っている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP105(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%从%EQUIP_TARGET_ANAME(ARG:0)%的脸上抬起了腰…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の顔から腰を上げた…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER105(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF
IF TALENT:(ARG:0):討厭女人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭女人", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE105
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%跨在%ANAME(MTAR:0)%的脸上，仿佛要压坏一样将屁股压了上去…
		;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は%ANAME(MTAR:0)%の顔に跨り、二人分のお尻で押し潰すように座っ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0)%命令%ANAME(MPLY:0)%和%ANAME(MPLY:1)%两个人跨在脸上把屁股推给她
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、二人で顔の上に跨って尻を押し付けるように命令した
		PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%就这样坐在%ANAME(MTAR:0)%的脸上，把两个人的屁股推给了她…
		;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は言われるまま%ANAME(MTAR:0)%の顔の上に座り込み、二人分のお尻を押し付けた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MPLY:1)%坐在%ANAME(MTAR:0)%的脸上
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に対して、%ANAME(MTAR:0)%の顔の上に座るように命令した
		PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MPLY:1)%就这样跨在%ANAME(MTAR:0)%的脸上，把两个人的屁股推给了她…
		;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は言われるまま%ANAME(MTAR:0)%の顔の上に跨り、二人分のお尻を押し付けた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST105

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN105
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_105()
RETURN POWER(2, 性的嗜好_辱められたい)

@COM_PREFERENCE_TARGET_105()
RETURN POWER(2, 性的嗜好_辱めたい)


@K36_NEVERMIND_EVENT_6_EXIST
    RETURN 1

@K36_NEVERMIND_EVENT_6_RATE
    RETURN 1 + (DAY - K36_和四季的日常_主事件_上次触发日期:6)*7

@K36_NEVERMIND_EVENT_6_STATUS
    RETURN 1

@K36_NEVERMIND_EVENT_6
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"%ANAME(MASTER)%和%ANAME(四季_ID)%一起在街上巡逻")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"%ANAME(四季_ID)%拉着%ANAME(MASTER)%到街上去巡逻")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"%ANAME(MASTER)%被%ANAME(四季_ID)%带着前往街上巡逻",1,1)
    CALL K36_NEVERMIND_EVENT_6_CALLEVENT
@K36_NEVERMIND_EVENT_6_CALLEVENT
    #DIM LAST_EVENT = 0
    
    [IF_DEBUG]
    REPEAT 100
        LAST_EVENT = IFRAND("1TO5",1,@"D{LAST_EVENT}",1)
        TRYCALLFORM K36_NEVERMIND_EVENT_6_{LAST_EVENT}
    REND
    [ENDIF]

    LAST_EVENT = IFRAND("1TO5",1,@"D{LAST_EVENT}",1)
    TRYCCALLFORM K36_NEVERMIND_EVENT_6_{LAST_EVENT}
    CATCH
        CALL K36_NEVERMIND_EVENT_6_1
        [IF_DEBUG]
            PRINTFORMW EVENT_6 事件ID{LAST_EVENT}错误
        [ENDIF]
    ENDCATCH
@K36_NEVERMIND_EVENT_6_1
    #DIM CONST ABL_NEED_TYPE = 50,51
    #DIMS CONST ABL_NEED_NAME = "武闘","防衛"
    #DIM ABL_NEED_NUM = 2
    #DIM ABL_TARGET
    SELECTCASE RAND:3
        CASE 0
            CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_生气,四季_额外_清空,四季_额外_生气)
            CALL K36_NORMAL_SENTENCE_IMAGE(@"在街上遇到了正在施暴的劫匪！")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「来吧！%K36_CALL_MASTER()%！」")
            CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%召唤着%ANAME(MASTER)%一起参加战斗",1)
        CASE 1
            CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_担忧,四季_额外_清空,四季_额外_生气)
            CALL K36_NORMAL_SENTENCE_IMAGE(@"在街上被劫匪偷袭了！")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「小心！%K36_CALL_MASTER()%！」")
            CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%提醒着%ANAME(MASTER)%",1)
        CASE 2
            CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_维持上次,四季_额外_清空,四季_额外_生气)
            CALL K36_NORMAL_SENTENCE_IMAGE(@"在街上看见了正在逃窜的流匪")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「我们追上去！」")
            CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%和%ANAME(MASTER)%一起追了上去",1)
    ENDSELECT
    ABL_TARGET = ABL_NEED_TYPE:(RAND:ABL_NEED_NUM)
    IF (ABL:四季_ID:ABL_TARGET + ABL:MASTER:ABL_TARGET) / 2 > 75 + RAND:25
        CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(MASTER)%和%ANAME(四季_ID)%快速地击败了歹徒")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"在简单的审问后从领头的劫匪那里得知了她们的据点")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"两人带着部队突袭了那里，彻底打掉了这个据点")
        CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"两人受到了国内很高的评价")
        CALL K36_NEVERMIND_ADD_RELATION_CTO(四季_ID,CFLAG:MASTER:所属)
        CALL K36_NEVERMIND_ADD_RELATION_CTO(MASTER,CFLAG:MASTER:所属)
        CALL K36_ADD_四季好感度(3)
        CALL K36_NEVERMIND_EVENT_6_SUCCESS
    ELSE
        CALL K36_NORMAL_SENTENCE_IMAGE(@"陷入了苦战！")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"……")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"勉强击败了敌人",1)
        CALL K36_NORMAL_SENTENCE_IMAGE(@"但她们的头目还是跑掉了",1)
        CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_担忧,四季_额外_清空,四季_额外_生气)
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「是我的问题呢……」")
        SIF ABL:四季_ID:ABL_TARGET > ABL:MASTER:ABL_TARGET
            CALL K36_NORMAL_SENTENCE_IMAGE(@"虽然%ANAME(四季_ID)%这么说，但是%ANAME(MASTER)%也明白是自己的问题",1)
        CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_普通,四季_额外_清空,四季_额外_生气)
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「下次……一定要成功！」")
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「好好准备吧！」")
        CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"%ANAME(MASTER)%被%ANAME(四季_ID)%拉着进行了训练")
        CALL K36_NEVERMIND_ADD_ABL(四季_ID,ABL_TARGET)
        CALL K36_NEVERMIND_ADD_ABL(MASTER,ABL_TARGET)
        CALL K36_ADD_四季好感度(1)
    ENDIF
@K36_NEVERMIND_EVENT_6_2
    #DIM CONST ABL_NEED_TYPE = 52
    #DIMS CONST ABL_NEED_NAME = "智略"
    #DIM ABL_NEED_NUM = 1
    #DIM ABL_TARGET
    #DIM P_PERSON
    CALL K36_NORMAL_SENTENCE_IMAGE(@"两人遇到了诉苦的民众")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为工作问题在苦恼")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为丈夫的安危在担忧")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为父母的问题在苦恼")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为前线战事在担忧")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为退伍金的问题在抱怨")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为战友的情况而担忧")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是因为对上司的不满而在发泄",1,1)
    ABL_TARGET = ABL_NEED_TYPE:(RAND:ABL_NEED_NUM)
    IF ABL:四季_ID:ABL_TARGET > ABL:MASTER:ABL_TARGET
        P_PERSON = 四季_ID
    ELSE
        P_PERSON = MASTER
    ENDIF
    CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(P_PERSON)%试着%S_TEXT("安抚/安慰")%了她")
    IF ABL:P_PERSON:ABL_TARGET > 60 + RAND:25
        CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_开心,四季_额外_清空)
        CALL KOJO_36_IMAGE
        CALL K36_NORMAL_SENTENCE_IMAGE(@"效果非常好！")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"她平静了下来，%S_TEXT("满意/高兴/开心")%地离开了",1)
        CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"两人受到了国内很高的评价")
        CALL K36_NEVERMIND_ADD_RELATION_CTO(四季_ID,CFLAG:MASTER:所属)
        CALL K36_NEVERMIND_ADD_RELATION_CTO(MASTER,CFLAG:MASTER:所属)
        CALL K36_ADD_四季好感度(3)
        CALL K36_NEVERMIND_EVENT_6_SUCCESS
    ELSE
        CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_担忧,四季_额外_清空)
        CALL KOJO_36_IMAGE
        CALL K36_NORMAL_SENTENCE_IMAGE(@"效果不太好……")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"她%S_TEXT("生气/郁郁/难过")%地离开了……",1)
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「唔……真是的！」")
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「明明都这样劝了！」")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%有些自责地说到")
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「回去要好好练习一下怎么%S_TEXT("安抚/安慰")%别人！」")
        CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"%ANAME(MASTER)%被%ANAME(四季_ID)%拉着练习如何劝说他人。")
        CALL K36_NEVERMIND_ADD_ABL(四季_ID,ABL_TARGET)
        CALL K36_NEVERMIND_ADD_ABL(MASTER,ABL_TARGET)
        CALL K36_ADD_四季好感度(1)
    ENDIF

@K36_NEVERMIND_EVENT_6_3
    #DIM CONST ABL_NEED_TYPE = 61
    #DIMS CONST ABL_NEED_NAME = "料理"
    #DIM ABL_NEED_NUM = 1
    #DIM ABL_TARGET
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"遇到了因为饥荒而逃难的灾民！")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"遇到了正在讨饭的乞丐！")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"遇到了饿到昏迷的人！")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"遇到了营养不良的孩子们！",1,1)
    CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%和%ANAME(MASTER)%赶紧为%S_TEXT("她们/她")%准备了食物")
    ABL_TARGET = ABL_NEED_TYPE:(RAND:ABL_NEED_NUM)
    IF (ABL:四季_ID:ABL_TARGET + ABL:MASTER:ABL_TARGET) / 2 > 50 + RAND:25
        SELECTCASE RAND:2
            CASE 0
                CALL K36_NORMAL_SENTENCE_IMAGE(@"接受了食物的她们流着泪表示很久没有吃到如此美味了")
                CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"吃饱后的她们决定加入我军")
                CALL K36_NEVERMIND_ADD_RANDCHARA(MASTER)
            CASE 1
                CALL K36_NORMAL_SENTENCE_IMAGE(@"接受了食物的她流着泪表示很久没有吃到如此美味了")
                CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"吃饱后的她决定来我军帮忙")
                CALL K36_NEVERMIND_ADD_SOLDIER(MASTER)
        ENDSELECT
        CALL K36_ADD_四季好感度(3)
        CALL K36_NEVERMIND_EVENT_6_SUCCESS
    ELSE
        CALL K36_NORMAL_SENTENCE_IMAGE(@"虽然味道不算上乘，但也能填饱肚子")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"表示感谢后，她们离开了",1)
        CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_担忧,四季_额外_清空)
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「看起来……不够好呢！」")
        CALL K36_SHIKIEIKI_SAY_IMAGE(@"「回去要好好练习一下料理了！」")
        CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"%ANAME(MASTER)%被%ANAME(四季_ID)%拉着练习了料理！")
        CALL K36_NEVERMIND_ADD_ABL(四季_ID,ABL_TARGET)
        CALL K36_NEVERMIND_ADD_ABL(MASTER,ABL_TARGET)
        CALL K36_ADD_四季好感度(1)
    ENDIF

@K36_NEVERMIND_EVENT_6_4
    CALL K36_NORMAL_SENTENCE_IMAGE(@"两人来到街上，看到了一位卖花的女孩。")
    CALL K36_NORMAL_SENTENCE_IMAGE(@"「请让我送%K36_CALL_映姬()%一朵花吧！」",1)
    CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(MASTER)%为%K36_CALL_映姬()%挑选了一朵花",1)
    SELECTCASE RAND:4 + 1
        CASE 1
            CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(MASTER)%买下了一朵樱花送给%ANAME(四季_ID)%")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「不是不喜欢……幸好不是紫色。」")
        CASE 2
            CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(MASTER)%买下了一朵向日葵送给%K36_CALL_映姬()%")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「不太适合我呢……喂瓜子什么的至少得炒熟了再吃吧！」")
        CASE 3
            CALL K36_NORMAL_SENTENCE_IMAGE(@"是一朵彼岸花")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「……有一种在异国见故人的感觉」")
        CASE 4
            CALL K36_NORMAL_SENTENCE_IMAGE(@"机灵鬼一样的%ANAME(MASTER)%买下了一张纸……",1)
            CALL K36_NORMAL_SENTENCE_IMAGE(@"然后叠成了一朵川崎玫瑰送给%ANAME(四季_ID)%")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「这种花可没法寄宿灵魂呢！」")
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「不过是%ANAME(MASTER)%亲手叠出来的花……有一种别样的美……」")
    ENDSELECT
    CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%开心地把花别在了胸口，两人继续巡逻……")
    CALL K36_ADD_四季好感度()

@K36_NEVERMIND_EVENT_6_5
    #DIM CONST ABL_NEED_TYPE = 53
    #DIMS CONST ABL_NEED_NAME = "政治"
    #DIM ABL_NEED_NUM = 1
    #DIM ABL_TARGET
    CALL K36_NORMAL_SENTENCE_IMAGE(@"突然被当地的官员拉住了")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是有她解决不了的问题")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是有一个困扰了很久的问题")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是有一个复杂的问题亟待解决")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是有一个牵扯多方的问题难以解决")
    CALL K36_NORMAL_PRINTDATA_IMAGE(@"似乎是有一个历史久远的问题需要解决",1,1)
    ABL_TARGET = ABL_NEED_TYPE:(RAND:ABL_NEED_NUM)
    IF (ABL:四季_ID:ABL_TARGET + ABL:MASTER:ABL_TARGET) / 2 > 50 + RAND:25
        CALL K36_NORMAL_SENTENCE_IMAGE(@"%ANAME(四季_ID)%和%ANAME(MASTER)%很干脆地解决了这个问题")
        CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"当地的经济增加了！")
        CALL K36_ADD_四季好感度(3)
        CALL K36_NEVERMIND_EVENT_6_SUCCESS
    ELSE
        CALL SET_SHIKIEIKI_FACE(四季_衣服_常服,四季_感情_生气,四季_额外_清空)
        CALL KOJO_36_IMAGE
        CALL K36_NORMAL_SENTENCE_IMAGE(@"遗憾的是，并没能解决这个问题")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"当地的官员摆着手表示没事")
        CALL K36_NORMAL_SENTENCE_IMAGE(@"但是%ANAME(四季_ID)%还是很内疚")
        CALL K36_一段时间后_IMAGE
        CALL K36_NORMAL_SENTENCE_IMAGE(@"回到据点后，两人通宵研究解决问题的方法……")
        CALL K36_NEVERMIND_ADD_ABL(四季_ID,ABL_TARGET)
        CALL K36_NEVERMIND_ADD_ABL(MASTER,ABL_TARGET)
        CALL K36_ADD_四季好感度(1)
    ENDIF

@K36_NEVERMIND_EVENT_6_SUCCESS
    SELECTCASE RAND:4 + 1
        CASE 1
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「干得漂亮！」")
        CASE 2
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「真厉害呢%ANAME(MASTER)%！」")
        CASE 3
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「幸亏有%ANAME(MASTER)%呢。」")
        CASE 4
            CALL K36_SHIKIEIKI_SAY_IMAGE(@"「辛苦你了%ANAME(MASTER)%」")
    ENDSELECT
    CALL K36_NORMAL_SENTENCE_IMAGE(@"事情完美解决后，%ANAME(四季_ID)%开心地称赞着%ANAME(MASTER)%")

@K36_NEVERMIND_ADD_RELATION_CTO(CHARA_ID,COUNTRY_ID = -1)
    #DIM CHARA_ID
    #DIM COUNTRY_ID
    SIF COUNTRY_ID == -1
        COUNTRY_ID = CFLAG:MASTER:所属
    CALL CHANGE_RELATION_C_TO_O(COUNTRY_ID, CHARA_ID, RAND(50,100), -1*RAND(25,50))

@K36_NEVERMIND_ADD_RELATION_CTO_ALL(CHARA_ID)
    #DIM CHARA_ID
	FOR LOCAL, 0, MAX_COUNTRY
		SIF IS_COUNTRY(LOCAL) && !IS_SP_COUNTRY(LOCAL)
            CALL K36_NEVERMIND_ADD_RELATION_CTO(CHARA_ID,LOCAL)
	NEXT

@K36_NEVERMIND_ADD_ABL(CHARA_ID,ABL_TYPE = -1)
    #DIM CHARA_ID
    #DIM ABL_TYPE
    SELECTCASE RAND:100 + TALENT:CHARA_ID:強運 * 20
	    CASE 0 TO 49
		    LOCAL:1 = 1
	    CASE 50 TO 89
		    LOCAL:1 = 2
		CASE IS >= 90
		    LOCAL:1 = 3
	ENDSELECT
    IF !GROUPMATCH(ABL_TYPE,50,51,52,53,60,61)
        ABL_TYPE = IFRAND("50,51,52,53,60,61",1)
    ENDIF
    ABL:CHARA_ID:ABL_TYPE += LOCAL:1
    CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"%ANAME(CHARA_ID)%的%ABLNAME:ABL_TYPE%提高了{LOCAL:1}、变成了{ABL:CHARA_ID:ABL_TYPE}！")

@K36_NEVERMIND_ADD_SOLDIER(CHARA_ID)
    #DIM CHARA_ID
    #DIM COUNTRY_ID
    COUNTRY_ID = CFLAG:CHARA_ID:所属
    IF !IS_COUNTRY(COUNTRY_ID)
        ;#; THROW COUNTRY_ID{COUNTRY_ID}不是合法国家
        RETURN
    ENDIF
    LOCAL:1 = RAND(MIN(DAY * 100, 5000), MIN(DAY * 1000, 10000))
	COUNTRY_SOLDIER:(COUNTRY_ID) += LOCAL:1
    CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"%ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%的兵数増加了{LOCAL:1}")

@K36_NEVERMIND_ADD_ECONOMY(CHARA_ID)
    #DIM CHARA_ID
    #DIM COUNTRY_ID
    #DIM DYNAMIC OWN_CITY_LIST,MAX_CITY
    #DIM DYNAMIC OWN_CITY_NUM
    #DIM DYNAMIC CITY_ID
    COUNTRY_ID = CFLAG:CHARA_ID:所属
    IF !IS_COUNTRY(COUNTRY_ID)
        ;#; THROW COUNTRY_ID{COUNTRY_ID}不是合法国家
        RETURN
    ENDIF
    FOR LOCAL, 1, MAX_CITY
        IF CITY_TYPE:LOCAL == 1
            BREAK
        ENDIF
        IF CITY_OWNER:LOCAL == COUNTRY_ID
            OWN_CITY_LIST:OWN_CITY_NUM = LOCAL
            OWN_CITY_NUM ++
        ENDIF
    NEXT
    CITY_ID = OWN_CITY_LIST:(RAND:OWN_CITY_NUM)
    LOCAL:1 = RAND(10000, 30000)
    CALL MODIFY_CITY_ECONOMY(CITY_ID,LOCAL:1 ,0)
    CALL K36_NORMAL_SENTENCE_IMAGE_NOTICE(@"%CITY_NAME:CITY_ID%的经济增加了{LOCAL:1 / 100}")

@K36_NEVERMIND_ADD_RANDCHARA(CHARAID)
    #DIM CHARAID
    #DIM COUNTRY_ID
    #DIM CHARA_ID
    COUNTRY_ID = CFLAG:CHARAID:所属
    IF !IS_COUNTRY(COUNTRY_ID)
        ;#; THROW COUNTRY_ID{COUNTRY_ID}不是合法国家
        RETURN
    ENDIF
    ;抄的少女士兵的
    CALL CREATE_RANDOM_CHARA(0,0,1)
	CHARA_ID = RESULT
	CALL TAG_INIT(CHARA_ID,RAND:162 + 1)
    ABL:CHARA_ID:武闘 = 33 + RAND:25
    ABL:CHARA_ID:防衛 = 33 + RAND:25
    ABL:CHARA_ID:知略 = 18 + RAND:5
    ABL:CHARA_ID:政治 = 13 + RAND:5
    ABL:CHARA_ID:歌唱 = 10 + RAND:30
    ABL:CHARA_ID:料理 = 10 + RAND:30
    ABL:CHARA_ID:Ｃ感 = RAND:3
    ABL:CHARA_ID:Ｖ感 = RAND:3
    ABL:CHARA_ID:Ｍ感 = RAND:3
    ABL:CHARA_ID:Ｂ感 = RAND:3
    ABL:CHARA_ID:性技 = RAND:3
    ABL:CHARA_ID:性交 = RAND:3
    ABL:CHARA_ID:奉仕 = RAND:3
    ABL:CHARA_ID:自慰 = RAND:3
    ABL:CHARA_ID:精愛 = RAND:3
    ABL:CHARA_ID:欲望 = RAND:3
    ABL:CHARA_ID:露出 = RAND:3
    ABL:CHARA_ID:性知識 = RAND:4
    TALENT:CHARA_ID:処女 = 1
    TALENT:CHARA_ID:無接吻経験 = 1
    CALL CHANGE_COUNTRY(CHARA_ID, COUNTRY_ID, 1)
    CFLAG:CHARA_ID:好感度 = 500
    CFLAG:CHARA_ID:依存度 = 500
    CFLAG:CHARA_ID:従属度 = 500
    CALL CHANGE_RELATION_O_TO_C(CHARA_ID,COUNTRY_ID,1500,-1500)
    CALL COLORPRINT(@"%ANAME(CHARA_ID)%加入了%GET_COUNTRY_NAME(COUNTRY_ID)%", カラー_注意)
    PRINTFORMW

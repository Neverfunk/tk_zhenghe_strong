;-------------------------------------------------
;コンフィグの初期設定
;-------------------------------------------------
@INIT_CONFIG_DAILY
DAILY_EVENT_COUNT = 2
KOJO_DAILY_EVENT_COUNT = 2
DAILY_EVENT_ODDS = 1
KOJO_DAILY_EVENT_ODDS = 1

;-------------------------------------------------
;ＳＬＧ部分に関する設定
;-------------------------------------------------
@CONFIG_EVENT_DAILY
#DIM コンフィグ所属, DAILY_GENRE_NUM, 299
#DIM イベント数, DAILY_GENRE_NUM
#DIM 表示, DAILY_GENRE_NUM
#DIMS CONST ジャンル名 = "色情", "雇用", "訓練", "外交", "特殊勢力", "其他"
#DIM 選択項目
#DIM 選択番号
#DIMS EVENT_NAME_ENG
#DIMS EVENT_NAME_JAP
#DIM FIRST_LINE
VARSET コンフィグ所属, -1
VARSET イベント数
VARSET 表示
FOR LOCAL, 0, VARSIZE("DAILY_EVENT_NAME_ENG", 1)
	EVENT_NAME_ENG = %DAILY_DISABLE_NAME:LOCAL%
	TRYCCALLFORM EVENT_DAILY_%EVENT_NAME_ENG%_GENRE
		コンフィグ所属:RESULT:(イベント数:RESULT) = LOCAL
		イベント数:RESULT++
		;DEBUGPRINTFORML %EVENT_NAME_ENG%:{RESULT}
	CATCH
		;DEBUGPRINTFORML %EVENT_NAME_ENG%見つからず
	ENDCATCH
NEXT


FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 日常事件的配置設定
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, DAILY_GENRE_NUM
	PRINTFORM ◇　%ジャンル名:LOCAL, 10, LEFT% 
	PRINTBUTTON @"[切換顯示]", LOCAL * 300 + 99
	PRINTFORM %TOSTR_SPACE(5)%
	PRINTBUTTON @"[全部関閉]", LOCAL * 300 + 98
	PRINTFORM %TOSTR_SPACE(5)%
	PRINTBUTTON @"[全部開啟]", LOCAL * 300 + 97
	PRINTL
	SIF 表示:LOCAL == 0
		CONTINUE
	FOR LOCAL:1, 0, VARSIZE("コンフィグ所属", 1)
		SIF コンフィグ所属:LOCAL:(LOCAL:1) == -1
			BREAK
		SIF GET_DAILY_INDEX_FROM_ENG(デイリー_通常, DAILY_DISABLE_NAME:(コンフィグ所属:LOCAL:(LOCAL:1))) == -1
			CONTINUE
		EVENT_NAME_JAP = %DAILY_EVENT_NAME:デイリー_通常:GET_DAILY_INDEX_FROM_ENG(デイリー_通常, DAILY_DISABLE_NAME:(コンフィグ所属:LOCAL:(LOCAL:1)))%
		IF DAILY_DISABLE:(コンフィグ所属:LOCAL:(LOCAL:1))
			SETCOLOR カラー_選択不可
		ENDIF
		PRINTBUTTON @"\@ DAILY_DISABLE:(コンフィグ所属:LOCAL:(LOCAL:1)) ? [関] # [開] \@", LOCAL * 300 + LOCAL:1
		PRINTFORM %TOSTR_SPACE(5)%
		PRINTFORM %EVENT_NAME_JAP, 30, LEFT%
		PRINTL
		RESETCOLOR
	NEXT
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [9990] 日常事件発生倍率 - {DAILY_EVENT_ODDS}
PRINTFORML [9991] 日常事件発生個数 - {DAILY_EVENT_COUNT}
PRINTFORML [9992] 口上日常事件倍率 - {KOJO_DAILY_EVENT_ODDS}
PRINTFORML [9993] 口上日常事件個数 - {KOJO_DAILY_EVENT_COUNT}
PRINTFORML [9999] 取消
INPUT
IF RESULT == 9999
	RETURN
ELSEIF GROUPMATCH(RESULT, 9990, 9991, 9992, 9993)
	SELECTCASE RESULT
		CASE 9990
			DAILY_EVENT_ODDS = ROUND_INCREMENT(DAILY_EVENT_ODDS, 0, 10)
		CASE 9991
			DAILY_EVENT_COUNT = ROUND_INCREMENT(DAILY_EVENT_COUNT, 0, 10)
		CASE 9992
			KOJO_DAILY_EVENT_ODDS = ROUND_INCREMENT(KOJO_DAILY_EVENT_ODDS, 0, 10)
		CASE 9993
			KOJO_DAILY_EVENT_COUNT = ROUND_INCREMENT(KOJO_DAILY_EVENT_COUNT, 0, 10)
	ENDSELECT
ELSE
	選択項目 = RESULT / 300
	選択番号 = RESULT % 300
	PRINTFORML {選択項目}
	PRINTFORML {選択番号}
	IF !INRANGE(選択項目, 0, VARSIZE("コンフィグ所属", 0))
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
	IF 選択番号 == 99
		表示:選択項目 = !表示:選択項目
	ELSEIF 選択番号 == 98 || 選択番号 == 97
		FOR LOCAL:1, 0, VARSIZE("コンフィグ所属", 1)
			SIF コンフィグ所属:選択項目:(LOCAL:1) == -1
				BREAK
			DAILY_DISABLE:(コンフィグ所属:選択項目:(LOCAL:1)) = 選択番号 - 97
		NEXT
	ELSEIF コンフィグ所属:選択項目:選択番号 == -1
	ELSE
		DAILY_DISABLE:(コンフィグ所属:選択項目:選択番号) = !DAILY_DISABLE:(コンフィグ所属:選択項目:選択番号)
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

;死に分岐
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

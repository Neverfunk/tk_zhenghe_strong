;-------------------------------------------------
;「部隊編成」の名称
;-------------------------------------------------
@SHOP_SLG_NAME31
RESULTS:0 '= "部隊編成"

;-------------------------------------------------
;「部隊編成」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK31
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
SIF CONFIG:302 != 0
	RETURN 0
SIF DAY < SLG_PP:1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「部隊編成」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY31
CALL FORM_ARMY(CFLAG:MASTER:所属)

;-------------------------------------------------
;「部隊編成」本体
;-------------------------------------------------
@FORM_ARMY(ARG:0)
LOCAL:5 = ARG:0
LOCAL:6 = GET_SUM_ARMY_DF(LOCAL:5)

CALL SINGLE_DRAWLINE
PRINTL ●部隊編成(在敵地的部隊、現在地存在敵人的部隊用紅色表示)
;TranslatedMark ●部隊編成(敵地にいる部隊、現在地に敵がいる部隊は赤く表示されます)
CALL SINGLE_DRAWLINE
PRINTFORML 総兵数:  %NUM_FORMAT(GET_SUM_SOLDIER(LOCAL:5))%
PRINTFORML 防衛兵数:%NUM_FORMAT(GET_SUM_ARMY_DF(LOCAL:5))%
PRINTFORML 残存兵数:%NUM_FORMAT(COUNTRY_SOLDIER:(LOCAL:5))%
CALL SINGLE_DRAWLINE

LOCAL:3 = 0
FOR LOCAL:0, 0, MAX_UNIT
	IF UNIT_SOLDIER:(LOCAL:5):(LOCAL:0) > 0
		LOCAL:3 = 1
		SIF CHECK_COUNTRY_RELATION_F(ARG:0, CITY_OWNER:(UNIT_POSITION:(LOCAL:5):(LOCAL:0))) == 0 || IS_STAY_ENEMY_UNIT(UNIT_POSITION:(LOCAL:5):(LOCAL:0))
			SETCOLOR カラー_警告
		PRINTBUTTON @"{LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]", LOCAL:0 + 1
		PRINT
		CALL SHOW_UNIT_INFO(LOCAL:5, LOCAL:0, 12)
		RESETCOLOR
	ENDIF
NEXT
IF !LOCAL:3
	PRINTFORML 現在編成中的部隊不存在
	;TranslatedMark 現在編成中の部隊はありません
ENDIF

CALL SINGLE_DRAWLINE
PRINTBUTTON "20[新建部隊]", 20
;TranslatedMark 20[新規部隊]
PRINTL
PRINTBUTTON "30[推薦編成除外設定]", 30
;TranslatedMark 30[おすすめ編成除外設定]
PRINTL
PRINTBUTTON "40[批量補充]", 40
;TranslatedMark 40[一括補充]
PRINTL
PRINTBUTTON "0[    取消]", 0
;TranslatedMark 0[    戻る]
PRINTL

$INPUT_LOOP
INPUT

IF RESULT == 20
	LOCAL:1 = 0
	FOR LOCAL:0, 0, MAX_UNIT
		IF UNIT_SOLDIER:(LOCAL:5):(LOCAL:0) <= 0
			LOCAL:1 = 1
			CALL CREATE_UNIT(LOCAL:5, LOCAL:0)
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		PRINTW 我們無法新建更多部隊
		;TranslatedMark これ以上部隊を編成することはできません
	ENDIF
	RESTART
ELSEIF RESULT == 30
	CALL EXCLUDE_BEST_COMMANDER
	RESTART
ELSEIF RESULT == 40
	CALL UNIT_SOLDIER_REPLENISH(ARG:0)
	RESTART
ELSEIF RESULT == 0
	RETURN
ELSEIF RESULT >= 1 && RESULT <= MAX_UNIT && UNIT_SOLDIER:(LOCAL:5):(RESULT - 1) > 0
	CALL UNIT_SETTING(LOCAL:5, RESULT - 1)
	RESTART
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ARG:0勢力、部隊ARG:1の情報表示  ARG:2=調整用スペースの数
;-------------------------------------------------
@SHOW_UNIT_INFO(ARG:0, ARG:1, ARG:2 = 0)
PRINTFORM 兵数:%NUM_FORMAT(UNIT_SOLDIER:(ARG:0):(ARG:1)), 7, LEFT% 所在:
LOCAL:2 = UNIT_POSITION:(ARG:0):(ARG:1)
LOCALS:0 = %GET_RELAYNAME(LOCAL:2)%
IF LOCALS:0 == "無名"
	LOCALS:0 = ----
ENDIF
PRINTFORM %LOCALS:0, 14, LEFT%
PRINT 　目標:
LOCAL:2 = UNIT_TARGET:(ARG:0):(ARG:1)
LOCALS:0 = %GET_RELAYNAME(LOCAL:2)%
IF LOCALS:0 == "無名"
	LOCALS:0 = 無
	;TranslatedMark なし
ENDIF
CALL GET_UNIT_COMMANDER_ALL(ARG:0, ARG:1)
CALL SET_BATTLE_MIRROR_VAL(0, RESULT:0, RESULT:1, RESULT:2)
PRINTFORM %LOCALS:0, 14, LEFT%
PRINTFORM 攻:
CALL PRINT_ALPHABET_RANK(ランク_部隊, ATTACK_LEVEL(0, 0))
PRINTFORM   防:
CALL PRINT_ALPHABET_RANK(ランク_部隊, DEFENSE_LEVEL(0, 0))
PRINTFORM   知:
CALL PRINT_ALPHABET_RANK(ランク_部隊, INTELLIGENCE_LEVEL(0, 0))
PRINTFORM   疲:{UNIT_TIRED_COUNT:(ARG:0):(ARG:1)}
PRINTL
PRINTFORML %TOSTR_SPACE(ARG:2)%将:%GET_UNIT_COMMANDER_NAME(ARG:0, ARG:1)%
CALL PRINT_IMAGE_SLG(ARG:0, ARG:1)
;-------------------------------------------------
;ARG:0勢力、部隊ARG:1の情報表示
;-------------------------------------------------
@SHOW_UNIT_INFO_SIMPLE(ARG:0, ARG:1)
PRINTFORM 兵数:{UNIT_SOLDIER:(ARG:0):(ARG:1), 7, LEFT}
CALL GET_UNIT_COMMANDER_ALL(ARG:0, ARG:1)
CALL SET_BATTLE_MIRROR_VAL(0, RESULT:0, RESULT:1, RESULT:2)
PRINTFORM 攻:
CALL PRINT_ALPHABET_RANK(ランク_部隊, ATTACK_LEVEL(0, 0))
PRINTFORM   防:
CALL PRINT_ALPHABET_RANK(ランク_部隊, DEFENSE_LEVEL(0, 0))
PRINTFORM   知:
CALL PRINT_ALPHABET_RANK(ランク_部隊, INTELLIGENCE_LEVEL(0, 0))
PRINTFORM   疲:{UNIT_TIRED_COUNT:(ARG:0):(ARG:1)}
PRINTFORML   将:%GET_UNIT_COMMANDER_NAME(ARG:0, ARG:1)%


;-------------------------------------------------
;ARG:0勢力のARG:1番部隊に対する設定
;ARG:2は侵攻ボタンから飛んできたフラグ
;-------------------------------------------------
@UNIT_SETTING(ARG:0, ARG:1, ARG:2 = 0)
;部隊がいる都市の勢力との関係を取得
CALL CHECK_COUNTRY_RELATION(ARG:0, CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)))
LOCAL:5 = RESULT:0
SIF IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(ARG:1))
	LOCAL:5 = 0

CALL SINGLE_DRAWLINE
PRINTFORML ●対部隊{ARG:1 + 1}的指示
;TranslatedMark ●部隊{ARG:1 + 1}への指示
PRINT
CALL SHOW_UNIT_INFO(ARG:0, ARG:1, 4)
CALL SINGLE_DRAWLINE

IF ARG:2 == 0 && LOCAL:5 >= 1
	PRINTBUTTON "0[移動／目標設定]", 0
	PRINTL
ELSEIF ARG:2 == 0 && LOCAL:5 < 1
	SETCOLOR カラー_選択不可
	PRINTPLAIN  0[移動／目標設定]
	PRINTL
	RESETCOLOR
ELSE
	PRINTBUTTON "0[決定這個部隊]", 0
	;TranslatedMark 0[この部隊で決定]
	PRINTL
ENDIF
IF LOCAL:5 >= 1
	PRINTBUTTON "1[  変更兵数]", 1
	;TranslatedMark 1[兵数の変更]
	PRINTL
	PRINTBUTTON "2[  変更将領]", 2
	;TranslatedMark 2[  将の変更]
	PRINTL
	PRINTBUTTON "3[      解散]", 3
	PRINTL
	SETCOLOR カラー_選択不可
	PRINTPLAIN  4[  強制解散]
	RESETCOLOR
	PRINTL
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN  1[兵数の変更]
	PRINTL
	PRINTPLAIN  2[  将の変更]
	PRINTL
	PRINTPLAIN  3[      解散]
	PRINTL
	RESETCOLOR
	PRINTBUTTON "4[  強制解散]", 4
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
PRINTBUTTON "9[      取消]", 9
;TranslatedMark 9[      戻る]
PRINTL

$INPUT_LOOP1
INPUT

IF RESULT == 0 && ARG:2 == 0 && LOCAL:5 >= 1
	CALL SINGLE_DRAWLINE
	CALL SET_CITY_COLOR_COUNTRY
	CALL SET_CITY_COLOR_UNIT
	CALL DRAWMAP
	CALL RESET_CITY_COLOR
	CALL SINGLE_DRAWLINE

	PRINTFORML 請決定部隊{ARG:1 + 1}的移動目標
	;TranslatedMark 部隊{ARG:1 + 1}の移動先を決めて下さい
	PRINTFORML 可以選択自勢力的都市、同盟・連合勢力的都市、以及和它們鄰接的敵対都市
	;TranslatedMark 自勢力の都市、同盟・連合勢力の都市、ならびにそれらに隣接した敵対都市のみが選択可能です
	PRINTFORML 現在的位置:%GET_RELAYNAME(UNIT_POSITION:(ARG:0):(ARG:1))%
	;TranslatedMark 現在の位置:%GET_RELAYNAME(UNIT_POSITION:(ARG:0):(ARG:1))%
	PRINTBUTTON "[取消]", 0
	;TranslatedMark [キャンセル]
	PRINTL

	$INPUT_LOOP2
	INPUT

	IF RESULT == 0
		RESTART
	ELSEIF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
		LOCAL:2 = RESULT - 1000

		;勢力間の関係を取得
		CALL CHECK_COUNTRY_RELATION(ARG:0, CITY_OWNER:(LOCAL:2))
		LOCAL:7 = RESULT:0

		;停戦中の勢力への移動
		IF LOCAL:7 == 1
			PRINTFORMW 部隊不能移動到已経停戦的勢力的都市
			;TranslatedMark 停戦中の勢力の都市へ部隊を移動させることはできません
			RESTART

		;現在位置と同じ場所を選択
		ELSEIF LOCAL:2 == UNIT_POSITION:(ARG:0):(ARG:1)
			UNIT_TARGET:(ARG:0):(ARG:1) = 0
			PRINTFORMW 取消設置目標
			;TranslatedMark 目標の設定を解除します
			RESTART

		;自勢力・友好勢力から自勢力・友好勢力への移動
		ELSEIF LOCAL:5 >= 1 && LOCAL:7 >= 2
			LOCAL:3 = UNIT_POSITION:(ARG:0):(ARG:1)
			LOCAL:4 = UNIT_TARGET:(ARG:0):(ARG:1)
			UNIT_POSITION:(ARG:0):(ARG:1) = LOCAL:2
			UNIT_TARGET:(ARG:0):(ARG:1) = 0
			PRINTFORML 部隊{ARG:1 + 1}向%GET_RELAYNAME(LOCAL:2)%移動
			;TranslatedMark 部隊{ARG:1 + 1}を%GET_RELAYNAME(LOCAL:2)%へ移動しました
			IF LOCAL:3 >= 1 && LOCAL:3 != LOCAL:4
				PRINTFORML 解除現在的移動目標
				;TranslatedMark 現在の移動目標は解除されます
			ENDIF
			WAIT
			RESTART

		;敵対する（攻め込める）都市を選択
		ELSEIF IS_INVADABLE(LOCAL:2, ARG:0)
			CALL FORCE_SELECT_DEPARTURE_POINT(LOCAL:2, ARG:1)
			RESTART
		ENDIF
	ENDIF
	GOTO INPUT_LOOP2
;侵攻・移動ボタンから飛んできた
ELSEIF RESULT == 0 && ARG:2 == 1
	RETURN 1
;兵数の変更
ELSEIF RESULT == 1 && LOCAL:5 >= 1
	LOCAL:2 = UNIT_SOLDIER:(ARG:0):(ARG:1)
	LOCAL:3 = COUNTRY_SOLDIER:(ARG:0) + LOCAL:2
	CALL SINGLE_DRAWLINE
	PRINTFORML 請輸入部隊的兵数(残存兵数:%NUM_FORMAT(LOCAL:3)%)
	;TranslatedMark 部隊の兵数を入力して下さい(残存兵数:%NUM_FORMAT(LOCAL:3)%)
	PRINTFORML 現在的兵数是%NUM_FORMAT(LOCAL:2)%
	;TranslatedMark 現在の兵数は%NUM_FORMAT(LOCAL:2)%です
	PRINTFORML 輸入0以下的数字是放棄変更
	;TranslatedMark 0以下の数を入力すると変更せずに戻ります

	CALL DRAW_SOLDIER_INPUT(LOCAL:3)

	$INPUT_LOOP3
	INPUT

	IF RESULT <= 0
		RESTART
	ELSEIF RESULT < 500
		CLEARLINE 1
		PRINTL 至少需要500的兵数
		;TranslatedMark 最低でも500の兵数が必要です
		GOTO INPUT_LOOP3
	ELSEIF RESULT <= LOCAL:3
		COUNTRY_SOLDIER:(ARG:0) += LOCAL:2
		UNIT_SOLDIER:(ARG:0):(ARG:1) = RESULT
		COUNTRY_SOLDIER:(ARG:0) -= RESULT
		PRINTFORMW {ARG:1 + 1}番隊的兵数設定為%NUM_FORMAT(RESULT)%
		;TranslatedMark {ARG:1 + 1}番隊の兵数を%NUM_FORMAT(RESULT)%に設定します
		RESTART
	ENDIF

	CLEARLINE 1
	PRINTL 兵数不足
	;TranslatedMark 兵数が不足しています
	GOTO INPUT_LOOP3

;将の変更
ELSEIF RESULT == 2 && LOCAL:5 >= 1
	CALL SELECT_UNIT_COMMANDER(ARG:0, ARG:1)
	RESTART

;解散
ELSEIF RESULT == 3 && LOCAL:5 >= 1
	PRINTFORML 解散部隊{ARG:1 + 1}。可以么？
	;TranslatedMark 部隊{ARG:1 + 1}を解散します。宜しいですか？
	CALL ASK_YN
	IF RESULT == 0
		FOR LOCAL, 0, 3
			LOCAL:(10 + LOCAL) = GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL)
		NEXT
		CALL CLEAR_UNIT(ARG:0, ARG:1)
		FOR LOCAL, 0, 3
			IF CHECK_BUILDING_FUNCTION_SELECT(-1,(ARG:0),建造物_空军基地) > 0 && LOCAL:(10 + LOCAL) >= 0
				COOLTIME:(LOCAL:(10 + LOCAL)):0 = 0
				BUILDING_TEXT = BUILDING_TEXT_空军基地_1(ARG:0)
			ENDIF
			SIF LOCAL:(10 + LOCAL) >= 0 && ASSIGNED_THIS_TURN:(LOCAL:(10 + LOCAL)):0
				COOLTIME:(LOCAL:(10 + LOCAL)):0 = 0
		NEXT
		RETURN
	ENDIF
	RESTART

;強制解散
ELSEIF RESULT == 4 && LOCAL:5 < 1
	PRINTFORML 讓部隊{ARG:1 + 1}脱離戦線、然後解散
	;TranslatedMark 部隊{ARG:1 + 1}を戦線から離脱させ、解散させます
	PRINTFORML 会失去一半兵力、武将也会3回合無法行動。確定麼？
	;TranslatedMark 兵の半分ほどが失われ、武将に3ターンのクールタイムがつきます。よろしいですか？
	CALL ASK_YN
	IF RESULT == 0
		COUNTRY_SOLDIER:(ARG:0) += UNIT_SOLDIER:(ARG:0):(ARG:1) * RAND(40, 60) / 100
		UNIT_SOLDIER:(ARG:0):(ARG:1) = 0
		FOR LOCAL, 0, GET_UNIT_COMMANDER_NUM(ARG:0, ARG:1)
			LOCAL:1 = GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL)
			;-----------验证是否存在信仰插件
			TRYCCALL SHOP_LIFE_MIRACLE
				CFLAG:(LOCAL:1):杀敌数 = 0
			CATCH
			ENDCATCH
			IF LOCAL:1 >= 0
				IF CHECK_BUILDING_FUNCTION_SELECT(-1,(ARG:0),建造物_空军基地) > 0
					BUILDING_TEXT = BUILDING_TEXT_空军基地_2(ARG:0)
					CALL ADD_COOLTIME(LOCAL:1, 1, 1, 0)
				ELSE
					CALL ADD_COOLTIME(LOCAL:1, 3, 1, 0)
				ENDIF
			ENDIF
		NEXT
		CALL CLEAR_UNIT(ARG:0, ARG:1)
		RETURN
	ENDIF
	RESTART
ELSEIF RESULT == 9
	RETURN
ENDIF
GOTO INPUT_LOOP1

;-------------------------------------------------
;都市ARG:0の防衛兵数を設定
;-------------------------------------------------
@SHOP_CITY_SOLDIER(ARG:0)
LOCAL:2 = CITY_SOLDIER:(ARG:0)
LOCAL:3 = COUNTRY_SOLDIER:(CITY_OWNER:(ARG:0)) + LOCAL:2
CALL SINGLE_DRAWLINE
PRINTFORML 請輸入%GET_CITYNAME(ARG:0)%的防衛兵数(残存兵数:%NUM_FORMAT(LOCAL:3)%)
;TranslatedMark %GET_CITYNAME(ARG:0)%の防衛兵数を入力して下さい(残存兵数:%NUM_FORMAT(LOCAL:3)%)
PRINTFORML 現在的防衛兵数是%NUM_FORMAT(LOCAL:2)%
;TranslatedMark 現在の防衛兵数は%NUM_FORMAT(LOCAL:2)%です
PRINTFORML 輸入0以下的数字是維持現在的兵数
;TranslatedMark 0以下の数を入力すると現在の兵数を維持します
CALL SINGLE_DRAWLINE

CALL DRAW_SOLDIER_INPUT(LOCAL:3)

$INPUT_LOOP1
INPUT

IF RESULT > 0 && RESULT < 500
	CLEARLINE 1
	PRINTL 至少需要500的兵数
	;TranslatedMark 最低でも500の兵数が必要です
	GOTO INPUT_LOOP1
ELSEIF RESULT >= 500 && RESULT <= LOCAL:3
	COUNTRY_SOLDIER:(CITY_OWNER:(ARG:0)) += CITY_SOLDIER:(ARG:0)
	CITY_SOLDIER:(ARG:0) = RESULT
	COUNTRY_SOLDIER:(CITY_OWNER:(ARG:0)) -= CITY_SOLDIER:(ARG:0)
ELSEIF RESULT > LOCAL:3
	CLEARLINE 1
	PRINTL 兵数不足
	;TranslatedMark 兵数が不足しています
	GOTO INPUT_LOOP1
ENDIF

;-------------------------------------------------
;都市ARG:0の守将を設定
;-------------------------------------------------
@SHOP_CITY_COMMANDER(ARG:0)
#DIM 元キャラ, MAX_CITY_COMMANDER
#DIM 都市攻撃
#DIM 都市防御
#DIM 都市知略
#DIM 都市妖術
#DIM FIRST_LINE
元キャラ:0 = GET_CITY_COMMANDER(ARG:0, 0), GET_CITY_COMMANDER(ARG:0, 1)

;士官が部隊に所属しているかどうかのマップを作成(高速化用)
CALL TMP_CREATE_IS_FREE_MAP

;ただし、現在その都市で守将をやってるキャラについては、フリーであるとみなす（でないと解除したときに選択できなくなるので）
FOR LOCAL, 0, 2
	LOCAL:1 = GET_CITY_COMMANDER(ARG:0, LOCAL)
	IF LOCAL:1 != -1
		PRINTFORML %ANAME(LOCAL:1)%
		TMP_IS_FREE:(LOCAL:1):0 = 0
	ENDIF
NEXT


;守将設定のページ数の計算
LOCAL:12 = 0
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみ ※寺子屋通いは除外
	IF (CFLAG:(LOCAL:0):所属 == CITY_OWNER:(ARG:0))
		LOCAL:12 ++
	ENDIF
NEXT
LOCAL:12 = (LOCAL:12 - 1) / 40 + 1
LOCAL:13 = 1
FIRST_LINE = LINECOUNT
$SHOW_LOOP2
CALL GET_CITY_COMMANDER_ALL(ARG:0)
LOCAL:4 = RESULT:0, RESULT:1
CALL SET_BATTLE_MIRROR_VAL(0, RESULT:0, RESULT:1, RESULT:2)
都市攻撃 = ATTACK_LEVEL(0, ARG:0)
都市防御 = DEFENSE_LEVEL(0, ARG:0)
都市知略 = INTELLIGENCE_LEVEL(0, ARG:0)
都市妖術 = MAGIC_LEVEL(0, ARG:0)
LOCAL:1 = MAX(GET_DIGIT(都市攻撃), GET_DIGIT(都市防御), GET_DIGIT(都市知略), GET_DIGIT(都市妖術), 6) + 1

CALL SINGLE_DRAWLINE
PRINTFORML 請設定%GET_CITYNAME(ARG:0)%的守将(最大2人)
;TranslatedMark %GET_CITYNAME(ARG:0)%の守将を設定して下さい(最大2人まで)
PRINTFORML 【現在的防衛能力】
;TranslatedMark 【現在の防衛能力】
PRINTFORM 攻撃:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市攻撃)
PRINTFORML {都市攻撃, LOCAL:1}
PRINTFORM 防御:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市防御)
PRINTFORML {都市防御, LOCAL:1}
PRINTFORM 知略:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市知略)
PRINTFORML {都市知略, LOCAL:1}
PRINTFORM 妖術:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市妖術)
PRINTFORML {都市妖術, LOCAL:1}
PRINTBUTTON "[能力一覧表示]", 11
PRINTL
CALL SINGLE_DRAWLINE
PRINTFORM %TOSTR_SPACE(16)%   武|  防|  知|  政|  妖|  歌|  料 %TOSTR_SPACE(19)%   武|  防|  知|  政|  妖|  歌|  料
PRINTL
LOCAL:25 = 15
LOCAL:1 = 0
LOCAL:14 = 0
LOCAL:15 = (LOCAL:13 - 1) * 40
LOCAL:16 = LOCAL:13 * 40
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみ　※寺子屋通いは除外
	IF (CFLAG:(LOCAL:0):所属 == CITY_OWNER:(ARG:0))
		IF LOCAL:14 >= LOCAL:15 && LOCAL:14 < LOCAL:16
			IF LOCAL:1 % 2 != 0
				PRINT 　　
			ELSEIF LOCAL:1 >= 1
				PRINTL
				LOCAL:25 ++
			ENDIF
			IF GROUPMATCH(LOCAL, LOCAL:4, LOCAL:5)
				SETCOLOR カラー_選択中
			ELSEIF CFLAG:LOCAL:行動不能状態 == 行動不能_臨月
				SETCOLOR カラー_警告
			ELSEIF TALENT:LOCAL:妊娠
				SETCOLOR カラー_注意
			ELSEIF CFLAG:LOCAL:行動不能状態 == 行動不能_育児
				SETCOLOR カラー_黄緑
			ENDIF
			IF TMP_IS_FREE:(LOCAL:0):0 == 0
				PRINTFORM [{NO:(LOCAL:0) + 99, 4}]%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH / 2, RIGHT%
				PRINTFORM
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):武闘)
				PRINTFORM {ABL:(LOCAL:0):武闘, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):防衛)
				PRINTFORM {ABL:(LOCAL:0):防衛, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):知略)
				PRINTFORM {ABL:(LOCAL:0):知略, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):政治)
				PRINTFORM {ABL:(LOCAL:0):政治, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):妖術)
				PRINTFORM {ABL:(LOCAL:0):妖術, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):歌唱)
				PRINTFORM {ABL:(LOCAL:0):歌唱, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):料理)
				PRINTFORM {ABL:(LOCAL:0):料理, 3}
			ELSE
				LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]
				LOCALS:0 += @"%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH / 2, RIGHT%"
				IF TMP_IS_FREE:LOCAL:0 == 1
					LOCALS:0 += @" 部"
				ELSEIF TMP_IS_FREE:LOCAL:0 == 2
					LOCALS:0 += @" 守"
				ELSEIF COOLTIME:(LOCAL:0):0
					LOCALS:0 += @" %TOFULL(TOSTR(COOLTIME:(LOCAL:0):0))%"
				ELSEIF TMP_IS_FREE:LOCAL:0 == -1
					LOCALS:0 += @" 他"
				ELSE
					LOCALS:0 += @"   "
				ENDIF

				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:武闘)%{ABL:(LOCAL:0):武闘, 3}|%ALPHABET_RANK(0, ABL:LOCAL:知略)%{ABL:(LOCAL:0):防衛, 3}|%ALPHABET_RANK(0, ABL:LOCAL:知略)%{ABL:(LOCAL:0):知略, 3}|"
				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:政治)%{ABL:(LOCAL:0):政治, 3}|%ALPHABET_RANK(0, ABL:LOCAL:妖術)%{ABL:(LOCAL:0):妖術, 3}|%ALPHABET_RANK(0, ABL:LOCAL:歌唱)%{ABL:(LOCAL:0):歌唱, 3}|%ALPHABET_RANK(0, ABL:LOCAL:料理)%{ABL:(LOCAL:0):料理, 3}"
				SETCOLOR カラー_選択不可
				PRINTPLAINFORM %LOCALS:0%
			ENDIF
			RESETCOLOR
			LOCAL:1 ++
		ENDIF
		LOCAL:14 ++
	ENDIF
NEXT
PRINTL
CALL SINGLE_DRAWLINE

IF LOCAL:12 >= 2
	LOCAL:25 ++
	IF LOCAL:13 > 1
		PRINT [  8] 察看前一頁
		;TranslatedMark [  8] 前のページ
	ELSE
		PRINT
	ENDIF
	LOCALS:0 = page{LOCAL:13}/{LOCAL:12}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:13 < LOCAL:12
		PRINT [  9] 察看後一頁
		;TranslatedMark [  9] 後のページ
	ENDIF
	PRINTL
	CALL SINGLE_DRAWLINE
ENDIF

PRINTL [  0] 決定
PRINTL [ 10] 推薦
;TranslatedMark [ 10] おすすめ
PRINTL

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 8 && LOCAL:13 > 1
	LOCAL:13 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 9 && LOCAL:13 < LOCAL:12
	LOCAL:13 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 11
	CALL CREATE_SELECTOR_SORT_LIST
	CALL SHOP_LIFE_CHARA_TABLE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 0
	;将をNoの若い順にソート
	FOR LOCAL:0, 0, 2
		IF LOCAL:(LOCAL:0 + 4) >= 0
			LOCAL:(LOCAL:0 + 6) = NO:(LOCAL:(LOCAL:0 + 4))
		ELSE
			LOCAL:(LOCAL:0 + 6) = 99999
		ENDIF
	NEXT
	IF LOCAL:6 > LOCAL:7
		SWAP LOCAL:4, LOCAL:5
	ENDIF
	CALL SET_CITY_COMMANDER(ARG:0, 0, LOCAL:4)
	CALL SET_CITY_COMMANDER(ARG:0, 1, LOCAL:5)
	;元からいたキャラでなければ、このターンに割り当てましたフラグをたてる
	SIF LOCAL:4 >= 0 && LOCAL:4 != 元キャラ:0 && LOCAL:4 != 元キャラ:1
		ASSIGNED_THIS_TURN:(LOCAL:4):0 = 1
	SIF LOCAL:5 >= 0 && LOCAL:5 != 元キャラ:0 && LOCAL:5 != 元キャラ:1
		ASSIGNED_THIS_TURN:(LOCAL:5):0 = 1
	;このターンに割り当てられたキャラでなければ、クールタイムを付与
	SIF 元キャラ:0 != -1 && 元キャラ:0 != LOCAL:4 && 元キャラ:0 != LOCAL:5 && !ASSIGNED_THIS_TURN:(元キャラ:0):0
		COOLTIME:(元キャラ:0):0 = 1
	SIF 元キャラ:1 != -1 && 元キャラ:1 != LOCAL:4 && 元キャラ:1 != LOCAL:5 && !ASSIGNED_THIS_TURN:(元キャラ:1):0
		COOLTIME:(元キャラ:1):0 = 1
	REDRAW 1
	RETURN
ELSEIF RESULT == 10
	CALL SET_CITY_COMMANDER_BEST(ARG:0, 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSE
	LOCAL:10 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:10 < 0 || CFLAG:(LOCAL:10):所属 != CITY_OWNER:(ARG:0) || TMP_IS_FREE:(LOCAL:10):0 != 0
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF

	;既に選択済みのキャラなら選択解除
	FOR LOCAL:0, 4, 6
		IF LOCAL:(LOCAL:0) == LOCAL:10
			CALL CLEAR_CITY_COMMANDER_FIND(ARG:0, LOCAL:10)
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	;未選択のキャラは選択
	FOR LOCAL:0, 4, 6
		IF LOCAL:(LOCAL:0) < 0
			CALL SET_CITY_COMMANDER_ANYWHERE(ARG:0, LOCAL:10)
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	CLEARLINE 2
	PRINTFORML 将領数目到達了極限
	;TranslatedMark 将の数が最大です
ENDIF
GOTO INPUT_LOOP2

;-------------------------------------------------
;ARG:0勢力のARG:1番に部隊を新規作成
;-------------------------------------------------
@CREATE_UNIT(ARG:0, ARG:1, ARG:2 = 0)
CALL SINGLE_DRAWLINE

SIF ARG:2 != 0
	GOTO POSITIONING_CANCEL

CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

PRINTL 請選択要編成部隊的都市
;TranslatedMark 部隊を編成する都市を設定して下さい
PRINTBUTTON "0[取消]", 0
;TranslatedMark 0[キャンセル]
PRINTL

$INPUT_LOOP0
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
	LOCAL:5 = RESULT - 1000
	IF CITY_OWNER:(LOCAL:5) == ARG:0
		UNIT_POSITION:(ARG:0):(ARG:1) = LOCAL:5
	ELSE
		CLEARLINE 1
		PRINTL 只能在自国領土内編成部隊
		;TranslatedMark 部隊が編成できるのは自国領内だけです
		GOTO INPUT_LOOP0
	ENDIF
ELSE
	GOTO INPUT_LOOP0
ENDIF

$POSITIONING_CANCEL

CALL SINGLE_DRAWLINE
PRINTFORML 請輸入部隊的兵数(残存兵数:{COUNTRY_SOLDIER:(ARG:0)})
;TranslatedMark 部隊の兵数を入力して下さい(残存兵数:{COUNTRY_SOLDIER:(ARG:0)})

CALL DRAW_SOLDIER_INPUT(COUNTRY_SOLDIER:(ARG:0))

$INPUT_LOOP1
INPUT

IF RESULT <= 0
	UNIT_POSITION:(ARG:0):(ARG:1) = 0
	RETURN
ELSEIF RESULT < 500
	CLEARLINE 1
	PRINTL 至少需要500的兵数
	;TranslatedMark 最低でも500の兵数が必要です
	GOTO INPUT_LOOP1
ELSEIF RESULT <= COUNTRY_SOLDIER:(ARG:0)
	UNIT_SOLDIER:(ARG:0):(ARG:1) = RESULT
	COUNTRY_SOLDIER:(ARG:0) -= RESULT
	PRINTFORMW 把{ARG:1 + 1}番隊的兵数設定成{RESULT}了
	;TranslatedMark {ARG:1 + 1}番隊の兵数を{RESULT}に設定します
ELSE
	CLEARLINE 1
	PRINTL 国家的兵数不足
	;TranslatedMark 国の兵数が不足しています
	GOTO INPUT_LOOP1
ENDIF

UNIT_TARGET:(ARG:0):(ARG:1) = 0
UNIT_COMMANDER:(ARG:0):(ARG:1) = 0

;部隊の将の設定
CALL SELECT_UNIT_COMMANDER(ARG:0, ARG:1)

RETURN 1

;-------------------------------------------------
;ARG:0勢力のARG:1番に部隊について、将を設定する
;-------------------------------------------------
@SELECT_UNIT_COMMANDER(ARG:0, ARG:1)
#DIM 元キャラ, MAX_UNIT_COMMANDER
#DIM 部隊攻撃
#DIM 部隊防御
#DIM 部隊知略
#DIM 部隊妖術
#DIM FIRST_LINE
CALL GET_UNIT_COMMANDER_ALL(ARG:0, ARG:1)
元キャラ:0 = RESULT:0, RESULT:1, RESULT:2

;士官が部隊に所属しているかどうかのマップを作成(高速化用)
CALL TMP_CREATE_IS_FREE_MAP

;ただし、現在その部隊に所属しているキャラについては、フリーであるとみなす（でないと解除したときに選択できなくなるので）
FOR LOCAL, 0, 3
	LOCAL:1 = GET_UNIT_COMMANDER(ARG:0, ARG:1, LOCAL)
	SIF LOCAL:1 != -1
		TMP_IS_FREE:(LOCAL:1):0 = 0
NEXT

;ページ数の計算
LOCAL:20 = 0
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみカウント
	IF (CFLAG:(LOCAL:0):所属 == ARG:0)
		LOCAL:20 ++
	ENDIF
NEXT
LOCAL:20 = (LOCAL:20 - 1) / 40 + 1
LOCAL:21 = 1
FIRST_LINE = LINECOUNT
$SHOW_LOOP2
CALL GET_UNIT_COMMANDER_ALL(ARG:0, ARG:1)
LOCAL:10 = RESULT:0, RESULT:1, RESULT:2
CALL SET_BATTLE_MIRROR_VAL(0, RESULT:0, RESULT:1, RESULT:2)
部隊攻撃 = ATTACK_LEVEL(0, 0)
部隊防御 = DEFENSE_LEVEL(0, 0)
部隊知略 = INTELLIGENCE_LEVEL(0, 0)
部隊妖術 = MAGIC_LEVEL(0, 0)

LOCAL:1 = MAX(GET_DIGIT(部隊攻撃), GET_DIGIT(部隊防御), GET_DIGIT(部隊知略), GET_DIGIT(部隊妖術), 6) + 1

CALL SINGLE_DRAWLINE
PRINTFORML 請設定部隊的将領(最多3人)
;TranslatedMark 部隊の将を設定して下さい(最大3人まで)
PRINTFORML 妊娠中（黄色)　臨月（赤色)　育児中(黄緑)的士官会比通常状態要弱
;TranslatedMark 妊娠中（黄色)　臨月（赤色)　育児中(黄緑)の士官は通常より弱体化します
PRINTFORML 【現在的部隊能力】
;TranslatedMark 【現在の部隊能力】
PRINTFORM 攻撃:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊攻撃)
PRINTFORML {部隊攻撃, LOCAL:1}
PRINTFORM 防御:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊防御)
PRINTFORML {部隊防御, LOCAL:1}
PRINTFORM 知略:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊知略)
PRINTFORML {部隊知略, LOCAL:1}
PRINTFORM 妖術:
CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊妖術)
PRINTFORML {部隊妖術, LOCAL:1}
PRINTBUTTON "[能力一覧表示]", 11
PRINTL
CALL SINGLE_DRAWLINE
PRINTFORM %TOSTR_SPACE(16)%   武|  防|  知|  政|  妖|  歌|  料 %TOSTR_SPACE(19)%   武|  防|  知|  政|  妖|  歌|  料
PRINTL

LOCAL:25 = 15
LOCAL:1 = 0
LOCAL:22 = 0
LOCAL:23 = (LOCAL:21 - 1) * 40
LOCAL:24 = LOCAL:21 * 40
FOR LOCAL:0, 0, CHARANUM
	;マスターと所属勢力が同じキャラのみ
	IF (CFLAG:(LOCAL:0):所属 == ARG:0)
		IF LOCAL:22 >= LOCAL:23 && LOCAL:22 < LOCAL:24
			IF LOCAL:1 % 2 != 0
				PRINT 　　
			ELSEIF LOCAL:1 >= 1
				PRINTL
				LOCAL:25 ++
			ENDIF
			IF MATCH(LOCAL, LOCAL:0, 10, 13)
				SETCOLOR カラー_選択中
			ELSEIF CFLAG:LOCAL:行動不能状態 == 行動不能_臨月
				SETCOLOR カラー_警告
			ELSEIF TALENT:LOCAL:妊娠
				SETCOLOR カラー_注意
			ELSEIF CFLAG:LOCAL:行動不能状態 == 行動不能_育児
				SETCOLOR カラー_黄緑
			ENDIF


			IF TMP_IS_FREE:(LOCAL:0):0 == 0
				PRINTFORM [{NO:(LOCAL:0) + 99, 4}]%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH / 2, RIGHT%
				PRINTFORM
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):武闘)
				PRINTFORM {ABL:(LOCAL:0):武闘, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):防衛)
				PRINTFORM {ABL:(LOCAL:0):防衛, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):知略)
				PRINTFORM {ABL:(LOCAL:0):知略, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):政治)
				PRINTFORM {ABL:(LOCAL:0):政治, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):妖術)
				PRINTFORM {ABL:(LOCAL:0):妖術, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):歌唱)
				PRINTFORM {ABL:(LOCAL:0):歌唱, 3}|
				CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(LOCAL:0):料理)
				PRINTFORM {ABL:(LOCAL:0):料理, 3}
			ELSE
				LOCALS:0 = [{NO:(LOCAL:0) + 99, 4}]
				LOCALS:0 += @"%SNAME(LOCAL:0), MAX_CHARANAME_LENGTH / 2, RIGHT%"
				IF TMP_IS_FREE:LOCAL:0 == 1
					LOCALS:0 += @" 部"
				ELSEIF TMP_IS_FREE:LOCAL:0 == 2
					LOCALS:0 += @" 守"
				ELSEIF COOLTIME:(LOCAL:0):0
					LOCALS:0 += @" %TOFULL(TOSTR(COOLTIME:(LOCAL:0):0))%"
				ELSEIF TMP_IS_FREE:LOCAL:0 == -1
					LOCALS:0 += @" 他"
				ELSE
					LOCALS:0 += @"   "
				ENDIF

				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:武闘)%{ABL:(LOCAL:0):武闘, 3}|%ALPHABET_RANK(0, ABL:LOCAL:防衛)%{ABL:(LOCAL:0):防衛, 3}|%ALPHABET_RANK(0, ABL:LOCAL:知略)%{ABL:(LOCAL:0):知略, 3}|"
				LOCALS:0 += @"%ALPHABET_RANK(0, ABL:LOCAL:政治)%{ABL:(LOCAL:0):政治, 3}|%ALPHABET_RANK(1, ABL:LOCAL:妖術)%{ABL:(LOCAL:0):妖術, 3}|%ALPHABET_RANK(2, ABL:LOCAL:歌唱)%{ABL:(LOCAL:0):歌唱, 3}|%ALPHABET_RANK(2, ABL:LOCAL:料理)%{ABL:(LOCAL:0):料理, 3}"
				SETCOLOR カラー_選択不可
				PRINTPLAINFORM %LOCALS:0%
			ENDIF
			RESETCOLOR
			LOCAL:1 ++
		ENDIF
		LOCAL:22 ++
	ENDIF
NEXT
PRINTL
CALL SINGLE_DRAWLINE

IF LOCAL:20 >= 2
	LOCAL:25 += 2
	IF LOCAL:21 > 1
		PRINT [  8] 察看前一頁
		;TranslatedMark [  8] 前のページ
	ELSE
		PRINT
	ENDIF
	LOCALS:0 = page{LOCAL:21}/{LOCAL:20}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:21 < LOCAL:20
		PRINT [  9] 察看後一頁
		;TranslatedMark [  9] 後のページ
	ENDIF
	PRINTL
	CALL SINGLE_DRAWLINE
ENDIF

PRINTL [  0] 決定
PRINTL [ 10] 推薦
;TranslatedMark [ 10] おすすめ
PRINTL

REDRAW 0

$INPUT_LOOP2
INPUT

IF RESULT == 8 && LOCAL:21 > 1
	LOCAL:21 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 9 && LOCAL:21 < LOCAL:20
	LOCAL:21 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 11
	CALL CREATE_SELECTOR_SORT_LIST
	CALL SHOP_LIFE_CHARA_TABLE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 0
	;将をNoの若い順にソート
	FOR LOCAL:0, 0, 3
		IF LOCAL:(LOCAL:0 + 10) >= 0
			LOCAL:(LOCAL:0 + 20) = NO:(LOCAL:(LOCAL:0 + 10))
		ELSE
			LOCAL:(LOCAL:0 + 20) = 99999
		ENDIF
	NEXT
	LOCAL:15 = 1
	WHILE LOCAL:15
		LOCAL:15 = 0
		FOR LOCAL:0, 0, 2
			IF LOCAL:(LOCAL:0 + 20) > LOCAL:(LOCAL:0 + 21)
				SWAP LOCAL:(LOCAL:0 + 10), LOCAL:(LOCAL:0 + 11)
				SWAP LOCAL:(LOCAL:0 + 20), LOCAL:(LOCAL:0 + 21)
				LOCAL:15 = 1
			ENDIF
		NEXT
	WEND

	;将の登録
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 0, LOCAL:10)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 1, LOCAL:11)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 2, LOCAL:12)

	;このターンに割り当てたフラグをたてる
	SIF LOCAL:10 >= 0 && !GROUPMATCH(LOCAL:10, 元キャラ:0, 元キャラ:1, 元キャラ:2)
		ASSIGNED_THIS_TURN:(LOCAL:10):0 = 1
	SIF LOCAL:11 >= 0 && !GROUPMATCH(LOCAL:11, 元キャラ:0, 元キャラ:1, 元キャラ:2)
		ASSIGNED_THIS_TURN:(LOCAL:11):0 = 1
	SIF LOCAL:12 >= 0 && !GROUPMATCH(LOCAL:12, 元キャラ:0, 元キャラ:1, 元キャラ:2)
		ASSIGNED_THIS_TURN:(LOCAL:12):0 = 1

	;このターンに割り当てたキャラでなければ、元将のクールタイム設定
	SIF 元キャラ:0 != -1 && !GROUPMATCH(元キャラ:0, LOCAL:10, LOCAL:11, LOCAL:12) && !ASSIGNED_THIS_TURN:(元キャラ:0):0
		COOLTIME:(元キャラ:0):0 = 1
	SIF 元キャラ:1 != -1 && !GROUPMATCH(元キャラ:1, LOCAL:10, LOCAL:11, LOCAL:12) && !ASSIGNED_THIS_TURN:(元キャラ:1):0
		COOLTIME:(元キャラ:1):0 = 1
	SIF 元キャラ:2 != -1 && !GROUPMATCH(元キャラ:2, LOCAL:10, LOCAL:11, LOCAL:12) && !ASSIGNED_THIS_TURN:(元キャラ:2):0
		COOLTIME:(元キャラ:2):0 = 1
	REDRAW 1
	RETURN
;おすすめ組み合わせの探索
ELSEIF RESULT == 10
	CALL CHECK_BEST_COMMANDER(CFLAG:MASTER:所属, 1)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 0, BEST_COMMANDER:0)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 1, BEST_COMMANDER:1)
	CALL SET_UNIT_COMMANDER(ARG:0, ARG:1, 2, BEST_COMMANDER:2)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSE
	LOCAL:5 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:5 < 0 || CFLAG:(LOCAL:5):所属 != ARG:0 || TMP_IS_FREE:(LOCAL:5):0 != 0
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF

	;既に選択済みのキャラなら選択解除
	FOR LOCAL:0, 10, 13
		IF LOCAL:(LOCAL:0) == LOCAL:5
			CALL CLEAR_UNIT_COMMANDER_FIND(ARG:0, ARG:1, LOCAL:5)
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	;未選択のキャラは選択
	FOR LOCAL:0, 10, 13
		IF LOCAL:(LOCAL:0) < 0
			CALL SET_UNIT_COMMANDER_ANYWHERE(ARG:0, ARG:1, LOCAL:5)
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO SHOW_LOOP2
		ENDIF
	NEXT

	CLEARLINE 2
	PRINTFORML 将領数目到達了極限
	;TranslatedMark 将の数が最大です
ENDIF
GOTO INPUT_LOOP2

;-------------------------------------------------
;兵数入力ボタンの描画 ARG:0=設定値の最大
;-------------------------------------------------
@DRAW_SOLDIER_INPUT(ARG:0)
PRINTL [     0]
IF ARG:0 >= 500
	PRINT [   500]
ENDIF
LOCAL:1 = MIN(10, ARG:0 / 1000 + 1)
FOR LOCAL:0, 1, LOCAL:1
	PRINTFORM [{LOCAL:0 * 1000, 6}]
NEXT
PRINTL
IF ARG:0 >= 10000
	LOCAL:1 = MIN(10, (ARG:0 - 10000) / 2000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 2000 + 10000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 30000
	LOCAL:1 = MIN(10, (ARG:0 - 30000) / 5000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 5000 + 30000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 80000
	LOCAL:1 = MIN(10, (ARG:0 - 80000) / 10000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 10000 + 80000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 200000
	LOCAL:1 = MIN(10, (ARG:0 - 200000) / 50000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 50000 + 200000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 700000
	LOCAL:1 = MIN(9, (ARG:0 - 700000) / 100000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 100000 + 700000}]
	NEXT
	PRINTL
ENDIF
PRINTBUTTON "[剩下全部]", ARG:0
;TranslatedMark [残り全て]

;-------------------------------------------------
;経済力入力ボタンの描画 ARG:0=設定値の最大
;-------------------------------------------------
@DRAW_ECONOMY_INPUT(ARG:0)
PRINTL [     0]
IF ARG:0 >= 50
	PRINT [    50]
ENDIF
LOCAL:1 = MIN(10, ARG:0 / 100 + 1)
FOR LOCAL:0, 1, LOCAL:1
	PRINTFORM [{LOCAL:0 * 100, 6}]
NEXT
PRINTL
IF ARG:0 >= 1000
	LOCAL:1 = MIN(10, (ARG:0 - 1000) / 200 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 200 + 1000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 3000
	LOCAL:1 = MIN(10, (ARG:0 - 3000) / 500 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 500 + 3000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 8000
	LOCAL:1 = MIN(10, (ARG:0 - 8000) / 1000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 1000 + 8000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 20000
	LOCAL:1 = MIN(10, (ARG:0 - 20000) / 5000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 5000 + 20000, 6}]
	NEXT
	PRINTL
ENDIF
IF ARG:0 >= 70000
	LOCAL:1 = MIN(9, (ARG:0 - 70000) / 10000 + 1)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTFORM [{LOCAL:0 * 10000 + 70000}]
	NEXT
	PRINTL
ENDIF
PRINTBUTTON "[経済力的20%]", ARG:0
;TranslatedMark [経済力の20%]

;-------------------------------------------------
;最強設定から外すキャラを指定
;-------------------------------------------------
@EXCLUDE_BEST_COMMANDER
#DIM FIRST_LINE
#DIM 消去行
#DIM 対象
#DIM 表示位置
#DIM 表示ページ
#DIM 総ページ数
#DIM キャラ数
#DIM キャラカウンタ
#DIM 表示開始位置
#DIM 表示終了位置
VARSET キャラ数
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		キャラ数 ++
NEXT

総ページ数 = (キャラ数 - 1) / 40 + 1
表示ページ = 1


CALL SINGLE_DRAWLINE
PRINTFORML 可以設定在部隊編成・守将設定中「推薦編成」時的除外角色
;TranslatedMark 部隊編成・守将設定における「おすすめ編成」から除外するキャラを設定できます
PRINTFORML 点撃啟用・除外的角色用桃色表示
;TranslatedMark クリックでトグル・除外されたキャラは桃色表示されます
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP


VARSET 表示位置
VARSET キャラカウンタ
表示開始位置 = (表示ページ - 1) * 40
表示終了位置 = 表示ページ * 40

FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		IF 表示開始位置 <= キャラカウンタ && キャラカウンタ <= 表示終了位置
			IF 表示位置 % 3 != 0
				PRINT 　　
			ELSEIF 表示位置 > 0
				PRINTL
			ENDIF
			IF CFLAG:(LOCAL:0):最強編成除外フラグ
				SETCOLOR カラー_ピンク
			ELSE
				RESETCOLOR
			ENDIF
			PRINTFORM [{LOCAL:0 + 99, 4}]%ANAME(LOCAL:0), 16, RIGHT%
			表示位置 ++
		ENDIF
		キャラカウンタ ++
	ENDIF
NEXT
RESETCOLOR
PRINTL
CALL SINGLE_DRAWLINE

IF 総ページ数 >= 2
	消去行 += 2
	IF 表示ページ > 1
		PRINT [  8] 察看前一頁
		;TranslatedMark [  8] 前のページ
	ELSE
		PRINT
	ENDIF
	LOCALS:0 = ページ{表示ページ}/{総ページ数}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF 表示ページ < 総ページ数
		PRINT [  9] 察看後一頁
		;TranslatedMark [  9] 後のページ
	ENDIF
	PRINTL
	CALL SINGLE_DRAWLINE
ENDIF

PRINTL [ 0]這樣就行了
;TranslatedMark [ 0]これでよし
REDRAW 0
INPUT
IF RESULT == 8 && 表示ページ > 1
	表示ページ --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 9 && 表示ページ < 総ページ数
	表示ページ ++
	CLEARLINE 消去行
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	RETURN -1
ENDIF
対象 = RESULT - 99
IF 対象 > CHARANUM || 対象 < 0 || CFLAG:対象:所属 != CFLAG:MASTER:所属
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

CFLAG:対象:最強編成除外フラグ = !CFLAG:対象:最強編成除外フラグ
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP


;-------------------------------------------------
;部隊に兵を一括補充
;-------------------------------------------------
@UNIT_SOLDIER_REPLENISH(ARG:0)
#DIM 補充可能部隊数
#DIM FIRST_LINE
#DIM 補充兵数
#DIM 端数
補充可能部隊数 = 0
FOR LOCAL, 0, MAX_UNIT
	SIF UNIT_SOLDIER:(ARG:0):LOCAL <= 0
		CONTINUE
	;部隊がいる都市の勢力との関係を取得
	CALL CHECK_COUNTRY_RELATION(ARG:0, CITY_OWNER:(UNIT_POSITION:(ARG:0):(LOCAL:0)))
	LOCAL:1 = RESULT:0
	SIF IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(LOCAL:0))
		LOCAL:1 = 0
	SIF LOCAL:1
		補充可能部隊数 ++
NEXT

IF 補充可能部隊数 <= 0
	PRINTFORMW 不存在可以補充的部隊
	;TranslatedMark 補充可能な部隊が居ません
	RETURN
ENDIF

FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 批量補充可補充的部隊的士兵
;TranslatedMark 補充可能な部隊に兵を一括で補充します
PRINTFORML 請輸入補充的兵數(0以下取消)
;TranslatedMark 補充する兵数を入力してください(0以下でキャンセル)
PRINTFORML 現在可以分配的兵数:{COUNTRY_SOLDIER:(ARG:0)}
;TranslatedMark 現在割り当て可能な兵数:{COUNTRY_SOLDIER:(ARG:0)}
CALL SINGLE_DRAWLINE

PRINTBUTTON "[盡可能的補充]", COUNTRY_SOLDIER:(ARG:0)
;TranslatedMark [可能なだけ補充]

INPUT

SIF RESULT <= 0
	RETURN
IF RESULT > COUNTRY_SOLDIER:(ARG:0)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

補充兵数 = RESULT
端数 = 補充兵数 % 補充可能部隊数
補充兵数 -= 端数

LOCAL:2 = 0

FOR LOCAL, 0, MAX_UNIT
	SIF UNIT_SOLDIER:(ARG:0):LOCAL <= 0
		CONTINUE
	;部隊がいる都市の勢力との関係を取得
	CALL CHECK_COUNTRY_RELATION(ARG:0, CITY_OWNER:(UNIT_POSITION:(ARG:0):(LOCAL:0)))
	LOCAL:1 = RESULT:0
	SIF IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(LOCAL:0))
		LOCAL:1 = 0
	IF LOCAL:1
		UNIT_SOLDIER:(ARG:0):LOCAL += 補充兵数 / 補充可能部隊数
		COUNTRY_SOLDIER:(ARG:0) -= 補充兵数 / 補充可能部隊数
		補充兵数 -=  補充兵数 / 補充可能部隊数
		IF LOCAL:2 == 0
			UNIT_SOLDIER:(ARG:0):LOCAL += 端数
			補充兵数 -= 端数
			LOCAL:2 = 1
		ENDIF
	ENDIF
NEXT

PRINTFORMW 補充完畢
;TranslatedMark 補充しました
RETURN

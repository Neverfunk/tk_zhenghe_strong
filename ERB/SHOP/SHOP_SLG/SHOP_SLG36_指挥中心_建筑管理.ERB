@BUILDING_BATCH_PROCESSING
	WHILE 1
        CALL SINGLE_DRAWLINE
        ALIGNMENT CENTER
        PRINTFORML
        PRINTFORML 欢迎来到建筑管理中心！尊敬的%ANAME(MASTER)%
        PRINTFORML 请选择你要进行的操作
        PRINTFORML
        CALL SINGLE_DRAWLINE

		PRINTBUTTON "[建筑统计]",1
		PRINTL
        PRINTL
		PRINTBUTTON "[清除重复建筑]",2
		PRINTL
        PRINTL
        PRINTBUTTON "[清除特定建筑]",3
		PRINTL
        PRINTL
		PRINTBUTTON "[清除所有建筑]",4
		PRINTL
        PRINTL
		PRINTBUTTON "[批量建造建筑]",5
		PRINTL
        PRINTL
		PRINTBUTTON "[定位建筑]",6
		PRINTL
        PRINTL
		[IF_DEBUG]
			PRINTBUTTON "[DEBUG:批量生成随机建筑]",6
			PRINTL
		[ENDIF]
		PRINTBUTTON "[返回]",0
		PRINTL
        ALIGNMENT LEFT
		INPUT
		IF RESULT == 0
			RETURN
        ENDIF
		IF RESULT == 1
			CALL BUILDING_BATCH_PROCESSING_CNT
        ENDIF
		IF RESULT == 2
			CALL BUILDING_BATCH_PROCESSING_CLEAR_UNNECESSARY_BUILDING
        ENDIF
        IF RESULT == 3
			CALL BUILDING_BATCH_PROCESSING_CLEAR_GIVEN_BUILDING
        ENDIF
		IF RESULT == 4
			CALL BUILDING_BATCH_PROCESSING_CLEAR_ALL_BUILDING
        ENDIF
		IF RESULT == 5
			CALL BUILDING_BATCH_PROCESSING_BUILD_BUILDING
        ENDIF
		IF RESULT == 6
			CALL BUILDING_BATCH_PROCESSING_BUILD_LOCATION
        ENDIF
		[IF_DEBUG]
			IF RESULT == 7
                CALL DEBUG_CREATE_BUILDING
            ENDIF
		[ENDIF]

	WEND
@BUILDING_BATCH_PROCESSING_CNT
	#DIM TMP_BUILDING_NUM,BUILDING_NUM
	#DIM CITY_ID
	#DIM SLOT_PTR
    VARSET TMP_BUILDING_NUM
	FOR CITY_ID,0,MAX_CITY

		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
			TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR)++
		NEXT
	NEXT
    CALL SINGLE_DRAWLINE
	FOR LOCAL,建造物_兵舎,建造物_公衆便所+1
		IF TMP_BUILDING_NUM:(LOCAL) > 0
			PRINTFORM 《%GET_BUILDING_NAME(LOCAL)%》
			PRINTFORM %TOSTR_SPACE(10 - STRLENS(GET_BUILDING_NAME(LOCAL)))%
			PRINTFORML 的数量是{TMP_BUILDING_NUM:(LOCAL)}
		ENDIF
	NEXT
    CALL SINGLE_DRAWLINE
	PRINTFORMW 统计完毕！
@BUILDING_BATCH_PROCESSING_CLEAR_UNNECESSARY_BUILDING
	#DIM TMP_BUILDING_NUM,BUILDING_NUM
	#DIM TMP_CITY_DANGER,MAX_CITY
	#DIM TMP_CITY_DANGER_NUM
	#DIM TMP_CITY_SAFE,MAX_CITY
	#DIM TMP_CITY_SAFE_NUM
	#DIM CITY_ID
	#DIM SLOT_PTR
	VARSET TMP_BUILDING_NUM
	VARSET TMP_CITY_DANGER
	VARSET TMP_CITY_DANGER_NUM
	VARSET TMP_CITY_SAFE
	VARSET TMP_CITY_SAFE_NUM
	PRINTFORML 将清除己方领土上多余的不可重复生效建筑/非危险都市的防御建筑
	PRINTFORML 优先清除威胁度较高都市的建筑
	CALL ASK_YN
	SIF RESULT == 1
		RETURN
	FOR CITY_ID,0,MAX_CITY
		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
			TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR)++
		NEXT
		IF GET_CITY_RISK(CITY_ID) >= 4
			TMP_CITY_DANGER:TMP_CITY_DANGER_NUM = CITY_ID
			TMP_CITY_DANGER_NUM++
		ELSE
			TMP_CITY_SAFE:TMP_CITY_SAFE_NUM = CITY_ID
			TMP_CITY_SAFE_NUM++
		ENDIF
	NEXT

	WHILE TMP_CITY_DANGER_NUM > 0
		TMP_CITY_DANGER_NUM--
		CITY_ID = TMP_CITY_DANGER:TMP_CITY_DANGER_NUM
		FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
			SIF !IS_BUILDING(CITY_BUILDING:CITY_ID:SLOT_PTR)
				CONTINUE
			IF BUILDING_LIMIT_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR) < TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR)
				TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR) --
				PRINTFORML 拆除了%CITY_NAME:CITY_ID%的《%GET_BUILDING_NAME(CITY_BUILDING:CITY_ID:SLOT_PTR)%》
				CITY_BUILDING:CITY_ID:SLOT_PTR = 0
			ENDIF
		NEXT
	WEND

	WHILE TMP_CITY_SAFE_NUM > 0
		TMP_CITY_SAFE_NUM--
		CITY_ID = TMP_CITY_SAFE:TMP_CITY_SAFE_NUM
		FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
			SIF !IS_BUILDING(CITY_BUILDING:CITY_ID:SLOT_PTR)
				CONTINUE
			IF BUILDING_LIMIT_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR) < TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR)
				TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR) --
				PRINTFORML 拆除了%CITY_NAME:CITY_ID%的《%GET_BUILDING_NAME(CITY_BUILDING:CITY_ID:SLOT_PTR)%》
				CITY_BUILDING:CITY_ID:SLOT_PTR = 0
			ENDIF
			IF GROUPMATCH(CITY_BUILDING:CITY_ID:SLOT_PTR,建造物_野战基地,建造物_前线指挥所,建造物_伤兵医院,建造物_弓櫓,建造物_守卫炮台)
				TMP_BUILDING_NUM:(CITY_BUILDING:CITY_ID:SLOT_PTR) --
				PRINTFORML 拆除了%CITY_NAME:CITY_ID%的《%GET_BUILDING_NAME(CITY_BUILDING:CITY_ID:SLOT_PTR)%》
				CITY_BUILDING:CITY_ID:SLOT_PTR = 0
			ENDIF
		NEXT
	WEND
	PRINTFORMW 清理完毕！

@BUILDING_BATCH_PROCESSING_CLEAR_GIVEN_BUILDING
    ; #DIM CITY_ID
    ; WHILE 1
    ;     PRINTFORML 请选择要清除的建筑
    ;     PRINTBUTTON "[兵舎]",建造物_兵舎
    ;     PRINTL
    ;     PRINTBUTTON "[独占市場]",建造物_独占市场
    ;     PRINTL
    ;     PRINTBUTTON "[湯治場]",建造物_湯治場
    ;     PRINTL
    ;     PRINTBUTTON "[醸造所]",建造物_醸造所
    ;     PRINTL
    ;     PRINTBUTTON "[超级计算机]",建造物_スパコン
    ;     PRINTL
    ;     PRINTBUTTON "[培養槽]",建造物_培養槽
    ;     PRINTL
    ;     PRINTBUTTON "[取消]",0
    ;     PRINTL
    ;     INPUT
    ;     IF RESULT == 0
    ;         CLEARLINE 8
    ;         RETURN
    ;     ELSEIF GROUPMATCH(RESULT,建造物_兵舎,建造物_独占市场,建造物_湯治場,建造物_醸造所,建造物_スパコン,建造物_培養槽)
    ;         CLEARLINE 8
    ;         FOR CITY_ID,0,MAX_CITY
    ;             SIF CITY_TYPE:CITY_ID == 1
    ;                 CONTINUE
    ;             SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
    ;                 CONTINUE
    ;             IF CITY_DEVELOPMENT:CITY_ID == RESULT
    ;                 PRINTFORML 拆除了%CITY_NAME:CITY_ID%的%GET_BUILDING_NAME(CITY_DEVELOPMENT:CITY_ID)%
	; 		        CITY_DEVELOPMENT:CITY_ID = 0
    ;             ENDIF
    ;         NEXT
    ;         RETURN
    ;         PRINTFORMW 清除完成！
    ;     ELSE
    ;         CLEARLINE 8
    ;     ENDIF
    ; WEND
@BUILDING_BATCH_PROCESSING_CLEAR_ALL_BUILDING
	#DIM CITY_ID
	SETCOLOR カラー_警告
	PRINTFORML 警告！警告！警告！
	RESETCOLOR
	PRINTFORML 将清除己方领土上所有建筑
	PRINTFORML 这个过程是不可逆的且不会返还任何资金！
	SETCOLOR カラー_警告
	PRINTFORML 警告！警告！警告！
	CALL ASK_YN
	SIF RESULT == 1
		RETURN
	FOR CITY_ID,0,MAX_CITY
		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		FOR LOCAL,0,MAX_BUILDING_SLOT_NUM
			CITY_BUILDING:CITY_ID:LOCAL = 0
		NEXT
	NEXT
	PRINTFORML 清理完毕！
	PRINTFORMW 如需撤回请及时读档！
	RESETCOLOR
@BUILDING_BATCH_PROCESSING_BUILD_BUILDING
	#DIM CITY_ID
	#DIM STOP_LINE
	#DIM COUNTRY_ID
	#DIM COST
	#DIM BUILD_TARGET
	#DIM SLOT_PTR
	COUNTRY_ID = CFLAG:MASTER:所属
	COST = GET_BUILDING_COST(COUNTRY_ID,建造物_等级_一级建筑)
	PRINTFORML 请注意建造需要消耗{COST}国库资金
	PRINTFORML 当前国库资金为{MONEY:COUNTRY_ID}
	IF MONEY:COUNTRY_ID < COST
		PRINTFORMW 国库里没有足够的钱进行任何建造
		RETURN
	ENDIF
	WHILE 1
		PRINTFORML 设置停止资金线
		PRINTFORML 资金小于设定值后将停止建造
		PRINTFORML 请输入0~{MONEY:COUNTRY_ID}的数值(0表示不设置停止线)
		INPUT
		IF RESULT >= 0 && RESULT <= MONEY:COUNTRY_ID
			CLEARLINE 1
			STOP_LINE = RESULT
			PRINTFORML 停止资金线 = {RESULT}
			BREAK
		ELSE
			CLEARLINE 4
		ENDIF
	WEND
	PRINTFORML 请选择需要建造的建筑
	PRINTBUTTON "[兵舎]",建造物_兵舎
	PRINTL
	PRINTBUTTON "[独占市場]",建造物_独占市场
	PRINTL
	PRINTBUTTON "[基础工业园区]",建造物_基础工业园区
	PRINTL
	PRINTBUTTON "[地方政府]",建造物_地方政府
	PRINTL
	PRINTBUTTON "[小型妓院]",建造物_小型妓院
	PRINTL
	PRINTBUTTON "[竞技场转播点]",建造物_竞技场转播点
	PRINTL
	PRINTBUTTON "[返回]",0
	PRINTL
	INPUT
	BUILD_TARGET = RESULT
	SIF !GROUPMATCH(BUILD_TARGET,建造物_兵舎,建造物_独占市场,建造物_基础工业园区,建造物_地方政府,建造物_小型妓院,建造物_竞技场转播点)
		RETURN
	FOR CITY_ID,0,MAX_CITY
		FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
			SIF !BUILDING_SLOT_AVAIL_CHECK(COUNTRY_ID,SLOT_PTR)
				CONTINUE
			SIF CITY_TYPE:CITY_ID == 1
				CONTINUE
			SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
				CONTINUE
			SIF CITY_BUILDING:CITY_ID:SLOT_PTR  != 0
				CONTINUE
			IF MONEY:COUNTRY_ID < COST
				PRINTFORMW 国库里没有足够的钱进行建造
				RETURN
			ENDIF
			IF MONEY:COUNTRY_ID <= STOP_LINE
				PRINTFORMW 金钱消耗抵达设定的停止线({STOP_LINE})
				RETURN
			ENDIF
			CITY_BUILDING:CITY_ID:SLOT_PTR  = BUILD_TARGET
			MONEY:COUNTRY_ID -= COST
			PRINTFORML 在了%CITY_NAME:CITY_ID%建造了%GET_BUILDING_NAME(BUILD_TARGET)%
		NEXT
	NEXT
    PRINTFORMW 建造完毕！

@BUILDING_BATCH_PROCESSING_BUILD_LOCATION
	#DIM CITY_ID
	#DIM STOP_LINE
	#DIM COST
	#DIM BUILD_TARGET
	#DIM SLOT_PTR
	WHILE 1
		PRINTFORML 定位建筑
		PRINTFORML 可以自行输入建筑代码进行搜索
		PRINTBUTTON "[旧淫腐街]",建造物_旧淫腐街
		PRINTL
		PRINTBUTTON "[合法性机构]",建造物_合法性机构
		PRINTL
		PRINTBUTTON "[事迹宣传台]",建造物_事迹宣传台
		PRINTL
		PRINTBUTTON "[梦境暗示仪]",建造物_梦境暗示仪
		PRINTL
		PRINTBUTTON "[和平部]",建造物_和平部
		PRINTL
		PRINTBUTTON "[真理部]",建造物_真理部
		PRINTL
		PRINTBUTTON "[友爱部]",建造物_友爱部
		PRINTL
		PRINTBUTTON "[“快乐”所]",建造物_“快乐”所
		PRINTL
		PRINTBUTTON "[噩梦发生器]",建造物_噩梦发生器
		PRINTL
		PRINTBUTTON "[长程火炮阵地]",建造物_长程火炮阵地
		PRINTL
		PRINTBUTTON "[返回]",0
		PRINTL
		INPUT
		BUILD_TARGET = RESULT
		SIF !IS_BUILDING(BUILD_TARGET)
			RETURN 0
		FOR CITY_ID,0,MAX_CITY
			FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
				SIF CITY_TYPE:CITY_ID == 1
					CONTINUE
				IF CITY_BUILDING:CITY_ID:SLOT_PTR  == BUILD_TARGET
					PRINTFORML %CITY_NAME:CITY_ID%的槽位{SLOT_PTR+1}存在建筑《%GET_BUILDING_NAME(BUILD_TARGET)%》
				ENDIF
			NEXT
		NEXT
		PRINTFORMW 定位完毕！
	WEND
@DEBUG_CREATE_BUILDING
    #DIM CITY_ID
	#DIM SLOT_PTR
	FOR CITY_ID,0,MAX_CITY
		FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM
			CITY_BUILDING:CITY_ID:SLOT_PTR = RAND(BUILDING_START,BUILDING_NUM)
		NEXT
	NEXT
    PRINTFORMW 生成完毕！

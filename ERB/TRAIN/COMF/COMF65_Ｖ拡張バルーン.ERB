;Ｖ拡張バルーン

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME65
LOCALS:0 = Ｖ拡張気球

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE65
;共通部分
CALL COM_ABLE_COMMON(65)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;拡張バルーンが必要
SIF !ITEM:拡張気球 && !ITEM:A_拡張気球
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
;ターゲットのＶが塞がっていれば不可
SIF IS_V_HOLD(MTAR:0)
	RETURN 0
SIF !CAN_REACH_GROIN(MPLY:0, MTAR:0) && !FLAG:RECHECKING
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM65
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;ターゲットのＶ拡張経験による諸々の倍率
LOCAL:10 = MIN(100 + EXP:(MTAR:0):Ｖ拡張経験 * 10 + MIN(ABL:(MTAR:0):Ｖ感, 6) * 40, 600)

;●プレイヤーについて処理
EXP:(MPLY:0):性技経験値 += MTAR_NUM / 2 + 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):接触 = 10
SOURCE:(MPLY:0):逸脱 = 100
SOURCE:(MPLY:0):性行動 = 150

IF IS_INITIATIVE(MPLY:0)
	SOURCE:(MPLY:0):嗜虐 = 120
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 50)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲットについて処理
EXP:(MTAR:0):Ｖ拡張経験 += 1

SOURCE:(MTAR:0):露出 = 70
SOURCE:(MTAR:0):逸脱 = 300
SOURCE:(MTAR:0):接触 = 10
SOURCE:(MTAR:0):性行動 = 300

IF TALENT:(MPLY:0):技師
	SOURCE:(MTAR:0):快Ｖ += SENSE_HOUSHI(MPLY:0, MTAR:0, 400) * (LOCAL:10 + 50) / 150
	SOURCE:(MTAR:0):苦痛 = 1200 * 100 / LOCAL:10
ELSE
	SOURCE:(MTAR:0):快Ｖ += SENSE_HOUSHI(MPLY:0, MTAR:0, 300) * (LOCAL:10 + 50) / 150
	SOURCE:(MTAR:0):苦痛 = 1600 * 100 / LOCAL:10
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 50, 80)

;Ｖ拡張バルーンの共通処理
CALL COM_COMMON65(MTAR:0)

IF TALENT:(MTAR:0):処女
	IF MPLY:0 == MASTER
		;処女喪失フラグ(主人に性交以外で奪われる)
		TCVAR:(MTAR:0):13 = 2
	ELSE
		;処女喪失フラグ(主人以外に性交以外で奪われる)
		TCVAR:(MTAR:0):13 = 4
	ENDIF
ENDIF

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP65
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP65(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

SOURCE:(LOCAL:2):性行動 += 50

;ターゲットのＶ拡張経験による諸々の倍率
LOCAL:10 = MIN(100 + EXP:(LOCAL:3):Ｖ拡張経験 * 10 + MIN(ABL:(LOCAL:3):Ｖ感, 6) * 40, 600)

EXP:(LOCAL:3):Ｖ拡張経験 += 1

SOURCE:(LOCAL:3):露出 += 30
SOURCE:(LOCAL:3):逸脱 += 120
SOURCE:(LOCAL:3):性行動 += 100

IF TALENT:(LOCAL:2):技師
	SOURCE:(LOCAL:3):快Ｖ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 200) * (LOCAL:10 + 50) / 150
	SOURCE:(LOCAL:3):苦痛 = 600 * 100 / LOCAL:10
ELSE
	SOURCE:(LOCAL:3):快Ｖ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 150) * (LOCAL:10 + 50) / 150
	SOURCE:(LOCAL:3):苦痛 = 800 * 100 / LOCAL:10
ENDIF

;倒錯度変化
TCVAR:(LOCAL:3):50 += 1

;Ｖ拡張バルーンの共通処理
CALL COM_COMMON65(LOCAL:3)

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE65(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%的小穴里，扩张气球挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP65(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%的小穴被扩张气球撑开了
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の膣は拡張バルーンで押し広げられている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP65(ARG:0)
PRINTFORMW 从%EQUIP_TARGET_ANAME(ARG:0)%的小穴中取出扩张气球
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の膣内から拡張バルーンを取り出した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER65(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 85

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE65
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = 上著環的
ELSE
	LOCALS:0 = 
ENDIF

IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 = %LOCALS:0%未经世事的
	LOCALS:1 = 無理やり
ELSE
	LOCALS:0 = %LOCALS:0%
	LOCALS:1 = 
ENDIF

IF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = %LOCALS:0%湿透了的
	LOCALS:1 = %LOCALS:1%
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%迷你秘缝
ELSE
	LOCALS:0 = %LOCALS:0%秘缝
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0)%的%LOCALS:0%中插入扩张气球，并充入空气将小穴扩张开
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:0%に拡張バルーンを挿入し、空気を送って%LOCALS:1%内部を押し広げ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %ANAME(MTAR:0)%让%ANAME(MPLY:0)%拿着扩张气球，让她插入自己的%LOCALS:0%中，并将小穴扩张开来
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に拡張バルーンを持たせると、それを自身の%LOCALS:0%へと挿入させて%LOCALS:1%内部を拡張させた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用气球扩张%ANAME(MTAR:0)%的小穴
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%の膣をバルーンで拡張するように命令した
		PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0)%的%LOCALS:0%中插入扩张气球，并充入空气将小穴扩张开
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:0%に拡張バルーンを挿入し、空気を送って%LOCALS:1%内部を押し広げた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST65

;--------------------------------------------------------
;装着時と継続状態での共通処理
;--------------------------------------------------------
@COM_COMMON65(ARG:0)

IF PALAM:(ARG:0):潤滑 < PALAMLV:1
	TIMES SOURCE:(ARG:0):快Ｖ, 0.05
	TIMES SOURCE:(ARG:0):苦痛, 3.00
ELSEIF PALAM:(ARG:0):潤滑 < PALAMLV:3
	TIMES SOURCE:(ARG:0):快Ｖ, 0.20
	TIMES SOURCE:(ARG:0):苦痛, 2.20
ELSEIF PALAM:(ARG:0):潤滑 < PALAMLV:5
	TIMES SOURCE:(ARG:0):快Ｖ, 0.60
	TIMES SOURCE:(ARG:0):苦痛, 1.50
ELSEIF PALAM:(ARG:0):潤滑 < PALAMLV:7
	TIMES SOURCE:(ARG:0):快Ｖ, 0.85
	TIMES SOURCE:(ARG:0):苦痛, 1.20
ELSE
	TIMES SOURCE:(ARG:0):快Ｖ, 1.00
	TIMES SOURCE:(ARG:0):苦痛, 1.00
ENDIF

IF TALENT:(ARG:0):体格 == 体格_小柄
	TIMES SOURCE:(ARG:0):苦痛, 1.30
ENDIF

IF TALENT:(ARG:0):処女 == 1
	SOURCE:(ARG:0):苦痛 = SOURCE:(ARG:0):苦痛 * 2 + 500
ENDIF

IF ARG:0 != MASTER
	IF !(TALENT:(ARG:0):恋慕 || TALENT:(ARG:0):親友)
		IF TALENT:(ARG:0):貞操観念
			IF TALENT:(ARG:0):処女
				SOURCE:(ARG:0):反感 = 5000
			ELSE
				SOURCE:(ARG:0):反感 = 1000
			ENDIF
		ELSEIF TALENT:(ARG:0):不在乎貞操
			IF TALENT:(ARG:0):処女
				SOURCE:(ARG:0):反感 = 300
			ENDIF
		ELSE
			IF TALENT:(ARG:0):処女
				SOURCE:(ARG:0):反感 = 2500
			ENDIF
		ENDIF
		IF TALENT:(ARG:0):恋人
			TIMES SOURCE:(ARG:0):反感, 0.10
		ENDIF
	ENDIF

	;好感度・従属度・支配度の影響
	LOCAL:0 = MAX(CFLAG:(ARG:0):2, CFLAG:(ARG:0):4, CFLAG:(ARG:0):5)
	IF LOCAL:0 < 100
		TIMES SOURCE:(ARG:0):反感, 1.00
	ELSEIF LOCAL:0 < 300
		TIMES SOURCE:(ARG:0):反感, 0.90
	ELSEIF LOCAL:0 < 500
		TIMES SOURCE:(ARG:0):反感, 0.80
	ELSEIF LOCAL:0 < 800
		TIMES SOURCE:(ARG:0):反感, 0.40
	ELSEIF LOCAL:0 < 1500
		TIMES SOURCE:(ARG:0):反感, 0.15
	ELSE
		SOURCE:(ARG:0):反感 = SOURCE:(ARG:0):反感 / ((LOCAL:0 - 1500) / 50 + 10)
	ENDIF
ENDIF

;対象の膣が緩む
CALL TIGHTNESS_DECREASE_V(ARG:0, RAND(5, 8))
CALL VIRGIN_COMMON(ARG:0, "扩张气球", GET_SITUATION_BY_TRAIN_MODE())

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN65
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_65()
RETURN POWER(2, 性的嗜好_道具を使いたい) + POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_65()
RETURN POWER(2, 性的嗜好_道具を使われたい) + POWER(2, 性的嗜好_虐げられたい)


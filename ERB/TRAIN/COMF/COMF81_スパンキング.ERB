;スパンキング

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME81
LOCALS:0 = 打屁股

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE81
;共通部分
CALL COM_ABLE_COMMON(81)
SIF RESULT == 0
	RETURN 0
;プレイヤーが必要
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットは最大でプレイヤー*2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > MPLY_NUM * 2
	RETURN 0
	
FOR LOCAL:0, 0, MTAR_NUM
	FOR LOCAL:1, 0, MPLY_NUM
		SIF !CAN_REACH_GROIN(MPLY:(LOCAL:1), MTAR:(LOCAL:0))
		RETURN 0
	NEXT
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM81
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)
	DOWNBASE:(LOCAL:1):体力 += 100
	EXP:(LOCAL:1):嗜虐経験値 += RAND:3 + 1

	SOURCE:(LOCAL:1):嗜虐 = 150
	SOURCE:(LOCAL:1):逸脱 = 50
	SOURCE:(LOCAL:1):接触 = 30
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 150, 50)
NEXT

;主導権に応じた優越・屈従のソース追加

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60
	
	IF TALENT:(LOCAL:1):美臀 || GET_HIPSIZE(LOCAL:1) >= 1
		FOR LOCAL:2, 0, MPLY_NUM
			SOURCE:(MPLY:(LOCAL:2)):嗜虐 += 30
		NEXT
	ENDIF

	SOURCE:(LOCAL:1):苦痛 = 50 + MIN(ABL:(MPLY:0):武闘, 90) * 3
	SOURCE:(LOCAL:1):露出 = 100
	SOURCE:(LOCAL:1):逸脱 = 50
	SOURCE:(LOCAL:1):接触 = 30

	;ターゲットが気絶している場合
	IF TCVAR:(LOCAL:1):52
		SOURCE:(LOCAL:1):気絶回復 = 300
	ENDIF

	EXP:(LOCAL:1):被虐経験値 += RAND:3 + 1


	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 0, 150)

	FOR LOCAL:2, 0, MPLY_NUM
		IF GETBIT(TALENT:(MPLY:(LOCAL:2)):淫乱系, 素質_淫乱_サド)
			TIMES SOURCE:(LOCAL:1):苦痛, 1.50
		ENDIF
	NEXT
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER81(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"加虐Lv{ABL:(ARG:0):加虐}", ABL:(ARG:0):加虐 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):老実
	CALL COM_ORDER_ELEMENT(ARG:0, "老実", -3)
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "加虐", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE81
;ターゲットの数で分岐
IF MTAR_NUM > 3
	LOCALS:0 = %ANAME(MTAR:0)%们
	LOCALS:1 = 按顺序
	LOCALS:2 = 尻

ELSEIF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
	LOCALS:1 = 轮流
	LOCALS:2 = 尻

ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
	LOCALS:1 = 
	LOCALS:2  = %STR_BODY("尻：長", MTAR:0)%
ENDIF

;ターゲットの数で分岐
IF MPLY_NUM > 3
	LOCALS:3 = %ANAME(MPLY:0)%们

ELSEIF MPLY_NUM == 2
	LOCALS:3 = %ANAME(MPLY:0)%和%ANAME(MPLY:1)%

ELSE
	LOCALS:3 = %ANAME(MPLY:0)%

ENDIF


SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:3%%LOCALS:1%拍打%LOCALS:0%的%LOCALS:2%，不断反复拍打直到都红肿了起来…
		;TranslatedMark %LOCALS:3%は%LOCALS:0%の%LOCALS:2%を%LOCALS:1%、赤く腫れ上がるほどに何度も叩い%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:0%命令%ANAME(MPLY:0)%用手拍打自己的%LOCALS:2%
		;TranslatedMark %LOCALS:0%は%ANAME(MPLY:0)%に自身の%LOCALS:2%を向け、それを手で叩くように命令した
		PRINTFORMW %LOCALS:3%听从了指示，%LOCALS:1%拍打了%LOCALS:0%的屁股好几次…
		;TranslatedMark %LOCALS:3%は言われるまま、%LOCALS:0%の尻を%LOCALS:1%何度も叩いた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:3%去打%LOCALS:0%的屁股
		;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:3%に対して、%LOCALS:0%の尻を叩くように命令した
		PRINTFORMW %ANAME(MPLY:0)%听从了指示，%LOCALS:1%打了%LOCALS:0%的%LOCALS:2%好几次…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%LOCALS:0%の%LOCALS:2%を%LOCALS:1%何度も叩いた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST81
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF (LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):怖主
		LOCAL:3 = CUP:(LOCAL:1):怒主

		IF ABL:(LOCAL:1):被虐 >= 5 && IS_PLAYABLE(LOCAL:1)
			PRINTFORML %ANAME(LOCAL:1)%虽然感觉到了疼痛，但还是很高兴地发出了甜蜜的声音
			;TranslatedMark %ANAME(LOCAL:1)%は痛みを感じているにもかかわらず嬉しそうに甘い声を上げている
		ELSEIF ABL:(LOCAL:1):被虐 >= 3
			PRINTFORML 虽然%ANAME(LOCAL:1)%发出了悲鸣，但那声音中夹杂着甜蜜的色彩
			;TranslatedMark %ANAME(LOCAL:1)%は悲鳴を上げるが、その声には甘い色が混じっている
		ELSEIF LOCAL:2 >= 3000
			PRINTFORML %ANAME(LOCAL:1)%由于过度的疼痛和恐怖而完全放弃了羞耻心，毫不在意被外面的人听到，大哭起来
			;TranslatedMark %ANAME(LOCAL:1)%はあまりの痛みと恐怖に恥も外聞もなく泣き叫んでいる
		ELSEIF LOCAL:2 >= 1500
			PRINTFORML %ANAME(LOCAL:1)%无法忍受疼痛，好几次发出悲鸣，发狂一样地挣扎着
			;TranslatedMark %ANAME(LOCAL:1)%は痛みに耐えきれず、何度も悲鳴を上げながら悶え狂っている
		ELSEIF LOCAL:2 >= 500
			PRINTFORML %ANAME(LOCAL:1)%痛得嘴里直哼哼
			;TranslatedMark %ANAME(LOCAL:1)%は痛みのあまり口から呻きを漏らしている
		ELSE
			IF LOCAL:3 >= 200 && !IS_INITIATIVE(MTAR:(LOCAL:0))
				PRINTFORML %ANAME(LOCAL:1)%一边忍耐着疼痛，一边瞪着%ANAME(MPLY:0)%
				;TranslatedMark %ANAME(LOCAL:1)%は痛みに耐えながら、%ANAME(MPLY:0)%を睨みつけた
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%咬紧牙关忍耐着疼痛
				;TranslatedMark %ANAME(LOCAL:1)%は歯を食いしばって痛みに耐えている
			ENDIF
		ENDIF
	ENDIF
	SIF GET_HIPSIZE(LOCAL:1) >= 1
		PRINTFORML 每次被打，%ANAME(LOCAL:1)%的%STR_BODY("尻肉：短", MTAR:0)%都在颤抖
		;TranslatedMark 叩かれるたび、%ANAME(LOCAL:1)%の%STR_BODY("尻肉：短", MTAR:0)%はふるふると震えている
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN81
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_81()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_81()
RETURN POWER(2, 性的嗜好_虐げられたい)


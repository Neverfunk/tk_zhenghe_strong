;---------------------
;対応するデイリーのDISABLEを返す。設定しない場合、イベントは発生しない。
;---------------------
@EVENT_DAILY_DERIVATION_HUNTER_SIMULATION_DUMMY_DISABLE()
RETURN DAILY_GET_DISABLE_CONFIG("HUNTER_SIMULATION")

;---------------------
;発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;対応するデイリーのDISABLEチェックを規約として必須とする
;---------------------
@EVENT_DAILY_DERIVATION_HUNTER_SIMULATION_DUMMY_DECISION()
SIF DVAR:后宫事件冷却 > 0
    DVAR:后宫事件冷却 --
RETURN 0

;---------------------
;本体 ここにはこないが安全のため
;---------------------
@EVENT_DAILY_DERIVATION_HUNTER_SIMULATION_DUMMY()

RETURN 1

;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_HUNTER_SIMULATION_RATE()
RETURN 100

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_HUNTER_SIMULATION_DECISION()
SIF DAY < 5
    RETURN 0
;MASTERがペニスを持っている
SIF !(HAS_PENIS(MASTER))
    RETURN 0
SIF ABL:MASTER:性技 < 3
    RETURN 0
SIF ABL:MASTER:武闘 + ABL:MASTER:防衛 + ABL:MASTER:知略 + ABL:MASTER:政治 < 250
    RETURN 0
SIF !IS_COUNTRY(CFLAG:MASTER:所属)
    RETURN 0
SIF GET_COUNTRY_BOSS(CFLAG:MASTER:所属) != MASTER
    RETURN 0
SIF DVAR:后宫事件冷却
    RETURN 0
;技能只能习得一系，除非全部遗忘
SIF CHECK_SKILL_DUPLICATE(MASTER, スキル_ジャンル_PERSONAL, 0, 41) || CHECK_SKILL_DUPLICATE(MASTER, スキル_ジャンル_PERSONAL, 0, 42)
    RETURN 0
RETURN 1

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_HUNTER_SIMULATION_GENRE()
RETURN デイリー_ジャンル_エロ

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_HUNTER_SIMULATION_SETTARGET()
FOR LOCAL, 1, CHARANUM
    SIF !GETBIT(TALENT:LOCAL:日常系, 素質_デイリー_专属奴隶)
        CONTINUE
    SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属 || CFLAG:LOCAL:特殊状態 || (!IS_SLAVE(LOCAL) && !IS_LOVER(LOCAL)) || !IS_FEMALE(LOCAL)
        CONTINUE
    IF !COOLTIME:LOCAL:0
        DAILY_TARGET:DAILY_TARGET_NUM = LOCAL
        DAILY_TARGET_NUM ++
    ENDIF
NEXT
SIF DAILY_TARGET_NUM < 12
    RETURN 0
RETURN 1

;---------------------
;イベントの進行度合いに応じてイベント名を変更したい場合に利用
;変更する必要がない場合、この関数は不要
;---------------------
@EVENT_DAILY_HUNTER_SIMULATION_NAME()
IF DVAR:演习历时 > 0
    RESULTS '= @"第{DVAR:演习历时 + 1}届模拟对抗演习"
ELSE
    RESULTS '= @"第1届模拟对抗演习开幕式"
ENDIF

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_HUNTER_SIMULATION()
#DIM 当前文字颜色
#DIM 轮数 = 1
#DIM 队伍, MAX_CHARA_NUM / 3, 3
#DIM 队伍数 = 0
;以下只计入队伍ID
#DIM A队, MAX_CHARA_NUM / 3
#DIM A队数 = 0
#DIM B队, MAX_CHARA_NUM / 3
#DIM B队数 = 0
#DIM 排名, MAX_CHARA_NUM / 3
#DIM 排名次序 = 0
#DIM 对战队伍, 2
#DIM 轮空少女, 2
;以下在对战/惩罚时计算
#DIM 感度, 2
#DIM 目标
#DIM 敏捷判定
#DIM 对战轮次
;以下为评价系统用(0-2=能力值,3=平均值)
#DIM 评武力, 4
#DIM 评防御, 4
#DIM 评知略, 4
#DIM 评感度, 4
#DIM 方差
#DIM 符号

#DIM 経験値総量
#DIM 獲得経験値, 6

VARSET 队伍:0:0, __INT_MAX__
VARSET A队, __INT_MAX__
VARSET B队, __INT_MAX__
VARSET 轮空少女
VARSET 獲得経験値
队伍数 = 0
A队数 = 0
B队数 = 0
排名次序 = 0

CALL FISHER_YATES_SHAFFLE(DAILY_TARGET_NUM)
FOR LOCAL, 0, DAILY_TARGET_NUM / 3 * 3
    队伍:队伍数:(LOCAL % 3) = DAILY_TARGET:(SHAFFLE_ARRAY:LOCAL)
    SIF LOCAL / 3 > 队伍数 && 队伍数 < DAILY_TARGET_NUM / 3
        队伍数 ++
NEXT
IF DAILY_TARGET_NUM % 3 != 0
    SIF DAILY_TARGET_NUM % 3 == 2
        轮空少女:1 = DAILY_TARGET:(SHAFFLE_ARRAY:(DAILY_TARGET_NUM - 2))
    轮空少女:0 = DAILY_TARGET:(SHAFFLE_ARRAY:(DAILY_TARGET_NUM - 1))
ENDIF
IF DVAR:演习历时
    PRINTFORML 要跳过开幕式吗？
    CALL ASK_YN
    SIF !RESULT
        GOTO EVENTMAIN
ENDIF
PRINTFORMW 那么，第{DVAR:演习历时 + 1}届模拟对抗演习正式开幕！
PRINTFORMW 接下来，让我们看一下参赛队伍～
当前文字颜色 = GETCOLOR()
FOR LOCAL, 0, 队伍数
    CALL SINGLE_DRAWLINE
    {
        CALL COLOR_PRINTW(@"%ANAME(队伍:LOCAL:0)%",TALENT:(队伍:LOCAL:0):髪色,
            "为队长，",当前文字颜色,
            @"%ANAME(队伍:LOCAL:1)%",TALENT:(队伍:LOCAL:1):髪色,
            "和",当前文字颜色,
            @"%ANAME(队伍:LOCAL:2)%",TALENT:(队伍:LOCAL:2):髪色,
            "为队员的",当前文字颜色,
            @"%ANAME(队伍:LOCAL:0)%队！",TALENT:(队伍:LOCAL:0):髪色)
    }
    评武力:0 = ABL:(队伍:LOCAL:0):武闘
    评武力:1 = ABL:(队伍:LOCAL:1):武闘
    评武力:2 = ABL:(队伍:LOCAL:2):武闘
    评武力:3 = (评武力:0 + 评武力:1 + 评武力:2) / 3
    评防御:0 = ABL:(队伍:LOCAL:0):防衛
    评防御:1 = ABL:(队伍:LOCAL:1):防衛
    评防御:2 = ABL:(队伍:LOCAL:2):防衛
    评防御:3 = (评防御:0 + 评防御:1 + 评防御:2) / 3
    评知略:0 = ABL:(队伍:LOCAL:0):知略
    评知略:1 = ABL:(队伍:LOCAL:1):知略
    评知略:2 = ABL:(队伍:LOCAL:2):知略
    评知略:3 = (评知略:0 + 评知略:1 + 评知略:2) / 3
    评感度:0 = (ABL:(队伍:LOCAL:0):Ｃ感 + ABL:(队伍:LOCAL:0):Ｖ感 + ABL:(队伍:LOCAL:0):Ａ感 + ABL:(队伍:LOCAL:0):Ｂ感 + ABL:(队伍:LOCAL:0):Ｍ感) / 5
    评感度:1 = (ABL:(队伍:LOCAL:1):Ｃ感 + ABL:(队伍:LOCAL:1):Ｖ感 + ABL:(队伍:LOCAL:1):Ａ感 + ABL:(队伍:LOCAL:1):Ｂ感 + ABL:(队伍:LOCAL:1):Ｍ感) / 5
    评感度:2 = (ABL:(队伍:LOCAL:2):Ｃ感 + ABL:(队伍:LOCAL:2):Ｖ感 + ABL:(队伍:LOCAL:2):Ａ感 + ABL:(队伍:LOCAL:2):Ｂ感 + ABL:(队伍:LOCAL:2):Ｍ感) / 5
    评感度:3 = (评感度:0 + 评感度:1 + 评感度:2) / 3
    ;三维评价：
    PRINTFORM 武力方面：
    SETCOLOR GET_RANK_COLOR(ALPHABET_RANK(ランク_ＳＬＧ, 评武力:3))
    方差 = (评武力:3 - 评武力:0) * (评武力:3 - 评武力:0) + (评武力:3 - 评武力:1) * (评武力:3 - 评武力:1) + (评武力:3 - 评武力:2) * (评武力:3 - 评武力:2)
    符号 = SIGN(评武力:3 - 评武力:0) * SIGN(评武力:3 - 评武力:1) * SIGN(评武力:3 - 评武力:2)
    SELECTCASE 评武力:3
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_A
            IF 方差 < 100
                PRINTFORMW 个个都是能独当一面的悍将
            ELSEIF 符号 < 0
                PRINTFORMW 即使有些许缺陷，其平均实力也是其他队无法匹敌的
            ELSE
                IF MAX((评武力:3 - 评武力:0), (评武力:3 - 评武力:1), (评武力:3 - 评武力:2)) > 20
                    PRINTFORMW 混进了极其恐怖的成员……
                ELSE
                    PRINTFORMW 离完美不远了
                ENDIF
            ENDIF
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_C
            IF 方差 < 100
                PRINTFORMW 是志同道合的猛将们
            ELSEIF 符号 < 0
                PRINTFORMW 只要能克服难关，一定能超常发挥
            ELSE
                IF MAX((评武力:3 - 评武力:0), (评武力:3 - 评武力:1), (评武力:3 - 评武力:2)) > 10
                    PRINTFORMW 队长需要有一定能力才能发挥出队伍的全部水平……
                ELSE
                    PRINTFORMW 就看能不能运用个体优势了
                ENDIF
            ENDIF
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_E
            IF 方差 < 100
                PRINTFORMW 她们是来证明自己能胜任的
            ELSEIF 符号 < 0
                PRINTFORMW 希望能正常发挥，不要掉链子
            ELSE
                IF MAX((评武力:3 - 评武力:0), (评武力:3 - 评武力:1), (评武力:3 - 评武力:2)) > 10
                    PRINTFORMW 队伍之间有高人指点……
                ELSE
                    PRINTFORMW 其中还是有一些闪光点的……
                ENDIF
            ENDIF
        CASEELSE
            IF 方差 < 100
                PRINTFORMW 不是干这个工作的……
            ELSEIF 符号 < 0
                PRINTFORMW 理论上应该有更高的水准……
            ELSE
                IF MAX((评武力:3 - 评武力:0), (评武力:3 - 评武力:1), (评武力:3 - 评武力:2)) > 10
                    PRINTFORMW 看来是有高人指点才敢组队的……
                ELSE
                    PRINTFORMW 还是有可造之才的……
                ENDIF
            ENDIF
    ENDSELECT
    RESETCOLOR
    PRINTFORM 防御方面：
    SETCOLOR GET_RANK_COLOR(ALPHABET_RANK(ランク_ＳＬＧ, 评防御:3))
    方差 = (评防御:3 - 评防御:0) * (评防御:3 - 评防御:0) + (评防御:3 - 评防御:1) * (评防御:3 - 评防御:1) + (评防御:3 - 评防御:2) * (评防御:3 - 评防御:2)
    符号 = SIGN(评防御:3 - 评防御:0) * SIGN(评防御:3 - 评防御:1) * SIGN(评防御:3 - 评防御:2)
    SELECTCASE 评防御:3
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_A
            IF 方差 < 100
                PRINTFORMW 个个都是万夫莫开的存在
            ELSEIF 符号 < 0
                PRINTFORMW 即使有些许缺陷，其平均实力也是其他队无法匹敌的
            ELSE
                IF MAX((评防御:3 - 评防御:0), (评防御:3 - 评防御:1), (评防御:3 - 评防御:2)) > 20
                    PRINTFORMW 混进了极其恐怖的成员……
                ELSE
                    PRINTFORMW 离完美不远了
                ENDIF
            ENDIF
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_C
            IF 方差 < 100
                PRINTFORMW 说得上铜墙铁壁
            ELSEIF 符号 < 0
                PRINTFORMW 只要能克服难关，一定能超常发挥
            ELSE
                IF MAX((评防御:3 - 评防御:0), (评防御:3 - 评防御:1), (评防御:3 - 评防御:2)) > 10
                    PRINTFORMW 队长需要有一定能力才能发挥出队伍的全部水平……
                ELSE
                    PRINTFORMW 就看能不能运用个体优势了
                ENDIF
            ENDIF
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_E
            IF 方差 < 100
                PRINTFORMW 能保命
            ELSEIF 符号 < 0
                PRINTFORMW 希望能正常发挥，不要掉链子
            ELSE
                IF MAX((评防御:3 - 评防御:0), (评防御:3 - 评防御:1), (评防御:3 - 评防御:2)) > 10
                    PRINTFORMW 队伍之间有高人指点……
                ELSE
                    PRINTFORMW 其中还是有一些闪光点的……
                ENDIF
            ENDIF
        CASEELSE
            IF 方差 < 100
                PRINTFORMW 只能说还有更重要的事可以干……
            ELSEIF 符号 < 0
                PRINTFORMW 理论上应该有更高的水准……
            ELSE
                IF MAX((评防御:3 - 评防御:0), (评防御:3 - 评防御:1), (评防御:3 - 评防御:2)) > 10
                    PRINTFORMW 看来是有高人指点才敢组队的……
                ELSE
                    PRINTFORMW 还是有可造之才的……
                ENDIF
            ENDIF
    ENDSELECT
    RESETCOLOR
    PRINTFORM 知略方面：
    SETCOLOR GET_RANK_COLOR(ALPHABET_RANK(ランク_ＳＬＧ, 评知略:3))
    方差 = (评知略:3 - 评知略:0) * (评知略:3 - 评知略:0) + (评知略:3 - 评知略:1) * (评知略:3 - 评知略:1) + (评知略:3 - 评知略:2) * (评知略:3 - 评知略:2)
    符号 = SIGN(评知略:3 - 评知略:0) * SIGN(评知略:3 - 评知略:1) * SIGN(评知略:3 - 评知略:2)
    SELECTCASE 评知略:3
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_A
            IF 方差 < 100
                PRINTFORMW 平时就是%ANAME(MASTER)%的贤内助
            ELSEIF 符号 < 0
                PRINTFORMW 即使有些许缺陷，其平均实力也是其他队无法匹敌的
            ELSE
                IF MAX((评知略:3 - 评知略:0), (评知略:3 - 评知略:1), (评知略:3 - 评知略:2)) > 20
                    PRINTFORMW 混进了极其恐怖的成员……
                ELSE
                    PRINTFORMW 离完美不远了
                ENDIF
            ENDIF
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_C
            IF 方差 < 100
                PRINTFORMW 不管何时都非常重要的将才
            ELSEIF 符号 < 0
                PRINTFORMW 只要能克服难关，一定能超常发挥
            ELSE
                IF MAX((评知略:3 - 评知略:0), (评知略:3 - 评知略:1), (评知略:3 - 评知略:2)) > 10
                    PRINTFORMW 队长需要有一定能力才能发挥出队伍的全部水平……
                ELSE
                    PRINTFORMW 就看能不能运用个体优势了
                ENDIF
            ENDIF
        CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_E
            IF 方差 < 100
                PRINTFORMW 一个人就可以解个大型谜题了
            ELSEIF 符号 < 0
                PRINTFORMW 希望能正常发挥，不要掉链子
            ELSE
                IF MAX((评知略:3 - 评知略:0), (评知略:3 - 评知略:1), (评知略:3 - 评知略:2)) > 10
                    PRINTFORMW 队伍之间有高人指点……
                ELSE
                    PRINTFORMW 其中还是有一些闪光点的……
                ENDIF
            ENDIF
        CASEELSE
            IF 方差 < 100
                PRINTFORMW 顶个啥来着……？
            ELSEIF 符号 < 0
                PRINTFORMW 理论上应该有更高的水准……
            ELSE
                IF MAX((评知略:3 - 评知略:0), (评知略:3 - 评知略:1), (评知略:3 - 评知略:2)) > 10
                    PRINTFORMW 看来是有高人指点才敢组队的……
                ELSE
                    PRINTFORMW 还是有可造之才的……
                ENDIF
            ENDIF
    ENDSELECT
    ;性评价：
    方差 = (评感度:3 - 评感度:0) * (评感度:3 - 评感度:0) + (评感度:3 - 评感度:1) * (评感度:3 - 评感度:1) + (评感度:3 - 评感度:2) * (评感度:3 - 评感度:2)
    IF 方差 < 5
        SETCOLOR カラー_ピンク
        PRINTFORM 感度方面：
        SETCOLOR GET_RANK_COLOR(ALPHABET_RANK(ランク_その他, 评感度:3))
        SELECTCASE 评感度:3
            CASE IS >= ランク閾値:ランク_その他:ランク_A
                PRINTFORMW 一碰到先走汁就会怀孕的体质♥
            CASE IS >= ランク閾値:ランク_その他:ランク_C
                PRINTFORMW 已经变成了%ANAME(MASTER)%主人的形状♥
            CASE IS >= ランク閾値:ランク_その他:ランク_E
                PRINTFORMW 需要更多的开发♥
            CASEELSE
                PRINTFORMW 还没怎么宠幸过……
        ENDSELECT
    ENDIF
    RESETCOLOR
NEXT
CALL SINGLE_DRAWLINE
$EVENTMAIN
PRINTFORMW 本次共有{队伍数}队队伍参赛
IF 轮空少女
    IF 轮空少女:1
        PRINTFORMW
    ENDIF
ENDIF
PRINTFORML 需要介绍规则吗？
CALL ASK_YN
IF !RESULT
    PRINTFORMW 本次演习采用了“淘汰制”规则
    PRINTFORML 稍后，全部队伍将会打乱顺序加入A队，每两队之间进行两两对抗
    PRINTFORMW 轮空的队伍则与对抗战败的队伍一起进入B队
    PRINTFORML 下一轮，B队中每两队之间也要进行两两对抗
    PRINTFORML 不同的是，轮空的队伍仍然维持在B队，但对抗中胜利的队伍返回A队，失败的队伍则被直接淘汰
    PRINTFORMW 淘汰的队伍嘛……就任由%ANAME(MASTER)%主人处置♥
    PRINTFORML 话说回来，这次惩罚的标准是……？
    PRINTFORM (目前是
    SELECTCASE DVAR:自动战败惩罚
        CASE 1
            CALL COLOR_PRINT("总是惩罚", カラー_ピンク)
        CASE -1
            CALL COLOR_PRINT("不予惩罚", カラー_灰色)
        CASE 0
            CALL COLOR_PRINT("每次手动选择", GETDEFCOLOR())
    ENDSELECT
    PRINTFORML )
    CALL ASK_MULTI("只要是战败就要让我惩罚", "这次就不惩罚了", "每次让我手动选择")
    SELECTCASE RESULT
        CASE 0
            PRINTFORMW 好的♥
            DVAR:自动战败惩罚 = 1
        CASE 1
            PRINTFORMW 好的
            DVAR:自动战败惩罚 = -1
        CASE 2
            PRINTFORMW 好的，到时候就依主人的♥
            DVAR:自动战败惩罚 = 0
    ENDSELECT
ELSE
    PRINTFORMW 好的，那么现在……各就各位……！
ENDIF
IF 轮空少女
    PRINTFORMW 等等……
    PRINTFORMW 好像%ANAME(轮空少女:0)%\@轮空少女:1 ? 和%ANAME(轮空少女:1)% # \@没被分到队伍的样子……
    PRINTFORMW 不过%ANAME(MASTER)%决定把\@轮空少女:1 ? 她们 # 她\@留到自己身边
    PRINTFORMW 于是没能参加比赛的%ANAME(轮空少女:0)%\@轮空少女:1 ? 和%ANAME(轮空少女:1)% # \@就只能在中央的裁判台上被%ANAME(MASTER)%不停的侵犯……
ENDIF
CALL SINGLE_DRAWLINE("+") 
PRINTFORMW 比赛开始！
FOR LOCAL, 0, 队伍数
    A队:LOCAL = LOCAL
NEXT
A队数 = 队伍数
WHILE 1
    CALL SINGLE_DRAWLINE
    PRINTFORM 第{轮数}轮淘汰，开始！
    FORCEWAIT
    ;第一轮：A队淘汰赛，淘汰人员进入B队
    IF A队数 > 0
        CALL FISHER_YATES_SHAFFLE(A队数)
        ARRAYMSORT SHAFFLE_ARRAY, A队
        IF A队数 % 2 == 1
            PRINTFORMW 因为轮空，%ANAME(队伍:(A队:(A队数 - 1)):0)%队将进入B队！
            B队:B队数 = A队:(A队数 - 1)
            B队数 ++
            A队:(A队数 - 1) = __INT_MAX__
            A队数 --
        ENDIF
        FOR 对战轮次, 0, A队数 / 2 
            对战队伍:0 = A队:(2 * 对战轮次)
            对战队伍:1 = A队:(2 * 对战轮次 + 1)
            感度:0 = 0
            感度:1 = 0
            [IF_DEBUG]
            FOR LOCAL:10, 0, A队数
                DEBUGPRINTFORM {A队:(LOCAL:10)}, 
            NEXT
            DEBUGPRINTL
            [ENDIF]
            DEBUGPRINTFORML 队伍信息：
            DEBUGPRINTFORML A队第{对战队伍:0}队成员：
            DEBUGPRINTFORML %ANAME(队伍:(对战队伍:0):0)%({队伍:(对战队伍:0):0})%ANAME(队伍:(对战队伍:0):1)%({队伍:(对战队伍:0):1})%ANAME(队伍:(对战队伍:0):2)%({队伍:(对战队伍:0):2})
            DEBUGPRINTFORML A队第{对战队伍:1}队成员：
            DEBUGPRINTFORML %ANAME(队伍:(对战队伍:1):0)%({队伍:(对战队伍:1):0})%ANAME(队伍:(对战队伍:1):1)%({队伍:(对战队伍:1):1})%ANAME(队伍:(对战队伍:1):2)%({队伍:(对战队伍:1):2})
            FOR LOCAL, 0, 3
                感度:0 += (ABL:(队伍:(对战队伍:0):LOCAL):Ｃ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ｖ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ａ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ｂ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ｍ感) / 5
                感度:1 += (ABL:(队伍:(对战队伍:1):LOCAL):Ｃ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ｖ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ａ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ｂ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ｍ感) / 5
            NEXT
            CALL EVENT_HUNTER_BATTLE(队伍:(对战队伍:0):0,队伍:(对战队伍:0):1,队伍:(对战队伍:0):2,队伍:(对战队伍:1):0,队伍:(对战队伍:1):1,队伍:(对战队伍:1):2)
            FORCEWAIT
            SELECTCASE RESULT
                CASE 0
                    PRINTFORMW 同归于尽……
                    PRINTFORMW 那么……只能用那招了……
                    PRINTFORMW ……
                    目标 = (RAND:(感度:0 + 感度:1) >= 感度:0)
                    PRINTFORMW %ANAME(队伍:(对战队伍:目标):0)%被突然出现的%ANAME(MASTER)%强奸了！
                    CALL FUCK(队伍:(对战队伍:目标):0, "Ｃ, Ｖ, Ｂ, 欲望, 精愛, 奉仕, 奉仕, 性交, 接吻, 口淫, Ｖ性交", "膣内射精, 接吻喪失, 処女喪失, 口内射精",　GET_ID(MASTER), @"%ANAME(MASTER)%的阴茎", ANAME(MASTER), "", "強姦")
                    CALL FUCK(MASTER, "欲望, 性技, 性交, 加虐, Ｃ, 射精, 露出, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(队伍:(对战队伍:目标):0)%的小穴", "強姦")
                    PRINTFORML 看来%ANAME(队伍:(对战队伍:目标):0)%在色气这方面赢了呢
                    PRINTFORMW 那赢家就是……%ANAME(队伍:(对战队伍:(目标)):0)%队！
                    DEBUGPRINTFORML 将{A队:(2 * 对战轮次 + (!目标))}队(编号：{(2 * 对战轮次 + (!目标))})转移至B队
                    B队:B队数 = A队:(2 * 对战轮次 + (!目标))
                    B队数 ++
                    A队:(2 * 对战轮次 + (!目标)) = __INT_MAX__
                    A队数 --
                CASEELSE
                    PRINTFORML 胜负已定！
                    PRINTFORMW 是\@RESULT == 1 ? %ANAME(队伍:(对战队伍:0):0)% # %ANAME(队伍:(对战队伍:1):0)%\@队的胜利！
                    IF RESULT == 1
                        DEBUGPRINTFORML 将{A队:(2 * 对战轮次 + 1)}队(编号：{(2 * 对战轮次 + 1)})转移至B队
                        B队:B队数 = A队:(2 * 对战轮次 + 1)
                        A队:(2 * 对战轮次 + 1) = __INT_MAX__
                    ELSE
                        DEBUGPRINTFORML 将{A队:(2 * 对战轮次)}队(编号：{(2 * 对战轮次)})转移至B队
                        B队:B队数 = A队:(2 * 对战轮次)
                        A队:(2 * 对战轮次) = __INT_MAX__
                    ENDIF
                    B队数 ++
                    A队数 --
            ENDSELECT
        NEXT
        ARRAYSORT A队
    ENDIF
    CALL SINGLE_DRAWLINE("-=")
    ;第二轮：B队淘汰赛，淘汰人员被淘汰，ID记录到排名数组便于排名
    IF B队数 >= 2
        IF B队数 > 2
            CALL FISHER_YATES_SHAFFLE(B队数)
            ARRAYMSORT SHAFFLE_ARRAY, B队
        ELSEIF A队数 == 0
            CALL COLOR_PRINTW("决赛！！！", 0xFF0000)
        ENDIF
        FOR 对战轮次, 0, B队数 / 2 
            对战队伍:0 = B队:(2 * 对战轮次)
            对战队伍:1 = B队:(2 * 对战轮次 + 1)
            感度:0 = 0
            感度:1 = 0
            [IF_DEBUG]
            FOR LOCAL:10, 0, B队数
                DEBUGPRINTFORM {B队:(LOCAL:10)}, 
            NEXT
            DEBUGPRINTL
            [ENDIF]
            DEBUGPRINTFORML 队伍信息：
            DEBUGPRINTFORML B队第{对战队伍:0}队成员：
            DEBUGPRINTFORML %ANAME(队伍:(对战队伍:0):0)%({队伍:(对战队伍:0):0})%ANAME(队伍:(对战队伍:0):1)%({队伍:(对战队伍:0):1})%ANAME(队伍:(对战队伍:0):2)%({队伍:(对战队伍:0):2})
            DEBUGPRINTFORML B队第{对战队伍:1}队成员：
            DEBUGPRINTFORML %ANAME(队伍:(对战队伍:1):0)%({队伍:(对战队伍:1):0})%ANAME(队伍:(对战队伍:1):1)%({队伍:(对战队伍:1):1})%ANAME(队伍:(对战队伍:1):2)%({队伍:(对战队伍:1):2})
            FOR LOCAL, 0, 3
                感度:0 += (ABL:(队伍:(对战队伍:0):LOCAL):Ｃ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ｖ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ａ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ｂ感 + ABL:(队伍:(对战队伍:0):LOCAL):Ｍ感) / 5
                感度:1 += (ABL:(队伍:(对战队伍:1):LOCAL):Ｃ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ｖ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ａ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ｂ感 + ABL:(队伍:(对战队伍:1):LOCAL):Ｍ感) / 5
            NEXT
            CALL EVENT_HUNTER_BATTLE(队伍:(对战队伍:0):0,队伍:(对战队伍:0):1,队伍:(对战队伍:0):2,队伍:(对战队伍:1):0,队伍:(对战队伍:1):1,队伍:(对战队伍:1):2)
            FORCEWAIT
            SELECTCASE RESULT
                CASE 0
                    PRINTFORMW 同归于尽……
                    PRINTFORMW 那么……只能用那招了……
                    PRINTFORMW ……
                    目标 = (RAND:(感度:0 + 感度:1) >= 感度:0)
                    PRINTFORMW %ANAME(队伍:(对战队伍:目标):0)%被突然出现的%ANAME(MASTER)%强奸了！
                    CALL FUCK(队伍:(对战队伍:目标):0, "Ｃ, Ｖ, Ｂ, 欲望, 精愛, 奉仕, 奉仕, 性交, 接吻, 口淫, Ｖ性交", "膣内射精, 接吻喪失, 処女喪失, 口内射精",　GET_ID(MASTER), @"%ANAME(MASTER)%的阴茎", ANAME(MASTER), "", "強姦")
                    CALL FUCK(MASTER, "欲望, 性技, 性交, 加虐, Ｃ, 射精, 露出, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(队伍:(对战队伍:目标):0)%的小穴", "強姦")
                    PRINTFORML 看来%ANAME(队伍:(对战队伍:目标):0)%在色气这方面赢了呢
                    PRINTFORMW 那赢家就是……%ANAME(队伍:(对战队伍:(目标)):0)%队！
                    DEBUGPRINTFORML 将{B队:(2 * 对战轮次 + (!目标))}队(编号：{(2 * 对战轮次 + (!目标))})淘汰
                    排名:排名次序 = B队:(2 * 对战轮次 + (!目标))
                    B队:(2 * 对战轮次 + (!目标)) = __INT_MAX__
                    B队数 --
                CASEELSE
                    PRINTFORML 胜负已定！
                    PRINTFORMW 是\@RESULT == 1 ? %ANAME(队伍:(对战队伍:0):0)% # %ANAME(队伍:(对战队伍:1):0)%\@队的胜利！
                    IF RESULT == 1
                        DEBUGPRINTFORML 将{B队:(2 * 对战轮次 + 1)}队(编号：{(2 * 对战轮次 + 1)})淘汰
                        排名:排名次序 = B队:(2 * 对战轮次 + 1)
                        B队:(2 * 对战轮次 + 1) = __INT_MAX__
                    ELSE
                        DEBUGPRINTFORML 将{B队:(2 * 对战轮次)}队(编号：{(2 * 对战轮次)})淘汰
                        排名:排名次序 = B队:(2 * 对战轮次)
                        B队:(2 * 对战轮次) = __INT_MAX__
                    ENDIF
                    B队数 --
            ENDSELECT
            IF DVAR:自动战败惩罚 >= 0
                IF DVAR:自动战败惩罚 == 1
                    PRINTFORMW 当然，作为败犬的%ANAME(队伍:(排名:排名次序):0)%队肯定是要遭受惩罚的～
                    CALL EVENT_HUNTER_DEFEATRAPE(队伍:(排名:排名次序):0, 队伍:(排名:排名次序):1, 队伍:(排名:排名次序):2)
                ELSE
                    PRINTFORML 那么，该如何处置败犬呢～
                    CALL ASK_YN("当然要用肉棒给她们开个反省会啦","就放过她们吧")
                    IF !RESULT
                        PRINTFORMW 这样啊～
                        PRINTFORMW 那就让主人好好教训她们吧♥
                        CALL EVENT_HUNTER_DEFEATRAPE(队伍:(排名:排名次序):0, 队伍:(排名:排名次序):1, 队伍:(排名:排名次序):2)
                    ELSE
                        PRINTFORMW 好的～
                        PRINTFORML 听到赦免的%ANAME(队伍:(排名:排名次序):0)%们连忙感谢
                        PRINTFORMW 但是她们脸上的表情似乎……有点失落……？
                    ENDIF
                ENDIF
            ENDIF
            排名次序 ++
        NEXT
        ARRAYSORT B队
    ENDIF
    IF B队数 == 1 && A队数 == 0
        排名:排名次序 = B队:0
        B队:0 = __INT_MAX__
        B队数 --
        排名次序 ++
        BREAK
    ENDIF
    轮数 ++
WEND

PRINTFORMW 最终，%ANAME(队伍:(排名:(排名次序 - 1)):0)%队站到了最后！
FORCEWAIT

PRINTFORML 要显示排名吗？
CALL ASK_YN
IF !RESULT
    FOR LOCAL, 排名次序 - 1, -1, -1
        {
            CALL COLOR_PRINTW(@"<第{排名次序 - LOCAL}名> ",当前文字颜色,
                @"%ANAME(队伍:(排名:LOCAL):0)%",TALENT:(队伍:(排名:LOCAL):0):髪色,
                @"%ANAME(队伍:(排名:LOCAL):1)%",TALENT:(队伍:(排名:LOCAL):1):髪色,
                @"%ANAME(队伍:(排名:LOCAL):2)%",TALENT:(队伍:(排名:LOCAL):2):髪色,
                " - ",当前文字颜色,
                @"%ANAME(队伍:(排名:LOCAL):0)%队",TALENT:(队伍:(排名:LOCAL):0):髪色)
        }
    NEXT
ENDIF

IF 轮空少女:0
    PRINTFORMW 随着比赛结束，%ANAME(轮空少女:0)%\@轮空少女:1 ? 和%ANAME(轮空少女:1)% # \@挺着充满精液的肚子回到了队伍……
    REPEAT 轮数
        IF 轮空少女:1
            CALL FUCK(轮空少女:1, "Ｃ, Ｖ, Ｂ, 欲望, 精愛, 奉仕, 奉仕, 性交, 接吻, 口淫, Ｖ性交", "膣内射精, 接吻喪失, 処女喪失, 口内射精",　GET_ID(MASTER), @"%ANAME(MASTER)%的阴茎", ANAME(MASTER), "", "調教")
            CALL FUCK(MASTER, "欲望, 性技, 性交, 加虐, Ｃ, 射精, 露出, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(轮空少女:1)%的小穴", "調教")
        ENDIF
        CALL FUCK(轮空少女:0, "Ｃ, Ｖ, Ｂ, 欲望, 精愛, 奉仕, 奉仕, 性交, 接吻, 口淫, Ｖ性交", "膣内射精, 接吻喪失, 処女喪失, 口内射精",　GET_ID(MASTER), @"%ANAME(MASTER)%的阴茎", ANAME(MASTER), "", "調教")
        CALL FUCK(MASTER, "欲望, 性技, 性交, 加虐, Ｃ, 射精, 露出, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(轮空少女:0)%的小穴", "調教")
    REND
ENDIF
PRINTFORMW 作为赢家，今夜就让%ANAME(队伍:(排名:(排名次序 - 1)):0)%、%ANAME(队伍:(排名:(排名次序 - 1)):1)%和%ANAME(队伍:(排名:(排名次序 - 1)):2)%负责侍寝♥
FOR LOCAL, 0, 3
    CALL FUCK_MAKELOVE(队伍:(排名:(排名次序 - 1)):LOCAL, GET_ID(MASTER), @"%ANAME(MASTER)%的肉棒", @"%ANAME(MASTER)%")
    CALL FUCK(MASTER, "欲望, 性技, 性交, Ｃ, 射精, 露出, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(队伍:(排名:(排名次序 - 1)):LOCAL)%的小穴", "和姦")
    経験値総量 = MIN(ARENA_GET_POWER(队伍:(排名:(排名次序 - 1)):LOCAL) / 3, ARENA_GET_POWER(MASTER)) * MAX(MIN(ABL:(队伍:(排名:(排名次序 - 1)):LOCAL):性技, ABL:MASTER:性技), 1) * 轮数
    FOR LOCAL:1, 0, 経験値総量
        獲得経験値:(IFRAND("0TO2", 1, "3TO5", RAND:100 < 50)) ++
    NEXT

    SIF 獲得経験値:0 > 0
        CALL PRINT_ADD_EXP(队伍:(排名:(排名次序 - 1)):LOCAL, "武闘経験値", 獲得経験値:0, 1)
    SIF 獲得経験値:1 > 0
        CALL PRINT_ADD_EXP(队伍:(排名:(排名次序 - 1)):LOCAL, "防衛経験値", 獲得経験値:1, 1)
    SIF 獲得経験値:2 > 0
        CALL PRINT_ADD_EXP(队伍:(排名:(排名次序 - 1)):LOCAL, "知略経験値", 獲得経験値:2, 1)
    SIF 獲得経験値:3 > 0
        CALL PRINT_ADD_EXP(队伍:(排名:(排名次序 - 1)):LOCAL, "政治経験値", 獲得経験値:3, 1)
    SIF 獲得経験値:4 > 0
        CALL PRINT_ADD_EXP(队伍:(排名:(排名次序 - 1)):LOCAL, "歌唱経験値", 獲得経験値:4, 1)
    SIF 獲得経験値:5 > 0
        CALL PRINT_ADD_EXP(队伍:(排名:(排名次序 - 1)):LOCAL, "料理経験値", 獲得経験値:5, 1)
    CALL TRAIN_AUTO_ABLUP(队伍:(排名:(排名次序 - 1)):LOCAL)
NEXT
PRINTFORMW 那么，第{DVAR:演习历时 + 1}届模拟对抗演习正式结束～
DVAR:演习历时 ++
DVAR:演习flag = 0
DVAR:后宫事件冷却 = 10
RETURN 1

@EVENT_HUNTER_BATTLE(左队:0,左队:1,左队:2,右队:0,右队:1,右队:2)
#DIM FIRSTLINE
#DIM 左队, 3
#DIM 右队, 3
#DIM 生命值, 2
#DIM 最大生命值, 2
#DIM 攻击力, 2
#DIM 总知略, 2
#DIM 感度因子, 2
#DIM 暴击率
#DIM 攻方 = 0
#DIM 守方
#DIM 攻方敏捷
#DIM 守方敏捷
#DIM 敏捷判定
#DIM 连击次数 = 0
#DIM 伤害
#DIM 压制flag = 0
FIRSTLINE = LINECOUNT
VARSET 生命值
VARSET 攻击力
VARSET 总知略
VARSET 感度因子
连击次数 = 0
压制flag = 0
攻方 = 0
FOR LOCAL, 0, 3
    生命值:0 += ABL_POWER(ABL:(左队:LOCAL):防衛, 左队:LOCAL) * 100
    攻击力:0 += ABL_POWER(ABL:(左队:LOCAL):武闘, 左队:LOCAL)
    总知略:0 += ABL_POWER(ABL:(左队:LOCAL):知略, 左队:LOCAL) / 100
    感度因子:0 += (ABL:(左队:LOCAL):Ｃ感 + ABL:(左队:LOCAL):Ｖ感 + ABL:(左队:LOCAL):Ａ感 + ABL:(左队:LOCAL):Ｂ感 + ABL:(左队:LOCAL):Ｍ感) / 5
    生命值:1 += ABL_POWER(ABL:(右队:LOCAL):防衛, 右队:LOCAL) * 100
    攻击力:1 += ABL_POWER(ABL:(右队:LOCAL):武闘, 右队:LOCAL)
    总知略:1 += ABL_POWER(ABL:(右队:LOCAL):知略, 右队:LOCAL) / 100
    感度因子:1 += (ABL:(右队:LOCAL):Ｃ感 + ABL:(右队:LOCAL):Ｖ感 + ABL:(右队:LOCAL):Ａ感 + ABL:(右队:LOCAL):Ｂ感 + ABL:(右队:LOCAL):Ｍ感) / 5
NEXT
DEBUGPRINTFORML {生命值:0}/{攻击力:0}/{总知略:0}/{感度因子:0}
DEBUGPRINTFORML {生命值:1}/{攻击力:1}/{总知略:1}/{感度因子:1}
最大生命值:0 = 生命值:0
最大生命值:1 = 生命值:1
WHILE 1
    CLEARLINE LINECOUNT - FIRSTLINE

    ;显示Trimp™同款UI
    PRINTFORML %@"%ANAME(左队:0)%队 ({生命值:0}\/{最大生命值:0})", 50, LEFT%  |  %@"%ANAME(右队:0)%队 ({生命值:1}\/{最大生命值:1})", 50, LEFT%
    PRINTFORML %@"{攻击力:0 * 8 / 10}-{攻击力:0 * 12 / 10}攻击力", 50, LEFT%  |  %@"{攻击力:1 * 8 / 10}-{攻击力:1 * 12 / 10}攻击力", 50, LEFT%
    CALL EVENT_TRIMPBAR(生命值:0, 最大生命值:0, 50)
    PRINTFORM   |  
    CALL EVENT_TRIMPBAR(生命值:1, 最大生命值:1, 50)
    PRINTL
    CALL SINGLE_DRAWLINE("=") 

    ;有一方生命值归0则结束对战
    SIF 生命值:0 == 0 || 生命值:1 == 0
        BREAK
    ;决定攻击顺序
    攻方敏捷 = 500 * 感度因子:攻方
    守方敏捷 = 500 * 感度因子:(!攻方)
    ;有些技能能令攻击方固定
    IF 压制flag
        压制flag = 0
    ;攻击方有((攻击方敏捷 / (连击次数 + 1)) /敏捷和)的几率继续攻击，否则攻守易位
    ELSEIF RAND(攻方敏捷 + 守方敏捷) >= 攻方敏捷 / (连击次数 + 1)
        连击次数 = 0
        攻方 = !攻方
    ENDIF
    守方 = !攻方
    连击次数 ++

    PRINTFORML \@攻方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队发动了攻击！
    敏捷判定 = RAND(攻方敏捷 + 守方敏捷) - 守方敏捷
    ; DEBUGPRINTFORML 敏捷判定：{敏捷判定}
    伤害 = 攻击力:攻方 * RAND(8, 13) / 10
    ;技能判定
    IF RAND(总知略:攻方 + 总知略:守方) < 总知略:攻方 - 总知略:守方
        CALL COLOR_PRINTL("压制! ",0xFF0000)
        PRINTFORML \@攻方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队的攻击压制了\@守方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@的行动！
        压制flag = 1
    ELSEIF RAND(总知略:攻方 + 总知略:守方) < 总知略:攻方 - 总知略:守方 / 2
        CALL COLOR_PRINTL("暴击!!",0xFFA824)
        伤害 *= 4
    ELSEIF RAND(总知略:攻方 + 总知略:守方) < 总知略:攻方 - 总知略:守方 / 2
        CALL COLOR_PRINTL("暴击! ",0xFFFF00)
        伤害 *= 2
    ENDIF
    IF !压制flag
        IF RAND(总知略:攻方 + 总知略:守方) < 总知略:守方 - 总知略:攻方
            CALL COLOR_PRINTL("反制! ",0xFF0000)
            PRINTFORMW 但是\@守方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@化解了\@攻方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队的攻击并夺得了进攻权！
            连击次数 = 0
            攻方 = !攻方
            压制flag = 1
            CONTINUE
        ELSEIF RAND(总知略:攻方 + 总知略:守方) < 总知略:守方 - 总知略:攻方 / 2
            CALL COLOR_PRINTL("反击! ",0xFFA824)
            PRINTFORML \@守方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队受到了{伤害}点伤害！
            PRINTFORMW 但是\@守方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队也将同等伤害反馈到了\@攻方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队！
            生命值:守方 = MAX(生命值:守方 - 伤害, 0)
            生命值:攻方 = MAX(生命值:攻方 - 伤害, 0)
            CONTINUE
        ELSEIF RAND(总知略:攻方 + 总知略:守方) < 总知略:守方 - 敏捷判定 / 20
            CALL COLOR_PRINTL("闪避! ",0xFFFF00)
            PRINTFORMW 但是\@守方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@队在千钧一发之际躲开了攻击！
            CONTINUE
        ENDIF
    ENDIF
    ;伤害结算
    PRINTFORMW \@守方 == 0 ? %ANAME(左队:0)% # %ANAME(右队:0)%\@受到了{伤害}点伤害！
    生命值:守方 = MAX(生命值:守方 - 伤害, 0)
WEND
IF 生命值:0 == 0 && 生命值:1 == 0
    RETURN 0
ELSE
    IF 生命值:1 == 0
        RETURN 1
    ELSEIF 生命值:0 == 0
        RETURN -1
    ELSE
        THROW 闹哪样？？？
    ENDIF
ENDIF

@EVENT_TRIMPBAR(ARG, ARG:1, ARG:2, ARGS = UNICODE(0x2585), ARGS:1 = UNICODE(0x2585))
#DIM COLOR
SELECTCASE ARG
    CASE IS >= ARG:1 / 2
        COLOR = 0x00B2EE
    CASE IS >= ARG:1 / 4
        COLOR = 0xFFFF00
    CASE IS >= ARG:1 / 10
        COLOR = 0xFFA824
    CASEELSE
        COLOR = 0xFF0000
ENDSELECT
CALL PRINT_COLORBAR(ARG, ARG:1, ARG:2, ARGS, ARGS:1, COLOR, 0xFFFFFF)
;写个注释
;ARG = 变量 ARG:1 = 变量最大值 ARG:2 = 条长度
;ARGS + ARG:3 = 内填色/图案 ARGS:1 + ARG:4 = 外填色/图案

@EVENT_HUNTER_DEFEATRAPE(ARG:0, ARG:1, ARG:2)
#DIM 経験値総量
#DIM 獲得経験値, 6
VARSET 獲得経験値
SELECTCASE RAND:5
    CASE 0
        PRINTFORML %ANAME(MASTER)%让她们和自己全裸对战
        PRINTFORML 但是羞耻心和主人的威压使得她们根本无法发挥全力
        PRINTFORML 结果每个少女一碰上%ANAME(MASTER)%，还没做出动作就被抱起来侵犯
        PRINTFORMW 这场小比赛，便以%ANAME(ARG:0)%队全队被充分中出后瘫倒在地结束……
    CASE 1
        PRINTFORML %ANAME(MASTER)%给%ANAME(ARG:0)%队全队人的身上加上各种各样的振动道具
        PRINTFORML 然后命令她们轮流跨上来，一边以骑乘位的姿势扭腰一边告诉%ANAME(MASTER)%自己的不足
        PRINTFORML 可是为了忍耐快感，骑上来的少女一个个紧闭嘴唇做着无谓的忍耐
        PRINTFORMW 结果就是直到高潮为止，她们也没说出一句完整的话……
    CASE 2
        PRINTFORML 身为败犬的队长，%ANAME(ARG:0)%被牢牢固定在%ANAME(MASTER)%的身上，以后背立位的姿势被当众侵犯着
        PRINTFORML 而%ANAME(ARG:1)%和%ANAME(ARG:2)%则插着狗尾形状的肛门振动棒，像狗一样趴在一旁等待着被侵犯
        PRINTFORML 每当%ANAME(ARG:0)%被内射之后，%ANAME(MASTER)%便拔出肉棒，用还沾有%ANAME(ARG:0)%的爱液的肉棒侵犯%ANAME(ARG:1)%和%ANAME(ARG:2)%，周而复始
        PRINTFORMW 直到%ANAME(ARG:0)%被释放为止，%ANAME(ARG:0)%队三人全都瘫倒在%ANAME(MASTER)%胯下，两腿间不断涌出精液……
    CASE 3
        PRINTFORML %ANAME(ARG:0)%全队身上除了小穴以外，其余敏感带上装上了跳蛋、夹子等各种各样的道具，在场地中间一字排开地站着
        PRINTFORML 只有小穴空着的三人最初还能乖乖罚站，但是不久便欲求不满似的摩擦着两腿
        PRINTFORML 又过了一段时间，其中忍不住的便第一个跪倒在地，祈求着让%ANAME(MASTER)%侵犯她那洪水泛滥的小穴
        PRINTFORMW 直到三人都齐刷刷跪下请求，%ANAME(MASTER)%才不紧不慢的当众侵犯起她们……
    CASE 4
        PRINTFORML %ANAME(ARG:0)%插上了狗尾形状的肛塞，被%ANAME(MASTER)%骑着屁股侵犯
        PRINTFORML %ANAME(MASTER)%一边抽插，一边用力拍着她的屁股让她沿着场地爬行
        PRINTFORML 就这样，%ANAME(ARG:0)%一路上高潮迭起，留下了一条精液和爱液的轨迹
        PRINTFORMW 待到%ANAME(ARG:0)%爬完一圈后，%ANAME(MASTER)%便拉上%ANAME(ARG:1)%和%ANAME(ARG:2)%，如法炮制地让她们边被侵犯边爬了一圈……
ENDSELECT
FOR LOCAL, 0, 3
    CALL FUCK(ARG:LOCAL, "Ｃ, Ｖ, Ｂ, 欲望, 精愛, 奉仕, 奉仕, 性交, 接吻, 口淫, Ｖ性交", "膣内射精, 接吻喪失, 処女喪失, 口内射精",　GET_ID(MASTER), @"%ANAME(MASTER)%的阴茎", ANAME(MASTER), "", "強姦")
    CALL FUCK(MASTER, "欲望, 性技, 性交, 加虐, Ｃ, 射精, 露出, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(ARG:LOCAL)%的小穴", "強姦")
    経験値総量 = MIN(ARENA_GET_POWER(ARG:LOCAL) / 3, ARENA_GET_POWER(MASTER)) * MAX(MIN(ABL:(ARG:LOCAL):性技, ABL:MASTER:性技), 1)
    FOR LOCAL:1, 0, 経験値総量
        獲得経験値:(IFRAND("0TO2", 1, "3TO5", RAND:100 < 50)) ++
    NEXT

    SIF 獲得経験値:0 > 0
        CALL PRINT_ADD_EXP(ARG:LOCAL, "武闘経験値", 獲得経験値:0, 1)
    SIF 獲得経験値:1 > 0
        CALL PRINT_ADD_EXP(ARG:LOCAL, "防衛経験値", 獲得経験値:1, 1)
    SIF 獲得経験値:2 > 0
        CALL PRINT_ADD_EXP(ARG:LOCAL, "知略経験値", 獲得経験値:2, 1)
    SIF 獲得経験値:3 > 0
        CALL PRINT_ADD_EXP(ARG:LOCAL, "政治経験値", 獲得経験値:3, 1)
    SIF 獲得経験値:4 > 0
        CALL PRINT_ADD_EXP(ARG:LOCAL, "歌唱経験値", 獲得経験値:4, 1)
    SIF 獲得経験値:5 > 0
        CALL PRINT_ADD_EXP(ARG:LOCAL, "料理経験値", 獲得経験値:5, 1)
    CALL TRAIN_AUTO_ABLUP(ARG:LOCAL)
NEXT
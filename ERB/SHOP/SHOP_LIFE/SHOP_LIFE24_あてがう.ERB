;-------------------------------------------------
;「あてがう」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME24
RESULTS:0 '= "性賄賂"

;-------------------------------------------------
;「あてがう」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK24
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
RETURN 1

;-------------------------------------------------
;「あてがう」でヤられる側の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA24(ARG:0)
;主人公はダメ
SIF ARG:0 == MASTER
	RETURN 0
;閨に呼ぶの選択可能キャラ存在判定
CALL SHOP_LIFE_CHECKCHARA_NEYA(ARG:0)
SIF RESULT && CFLAG:(ARG:0):慰安参加者
	RETURN 2
RETURN CHECK91(ARG:0, 2, RESULT)

;-------------------------------------------------
;「あてがう」であてがう先になる可否判定
;　無限ループ回避のため性知識が必要
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_SUB24(ARG:0)
SIF CFLAG:(ARG:0):閨に呼ぶで選択中 && !CFLAG:(ARG:0):慰安参加者
	RETURN 2

LOCAL:0 = TMP_COUNTRY_RELATION:(CFLAG:MASTER:所属):(CFLAG:(ARG:0):所属)
;捕虜でない、非主人公、同一勢力or共闘勢力、性知識1以上
RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && !CFLAG:(ARG:0):捕虜先 && LOCAL:0 >= 2 && ABL:(ARG:0):性知識 > 0 && CFLAG:(ARG:0):行動不能状態 != 行動不能_臨月)
RETURN 0

;-------------------------------------------------
;「あてがう」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY24
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「あてがう」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON24(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):閨に呼ぶで選択中, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
RETURN 0

;-------------------------------------------------
;「あてがう」の右カラムキャラリスト２のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST2_BUTTON24(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):慰安参加者, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, 0, "BUTTOM")
RETURN 0

;-------------------------------------------------
;「あてがう」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP24(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「あてがう」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM24(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
RETURN RESULT


;-------------------------------------------------
;「あてがう」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW24
#DIM 選択人数
#DIM あてがう先
#DIM あてがわれる側
#DIM 最大人数

最大人数 = MIN(CALC_SHOP_TIME() - SHOP_TIME, 8)
選択人数 = 0
あてがう先 = 0
あてがわれる側 = 0

FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうか判定
	RESULT = 0
	IF CFLAG:(LOCAL:0):慰安参加者
		選択人数 ++
		あてがう先 ++
	ENDIF
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT == 1
		;選択中の人数を数える
		IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
			選択人数 ++
			あてがわれる側 ++
			IF 選択人数 > 最大人数
				CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
			ENDIF
		ENDIF
	ELSE
		;条件を満たしていなければ強制的にＯＦＦ
		CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
	ENDIF
NEXT

CALL COLUMN_RIGHT_TITLE("対象者選択", TOSTR(選択人数), TOSTR(最大人数), "人数", "避妊時需3,000")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

IF あてがう先 > 0 && あてがわれる側 > 0
	PRINTBUTTON "[就這樣決定]", 1
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [就這樣決定]
	RESETCOLOR
ENDIF

PRINTPLAIN   
IF FLAG:慰安避妊薬
	PRINTBUTTON "[有避孕]", 2
ELSE
	IF MONEY > 選択人数 * 3000
		PRINTBUTTON "[無避孕]", 2
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [無避孕]
		RESETCOLOR
	ENDIF
ENDIF

PRINTPLAIN   
CALL COLOR_PRINT("禮物↑和贈送対象↓合計必須選択2名", カラー_注釈)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト上とページ移動
CALL COLUMN_RIGHT_CHARALIST_TOP(1)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

PRINTFORM 請選択要奉侍的角色（攻方）
PRINTPLAIN   
SIF 選択人数 >= 最大人数
	SETCOLOR カラー_オレンジ
PRINTFORM (現在{選択人数}/{最大人数}人)
RESETCOLOR
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト下とページ移動
CALL COLUMN_RIGHT_CHARALIST_BOTTOM(1)

RETURN 0

;-------------------------------------------------
;「あてがう」の右カラムボタン入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB24(ARG:0)
;このメンバーで決定
IF ARG:0 == 1
	CALL DECIDE_ATEGAU_MEMBER()
	SIF !RESULT
		RETURN 0
;避妊ありなし
ELSEIF ARG:0 == 2
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:閨に呼ぶで選択中
			LOCAL:1 ++
	NEXT
	SIF FLAG:慰安避妊薬 || MONEY >= LOCAL:1 * 3000
		FLAG:慰安避妊薬 = !FLAG:慰安避妊薬
	RETURN 0
ENDIF
RETURN 0

;-------------------------------------------------
;「あてがう」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP24(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中 || CFLAG:(LOCAL:0):慰安参加者
		LOCAL:5 ++
	ENDIF
NEXT
IF !CFLAG:(ARG:0):慰安参加者 && (CFLAG:(ARG:0):閨に呼ぶで選択中 || LOCAL:5 < MIN(CALC_SHOP_TIME() - SHOP_TIME, 8))
	;調教参加フラグを反転
	CFLAG:(ARG:0):閨に呼ぶで選択中 = !CFLAG:(ARG:0):閨に呼ぶで選択中
	LOCAL:5 += CFLAG:(ARG:0):閨に呼ぶで選択中 == 0 ? -1 # 1
	SIF FLAG:慰安避妊薬 && LOCAL:5 * 3000 > MONEY
		FLAG:慰安避妊薬 = 0
ENDIF
RETURN 0

;-------------------------------------------------
;「あてがう」のリスト２入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_SUB24(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中 || CFLAG:(LOCAL:0):慰安参加者
		LOCAL:5 ++
	ENDIF
NEXT
IF !CFLAG:(ARG:0):閨に呼ぶで選択中 && (CFLAG:(ARG:0):慰安参加者 || LOCAL:5 < MIN(CALC_SHOP_TIME() - SHOP_TIME, 8))
	;調教参加フラグを反転
	CFLAG:(ARG:0):慰安参加者 = !CFLAG:(ARG:0):慰安参加者
	LOCAL:5 += CFLAG:(ARG:0):慰安参加者 == 0 ? -1 # 1
	SIF FLAG:慰安避妊薬 && LOCAL:5 * 3000 > MONEY
		FLAG:慰安避妊薬 = 0
ENDIF
RETURN 0

;-------------------------------------------------
;「あてがう」の右カラムボタン入力処理本体
;-------------------------------------------------
@DECIDE_ATEGAU_MEMBER()
#DIM あてがう先
#DIM あてがわれる側
#DIM 参加人数

あてがう先 = 0
あてがわれる側 = 0
参加人数 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中 || CFLAG:(LOCAL:0):慰安参加者
		CFLAG:(LOCAL:0):調教参加フラグ  = 1
		CFLAG:(LOCAL:0):強制友好化 = 1
		IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
			あてがわれる側 ++
			CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
		ELSE
			あてがう先 ++
		ENDIF
		IF FLAG:慰安避妊薬
			PRINTFORML %ANAME(LOCAL:0)%服用了避孕薬
			CFLAG:(LOCAL:0):避妊薬残ターン += 1
			MONEY -= 3000
		ENDIF
		参加人数 ++
	ENDIF
NEXT
IF あてがわれる側 == 0 || あてがう先 == 0
	;参加キャラがいないなら処理せず0を返す
	RETURN 0
ENDIF

;START_TRAIN_COMMONでSHOP_TIMEが+1されるので、それをひいておく
SHOP_TIME --

SHOP_TIME += 参加人数

FLAG:慰安モード = 2
ITEM:A_跳蛋 = 1
ITEM:A_振動棒 = 1
ITEM:A_肛用振動棒 = 1
ITEM:A_穿戴式假陰茎 = 1
ITEM:A_飛機杯 = 1
ITEM:A_陰蒂夾 = 1
ITEM:A_乳頭夾 = 1
ITEM:A_麻薬 = 99
ITEM:A_縄 = 1
ITEM:A_鞭 = 1
ITEM:A_眼罩 = 1
ITEM:A_口枷 = 1
ITEM:A_鼻勾 = 1
ITEM:A_馬克筆 = 1
ITEM:A_潤滑液 = 99
ITEM:A_避孕套 = 99
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
ITEM:A_桃源香 = 99
ITEM:A_精力超群丸 = 99

FLAG:調教モード = 7

;ウフフフラグをONに
FLAG:ウフフフラグ = 1

;--信仰--触发调教的类型 0=会见 1=调教俘虏 2=侍寝 3=夜袭  4=慰安民众 5=留宿 6=性贿赂
MIRACLE_LUST_TRAIN_FORM = 6
;--信仰--玩家制造色欲调教事件变量赋值--
MIRACLE_LUST_TRAIN_TARGET:0 = 0 ;人数初始化
MIRACLE_LUST_TRAIN_MAIN = MASTER
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):調教参加フラグ
		MIRACLE_LUST_TRAIN_TARGET:0 ++ ;调教人数增加
		MIRACLE_LUST_TRAIN_TARGET:(MIRACLE_LUST_TRAIN_TARGET:0) = LOCAL:0 ;调教参与者记录
		MIRACLE_LUST_PRE_FALL:(MIRACLE_LUST_TRAIN_TARGET:0) = MIRACLE_FALLCOUNT(LOCAL:0) ;调教前陷落代码,函数在 祷告_践行教义事件.ERB
		MIRACLE_LUST_PRE_MAKELOVE:(MIRACLE_LUST_TRAIN_TARGET:0) = TALENT:(LOCAL:0):合意 ;调教前是否取得对象合意
		MIRACLE_LUST_PRE_LIKE:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):好感度 ;调教前对象好感度
		MIRACLE_LUST_PRE_TRUST:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):依存度 ;调教前对象依存度
		MIRACLE_LUST_PRE_BELONG:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):従属度 ;调教前对象従属度
		MIRACLE_LUST_PRE_CONTROL:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):支配度 ;调教前对象支配度
	ENDIF
NEXT
;--信仰--结束--

;行動開始時の共通処理
CALL START_TRAIN_COMMON
RETURN 1


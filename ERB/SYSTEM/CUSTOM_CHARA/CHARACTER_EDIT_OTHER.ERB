@CHARACTER_EDIT_UI_OTHER(対象)
#DIM 対象

IF FLAG:周回数 > 0
	PRINTFORM 所属:
	IF CFLAG:対象:所属 == 0
		IF CFLAG:対象:特殊状態 == 特殊状態_死亡
			PRINTFORM 無所属(死亡)          
		ELSEIF CFLAG:対象:特殊状態 == 特殊状態_放浪
			PRINTFORM 無所属(放浪)          
		ELSE
			PRINTFORM 無所属(未登場)        
		ENDIF
	ELSE
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属)), MAX_CHARANAME_LENGTH + 4, LEFT%
	ENDIF
	IF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
		SETCOLOR カラー_選択不可
		PRINTPLAIN ※君主無法変更※
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[変更] ", 0)
		CALL PRINTBUTTON_EX("[一覧] ", 1)
	ENDIF
	PRINTL

	PRINTFORM 俘虜:
	IF CFLAG:対象:捕虜先 == 0
		PRINTFORM %"無", MAX_CHARANAME_LENGTH + 4, LEFT%
	ELSE
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:捕虜先)), MAX_CHARANAME_LENGTH + 4, LEFT%
	ENDIF
	IF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
		SETCOLOR カラー_選択不可
		PRINTPLAIN ※君主無法変更※
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[変更] ", 10)
		CALL PRINTBUTTON_EX("[一覧] ", 11)
	ENDIF
	PRINTL
	CALL COLOR_LINE
	PRINTBUTTON "[冷却期]:", 109
	IF COOLTIME:対象:0 > 0
		PRINTFORM 剩余{COOLTIME:対象:0}期
	ELSE
		PRINTFORM 无
	ENDIF
	PRINTL

	CALL COLOR_LINE
ENDIF
;特殊処理
PRINTBUTTON "[      性別]:", 100
PRINTFORML %TOSTR_SEX(TALENT:対象:GETNUM(TALENT, "性別"))%
PRINTBUTTON "[      陰毛]:", 105
PRINTFORML %GET_INMOU_NAME(TALENT:対象:陰毛現在値)%
PRINTBUTTON "[      腋毛]:", 106
PRINTFORML %GET_ARMPIT_NAME(TALENT:対象:腋毛現在値)%
PRINTBUTTON "[      文身]:", 107
CALL PRINT_TATTOO(対象)
PRINTL
PRINTBUTTON "[      穿孔]:", 108
CALL PRINT_PIERCE(対象)
PRINTL
CALL COLOR_LINE
PRINTFORM タグ:
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 1000 + LOCAL, HAS_TAG(対象, LOCAL))
NEXT
PRINTL
CALL COLOR_LINE

CALL SHOW_BODYSIZE(対象)
SIF HAS_VAGINA(対象)
	PRINTFORM 乳房:%TOSTR_BUSTSIZE(GET_BUSTSIZE(対象)), 10, LEFT% 
SIF HAS_PENIS(対象)
	PRINTFORM 陰茎:%TOSTR_PENISSIZE(GET_PENISSIZE(対象)), 10, LEFT%
PRINTFORML   尻:%TOSTR_HIPSIZE(GET_HIPSIZE(対象)), 10, LEFT%

IF HAS_VAGINA(対象)
	PRINTFORM 阴道紧致度:%TOSTR_TIGHTNESS(TALENT:(対象):Ｖ緊緻度), 10, LEFT% 
ENDIF
PRINTFORM 肛门紧致度:%TOSTR_TIGHTNESS(TALENT:(対象):Ａ緊緻度), 10, LEFT% 
PRINTL
PRINT 髪色:
CALL PRINT_HAIR_COLOR(TALENT:対象:髪色) 
PRINTFORML 髪長:%TOSTR_HAIR_LENGTH(TALENT:対象:頭髮長度)%
PRINTBUTTON "[身体設定]", 101
IF 対象 != MASTER
	PRINTL
	CALL COLOR_LINE
	PRINTBUTTON "[将所有指令的实施次数置为100]", 102
	PRINTL
	PRINTBUTTON "[将所有指令的实施次数置为0]", 103
	PRINTL
ENDIF
PRINTBUTTON "[清除胃・子宮・腸内精液]", 104


@CHARACTER_EDIT_INPUT_OTHER(対象, 入力値)
#DIM 対象
#DIM 入力値

SELECTCASE RESULT
	;性別
	CASE 100
		LOCAL:1 = HAS_PENIS(対象)
		LOCAL:2 = IS_MALE(対象)
		TALENT:対象:性別 = ROUND_INCREMENT(TALENT:対象:性別, 0, 5)
		SIF LOCAL:2 != IS_MALE(対象)
			CALL SET_BODYSIZE(対象)
		SIF LOCAL:1 == 0 && HAS_PENIS(対象)
			CALL SET_PENISSIZE(対象)
	;胸
	CASE 101
		CALL BEAUTIFY(対象)
	;性癖
	CASE 102
		PRINTL 所有指令的实施次数都是100
		PRINTL 对于已经超过100的，则保持现在的值
		PRINTL 主要用于在性癖设定中自由改变偏好Lv。
		CALL ASK_YN("实行", "取消")
		IF RESULT == 0
			FOR LOCAL:0, 0, 900
				COM_EXP:対象:(LOCAL:0) = MAX(COM_EXP:対象:(LOCAL:0), 1000)
				COM_EXP:対象:(LOCAL:0 + 1000) = MAX(COM_EXP:対象:(LOCAL:0), 1000)
				COM_EXP:対象:(LOCAL:0 + 2000) = MAX(COM_EXP:対象:(LOCAL:0), 1000)
			NEXT
		ENDIF
	CASE 103
		PRINTL 所有指令的实施次数都是0
		PRINTL 主要用于在性癖设定中培养偏好的Lv
		CALL ASK_YN("实行", "取消")
		IF RESULT == 0
			FOR LOCAL:0, 0, 900
				COM_EXP:対象:(LOCAL:0) = 0
				COM_EXP:対象:(LOCAL:0 + 1000) = 0
				COM_EXP:対象:(LOCAL:0 + 2000) = 0
			NEXT
		ENDIF
	CASE 104
		PRINTW 清除胃・子宮・腸内精液
		CALL CLEAR_CREAMPIE_HISTORY(対象, 1)
	;陰毛
	CASE 105
		CALL ASK_MULTI("白虎", "光滑", "稀薄", "柔毛", "蓬鬆", "標準", "略多", "茂盛", "濃密", "剛毛", "尻まで", "♥")
		TALENT:対象:陰毛現在値 = RESULT -1
		TALENT:対象:陰毛目標値 = RESULT - 1
	;腋毛
	CASE 106
		CALL ASK_MULTI("脱毛", "光滑", "修剪完成", "稀薄", "蓬鬆", "普通", "無精", "茂盛", "濃密", "剛毛", "密林")
		TALENT:対象:腋毛現在値 = RESULT -1
		TALENT:対象:腋毛目標値 = RESULT - 1
	CASE 107
		CALL TATTOO_MENU(対象, 1)
	CASE 108
		CALL PIERCE_MENU(対象, 1)
	CASE 109
		PRINTFORML 要将冷却时间设置为多少？(输入负数则维持现状)
		INPUT
		IF RESULT < 0
		ELSE
			COOLTIME:対象:0 = RESULT
		ENDIF
	CASE 1000 TO 1000 + VARSIZE("TAG_NAME")
		CALL INVERT_TAG(対象, 入力値 - 1000)
ENDSELECT

IF FLAG:周回数 > 0
	SELECTCASE 入力値
		CASE 0
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			IF CFLAG:対象:所属 == 0 && CFLAG:対象:特殊状態 == 0 && !CFLAG:対象:捕虜先
				CFLAG:対象:特殊状態 = 特殊状態_放浪
			ELSEIF CFLAG:対象:所属 == 0 && CFLAG:対象:特殊状態 == 特殊状態_放浪
				CFLAG:対象:特殊状態 = 特殊状態_死亡
			ELSE
				LOCAL:1 = 0
				FOR LOCAL, CFLAG:対象:所属 + 1, MAX_COUNTRY
					SIF !IS_COUNTRY(LOCAL)
						CONTINUE
					CALL CHANGE_COUNTRY(対象, LOCAL)
					LOCAL:1 = 1
					BREAK
				NEXT
				IF LOCAL:1 == 0
					CALL CHANGE_COUNTRY(対象, 0)
					CFLAG:対象:特殊状態 = 0
				ENDIF
			ENDIF
		CASE 1
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
			NEXT
			PRINTFORML [{MAX_COUNTRY}] 無所属(未登場)
			PRINTFORML [{MAX_COUNTRY + 1}] 無所属(放浪)
			PRINTFORML [{MAX_COUNTRY + 2}] 無所属(死亡)
			INPUT
			IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
				CALL CHANGE_COUNTRY(対象, RESULT, 0)
			ELSEIF RESULT == MAX_COUNTRY
				CALL CHANGE_COUNTRY(対象, 0, 1)
				CFLAG:対象:特殊状態 = 0
			ELSEIF RESULT == MAX_COUNTRY + 1
				CALL CHANGE_COUNTRY(対象, 0, 1)
			ELSEIF RESULT == MAX_COUNTRY + 2
				CALL CHANGE_COUNTRY(対象, 0, 1, 1)
			ENDIF
		CASE 10
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			LOCAL:1 = 0
			FOR LOCAL:0, CFLAG:対象:捕虜先 + 1, MAX_COUNTRY
				IF IS_COUNTRY(LOCAL)
					CALL CAPTURE(対象, LOCAL)
					LOCAL:1 = 1
					BREAK
				ENDIF
			NEXT
			SIF LOCAL:1 == 0
				CALL CAPTURE(対象, 0)
		CASE 11
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
			NEXT
			PRINTFORML [{MAX_COUNTRY}] 無
			INPUT
			IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
				CALL CAPTURE(対象, RESULT)
			ELSEIF RESULT == MAX_COUNTRY
				CALL CAPTURE(対象, 0)
			ENDIF      
	ENDSELECT
ENDIF

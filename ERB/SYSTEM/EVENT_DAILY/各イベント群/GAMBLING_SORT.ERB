;----------------------------
;仕分けのバイト
;スロットとビンゴと比較して実力でなんとかなるやつ
;----------------------------
@GAMBLING_SORT()
#DIMS CONST 柄 = "◆", "✕", "△", "★", "●", "■", "犯"
#DIMS フィールド, 7
#DIM START_LINE
#DIM PLAY_AREA_LINE
#DIMS 今回の柄
#DIM 残り時間
#DIM 前回時間
#DIM スコア
#DIM ゲーム中
#DIM 選択回数
VARSET 今回の柄
VARSET スコア
VARSET ゲーム中
VARSET 選択回数

CALL FISHER_YATES_SHAFFLE(VARSIZE("柄"))

FOR LOCAL, 0, VARSIZE("柄")
	フィールド:LOCAL = %柄:(SHAFFLE_ARRAY:LOCAL)%
NEXT

START_LINE = LINECOUNT

$MAIN

CALL PRINT_CASINO_SORTING_README
CALL PRINTBUTTON_EXL("[プレイ開始]", 999, !ゲーム中)
CALL SINGLE_DRAWLINE
PRINTL

PLAY_AREA_LINE = LINECOUNT

$SHOW_PLAY_AREA

PRINTFORM 分数:{スコア}
SIF ゲーム中
	PRINTFORM     NOW:请选择【%今回の柄%】！
PRINTL

FOR LOCAL, 0, VARSIZE("フィールド")
	PRINTBUTTON @"[%フィールド:LOCAL%]", LOCAL
	PRINT
NEXT

IF !ゲーム中
	INPUT
ELSE
	TINPUT 残り時間, -1
	IF RESULT == -1
		CALL COLOR_PRINTW("!!!!!TIME UP!!!!!", カラー_注意)
		PRINTFORMW 「现在的得分是……{スコア}么」
		RETURN スコア >= 30
	ENDIF
ENDIF

IF RESULT == 999 && !ゲーム中
	PRINTFORMW 「那么，游戏开始了……」
	PRINTFORMW 「3」
	PRINTFORMW 「2」
	PRINTFORMW 「1」
	ゲーム中 = 1
	今回の柄 = %柄:(RAND:(VARSIZE("柄")))%
	前回時間 = GETMILLISECOND()
	残り時間 = 30000
	CLEARLINE LINECOUNT - PLAY_AREA_LINE
	GOTO SHOW_PLAY_AREA
ELSEIF !ゲーム中
	CLEARLINE LINECOUNT - START_LINE
	GOTO MAIN
ENDIF

IF INRANGE(RESULT, 0, VARSIZE("フィールド"))
	残り時間 = 残り時間 - (GETMILLISECOND() - 前回時間)
	前回時間 = GETMILLISECOND()
	IF フィールド:RESULT == 今回の柄
		スコア ++
	ELSE
		IF 今回の柄 == "犯"
			PRINTFORMW 失误选中【犯】字了！
			RETURN 2
		ELSE
			スコア --
		ENDIF
	ENDIF
	選択回数 ++
	IF 選択回数 % 10 == 0
		CALL FISHER_YATES_SHAFFLE(VARSIZE("柄"))

		FOR LOCAL, 0, VARSIZE("柄")
			フィールド:LOCAL = %柄:(SHAFFLE_ARRAY:LOCAL)%
		NEXT
	ENDIF
	今回の柄 = %柄:(RAND:(VARSIZE("柄")))%
	CLEARLINE LINECOUNT - PLAY_AREA_LINE
	GOTO SHOW_PLAY_AREA
ENDIF


@PRINT_CASINO_SORTING_README()
CALL SINGLE_DRAWLINE
PRINTFORML 「请快速选择显示的图案」
PRINTFORML 「如果选择正确的图案+1分，如果弄错的话-1分」
PRINTFORML 「限制时间是30秒。结束的时候如果有30分的话就是%ANAME(MASTER)%的胜利」
PRINTFORML 「但是，请注意如果选择了【犯】图案的话，那么游戏结束且是我们取得勝利」
PRINTFORML 
CALL SINGLE_DRAWLINE


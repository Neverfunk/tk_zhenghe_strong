;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:藍_エクセレントさんすう教室
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("紫"), NAME_TO_CHARA("マミゾウ"), NAME_TO_CHARA("橙"), NAME_TO_CHARA("幽幽子"))
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)


;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K21_EXCELLENT_MATHMATIC_CLASS(対象)
#DIM 対象
#DIM かける数
#DIM かけられる数
#DIM 答え

SIF !CFLAG:対象:面識
	CFLAG:対象:面識 = 1




PRINTFORMW 「唔……有点空闲时间啊。」
PRINTFORMW 「咦？　这不是%ANAME(MASTER)%吗？怎么样，能陪我消磨一下时间吗？」
PRINTFORMW 巡视据点的时候，遇到了%ANAME(対象)%
PRINTFORML 那麼、要怎麼辦……
PRINTFORML 
CALL ASK_YN("好啊", "我还有事")

IF RESULT == 1
	PRINTFORMW 「嗯，是吗……」
	PRINTFORMW 遗憾的是我现在很忙
	PRINTFORMW 所以在拒绝后离开了……
	KDVAR:対象:藍_エクセレントさんすう教室 = 1
	RETURN 1
ENDIF

PRINTFORMW 「哦，是吗，能陪我一下吗」
PRINTFORMW 「什么嘛，只是简单的猜谜而已」
PRINTFORMW 「好了，请听题？」

かけられる数 = RAND(1, 100)
かける数 = RAND(1, 100)
答え = かけられる数 * かける数

CALL COLOR_PRINTL(@"「{かけられる数} ×　{かける数}的答案是！？」", カラー_注意)

TINPUT 10000, -1, 1, ""

IF RESULT != 答え
	PRINTFORMW 「很遗憾，没有对」
	PRINTFORMW 「嘛，再挑战一次吧？」
	PRINTFORMW 很遗憾，我好像弄错了……
	PRINTFORMW 但是，好像这成了%ANAME(対象)%的消遣
	RETURN 1
ENDIF

PRINTFORMW 「哦哦，没错，这正是正确答案。这不是很能干吗？」
PRINTFORMW 「嗯。很好地打发了时间哦。谢谢。」
PRINTFORMW 看来答对了……
CALL COLOR_PRINTW(@"%ANAME(MASTER)%的知略上升了3！", カラー_注意)
ABL:MASTER:知略 += 3
KDVAR:対象:藍_エクセレントさんすう教室 = 1

RETURN 1



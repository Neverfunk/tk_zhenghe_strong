;-------------------------------------------------
;従属国を検索・表示する
;-------------------------------------------------
@Colonial_Office_indicate_1
#DIM 表示勢力

CALL TMP_CREATE_COUNTRY_BOSS_MAP

PRINTL ◇辺境伯◇
FOR LOCAL:0, 0, MAX_TREATY_M
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):0
					IF IS_SP_COUNTRY(表示勢力)
						CONTINUE
					ENDIF
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

PRINTL ◇殖民地◇
FOR LOCAL:0, 0, MAX_TREATY_CO
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):0
					IF IS_SP_COUNTRY(表示勢力)
						CONTINUE
					ENDIF
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

PRINTL ◇傀儡国◇
FOR LOCAL:0, 0, MAX_TREATY_P
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):0
					IF IS_SP_COUNTRY(表示勢力)
						CONTINUE
					ENDIF
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

CALL SINGLE_DRAWLINE
PRINTBUTTON " 0[    取消]", 0
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF IS_COUNTRY(RESULT)
	REDRAW 1
	RETURN RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF


;-------------------------------------------------
;特殊勢力を含めた従属国を検索・表示する
;-------------------------------------------------
@Colonial_Office_indicate_2
#DIM 表示勢力

CALL TMP_CREATE_COUNTRY_BOSS_MAP

PRINTL ◇辺境伯◇
FOR LOCAL:0, 0, MAX_TREATY_M
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):0
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

PRINTL ◇殖民地◇
FOR LOCAL:0, 0, MAX_TREATY_CO
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):0
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

PRINTL ◇傀儡国◇
FOR LOCAL:0, 0, MAX_TREATY_P
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):0
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

CALL SINGLE_DRAWLINE
PRINTBUTTON " 0[    取消]", 0
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF IS_COUNTRY(RESULT)
	REDRAW 1
	RETURN RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF


;-------------------------------------------------
;自国・従属国・宗主国・永久同盟国以外の勢力を検索・表示する
;-------------------------------------------------
@Colonial_Office_indicate_3
#DIM 表示勢力
#DIM 連邦ナンバー
#DIM 連邦構成国, MAX_COUNTRY

CALL TMP_CREATE_COUNTRY_BOSS_MAP

;連邦ナンバー・連邦構成国を検索
FOR LOCAL:0, 0, MAX_COUNTRY
	FOR LOCAL:1, 0, MAX_COUNTRY
		IF TREATY_UNI_COUNTRY:(LOCAL:0):(LOCAL:1):0 == CFLAG:MASTER:所属 || TREATY_UNI_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			連邦ナンバー = LOCAL:0
			連邦構成国:(CFLAG:MASTER:所属) = 1
			BREAK
		ENDIF
	NEXT
NEXT

FOR LOCAL:0, 0, MAX_COUNTRY
	IF TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):0 > 0
		連邦構成国:(TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):0) = 1
	ELSEIF TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):1 > 0
		連邦構成国:(TREATY_UNI_COUNTRY:連邦ナンバー:(LOCAL:0):1) = 1
	ENDIF
NEXT

FOR LOCAL:0, 0, MAX_COUNTRY
	IF 連邦構成国:(LOCAL:0) != 1 && IS_COUNTRY(LOCAL:0)
		表示勢力 = LOCAL:0
		SETCOLOR COUNTRY_COLOR:(表示勢力)
		PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
		RESETCOLOR
		PRINTL
	ENDIF
NEXT
PRINTL

CALL SINGLE_DRAWLINE
PRINTBUTTON " 0[    取消]", 0
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF IS_COUNTRY(RESULT) && 連邦構成国:(RESULT) != 1
	REDRAW 1
	RETURN RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF


;-------------------------------------------------
;特殊勢力を含めた従属国及び宗主国を検索・表示する
;-------------------------------------------------
@Colonial_Office_indicate_4
#DIM 表示勢力

CALL TMP_CREATE_COUNTRY_BOSS_MAP

IF COUNTRY_Protectorate:(CFLAG:MASTER:所属) >= 2
	PRINTL ◇宗主国◇
	FOR LOCAL:0, 0, MAX_TREATY_M
		FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
			IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:1):0 == CFLAG:MASTER:所属 || TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:1):0 == CFLAG:MASTER:所属 || TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:1):0 == CFLAG:MASTER:所属
				FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
					IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):1 > 0 && TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):1 != CFLAG:MASTER:所属
						表示勢力 = TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):1
						SETCOLOR COUNTRY_COLOR:(表示勢力)
						PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
						RESETCOLOR
						PRINTL
					ENDIF
					IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):1 > 0 && TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):1 != CFLAG:MASTER:所属
						表示勢力 = TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):1
						SETCOLOR COUNTRY_COLOR:(表示勢力)
						PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
						RESETCOLOR
						PRINTL
					ENDIF
					IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):1 > 0 && TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):1 != CFLAG:MASTER:所属
						表示勢力 = TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):1
						SETCOLOR COUNTRY_COLOR:(表示勢力)
						PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
						RESETCOLOR
						PRINTL
					ENDIF
				NEXT
			ENDIF
		NEXT
	NEXT
	PRINTL
ENDIF

PRINTL ◇辺境伯◇
FOR LOCAL:0, 0, MAX_TREATY_M
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_M_COUNTRY:(LOCAL:0):(LOCAL:2):0
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

PRINTL ◇殖民地◇
FOR LOCAL:0, 0, MAX_TREATY_CO
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_CO_COUNTRY:(LOCAL:0):(LOCAL:2):0
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

PRINTL ◇傀儡国◇
FOR LOCAL:0, 0, MAX_TREATY_P
	FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
		IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:1):1 == CFLAG:MASTER:所属
			FOR LOCAL:2, 0, MAX_TREATY_COUNTRY
				IF TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):0 > 0
					表示勢力 = TREATY_P_COUNTRY:(LOCAL:0):(LOCAL:2):0
					SETCOLOR COUNTRY_COLOR:(表示勢力)
					PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(表示勢力)), MAX_CHARANAME_LENGTH / 2, LEFT%]", 表示勢力
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT
PRINTL

CALL SINGLE_DRAWLINE
PRINTBUTTON " 0[    取消]", 0
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF IS_COUNTRY(RESULT)
	REDRAW 1
	RETURN RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF


;-------------------------------------------------
;領土を譲渡する
;-------------------------------------------------
@Colonial_Office_TRANSFER_state
#DIM FIRST_LINE
#DIM 自国都市領有数
#DIM 都市
#DIM 都市所持勢力
#DIM 譲受勢力
#DIM 譲受勢力名

;自国の都市の数を数える
自国都市領有数 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属 && GET_CITYNAME(LOCAL:0) != "無名" && !IS_STAY_ENEMY_UNIT(LOCAL:0)
		自国都市領有数 ++
	ENDIF
NEXT

IF 自国都市領有数 <= 1
	PRINTFORMW 没有可以割让的领土
	RETURN 0
ENDIF

PRINTFORML 请选择要交付给的従属国
CALL Colonial_Office_indicate_2

IF RESULT == 0
	RETURN 0
ENDIF

譲受勢力 = RESULT
譲受勢力名 = GET_COUNTRY_BOSS(譲受勢力)

PRINTFORML 请选择要割让的领土
CALL SINGLE_DRAWLINE

FIRST_LINE = LINECOUNT
$SHOW_LOOP
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

PRINTBUTTON " 0[取消]", 0
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

SIF RESULT == 0
	RETURN

IF !INRANGE(RESULT, 1000, 1000 + MAX_CITY - 1)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

都市 = RESULT - 1000
都市所持勢力 = CITY_OWNER:都市

IF 都市所持勢力 != CFLAG:MASTER:所属
	PRINTFORMW 只能割让自己的城市
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

IF 都市 >= 0 && CITY_OWNER:都市 == CFLAG:MASTER:所属

	;対象キャラの情報を表示
	CALL SINGLE_DRAWLINE
	CALL SHOW_CITY_INFO(都市)
	CALL SINGLE_DRAWLINE
	PRINTFORML 要割让这块领土吗？

	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF

	PRINTFORMW %GET_CITYNAME(都市)%成为了%ANAME(譲受勢力名)%势力的领土
	CALL SINGLE_MINIMIZE(都市)
	CITY_OWNER:(都市) = 譲受勢力
	;互いの国同士の好感度を上昇させ敵対値を低下させる
	CALL DIPLOMACY_IMPROVE_RELATION(譲受勢力, 100)
	RETURN 1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;領土を譲受ける
;-------------------------------------------------
@Colonial_Office_ACQUISITION_state
#DIM FIRST_LINE
#DIM 相手方都市領有数
#DIM 都市
#DIM 都市所持勢力
#DIM 譲渡勢力
#DIM 譲渡勢力名

PRINTFORML 请选择成为对象的従属国
CALL Colonial_Office_indicate_2

IF RESULT == 0
	RETURN 0
ENDIF

譲渡勢力 = RESULT
譲渡勢力名 = GET_COUNTRY_BOSS(譲渡勢力)

;相手方の都市の数を数える
相手方都市領有数 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == 譲渡勢力 && GET_CITYNAME(LOCAL:0) != "無名" && !IS_STAY_ENEMY_UNIT(LOCAL:0)
		相手方都市領有数 ++
	ENDIF
NEXT

IF 相手方都市領有数 <= 1
	PRINTFORMW 对方没有可以割让的领土
	RETURN 0
ENDIF

PRINTFORML 请选择要接收的领土
CALL SINGLE_DRAWLINE

FIRST_LINE = LINECOUNT
$SHOW_LOOP
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

PRINTBUTTON " 0[取消]", 0
PRINTL

REDRAW 0

$INPUT_LOOP
INPUT

SIF RESULT == 0
	RETURN

IF !INRANGE(RESULT, 1000, 1000 + MAX_CITY - 1)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

都市 = RESULT - 1000
都市所持勢力 = CITY_OWNER:都市

IF 都市所持勢力 != 譲渡勢力
	PRINTFORMW 只能选择%ANAME(譲渡勢力名)%的领土
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

IF 都市 >= 0 && CITY_OWNER:都市 == 譲渡勢力

	;対象キャラの情報を表示
	CALL SINGLE_DRAWLINE
	CALL SHOW_CITY_INFO(都市)
	CALL SINGLE_DRAWLINE
	PRINTFORML 你要接收这块领地吗？

	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF

	PRINTFORMW %GET_CITYNAME(都市)%成为了自己国家的领地
	CALL SINGLE_MINIMIZE(都市)
	CITY_OWNER:(都市) = CFLAG:MASTER:所属
	;互いの国同士の敵対値を上昇させ好感度を低下させる
	CALL DIPLOMACY_WORSEN_RELATION(譲渡勢力, 100)
	RETURN 1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;資金を譲渡する
;-------------------------------------------------
@Colonial_Office_TRANSFER_funds
#DIM FIRST_LINE, 2
#DIM CONST 支払額 = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
#DIM 譲受勢力
#DIM 譲受勢力名

FIRST_LINE:0 = LINECOUNT

PRINTFORML 请选择成为对象的従属国
CALL Colonial_Office_indicate_2

IF RESULT == 0
	RETURN 0
ENDIF

譲受勢力 = RESULT
譲受勢力名 = GET_COUNTRY_BOSS(譲受勢力)

$SEND_MONEY_LOOP
PRINTFORML 请输入想要转让给%ANAME(譲受勢力名)%勢力的国庫資金的数字(手动输入・输入0以下取消, 最大50000)

CALL ICPRINT(@"現在的国庫:<{MONEY:(CFLAG:MASTER:所属)}>", "L", カラー_注意)
FOR LOCAL, 0, VARSIZE("支払額")
	SIF MONEY:(CFLAG:MASTER:所属) < 支払額:LOCAL
		BREAK
	PRINTBUTTON @"[支付{支払額:LOCAL}]", 支払額:LOCAL
	PRINTL
NEXT
PRINTFORML
PRINTBUTTON @"[支付{MONEY:(CFLAG:MASTER:所属)}]", MONEY:(CFLAG:MASTER:所属)
PRINTFORM [0] 取消
INPUT
IF RESULT <= 0
	CLEARLINE LINECOUNT - FIRST_LINE:0
	RESTART
ELSEIF MONEY:(CFLAG:MASTER:所属) < RESULT || RESULT > 50000
	CLEARLINE LINECOUNT - FIRST_LINE:1
	GOTO SEND_MONEY_LOOP
ENDIF

PRINTFORML 向%ANAME(譲受勢力名)%势力转让了国库资金 {RESULT}
MONEY:(CFLAG:MASTER:所属) -= RESULT
MONEY:譲受勢力 += RESULT

LOCAL = CALC_SEND_MONEY_EFFECT(RESULT)
;好感度上昇・敵対値下降基準値
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:2 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10


LOCAL:3 = MAX(LOCAL * (LOCAL:1 + LOCAL:2 + 100) / 100, 5)
LOCAL:3 = LOCAL:3 * 100 / GET_DIFFICULTY_CORRECTION()

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(譲受勢力, LOCAL:3)

WAIT

RETURN 1

;-------------------------------------------------
;兵士を譲渡する
;-------------------------------------------------
@Colonial_Office_TRANSFER_soldier
#DIM 譲受勢力
#DIM 譲受勢力名
#DIM 自国残存兵数
#DIM 相手国残存兵数

自国残存兵数 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属)

IF 自国残存兵数 < 500
	PRINTFORMW 至少需要保留500名士兵
	RETURN 0
ENDIF

PRINTFORML 请选择成为对象的従属国
CALL Colonial_Office_indicate_2

IF RESULT == 0
	RETURN 0
ENDIF

譲受勢力 = RESULT
譲受勢力名 = GET_COUNTRY_BOSS(譲受勢力)
相手国残存兵数 = COUNTRY_SOLDIER:譲受勢力

SETCOLOR COUNTRY_COLOR:(CFLAG:MASTER:所属)
PRINTFORML [%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%]
RESETCOLOR
PRINTFORML 総兵数:  {GET_SUM_SOLDIER(CFLAG:MASTER:所属)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(CFLAG:MASTER:所属)}
PRINTFORML 残存兵数:{自国残存兵数}
PRINTFORML

SETCOLOR COUNTRY_COLOR:譲受勢力
PRINTFORML [%ANAME(譲受勢力名)%]
RESETCOLOR
PRINTFORML 総兵数:  {GET_SUM_SOLDIER(譲受勢力)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(譲受勢力)}
PRINTFORML 残存兵数:{相手国残存兵数}
PRINTFORML
CALL SINGLE_DRAWLINE

PRINTFORML 请输入要转让的兵力数(0以下取消)

CALL DRAW_SOLDIER_INPUT(自国残存兵数)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 > 0 && LOCAL:2 < 500
	CLEARLINE 1
	PRINTL 至少需要转让500名士兵
	GOTO INPUT_LOOP
ELSEIF RESULT > 自国残存兵数
	CLEARLINE 1
	PRINTL 兵力不足
	GOTO INPUT_LOOP
ENDIF

COUNTRY_SOLDIER:(CFLAG:MASTER:所属) -= LOCAL:2
COUNTRY_SOLDIER:譲受勢力 += LOCAL:2

PRINTFORMW %ANAME(MASTER)%向%ANAME(譲受勢力名)%转让了兵力{LOCAL:2}

;好感度上昇値・敵対値下降値の計算
;同じ兵数でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:10 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
LOCAL:11 = GET_SUM_ECONOMY(譲受勢力)
LOCAL:12 = SQRT(LOCAL:2 * 26000 / LOCAL:10) + SQRT(LOCAL:2 * 26000 / LOCAL:11)
LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP

LOCAL:13 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:14 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:13 + LOCAL:14 + 100) / 100

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(譲受勢力, LOCAL:12)

RETURN 1

;-------------------------------------------------
;兵士を譲受ける
;-------------------------------------------------
@Colonial_Office_ACQUISITION_soldier
#DIM 譲渡勢力
#DIM 譲渡勢力名
#DIM 自国残存兵数
#DIM 相手国残存兵数

自国残存兵数 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属)

PRINTFORML 请选择成为对象的従属国
CALL Colonial_Office_indicate_2

IF RESULT == 0
	RETURN 0
ENDIF

譲渡勢力 = RESULT
譲渡勢力名 = GET_COUNTRY_BOSS(譲渡勢力)
相手国残存兵数 = COUNTRY_SOLDIER:譲渡勢力

IF 相手国残存兵数 < 500
	PRINTFORMW 对方最少需要剩余500人
	RETURN 0
ENDIF

SETCOLOR COUNTRY_COLOR:(CFLAG:MASTER:所属)
PRINTFORML [%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%]
RESETCOLOR
PRINTFORML 総兵数:  {GET_SUM_SOLDIER(CFLAG:MASTER:所属)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(CFLAG:MASTER:所属)}
PRINTFORML 残存兵数:{自国残存兵数}
PRINTFORML

SETCOLOR COUNTRY_COLOR:譲渡勢力
PRINTFORML [%ANAME(譲渡勢力名)%]
RESETCOLOR
PRINTFORML 総兵数:  {GET_SUM_SOLDIER(譲渡勢力)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(譲渡勢力)}
PRINTFORML 残存兵数:{相手国残存兵数}
PRINTFORML
CALL SINGLE_DRAWLINE

PRINTFORML 请输入要接收的兵力数(0以下取消)

CALL DRAW_SOLDIER_INPUT(相手国残存兵数)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 > 0 && LOCAL:2 < 500
	CLEARLINE 1
	PRINTL 至少需要接收500名士兵
	GOTO INPUT_LOOP
ELSEIF RESULT > 相手国残存兵数
	CLEARLINE 1
	PRINTL 兵力不足
	GOTO INPUT_LOOP
ENDIF

COUNTRY_SOLDIER:譲渡勢力 -= LOCAL:2
COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += LOCAL:2

PRINTFORMW %ANAME(MASTER)%从%ANAME(譲渡勢力名)%那里接收了兵力{LOCAL:2}

RETURN 1

;-------------------------------------------------
;捕虜を譲受ける
;-------------------------------------------------
@Colonial_Office_ACQUISITION_prisoner
#DIM FIRST_LINE
#DIM 譲渡勢力
#DIM 譲渡勢力名
#DIM 自国勢力
#DIM 自国勢力名
#DIM 捕虜所持数
#DIM 対象

PRINTFORML 请选择成为对象的従属国
CALL Colonial_Office_indicate_2

IF RESULT == 0
	RETURN 0
ENDIF

譲渡勢力 = RESULT
譲渡勢力名 = GET_COUNTRY_BOSS(譲渡勢力)
自国勢力 = CFLAG:MASTER:所属
自国勢力名 = GET_COUNTRY_BOSS(自国勢力)
捕虜所持数 = 0
Colonial_prisoner = 譲渡勢力

;対象従属国の捕虜の数をカウント
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):捕虜先 == 譲渡勢力
		捕虜所持数 ++
	ENDIF
NEXT

IF 捕虜所持数 <= 0
	PRINTFORMW 对象势力没有可选择的俘虏
	RETURN 0
ENDIF

FIRST_LINE = LINECOUNT

$CHARA_LIST_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 请选择想要转让的俘虏
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("ACQUISITION_prisoner", "NONE")


SIF RESULT == -1
	RETURN 0

対象 = RESULT

;対象キャラの情報を表示
FLAG:能力表示モード = 0
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(対象)
CALL SINGLE_DRAWLINE
PRINTFORML 要将这名角色作为%ANAME(自国勢力名)%势力的俘虏吗？

;はい／いいえ入力処理
CALL ASK_YN()
IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO CHARA_LIST_LOOP
ENDIF

; CFLAG:対象:捕虜先 = 自国勢力
CALL CAPTURE(対象,自国勢力)
PRINTFORML %ANAME(対象)%成为了%ANAME(自国勢力名)%势力的俘虏

RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_ACQUISITION_prisoner(対象)
#DIM 対象
RETURN 対象 != MASTER && 対象 != GET_COUNTRY_BOSS(CFLAG:対象:所属) && CFLAG:対象:捕虜先 == Colonial_prisoner && !IS_SP_CHARA(対象)



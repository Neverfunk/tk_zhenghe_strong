@SLG_BUILDING_TURNEND_DECREASE_COOLTIME()
    #DIM COUNTRY_ID
    #DIM CHARA_ID
    FOR COUNTRY_ID,0,MAX_COUNTRY
        SIF !IS_COUNTRY(COUNTRY_ID)
            CONTINUE
        IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_永远亭药业)
            BUILDING_TEXT = BUILDING_TEXT_永远亭药业_2(COUNTRY_ID)
            FOR CHARA_ID,0,CHARANUM
                COOLTIME:(CHARA_ID):0 = MAX(0,COOLTIME:(CHARA_ID):0 - RAND:5)
            NEXT
        ENDIF
    NEXT
@SLG_BUILDING_TURNEND_INCREASE_COOLTIME()
    #DIM 梦魔_COUNTRY_ID
    梦魔_COUNTRY_ID = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_サキュバス)
    SIF !IS_COUNTRY(梦魔_COUNTRY_ID)
        RETURN
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,梦魔_COUNTRY_ID,建造物_噩梦祭坛) > 0
        LOCAL = RESULT:1 * 2
        BUILDING_TEXT = BUILDING_TEXT_噩梦祭坛(梦魔_COUNTRY_ID)
        CALL SLG_BUILDING__INCREASE_COOLTIME_噩梦祭坛(LOCAL)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,梦魔_COUNTRY_ID,建造物_美梦祭坛) > 0
        BUILDING_TEXT = BUILDING_TEXT_美梦祭坛(梦魔_COUNTRY_ID)
        CALL SLG_BUILDING_INCREASE_COOLTIME_美梦祭坛()
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,梦魔_COUNTRY_ID,建造物_春梦祭坛) > 0
        CALL SLG_BUILDING_INCREASE_COOLTIME_春梦祭坛(梦魔_COUNTRY_ID)
    ENDIF

@SLG_BUILDING__INCREASE_COOLTIME_噩梦祭坛(SET_TIMES)
    #DIM SET_TIMES
    #DIM AVAIL_CHARA_ID,MAX_CHARA_NUM
    #DIM AVAIL_CHARA_NUM
    VARSET AVAIL_CHARA_ID
    VARSET AVAIL_CHARA_NUM
    FOR LOCAL, 0, CHARANUM
        IF SELECT_CHARA_RANDOM_CHARA_梦魔(LOCAL) > 0
            AVAIL_CHARA_ID:AVAIL_CHARA_NUM = LOCAL
            AVAIL_CHARA_NUM++
        ENDIF
    NEXT
    IF AVAIL_CHARA_NUM>0
        FOR LOCAL,0,SET_TIMES
            LOCAL:1 = (RAND:AVAIL_CHARA_NUM)
            IF RAND:100 > ABL:(AVAIL_CHARA_ID:(LOCAL:1)):知略
                CALL ADD_COOLTIME(AVAIL_CHARA_ID:(LOCAL:1),3)
            ENDIF
        NEXT
    ENDIF

@SLG_BUILDING_INCREASE_COOLTIME_美梦祭坛()
    #DIM SET_TIMES
    #DIM AVAIL_COUNTRY_ID,MAX_COUNTRY
    #DIM AVAIL_COUNTRY_NUM
    VARSET AVAIL_COUNTRY_ID
    VARSET AVAIL_COUNTRY_NUM
    FOR LOCAL, 0, MAX_COUNTRY
        SIF !IS_COUNTRY(LOCAL) || IS_SP_COUNTRY(LOCAL)
            CONTINUE
        LOCAL:1 = GET_COMMANDER_NUM(LOCAL)
        IF (LOCAL:1 / 4) * 10 >= 100
            CONTINUE
        ELSE
            IF RAND:100 < (LOCAL:1 / 4) * 10
                CONTINUE
            ELSE
                AVAIL_COUNTRY_ID = LOCAL
                AVAIL_COUNTRY_NUM++
            ENDIF
        ENDIF
    NEXT
    IF AVAIL_COUNTRY_NUM > 0
        FOR LOCAL,0,CHARANUM
            SIF !SELECT_CHARA_RANDOM_CHARA_梦魔(LOCAL)
                CONTINUE
            LOCAL:2 = 0
            FOR LOCAL:1,0,AVAIL_COUNTRY_NUM
                SIF LOCAL:2
                    BREAK
                IF CFLAG:LOCAL:所属 == AVAIL_COUNTRY_ID:(LOCAL:1)
                    LOCAL:2 = 1
                    CALL ADD_COOLTIME(LOCAL,3)
                ENDIF
            NEXT
        NEXT
    ENDIF



@SLG_BUILDING_INCREASE_COOLTIME_春梦祭坛(梦魔_COUNTRY_ID)
    #DIM 梦魔_COUNTRY_ID
    #DIM AVAIL_CHARA_ID,MAX_CHARA_NUM
    #DIM AVAIL_CHARA_NUM
    VARSET AVAIL_CHARA_ID
    VARSET AVAIL_CHARA_NUM
    FOR LOCAL, 0, CHARANUM
        IF SELECT_CHARA_RANDOM_SP_COUNTERY_CHANGE_COUNTRY(LOCAL) > 0 && LOCAL != MASTER
            AVAIL_CHARA_ID:AVAIL_CHARA_NUM = LOCAL
            AVAIL_CHARA_NUM++
        ENDIF
    NEXT
    ;#;PRINTFORML 春梦祭坛!!!!! {AVAIL_CHARA_NUM}
    IF AVAIL_CHARA_NUM > 0
        LOCAL = RAND:AVAIL_CHARA_NUM
        ;#;PRINTFORML {LOCAL},{AVAIL_CHARA_ID:LOCAL},
        CALL CHANGE_COUNTRY(AVAIL_CHARA_ID:LOCAL,梦魔_COUNTRY_ID,1)
        BUILDING_TEXT = BUILDING_TEXT_春梦祭坛(梦魔_COUNTRY_ID,AVAIL_CHARA_ID:LOCAL)
    ENDIF

@SELECT_CHARA_RANDOM_CHARA_梦魔(CHARA_ID)
    #FUNCTION
    #DIM CHARA_ID
    SIF IS_SP_COUNTRY_CHARA(CHARA_ID)
        RETURNF 0
    SIF IS_ANIMAL(CHARA_ID)
        RETURNF 0
    SIF CFLAG:CHARA_ID:捕虜先
        RETURNF 0
    SIF TALENT:CHARA_ID:奴隷市場モブ
        RETURNF 0
    SIF IS_COUNTRY(CFLAG:CHARA_ID:所属) && (GET_COUNTRY_BOSS(CFLAG:CHARA_ID:所属) == CHARA_ID)
        RETURNF 0
    RETURNF 1

@SELECT_CHARA_RANDOM_SP_COUNTERY_CHANGE_COUNTRY(CHARA_ID)
    #FUNCTION
    #DIM CHARA_ID
    SIF CHARA_ID == MASTER
        RETURNF 0
    SIF IS_SP_COUNTRY_CHARA(CHARA_ID)
        RETURNF 0
    SIF IS_ANIMAL(CHARA_ID)
        RETURNF 0
    SIF IS_COUNTRY(CFLAG:CHARA_ID:所属) && (GET_COUNTRY_BOSS(CFLAG:CHARA_ID:所属) == CHARA_ID)
        RETURNF 0
    RETURNF 1

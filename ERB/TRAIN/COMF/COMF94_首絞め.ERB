;スパンキング

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME94
LOCALS:0 = 絞首

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE94
;共通部分
CALL COM_ABLE_COMMON(94)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
SIF !CAN_REACH_BODY(MPLY:0, MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM94
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 100

SOURCE:(MPLY:0):嗜虐 = 120
SOURCE:(MPLY:0):逸脱 = 50
SOURCE:(MPLY:0):接触 = 30
EXP:(MPLY:0):嗜虐経験値 += 1


;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 50)

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60
	
	SOURCE:(LOCAL:1):苦痛 = 250
	SOURCE:(LOCAL:1):露出 = 100
	SOURCE:(LOCAL:1):逸脱 = 70
	SOURCE:(LOCAL:1):接触 = 30
	EXP:(LOCAL:1):被虐経験値 += 1

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 0, 150)

	IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド)
		TIMES SOURCE:(LOCAL:1):苦痛, 1.50
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP94
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP94(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

SOURCE:(LOCAL:2):嗜虐 = 100
SOURCE:(LOCAL:2):逸脱 = 60
SOURCE:(LOCAL:2):接触 = 30
EXP:(LOCAL:2):嗜虐経験値 += 1

DOWNBASE:(LOCAL:3):体力 += 10

SOURCE:(LOCAL:3):苦痛 += 100
SOURCE:(LOCAL:3):逸脱 += 150
SOURCE:(LOCAL:3):接触 = 20
EXP:(LOCAL:3):被虐経験値 += 1

;倒錯度変化
TCVAR:(LOCAL:3):50 += 4

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE94(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%被%EQUIP_PLAYER_ANAME(ARG:0)%掐着脖子
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%は首を絞められ、もがいている…

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP94(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%被掐着脖子，正在挣扎…
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%は首を絞められ、もがいている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP94(ARG:0)
PRINTFORMW 从%EQUIP_TARGET_ANAME(ARG:0)%的脖子上放开了手
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の首から手を放した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER94(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 95

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"加虐Lv{ABL:(ARG:0):加虐}", ABL:(ARG:0):加虐 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):老実
	CALL COM_ORDER_ELEMENT(ARG:0, "老実", -3)
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "加虐", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE94
LOCALS:0 = %ANAME(MTAR:0)%

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%把手放在%LOCALS:0%的脖子上，紧紧地掐住了…
		;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%の首に手をかけ、ぎりぎりと締め上げ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:0%命令%ANAME(MPLY:0)%掐住自己的脖子
		;TranslatedMark %LOCALS:0%は%ANAME(MPLY:0)%に、首を絞めるように命令した
		PRINTFORMW %ANAME(MPLY:0)%听从命令，开始用手紧紧地掐住%LOCALS:0%的脖子…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%LOCALS:0%の首をぎりぎりと締め上げ始めた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%掐住%LOCALS:0%的脖子
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:0%の首を締めるよう命令した
		PRINTFORMW %ANAME(MPLY:0)%听从命令，开始用手紧紧地掐住%LOCALS:0%的脖子…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%LOCALS:0%の首をぎりぎりと締め上げ始めた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST94
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF (LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):怖主
		LOCAL:3 = CUP:(LOCAL:1):怒主

		IF ABL:(LOCAL:1):被虐 >= 5
			PRINTFORML %ANAME(LOCAL:1)%虽然因为窒息而喘不过气，但还是很开心地发出甜蜜的声音
			;TranslatedMark %ANAME(LOCAL:1)%は酸欠に喘ぎながらも嬉しそうに甘い声を上げている
		ELSEIF ABL:(LOCAL:1):被虐 >= 3
			PRINTFORML %ANAME(LOCAL:1)%虽然很痛苦，但在喘息中却夹杂着甜蜜的色彩
			;TranslatedMark %ANAME(LOCAL:1)%は苦しげにしつつも、その喘ぎには甘い色が混じっている
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%因窒息而喘气
			;TranslatedMark %ANAME(LOCAL:1)%は酸欠に喘いでいる
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN94
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_94()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_94()
RETURN POWER(2, 性的嗜好_虐げられたい)


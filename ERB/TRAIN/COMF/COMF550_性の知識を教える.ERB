;性の知識を教える

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME550
RESULTS:0 = 教導性知識
RESULTS:1 = 被要求教導性知識
RESULTS:2 = 讓対方教導性知識
RESULTS:3 = 被教導性知識
RESULTS:4 = 讓対方教導性知識
RESULTS:5 = 教導性知識

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE550
;共通部分
CALL COM_ABLE_COMMON(550)
SIF RESULT == 0
	RETURN 0
;子育て中、外来逆調教は不可
SIF !GROUPMATCH(FLAG:調教モード, 0, 1, 2, 5, 6)
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;プレイヤーの性知識がLv1以上かつターゲットの性知識がLv0でないと不可
SIF ABL:(MPLY:0):性知識 < 1 || ABL:(MTAR:0):性知識 != 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM550
SOURCE:(MTAR:0):歓楽 = 20

;親密に応じた歓楽のソース追加
CALL ADD_SOURCE_KANRAKU(MTAR:0, 150)

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 80, 80)

EXP:(MTAR:0):性知識経験値 += 100

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER550(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 55

CALL COM_ORDER_BASE(ARG:0)

IF TALENT:(ARG:0):反抗的
	CALL COM_ORDER_ELEMENT(ARG:0, "反抗的", -5)
ENDIF
IF TALENT:(ARG:0):自尊心高
	CALL COM_ORDER_ELEMENT(ARG:0, "自尊心高", -5)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -4)
ENDIF
IF TALENT:(ARG:0):冷漠
	CALL COM_ORDER_ELEMENT(ARG:0, "冷漠", -4)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 7)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -5)
ENDIF
IF TALENT:(ARG:0):孤高
	CALL COM_ORDER_ELEMENT(ARG:0, "孤高", -(10 * KOKOU_RATE(MTAR:0) / 100))
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -10)
ENDIF
IF TALENT:(ARG:0):不知羞恥
	CALL COM_ORDER_ELEMENT(ARG:0, "不知羞恥", 8)
ENDIF
IF TALENT:(ARG:0):接受快感
	CALL COM_ORDER_ELEMENT(ARG:0, "接受快感", 8)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -20)
ENDIF
IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乱)
	CALL COM_ORDER_ELEMENT(ARG:0, "淫乱", 10)
ENDIF

IF IS_SAMESEX(MPLY:0, ARG:0)
	CALL COM_ORDER_ELEMENT(ARG:0, "同性", 35)
ENDIF

IF TALENT:(ARG:0):親愛
	CALL COM_ORDER_ELEMENT(ARG:0, "親愛", 25)
ELSEIF TALENT:(ARG:0):恋慕
	CALL COM_ORDER_ELEMENT(ARG:0, "恋慕", 10)
ENDIF

IF TALENT:(ARG:0):恋人
	CALL COM_ORDER_ELEMENT(ARG:0, "恋人", 20)
ELSEIF TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "親友", 20)
ENDIF

IF TALENT:(ARG:0):隷属
	CALL COM_ORDER_ELEMENT(ARG:0, "隷属", 25)
ELSEIF TALENT:(ARG:0):服従
	CALL COM_ORDER_ELEMENT(ARG:0, "服従", 10)
ENDIF

IF TALENT:(ARG:0):烙印
	CALL COM_ORDER_ELEMENT(ARG:0, "烙印", 20)
ENDIF

IF TALENT:(ARG:0):所有者
	CALL COM_ORDER_ELEMENT(ARG:0, "所有者", 25)
ELSEIF TALENT:(ARG:0):主人
	CALL COM_ORDER_ELEMENT(ARG:0, "主人", 10)
ENDIF


CALL COM_ORDER_ELEMENT(ARG:0, "合意", 15)

IF TALENT:(ARG:0):自尊心高
	LOCAL:0 = 4
ELSEIF TALENT:(ARG:0):自尊心低
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF
IF MARK:(ARG:0):既成事実 >= 1
	CALL COM_ORDER_ELEMENT(ARG:0, @"既成事実Lv{MARK:(ARG:0):既成事実}", MARK:(ARG:0):既成事実 * 3 * LOCAL:0)
ENDIF

IF TALENT:MASTER:魅惑
	CALL COM_ORDER_ELEMENT(ARG:0, "主人公の魅惑", 6)
ENDIF
IF TALENT:MASTER:謎之魅力
	CALL COM_ORDER_ELEMENT(ARG:0, "主人公の謎之魅力", 6)
ENDIF

IF PALAM:(ARG:0):酔意 >= 150
	CALL COM_ORDER_ELEMENT(ARG:0, "酔意", PALAM:(ARG:0):酔意 / 150)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE550
;プレイヤーが主人公側
IF MPLY:0 == MASTER
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORML %ANAME(MPLY:0)%向%ANAME(MTAR:0)%説著性知識
		PRINTFORMW %ANAME(MTAR:0)%意興盎然的聽著%ANAME(MPLY:0)%説的内容…
	;主導権が相手側
	ELSE
		PRINTFORML %ANAME(MTAR:0)%懇求著%ANAME(MPLY:0)%教導自己大人的知識
		PRINTFORMW 被問道的%ANAME(MPLY:0)%向%ANAME(MTAR:0)%教導了性知識…
	ENDIF
	PRINTL 
	PRINTFORMW %ANAME(MTAR:0)%好像掌握了正確的性知識

;プレイヤーが相手側
ELSE
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORML %ANAME(MTAR:0)%懇求著%ANAME(MPLY:0)%教導自己大人的知識
		IF TALENT:(MPLY:0):小悪魔 || TALENT:(MPLY:0):不知羞恥
			PRINTFORMW %ANAME(MPLY:0)%露出得意的微笑、向%ANAME(MTAR:0)%教導了大量的性知識…
		ELSEIF TALENT:(MPLY:0):害羞
			PRINTFORMW %ANAME(MPLY:0)%雖然滿臉通紅、但還是開始対%ANAME(MTAR:0)%談著性知識…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%露出了有些困惑的表情、対%ANAME(MTAR:0)%訴說著性知識…
		ENDIF
	;主導権が相手側
	ELSE
		IF FLAG:ウフフフラグ
			PRINTFORMW %ANAME(MPLY:0)%通過和實踐相結合慢慢地向%ANAME(MTAR:0)%教導著性知識…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%向%ANAME(MTAR:0)%教導著性知識
		ENDIF
	ENDIF
	PRINTL 
	PRINTFORMW %ANAME(MTAR:0)%掌握了正確的性知識
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST550

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN550
RETURN コマンド_共通

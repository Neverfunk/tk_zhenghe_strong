;順番にＡ挿入

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME56
LOCALS:0 = 依次Ａ插入

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE56
;共通部分
CALL COM_ABLE_COMMON(56)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは2人以上
SIF MTAR_NUM < 2
	RETURN 0
FOR LOCAL:0, 0, MTAR_NUM
	SIF IS_A_HOLD(MTAR:(LOCAL:0))
		RETURN 0
	SIF !CAN_FUCK(MPLY:0, MTAR:(LOCAL:0), 56)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM56
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;プレイヤーが行動不能または拘束中なら騎乗位挿入にする
LOCAL:6 = !IS_PLAYABLE(MPLY:0) || IS_BIND(MPLY:0)

;●プレイヤー側の処理
EXP:(MPLY:0):性技経験値 += 2
EXP:(MPLY:0):性交経験値 += 3

LOCAL:5 = 0
FOR LOCAL:0, 0, MTAR_NUM
	IF LOCAL:6
		SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_TARGET_P(MTAR:(LOCAL:0), MPLY:0, 1350)
	ELSE
		SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:(LOCAL:0), MPLY:0, 1350)
	ENDIF

	CALL DOUTEI_COMMON(MPLY:0, @"%ANAME(MTAR:(LOCAL:0))%们の肛门", "乱交")


	;Ａ性交系の共通処理
	CALL COM_COMMON_ASEX_P(MPLY:0, MTAR:(LOCAL:0))

	LOCAL:5 += SOURCE:(MPLY:0):快Ｐ
	SOURCE:(MPLY:0):快Ｐ = 0
NEXT
SOURCE:(MPLY:0):快Ｐ = LOCAL:5 / MTAR_NUM
SOURCE:(MPLY:0):露出 = 100
SOURCE:(MPLY:0):性行動 = 240

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 200, 100)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 2
	EXP:(LOCAL:2):性交経験値 += 3

	IF LOCAL:6
		SOURCE:(LOCAL:2):快Ａ = SENSE_SEX_PLAYER(LOCAL:2, MPLY:0, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):快Ａ = SENSE_SEX_TARGET(LOCAL:2, MPLY:0, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):露出 = 400
	SOURCE:(LOCAL:2):性行動 = 240

	;Ａ性交系の共通処理
	CALL COM_COMMON_ASEX_A(LOCAL:2, MPLY:0)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
NEXT

;体位フラグのセット
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1


;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP56(ARG:0)
;全プレイヤーに対して処理
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)
	DOWNBASE:(LOCAL:1):体力 += 50

	;プレイヤーが行動不能または拘束中なら騎乗位挿入にする
	LOCAL:6 = !IS_PLAYABLE(LOCAL:1) || IS_BIND(LOCAL:1)

	;●プレイヤー側の処理
	EXP:(LOCAL:1):性技経験値 += 2
	EXP:(LOCAL:1):性交経験値 += 2


	LOCAL:5 = 0
	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:2)
		IF LOCAL:6
			SOURCE:(LOCAL:1):快Ｐ = SENSE_SEX_TARGET_P(LOCAL:3, LOCAL:1, 750)
		ELSE
			SOURCE:(LOCAL:1):快Ｐ = SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:1, 750)
		ENDIF

		;性交系の共通処理
		CALL COM_COMMON_ASEX_P(LOCAL:1, LOCAL:3)

		LOCAL:5 += SOURCE:(LOCAL:1):快Ｐ
		SOURCE:(LOCAL:1):快Ｐ = 0

	NEXT
	SOURCE:(LOCAL:1):快Ｐ = LOCAL:5 / MEQUIP_PLAYER_NUM:(ARG:0)
	SOURCE:(LOCAL:1):露出 += 100
	SOURCE:(LOCAL:1):性行動 += 450

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 200, 100)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:1):4 = 1
NEXT
;体位フラグのセット
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;●ターゲット側の処理
FOR LOCAL:1, 0,  MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 =  MEQUIP_TARGET:(ARG:0):(LOCAL:1)
	LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(RAND:(MEQUIP_PLAYER_NUM:(ARG:0)))

	EXP:(LOCAL:2):性技経験値 += 2
	EXP:(LOCAL:2):性交経験値 += 2
	EXP:(LOCAL:2):輪姦経験 += MEQUIP_PLAYER_NUM:(ARG:0) / 2

	IF LOCAL:6
		SOURCE:(LOCAL:2):快Ａ += SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, MAX(1200 - MEQUIP_TARGET_NUM:(ARG:0) * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):快Ａ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, MAX(1200 - MEQUIP_TARGET_NUM:(ARG:0) * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):露出 += 400
	SOURCE:(LOCAL:2):性行動 += 450

	;性交系の共通処理
	CALL COM_COMMON_ASEX_A(LOCAL:2, LOCAL:3)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
NEXT

RETURN 1


;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP56
RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER56(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;膣性交共通
CALL COM_ORDER_ASEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE56(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%的尻穴順番に挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP56(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%顺序犯了%EQUIP_TARGET_ANAME(ARG:0)%的肛门……
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%のアナルを順番に犯している……

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP56(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%离开了%EQUIP_TARGET_ANAME(ARG:0)%
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%から離れた



;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE56
IF MTAR_NUM >= 3
	LOCALS:0 = %ANAME(MTAR:0)%们
	;%ANAME(MTAR:0)%たち
	LOCALS:2 = 横向一列
	;横一列に
	LOCALS:3 = 依次
	;順番に
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
	;%ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	LOCALS:2 = 一左一右
	;左右に
	LOCALS:3 = 交替
    ;交互に
ENDIF

;プレイヤーが行動不能または拘束中の場合(騎乗位)
IF !IS_PLAYABLE(MPLY:0) || IS_BIND(MPLY:0)
	IF TCVAR:(MPLY:0):52
		LOCALS:1 = 昏厥失去意识的
;失神して意識のない
	ELSEIF IS_BIND(MPLY:0)
		LOCALS:1 = 被束缚得动弹不得的
;拘束されて身動きの取れない
	ELSEIF TCVAR:(MPLY:0):51
		LOCALS:1 = 累得动弹不得的
;疲労して動けない
	ELSE
		LOCALS:1 =
	ENDIF

	;ペニスバンド装着中
	IF IS_EQUIP_PLAYER((MPLY:0), 50)
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%让%LOCALS:0%%LOCALS:3%用尻穴叼紧假肉棒，从下面把腰往上顶侵犯肛门…
				;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%に%LOCALS:3%尻穴で張り型を咥えさせ、下から腰を突き上げアナルを犯していった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%用尻穴%LOCALS:3%含进%LOCALS:1%%ANAME(MPLY:0)%的假肉棒，一边夹紧肠壁一边扭动腰…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の張り型を尻穴で%LOCALS:3%咥え込み、腸壁を締め付けながら腰を動かした…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%用肛门叼紧%LOCALS:1%%ANAME(MPLY:0)%的假肉棒
				;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%%ANAME(MPLY:0)%の張り型をアナルで%LOCALS:3%咥え込むように命令した
				PRINTFORMW %LOCALS:0% %LOCALS:3%跨在%LOCALS:1%%ANAME(MPLY:0)%的腰上，将假肉棒插入各自的尻穴中…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の腰に%LOCALS:3%跨り、張り型をそれぞれの尻穴へと挿入していった…
		ENDSELECT

	;ペニスバンド不使用
	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%让%LOCALS:0%%LOCALS:3%用尻穴叼紧假肉棒，从下面把腰往上顶侵犯肛门…
				;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%に%LOCALS:3%尻穴でペニスを咥えさせ、下から腰を突き上げアナルを犯していった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%把%LOCALS:1%%ANAME(MPLY:0)%的肉棒用尻孔%LOCALS:3%含进去，一边夹紧肠壁一边扭动腰…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%のペニスを尻穴で%LOCALS:3%咥え込み、腸壁を締め付けながら腰を動かした…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%用肛门叼紧%LOCALS:1%%ANAME(MPLY:0)%的肉竿
				;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%%ANAME(MPLY:0)%の竿をアナルで%LOCALS:3%咥え込むように命令した
				PRINTFORMW %LOCALS:0%%LOCALS:3%跨坐到%LOCALS:1%%ANAME(MPLY:0)%的腰上，将肉棒插入各自的尻穴中…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の腰に%LOCALS:3%跨り、ペニスをそれぞれの尻穴へと挿入していった…
		ENDSELECT
	ENDIF

;プレイヤーが行動可能な場合
ELSE
	;ペニスバンド装着中
	IF IS_EQUIP_PLAYER((MPLY:0), 50)
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%把%LOCALS:0%%LOCALS:2%排列起来，然后%LOCALS:3%用假肉棒侵犯尻穴…
				;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%を%LOCALS:2%並べると、%LOCALS:3%張り型で尻穴を犯していった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%一起把%ANAME(MPLY:0)%推到，%LOCALS:3%跨坐上用各自的肛门叼紧假肉棒…
				;TranslatedMark %LOCALS:0%は一斉に%ANAME(MPLY:0)%を押し倒すと、%LOCALS:3%上に跨って張り型をそれぞれのアナルで咥え込ん%PLAYER_STR("でいった", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%侵犯%LOCALS:0%的肛门
				;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%LOCALS:0%のアナルを%LOCALS:3%犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%在%LOCALS:2%排列的%LOCALS:0%的尻穴里%LOCALS:3%插入假肉棒…
				;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:2%並べられた%LOCALS:0%の尻穴に、%LOCALS:3%張り型を挿入していった…
		ENDSELECT

	;ペニスバンド不使用
	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%把%LOCALS:0%%LOCALS:2%排列起来，%LOCALS:3%把肉棒刺进了尻穴…
				;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%を%LOCALS:2%並べると、%LOCALS:3%尻穴へとペニスを突き入れていった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%一起把%ANAME(MPLY:0)%推到，%LOCALS:3%跨坐上用各自的肛门叼紧肉棒…
				;TranslatedMark %LOCALS:0%は一斉に%ANAME(MPLY:0)%を押し倒すと、%LOCALS:3%上に跨りそれぞれのアナルでペニスを咥え込ん%PLAYER_STR("でいった", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%侵犯%LOCALS:0%的肛门
				;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%LOCALS:0%のアナルを%LOCALS:3%犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%在%LOCALS:2%排列的%LOCALS:0%的尻穴里%LOCALS:3%插入肉棒…
				;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:2%並べられた%LOCALS:0%の尻穴に、%LOCALS:3%ペニスを挿入していった…
		ENDSELECT
	ENDIF
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN56
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_56()
RETURN POWER(2, 性的嗜好_性交したい)

@COM_PREFERENCE_TARGET_56()
RETURN POWER(2, 性的嗜好_性交されたい) + POWER(2, 性的嗜好_輪姦されたい)

@COM_STACK_SPERM_MPLY_TO_MTAR_56()
RETURN 射精部位_アナル

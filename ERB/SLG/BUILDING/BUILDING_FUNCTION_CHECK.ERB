@BUILDING_SLOT_AVAIL_CHECK(COUNTRY_ID,SLOT_PTR)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM SLOT_PTR

    SIF !IS_COUNTRY(COUNTRY_ID)
        RETURNF 0

    SIF !CHECK_IN_SLOT_RANGE(SLOT_PTR)
        RETURNF 0

    IF SLOT_PTR == 0
        RETURNF 1
    ELSEIF SLOT_PTR == 1 && TECHNOLOGY_STATUS:(COUNTRY_ID):TECHNOLOGY_BUILDINGS >= 1
        RETURNF 1
    ELSEIF SLOT_PTR == 2 && TECHNOLOGY_STATUS:(COUNTRY_ID):TECHNOLOGY_BUILDINGS >= 4
        RETURNF 1
    ENDIF
    RETURNF 0
@IS_BUILDING(BUILDING_ID)
    #FUNCTION
    #DIM BUILDING_ID
    SIF BUILDING_ID >= BUILDING_START && BUILDING_ID < BUILDING_NUM
        RETURNF 1
    RETURNF 0
@CHECK_IN_SLOT_RANGE(SLOT_PTR)
    #FUNCTION
    #DIM SLOT_PTR

    SIF !INRANGE(SLOT_PTR, 0, MAX_BUILDING_SLOT_NUM - 1)
        RETURNF 0
    RETURNF 1


@CHECK_CITY_HAVE_BUILDING(CITY_ID,BUILDING_ID)
    #FUNCTION
    #DIM CITY_ID
    #DIM BUILDING_ID
    #DIM SLOT_PTR
    FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM-1
        IF CITY_BUILDING:CITY_ID:SLOT_PTR == BUILDING_ID
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0

@CHECK_CITY_EXIST_ANY_BUILDING(CITY_ID)
    #FUNCTION
    #DIM CITY_ID
    #DIM SLOT_PTR
    FOR SLOT_PTR,0,MAX_BUILDING_SLOT_NUM-1
        IF CITY_BUILDING:CITY_ID:SLOT_PTR >0 && CITY_BUILDING:CITY_ID:SLOT_PTR <= BUILDING_NUM
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0

@CHECK_BUILDING_TECH_AVAIL(COUNTRY_ID,BUILDING_LEVEL_TMP)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_LEVEL_TMP
    SELECTCASE BUILDING_LEVEL_TMP
        CASE 建造物_等级_一级建筑
            RETURNF 1
        CASE 建造物_等级_二级建筑
            IF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_BUILDINGS < 2
                RETURNF 0
            ELSE
                RETURNF 1
            ENDIF
        CASE 建造物_等级_三级建筑
            IF TECHNOLOGY_STATUS:COUNTRY_ID:TECHNOLOGY_BUILDINGS < 5
                RETURNF 0
            ELSE
                RETURNF 1
            ENDIF
    ENDSELECT

@CHECK_COUNTRY_TYPE_LIMIT(COUNTRY_ID,BUILDING_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_ID
    #DIM SP_ID
    #DIM UIN_ID
    IF !IS_SP_COUNTRY(COUNTRY_ID) && BUILDING_COUNTRY_ID:BUILDING_ID:0 == 1
        RETURNF 1
    ENDIF
    IF BUILDING_LIMIT_UNI:BUILDING_ID
        FOR LOCAL:0, 0, MAX_COUNTRY
            FOR LOCAL:1, 0, MAX_COUNTRY
                IF TREATY_UNI_COUNTRY:(LOCAL:0):(LOCAL:1):0 == COUNTRY_ID || TREATY_UNI_COUNTRY:(LOCAL:0):(LOCAL:1):1 == COUNTRY_ID
                    UIN_ID = LOCAL:0
                    BREAK
                ENDIF
            NEXT
        NEXT
    ENDIF

    FOR SP_ID,1,8
        SIF !BUILDING_COUNTRY_ID:BUILDING_ID:SP_ID
            CONTINUE
        IF COUNTRY_ID == (GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:SP_ID))
            RETURNF 1
        ENDIF
        IF BUILDING_LIMIT_UNI:BUILDING_ID && IS_SP_COUNTRY_IN_UNION:UIN_ID:SP_ID == 1
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0
@CHECK_COUNTRY_NUM_LIMIT(COUNTRY_ID,BUILDING_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_ID
    IF BUILDING_LIMIT_NUM:BUILDING_ID == 建造物_数量限制_无
        RETURNF 1
    ELSEIF BUILDING_LIMIT_NUM:BUILDING_ID == 建造物_数量限制_1
        IF TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:BUILDING_ID > 0
            RETURNF 0
        ELSE
            RETURNF 1
        ENDIF
    ELSE
        RETURNF 1
    ENDIF
;建筑角色需求判定（建造时）
@CHECK_BUILDING_CHARA_LIMIT_BUILDING(COUNTRY_ID,BUILDING_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_ID
    #DIM CHARA_ID
    SIF BUILDING_TOHO_REQ_ID:BUILDING_ID:0 == 0
        RETURNF 1
    CHARA_ID = BUILDING_TOHO_REQ_ID:BUILDING_ID:0
    SIF CFLAG:CHARA_ID:所属 != COUNTRY_ID
        RETURNF 0
    IF CFLAG:MASTER:所属 == COUNTRY_ID
        SIF CFLAG:CHARA_ID:好感度 < 0 && ! MASTER == CHARA_ID
            RETURNF 0
    ENDIF
    IF IS_SP_COUNTRY(COUNTRY_ID)
        SIF !GETBIT(TALENT:CHARA_ID:特殊勢力陥落系, SP_COUNTRY_TO_CONST(COUNTRY_ID))
            RETURNF 0
    ENDIF
    RETURNF 1

;建筑角色需求判定（使用时）
@CHECK_BUILDING_CHARA_LIMIT_USING(COUNTRY_ID,BUILDING_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_ID
    #DIM CHARA_ID
    SIF BUILDING_TOHO_REQ_ID:BUILDING_ID:0 == 0
        RETURNF 1
    SIF !CHECK_BUILDING_CHARA_LIMIT_BUILDING(COUNTRY_ID,BUILDING_ID)
        RETURNF 0
    CHARA_ID = BUILDING_TOHO_REQ_ID:BUILDING_ID:0
    SIF !CHECK_CHARA_IS_FREE(CHARA_ID)
        RETURNF 0
    RETURNF 1


;建筑特殊国家类型需求判定（使用时）
@CHECK_BUILDING_SP_COUNTRY_LIMIT_USING(COUNTRY_ID,BUILDING_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_ID
    #DIM SP_ID
    SIF BUILDING_TYPE:BUILDING_ID != 建造物_类型_特殊类
        RETURNF 1
    SIF !IS_SP_COUNTRY(COUNTRY_ID)
        RETURNF 0
    FOR SP_ID,1,8
        SIF !BUILDING_COUNTRY_ID:BUILDING_ID:SP_ID
            CONTINUE
        IF COUNTRY_ID == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:SP_ID)
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0

@CHECK_CHARA_IS_FREE(CHARA_ID)
    #FUNCTION
    #DIM CHARA_ID
    IF CFLAG:(CHARA_ID):捕虜先 || CFLAG:(CHARA_ID):特殊状態 || CFLAG:(CHARA_ID):行動不能状態 == 行動不能_子供
		RETURNF 0
	ELSEIF TALENT:(CHARA_ID):触手妊娠 || TALENT:(CHARA_ID):Ａ触手妊娠 || TALENT:(CHARA_ID):Ｕ触手妊娠 || IS_ANIMAL(CHARA_ID)
		RETURNF 0
	ELSEIF COOLTIME:(CHARA_ID):0 > 3
        RETURNF 0
	ENDIF
    RETURNF 1

@CHECK_BUILDING_SKILL(COUNTRY_ID,BUILDING_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM BUILDING_ID
    SIF BUILDING_HAVE_SKILL:BUILDING_ID < 1 || !CHECK_COUNTRY_TYPE_LIMIT(COUNTRY_ID,BUILDING_ID)
        RETURNF 0
    SIF TMP_BUILDING_EXIST_FLAG:(COUNTRY_ID):BUILDING_ID < 1
        RETURNF -1
    SIF BUILDING_SKILL_COLDTIME:COUNTRY_ID:BUILDING_ID > 0
        RETURNF -2
    IF BUILDING_TOHO_REQ_ID:BUILDING_ID:0 > 0
        SIF !CHECK_BUILDING_CHARA_LIMIT_USING(COUNTRY_ID,BUILDING_ID)
            RETURNF -3
    ENDIF
    RETURNF 1
;ARG:0 = 1 表示AI建造判定
;AI建筑建造资金消耗减少50%
;AI建筑城市经济要求减少25%
@CAN_BUILD_BUILDING(CITY_ID,BUILDING_ID,ARG:0 = 0)
    #FUNCTION
    #DIM BUILDING_ID
    #DIM COUNTRY_ID
    #DIM CITY_ID
    COUNTRY_ID = CITY_OWNER:CITY_ID
    SIF !CHECK_BUILDING_CHARA_LIMIT_BUILDING(COUNTRY_ID,BUILDING_ID)
        RETURNF -6
    SIF !CHECK_COUNTRY_TYPE_LIMIT(COUNTRY_ID,BUILDING_ID)
        RETURNF -3
    SIF !CHECK_COUNTRY_NUM_LIMIT(COUNTRY_ID,BUILDING_ID)
        RETURNF -4
    SIF !CHECK_BUILDING_TECH_AVAIL(COUNTRY_ID,BUILDING_LEVEL:BUILDING_ID)
        RETURNF -1
    SIF (CITY_ECONOMY:CITY_ID) / 100 < GET_CITY_ECONOMY_REQ(COUNTRY_ID,BUILDING_LEVEL:BUILDING_ID,ARG:0)
        RETURNF -5
    SIF MONEY:COUNTRY_ID < GET_BUILDING_COST(COUNTRY_ID,BUILDING_LEVEL:BUILDING_ID,ARG:0)
        RETURNF -2
    RETURNF 1


@CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !IS_COUNTRY(COUNTRY_ID)
        RETURNF 0
    SIF CONFIG:600 == 0
        RETURNF 0
    SIF CONFIG:600 == 2
        RETURNF 1
    IF CONFIG:600 == 1 && COUNTRY_ID == CFLAG:MASTER:所属
        RETURNF 1
    ELSE
        RETURNF 0
    ENDIF

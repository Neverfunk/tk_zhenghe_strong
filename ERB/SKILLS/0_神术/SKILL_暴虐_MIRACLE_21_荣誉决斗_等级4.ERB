;-----------------------------------
;基本値計算に先んじて処理するもの
;-----------------------------------
@SKILL_0_MIRACLE_21_EXIST
RETURN 1

@SKILL_0_MIRACLE_21_NAME
RESULTS = 荣誉决斗

;レベルは1-5まで
@SKILL_0_MIRACLE_21_LEVEL
RETURN 4

@SKILL_0_MIRACLE_21_FROMWHO
;0或不存在=通用 1=暴虐 2=诡术 3=腐朽 4=色欲
RETURN 1

@SKILL_0_MIRACLE_21_SELFLEARN(对象)
#DIM 对象
#DIM 上任
#DIM 优先度
#DIM 技能ID = 21  ;本技能ID
优先度 = 0
;信仰等级要求
CALLFORM SKILL_0_MIRACLE_{技能ID}_LEVEL
SIF ABL:对象:信仰 < FAITH_LEVEL:RESULT
	RETURN 0
;神术等级加成,一般认为越高越强
优先度 += RESULT * 20
;已有技能联动
;角色属性
SIF ABL:对象:武闘 + ABL:对象:防衛 >= 140
	优先度 += 30
SIF ABL:对象:武闘 + ABL:对象:防衛 >= 180
	优先度 += 30
SIF ABL:对象:武闘 + ABL:对象:防衛 <= 60
	优先度 -= 100
SIF ABL:对象:武闘 + ABL:对象:防衛 <= 100
	优先度 -= 60
;角色是否主君
;领土数量
;被俘虏时
;部队/都市就任情况
上任 = 0
IF CFLAG:对象:所属 != 0
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_COMMANDER:(LOCAL:0) != 0
			FOR LOCAL:1, 0, MAX_CITY_COMMANDER
				IF GET_CITY_COMMANDER(LOCAL:0, LOCAL:1) == 对象
					;对象 = 都市LOCAL:0的第LOCAL:1守将
					上任 = 1
					优先度 += 50
				ENDIF
			NEXT
		ENDIF
	NEXT
	FOR LOCAL:0, 0, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0)
			FOR LOCAL:1, 0, 10
				IF UNIT_COMMANDER:(LOCAL:0):(LOCAL:1) != 0
					FOR LOCAL:2, 0, MAX_UNIT_COMMANDER
						IF GET_UNIT_COMMANDER(LOCAL:0, LOCAL:1, LOCAL:2) ==对象
							;对象 = 势力LOCAL:0的部队LOCAL:1的第LOCAL:2将领
							上任 = 1
							优先度 += 50
						ENDIF
					NEXT
				ENDIF
			NEXT
		ENDIF
	NEXT
ENDIF
SIF !上任
	优先度 -= 300
;势力方针/倾向
IF CFLAG:对象:所属 == CFLAG:MASTER:所属 ;此分支暂时无用
	SELECTCASE COUNTRY_POLICY:(CFLAG:对象:所属)
		CASE 政策_経済
			优先度 -= 20
		CASE 政策_徴兵
			优先度 += 20
		CASE 政策_防衛
			优先度 -= 10
		CASEELSE ;政策未定
	ENDSELECT
ELSE
	SELECTCASE COUNTRY_AI_TYPE:(CFLAG:对象:所属)
		CASE AI_通常
			优先度 += 0
		CASE AI_好戦
			优先度 += 20
		CASE AI_防衛
			优先度 += 0
		CASE AI_外交
			优先度 -= 20
		CASE AI_内政
			优先度 -= 20
		CASE AI_蛮行
			优先度 += 30
		CASE AI_ホフゴブリン
	ENDSELECT
ENDIF
;角色成长型
SELECTCASE TALENT:对象:300 
	CASE 0 ;万能 武防知政
		优先度 += 30
	CASE 1 ;知将 武防知
		优先度 += 30
	CASE 2 ;学士 政治 知略
		优先度 -= 50
	CASE 3 ;两道 武防政
		优先度 += 30
	CASE 4 ; 武官 武防
		优先度 += 30
	CASE 5 ;军师 知略
		优先度 -= 50
	CASE 6 ;文官 政治
		优先度 -= 50
	CASE 7 ;芸能 歌唱
		优先度 -= 50
	CASE 8 ;料理
		优先度 -= 50
	CASE 9 ;天才
		优先度 += 30
ENDSELECT
RETURN MAX(优先度,0)

;対象選択
@SKILL_0_MIRACLE_21_SETTARGET(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊
#DIM 加和
#DIM 角色
;我方部隊対象
COMBAT_SKILL_TARGET = -1
SIF BATTLE_COMMANDER_NUM:対象側 == 0
	RETURN 0

加和 = 0
FOR LOCAL,0,(BATTLE_COMMANDER_NUM:対象側),1
	角色 = BATTLE_COMMANDER:対象側:LOCAL
	SIF 角色 == -1
		CONTINUE
	IF ABL:角色:武闘 + ABL:角色:防衛 > 加和
		加和 = ABL:角色:武闘 + ABL:角色:防衛
		COMBAT_SKILL_TARGET = LOCAL
	ENDIF
NEXT

RETURN 1

;発動判定
@SKILL_0_MIRACLE_21_CAN_INVOKE(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊
対象側 = !発動側

RETURN BATTLE_COMMANDER_NUM:対象側 > 0

;効果をここに記述
@SKILL_0_MIRACLE_21_INVOKE(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊

#DIM 决斗对象
#DIM 战斗参数1
#DIM 战斗参数2
#DIM 角色
対象側 = !発動側
决斗对象 = BATTLE_COMMANDER:対象側:COMBAT_SKILL_TARGET
IF 决斗对象 == -1
	PRINTFORML 但%ANAME(発動者)%没有发现对手的踪影...
	RETURN 0
ENDIF
PRINTFORML %ANAME(発動者)%向%ANAME(决斗对象)%发起了荣誉决斗
PRINTFORML 在双方士兵的注目下,%ANAME(発動者)%和%ANAME(决斗对象)%开始了一对一的战斗
战斗参数1 = ABL:発動者:武闘 + ABL:発動者:防衛
战斗参数2 = ABL:决斗对象:武闘 + ABL:决斗对象:防衛
IF ABL:発動者:信仰 > 90 
	TIMES 战斗参数1, 1.15
	PRINTFORML %ANAME(発動者)%得到了信仰之力的增幅
ELSEIF ABL:発動者:信仰 > 70 
	TIMES 战斗参数1, 1.1
	PRINTFORML %ANAME(発動者)%得到了信仰之力的增幅
ELSEIF ABL:発動者:信仰 > 50 
	TIMES 战斗参数1, 1.05
	PRINTFORML %ANAME(発動者)%得到了信仰之力的增幅
ENDIF
IF ABL:决斗对象:信仰 > 90 
	TIMES 战斗参数2, 1.15
	PRINTFORML %ANAME(决斗对象)%得到了信仰之力的增幅
ELSEIF ABL:决斗对象:信仰 > 70 
	TIMES 战斗参数2, 1.1
	PRINTFORML %ANAME(决斗对象)%得到了信仰之力的增幅
ELSEIF ABL:决斗对象:信仰 > 50 
	TIMES 战斗参数2, 1.05
	PRINTFORML %ANAME(决斗对象)%得到了信仰之力的增幅
ENDIF
IF 战斗参数1 > 战斗参数2
	CALL BATTLE_KNOCKOUT(対象側, COMBAT_SKILL_TARGET)
	PRINTFORML %ANAME(発動者)%取得了决斗胜利!%ANAME(决斗对象)%无力参战了
ELSEIF 战斗参数1 < 战斗参数2
	FOR LOCAL,0,(BATTLE_COMMANDER_NUM:発動側),1
		SIF BATTLE_COMMANDER:発動側:LOCAL == 発動者
			角色 = LOCAL
	NEXT
	CALL BATTLE_KNOCKOUT(発動側, 角色)
	PRINTFORML %ANAME(决斗对象)%取得了决斗胜利!%ANAME(発動者)%无力参战了
ELSE
	PRINTFORML %ANAME(决斗对象)%与%ANAME(発動者)%大战了三百回合，但没有分出胜负。
ENDIF

@SKILL_0_MIRACLE_21_EXPLANATION
RESULTS = 与敌武闘防衛之和最高一人决斗,败者战斗不能

@SKILL_0_MIRACLE_21_RATE(発動者, 発動番号, 発動側, 発動勢力, 発動部隊, 対象勢力, 対象部隊)
#DIM 発動者
#DIM 発動番号
#DIM スキル
#DIMS ジャンル
#DIM 発動側
#DIM 発動勢力
#DIM 発動部隊
#DIM 対象側
#DIM 対象勢力
#DIM 対象部隊
RETURN 1000

;-------------------------------------------------
;「アイテム」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME71
RESULTS:0 '= "道具"

;-------------------------------------------------
;「アイテム」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK71
SIF CFLAG:MASTER:捕虜先
	RETURN 0
RETURN 1

;-------------------------------------------------
;「アイテム」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY71
CALL SHOP_ITEM
RETURN 1

;-------------------------------------------------
;「アイテム」の右カラムオフ判定
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_COLUMN_RIGHT_OFF_CHECK71

;-------------------------------------------------
;「アイテム」の処理
;-------------------------------------------------
@SHOP_ITEM
#DIM FIRST_LINE_ITEM_MAIN
#DIM IS_GISHI
#DIM ITEM_NUM_DISPFLAG

REDRAW 0
FIRST_LINE_ITEM_MAIN = LINECOUNT

;[技師]持ちが主人公勢力に存在するかどうかを判定
SIF TALENT:MASTER:技師
	IS_GISHI = 1

;タイトル
CALL SINGLE_DRAWLINE
PRINTPLAIN  
CALL COLUMN_RIGHT_TITLE("請選択要購入/開発/売却的道具", "", "", "0", "200～全")
PRINTL 
CALL SINGLE_DRAWLINE

;所持アイテム表示
CALL COLOR_PRINT(" 所持 ", カラー_注釈, "金:", カラー_選択不可, @"%NUM_FORMAT(MONEY)%", カラー_選択中)

LOCAL:1 = 999
FOR LOCAL:0, 100, 300
	IF ITEM:(LOCAL:0) >= 1
		IF LOCAL:1 > 70
			LOCAL:1 = 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(6)%
		ENDIF
		LOCALS:0 = %ITEMNAME:(LOCAL:0)%
		IF ITEM:(LOCAL:0) >= 2
			LOCALS:0 = %LOCALS:0%×{ITEM:(LOCAL:0)}
		ENDIF
		LOCAL:1 += STRLENS(LOCALS:0) + 2
		PRINTFORM %LOCALS:0%　
	ENDIF
NEXT
IF LOCAL:1 == 999
	PRINTL 
	PRINTFORM %TOSTR_SPACE(6)%
	CALL COLOR_PRINT("沒有所持道具", カラー_選択不可)
ENDIF
PRINTL 

SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR

CALL COLOR_PRINT(" 購入 ", カラー_注釈, "選択後顯示説明", カラー_選択不可)
PRINTL 
PRINTFORM %TOSTR_SPACE(6)%
LOCAL:1 = 0
FOR LOCAL:0, 100, 300
	IF ITEMNAME:(LOCAL:0) != "" && ITEMSALES:(LOCAL:0)
		IF LOCAL:1 > 0 && LOCAL:1 % 3 == 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(6)%
		ELSEIF LOCAL:1 > 0
			PRINTFORM %TOSTR_SPACE(9)%
		ENDIF
		LOCAL:1 ++
		CALL GET_PRICE(LOCAL:0)
		;所持金不足 or 所持数オーバーで購入できない時は色を変える（がクリックはできる）
		IF MONEY < RESULT || ITEM:(LOCAL:0) >= 99
			SETCOLOR カラー_選択不可
			PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%]%NUM_FORMAT(RESULT), 8%", LOCAL:0
			RESETCOLOR
		ELSE
			PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%]%NUM_FORMAT(RESULT), 8%", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL 

SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
CALL COLOR_PRINT(@" 開発 ", カラー_注釈)

IF IS_GISHI
	CALL COLOR_PRINT(@"素質", カラー_選択不可, "<技師>折扣", カラー_緑, "有", カラー_選択不可, @"%"開発費", 20%%TOSTR_SPACE(2)%%"期間", 6%%TOSTR_SPACE(7)%%"開発費", MAX_CHARANAME_LENGTH + 21%%TOSTR_SPACE(2)%%"期間", 6%", カラー_注釈)
ELSE
	CALL COLOR_PRINT("无素質<技師>折扣", カラー_選択不可, @"%"開発費", 10%%TOSTR_SPACE(2)%%"期間", 4%%TOSTR_SPACE(9)%%"開発費", MAX_CHARANAME_LENGTH + 11%%TOSTR_SPACE(2)%%"期間", 4%", カラー_注釈)
ENDIF
PRINTL 

LOCAL:1 = 0
FOR LOCAL:0, 200, 300
	IF ITEMNAME:(LOCAL:0) != "" && !ITEMSALES:(LOCAL:0) && ITEMTIME:(LOCAL:0) == 0 && (!INRANGE(LOCAL:0, 200, 249) || ITEM:LOCAL == 0)
		RESULT = 1
		TRYCALLFORM ITEM_ABLE{LOCAL:0}
		IF RESULT
			IF LOCAL:1 == 0
				PRINTFORM %TOSTR_SPACE(6)%
			;２列で改行
			ELSEIF LOCAL:1 > 0 && LOCAL:1 % 2 == 0
				PRINTL 
				PRINTFORM %TOSTR_SPACE(6)%
			;技師ありだと横幅を広くとるので合間のスペースは狭めにとる
			ELSEIF LOCAL:1 > 0 && IS_GISHI
				PRINTFORM %TOSTR_SPACE(7)%
			;技師なしだと横幅が狭いので合間のスペースは広めにとる
			ELSEIF LOCAL:1 > 0
				PRINTFORM %TOSTR_SPACE(9)%
			ENDIF
			LOCAL:1 ++
			IF IS_GISHI
				IF MONEY < ITEMPRICE:(LOCAL:0) * 75 / 100
					SETCOLOR カラー_選択不可
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTPLAINFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					PRINTPLAINFORM →%NUM_FORMAT((ITEMPRICE:(LOCAL:0) * 75 / 100)), 8%
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTPLAINFORM {ITEMTERM:(LOCAL:0), 2}
					PRINTPLAINFORM →{MAX(ITEMTERM:(LOCAL:0) / 2, 1), 2}
					RESETCOLOR
				ELSE
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					SETCOLOR カラー_緑
					PRINTFORM →%NUM_FORMAT((ITEMPRICE:(LOCAL:0) * 75 / 100)), 8%
					RESETCOLOR
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTFORM {ITEMTERM:(LOCAL:0), 2}
					SETCOLOR カラー_緑
					PRINTFORM →{MAX(ITEMTERM:(LOCAL:0) / 2, 1), 2}
					RESETCOLOR
				ENDIF
			ELSE
				IF MONEY < ITEMPRICE:(LOCAL:0)
					SETCOLOR カラー_選択不可
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTFORM {ITEMTERM:(LOCAL:0), 4}
					RESETCOLOR
				ELSE
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTFORM {ITEMTERM:(LOCAL:0), 4}
				ENDIF
			ENDIF
		ENDIF
	ENDIF
NEXT
IF LOCAL:1 != 0
	PRINTL 
ENDIF

CALL SINGLE_DRAWLINE("-", カラー_灰色)
CALL COLOR_PRINTL(" 開発中的道具 ", カラー_注釈)
CALL SINGLE_DRAWLINE("-", カラー_灰色)

LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("ITEMTIME")
	SIF ITEMTIME:LOCAL == 0
		CONTINUE
	IF LOCAL:1 == 0
		PRINTFORM %TOSTR_SPACE(6)%
	;3列で改行
	ELSEIF LOCAL:1 > 0 && LOCAL:1 % 4 == 0
		PRINTL 
		PRINTFORM %TOSTR_SPACE(6)%
	ENDIF
	PRINTFORM %ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH, LEFT%
	PRINTPLAINFORM %TOSTR_SPACE(2)%
	SETCOLOR カラー_緑
	PRINTPLAINFORM {ITEMTERM:(LOCAL:0) + 1 - ITEMTIME:(LOCAL:0), 2}
	PRINTFORM %TOSTR_SPACE(3)%
	RESETCOLOR
	LOCAL:1 ++
NEXT
IF LOCAL:1 != 0
	PRINTL 
ENDIF




SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
CALL COLOR_PRINTL(" 銷毀 ", カラー_注釈, "可以以購入値出售", カラー_選択不可)
PRINTFORM %TOSTR_SPACE(6)%
LOCAL:1 = 0
FOR LOCAL:0, 500, 900
	LOCAL:2 = LOCAL:0 - 500
	IF ITEMNAME:(LOCAL:2) != "" && ITEM:(LOCAL:2) > 0
		LOCALS:0 = %ITEMNAME:(LOCAL:2)%
		IF ITEM:(LOCAL:2) >= 2
			LOCALS:0 = %LOCALS:0%×{ITEM:(LOCAL:2)}
		ENDIF
		IF LOCAL:1 > 0 && LOCAL:1 % 3 == 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(6)%
		ELSEIF LOCAL:1 > 0
			PRINTFORM %TOSTR_SPACE(9)%
		ENDIF
		LOCAL:1 ++
		CALL GET_PRICE(LOCAL:2)
		PRINTBUTTON @"[%LOCALS:0, MAX_CHARANAME_LENGTH, LEFT%]%NUM_FORMAT(RESULT), 8%", LOCAL:0
	ENDIF
NEXT
PRINTL 

FOR LOCAL:0, LINECOUNT - FIRST_LINE_ITEM_MAIN, LINES_SHOP_DRAW + 1
	PRINTL 
NEXT

SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTPLAIN  
PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", 999
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT 999

IF RESULT == 999
	REDRAW 1
	RETURN

;購入あるいは開発
ELSEIF RESULT >= 100 && RESULT < 300 && ITEMNAME:RESULT != ""
	LOCAL:2 = RESULT

	;購入アイテム
	IF ITEMSALES:(LOCAL:2)
		CALL GET_PRICE(LOCAL:2)
		LOCAL:3 = RESULT

		;アイテムの種別を判定
		IF (LOCAL:2 >= 150 && LOCAL:2 < 200) || (LOCAL:2 >= 250 && LOCAL:2 < 280)
			LOCAL:5 = 1
		ELSE
			LOCAL:5 = 0
		ENDIF

		;アイテムの説明を表示し、購入の入力処理(开始)
		CALL ITEM_EXPLANATION(LOCAL:2, LOCAL:3, LOCAL:5)
		LOCAL:4 = RESULT

		;購入個数が0なら戻る
		IF LOCAL:4 == 0
			CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
			RESTART
		ENDIF

		CLEARLINE 2
		;購入アイテム
		IF LOCAL:2 < 150
			IF ITEMNAME:(LOCAL:2) == "太平要術之書"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSEIF ITEMNAME:(LOCAL:2) == "淫技大全"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSE
				PRINTFORMW %TOSTR_SPACE(6)%購買了%ITEMNAME:(LOCAL:2)%
				ITEM:(LOCAL:2) ++
				ITEMSALES:(LOCAL:2) = 0
			ENDIF
			MONEY -= LOCAL:3
			RESTART
		;購入アイテム(消耗品)
		ELSEIF LOCAL:2 < 200 || (LOCAL:2 >= 250 && LOCAL:2 < 280)
			PRINTFORMW %TOSTR_SPACE(6)%購買了{LOCAL:4}個%ITEMNAME:(LOCAL:2)%
			MONEY -= LOCAL:3 * LOCAL:4
			ITEM:(LOCAL:2) += LOCAL:4
			RESTART
		;購入アイテム(その場使用)
		ELSEIF LOCAL:2 >= 280 && LOCAL:2 < 300
			IF GROUPMATCH(LOCAL:2, GETNUM(ITEM, "犬"), GETNUM(ITEM, "豚"), GETNUM(ITEM, "馬"), GETNUM(ITEM, "猿"))
				PRINTFORMW %TOSTR_SPACE(6)%購買了%ITEMNAME:(LOCAL:2)%
				MONEY -= LOCAL:3
				SELECTCASE LOCAL:2
					CASE GETNUM(ITEM, "犬")
						CALL ADD_ANIMAL(CFLAG:MASTER:所属, 動物_犬)
					CASE GETNUM(ITEM, "豚")
						CALL ADD_ANIMAL(CFLAG:MASTER:所属, 動物_豚)
					CASE GETNUM(ITEM, "馬")
						CALL ADD_ANIMAL(CFLAG:MASTER:所属, 動物_馬)
					CASE GETNUM(ITEM, "猿")
						CALL ADD_ANIMAL(CFLAG:MASTER:所属, 動物_猿)
				ENDSELECT
				RESTART
			ELSE
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF RESULT
					MONEY -= LOCAL:3
				ENDIF
				RESTART
			ENDIF
		ENDIF

	;開発アイテム
	ELSEIF ITEMTIME:(LOCAL:2) == 0 && !ITEMSALES:(LOCAL:2)
		RESULT = 1
		TRYCALLFORM ITEM_ABLE{LOCAL:2}
		IF RESULT
			LOCAL:3 = ITEMPRICE:(LOCAL:2)
			IF IS_GISHI
				TIMES LOCAL:3, 0.75
			ENDIF

			;アイテムの説明を表示し、購入の入力処理(开始)
			CALL ITEM_EXPLANATION(LOCAL:2, LOCAL:3, 2)
			LOCAL:4 = RESULT

			;購入個数が0なら戻る
			IF LOCAL:4 == 0
				CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
				RESTART
			ENDIF

			CLEARLINE 2
			PRINTFORMW %TOSTR_SPACE(6)%開始了%ITEMNAME:(LOCAL:2)%的開発
			IF IS_GISHI
				ITEMTIME:(LOCAL:2) = ITEMTERM:(LOCAL:2) + 1 - MAX(ITEMTERM:(LOCAL:2) / 2, 1)
			ELSE
				ITEMTIME:(LOCAL:2) = 1
			ENDIF
			MONEY -= LOCAL:3
			RESTART
		ENDIF
	ENDIF

;破棄
ELSEIF 500 <= RESULT && RESULT <= 900 && ITEMNAME:(RESULT - 500) != ""
	CLEARLINE 1
	LOCAL:1 = RESULT - 500
	IF ITEM:(LOCAL:1) > 0
		CALL GET_PRICE(LOCAL:1)
		LOCAL:2 = RESULT
		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTFORM  ★ %ITEMNAME:(LOCAL:1)%
		CALL COLOR_PRINT(@"  価格:", カラー_注釈)
		CALL COLOR_PRINT(@"%NUM_FORMAT(LOCAL:2)%", カラー_選択中)
		PRINTL 

		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		CALL COLOR_PRINT(@"把目前持有的{ITEM:(LOCAL:1)}個%ITEMNAME:(LOCAL:1)%賣掉", カラー_注釈)
		PRINTL 

		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		CALL COLOR_PRINT("請選択要出售的数目", ,"取消交易", カラー_注釈)
		PRINTL 

		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		CALL PROMPTLY_BUTTONS(0, ITEM:(LOCAL:1), 0)
		LOCAL:3 = RESULT
		;購入個数が0なら戻る
		IF LOCAL:3 == 0 || LOCAL:3 > ITEM:(LOCAL:1)
			RESTART
		ENDIF
		CLEARLINE 2
		MONEY += RESULT * LOCAL:2
		PRINTFORMW %TOSTR_SPACE(6)%出售了{RESULT}個%ITEMNAME:(LOCAL:1)%、獲得了%NUM_FORMAT(RESULT * LOCAL:2)%金
		ITEM:(LOCAL:1) -= RESULT
		IF LOCAL:1 < 150
			ITEMSALES:(LOCAL:1) = 1
		ELSEIF ITEMTERM:(LOCAL:1) > 0
			ITEMTIME:(LOCAL:1) = 0
		ENDIF
		RESTART
	ENDIF
ENDIF

CLEARLINE 1
REUSELASTLINE 
REDRAW 2
GOTO INPUT_LOOP

;-------------------------------------------------
;アイテムの説明を表示し、購入の入力処理(开始)
;ARG:0=アイテム番号
;ARG:1=アイテムの値段
;ARG:2=アイテムのタイプ(0=単品 1=消耗品 2=開発)
;戻り値として購入個数を返す
;-------------------------------------------------
@ITEM_EXPLANATION(ARG:0, ARG:1, ARG:2)
#DIM RETURN_NUM
#DIM MAXITEM_NUM

RETURN_NUM = 0
MAXITEM_NUM = 0

CLEARLINE 1

SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTFORM  ★ %ITEMNAME:(ARG:0)%  
IF ARG:2 == 2
	CALL COLOR_PRINT(@"開発費:", カラー_注釈)
	CALL COLOR_PRINT(@"%NUM_FORMAT(ARG:1)%", カラー_選択中)
ELSE
	CALL COLOR_PRINT(@"価格:", カラー_注釈)
	CALL COLOR_PRINT(@"%NUM_FORMAT(ARG:1)%", カラー_選択中)
ENDIF
PRINTL 
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
SETCOLOR カラー_注釈
TRYCALLFORM ITEM_EXPLANATION{ARG:0}
RESETCOLOR

SELECTCASE ARG:2
	CASE 0
		PRINTL 
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		IF MONEY >= ARG:1
			PRINTBUTTON @"[%"購入這件道具", MAX_CHARANAME_LENGTH, LEFT%]", 0
		ELSE
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM [%"購入這件道具", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
		PRINTL 
		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAIN  
		PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", 1
		PRINTL 
		INPUT 1
		RETURN_NUM = (RESULT == 0)

	CASE 1
		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		IF ITEM:(ARG:0) >= 99
			SETCOLOR カラー_選択不可
			PRINTW  所持数目到底極限無法購買
			RESETCOLOR
			RETURN_NUM = 0
		ELSEIF MONEY < ARG:1
			SETCOLOR カラー_選択不可
			PRINTW  所持金不足無法購買
			RESETCOLOR
			RETURN_NUM = 0
		ELSE
			PRINTPLAINFORM %TOSTR_SPACE(6)%請選択購買的数量  
			CALL COLOR_PRINT(@"取消交易", カラー_注釈)
			PRINTL 
			SETCOLOR カラー_灰色
			CALL SINGLE_DRAWLINE
			RESETCOLOR
			$INPUT_LOOP_NUM
			MAXITEM_NUM = MONEY / ARG:1
			IF ITEM:(ARG:0) + MAXITEM_NUM > 99
				MAXITEM_NUM = MAXITEM_NUM - ( MAXITEM_NUM + ITEM:(ARG:0) - 99 )
			ENDIF
			SETCOLOR カラー_灰色
			CALL SINGLE_DRAWLINE
			RESETCOLOR
			;ARG:0=最小値 ARG:1=最大値 ARG:2=戻るボタンの番号
			CALL PROMPTLY_BUTTONS(0, MAXITEM_NUM, 0)
			IF ITEM:(ARG:0) + RESULT > 99
				CLEARLINE 1
				PRINTW 道具的数量超過了99個
				GOTO INPUT_LOOP_NUM
			ELSEIF RESULT * ARG:1 > MONEY
				CLEARLINE 1
				PRINTW 所持金不足
				GOTO INPUT_LOOP_NUM
			ENDIF
			RETURN_NUM = MAX(RESULT, 0)
		ENDIF

	CASE 2
		PRINTL 
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		IF MONEY >= ARG:1
			PRINTBUTTON @"[%"開発這件道具", MAX_CHARANAME_LENGTH, LEFT%]", 0
		ELSE
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM [%"開発這件道具", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
		PRINTL 
		SETCOLOR カラー_灰色
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAIN  
		PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", 1
		PRINTL 
		INPUT 1
		RETURN_NUM = (RESULT == 0)
ENDSELECT

RETURN RETURN_NUM

;-------------------------------------------------
;即決ボタン
;
;PROMPTLY_MIN		最小入力値
;PROMPTLY_MAX		最大入力値
;PROMPTLY_BACK		戻るボタンに割り当てる番号
;
;DUM_DISPFLAG		ボタン表示フラグ
;PROMPTLY_NUM		ボタンに表示する数値
;PROMPTLY_POWER_NUM	ボタンに累乗する数値
;
;CALL PROMPTLY_BUTTONS(0, 100, 0)
;-------------------------------------------------
@PROMPTLY_BUTTONS(PROMPTLY_MIN, PROMPTLY_MAX, PROMPTLY_BACK)
#DIM PROMPTLY_MIN
#DIM PROMPTLY_MAX
#DIM PROMPTLY_BACK
#DIM PROMPTLY_DISP
#DIM PROMPTLY_NUM
#DIM PROMPTLY_POWER_NUM
#DIM FIRST_LINE
#DIM SAVE_REDRAW

CURRENTREDRAW
SAVE_REDRAW = RESULT:0
REDRAW 0

FIRST_LINE = LINECOUNT
$SHOP_LOOP

PROMPTLY_NUM = 0
PROMPTLY_POWER_NUM = 0
PRINTPLAINFORM %TOSTR_SPACE(5)%
FOR LOCAL:1, 0, 2
	POWER PROMPTLY_POWER_NUM, 10, LOCAL:1
	PROMPTLY_DISP = 0
	FOR LOCAL:0, 1, 10
		PROMPTLY_NUM = LOCAL:0 * PROMPTLY_POWER_NUM
		IF PROMPTLY_NUM <= PROMPTLY_MAX
			PRINTPLAIN  
			PRINTBUTTON @"[%NUM_FORMAT(PROMPTLY_NUM), 2%]", PROMPTLY_NUM
			PROMPTLY_DISP ++
		ENDIF
	NEXT
	IF PROMPTLY_DISP
		PRINTL 
		PRINTPLAINFORM %TOSTR_SPACE(5)%
	ENDIF
NEXT
PRINTL 
PRINTPLAINFORM %TOSTR_SPACE(6)%
PRINTBUTTON @"[%"決定最大数量", MAX_CHARANAME_LENGTH - 2, LEFT%{PROMPTLY_MAX, 2}]", PROMPTLY_MAX
PRINTL 
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTPLAIN  
PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", PROMPTLY_BACK
PRINTL 

INPUT 0
IF RESULT == PROMPTLY_BACK
	CLEARLINE 1
	RETURN PROMPTLY_BACK
ELSE
	CLEARLINE 1
	IF PROMPTLY_MIN > RESULT:0 || PROMPTLY_MAX < RESULT:0
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOP_LOOP
	ENDIF
	PRINTL 
	REDRAW SAVE_REDRAW
	RETURN RESULT:0
ENDIF


;-------------------------------------------------
;動物を追加する
;-------------------------------------------------
@ADD_ANIMAL(ARG:0, ARG:1)
CALL ADD_VOID_CHARA()
LOCAL = RESULT
SELECTCASE ARG:1
	CASE 動物_犬
		NAME:LOCAL     = 犬
		CALLNAME:LOCAL = 犬
	CASE 動物_豚
		NAME:LOCAL     = 豚
		CALLNAME:LOCAL = 豚
	CASE 動物_馬
		NAME:LOCAL     = 馬
		CALLNAME:LOCAL = 馬
	CASE 動物_猿
		NAME:LOCAL     = 猿
		CALLNAME:LOCAL = 猿
ENDSELECT
MAXBASE:LOCAL:体力 = 3000
MAXBASE:LOCAL:気力 = 3000
MAXBASE:LOCAL:精神力 = 1500

BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
BASE:LOCAL:気力 = MAXBASE:LOCAL:気力
BASE:LOCAL:精神力 = MAXBASE:LOCAL:精神力

ABL:LOCAL:Ｃ感 = 5
ABL:LOCAL:欲望 = 5
ABL:LOCAL:性技 = 5
ABL:LOCAL:性知識 = 5
ABL:LOCAL:奉仕 = 3
ABL:LOCAL:性交 = 10
ABL:LOCAL:射精 = 5

EXP:LOCAL:絶頂経験 = 300

ABL:LOCAL:武闘 = 5
ABL:LOCAL:防衛 = 5
ABL:LOCAL:知略 = 5
ABL:LOCAL:政治 = 1
ABL:LOCAL:歌唱 = 1
ABL:LOCAL:料理 = 1
TALENT:LOCAL:不在乎貞操 = 1
TALENT:LOCAL:動物耳 = 1
TALENT:LOCAL:尾巴 = 1
TALENT:LOCAL:服従 = 1
TALENT:LOCAL:隷属 = 1
TALENT:LOCAL:精力超群 = 1
TALENT:(LOCAL:0):合意 = 1
TALENT:LOCAL:陰毛現在値 = 陰毛_標準
TALENT:LOCAL:陰毛目標値 = 陰毛_標準
TALENT:LOCAL:Ｖ緊緻度 = GET_DEFAULT_TIGHTNESS("普通")
TALENT:LOCAL:Ａ緊緻度 = GET_DEFAULT_TIGHTNESS("普通")
CFLAG:(LOCAL):好感度 = 3000
CFLAG:(LOCAL):従属度 = 10000
CFLAG:(LOCAL):依存度 = 5000
CFLAG:(LOCAL):支配度 = 3000

CFLAG:(LOCAL:0):面識 = 1
CALL CHANGE_COUNTRY(LOCAL, ARG:0)

TALENT:LOCAL:動物 = ARG:1


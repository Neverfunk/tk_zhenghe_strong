;顔面騎乗

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME101
LOCALS:0 = 顔面騎乗

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE101
;共通部分
CALL COM_ABLE_COMMON(101)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
SIF IS_M_HOLD(MTAR:0)
	RETURN 0
;Target is fucking someone in any position except cowgirl/reverse cowgirl
SIF IS_INSERT_SINGLE(MTAR:0, "Ｐ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｐ"), 5, 6)
	RETURN 0
;target being fucked in any position except missionary
SIF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｖ"), 1)
	RETURN 0
;target being fucked in any position except missionary
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 1)
	RETURN 0
;player fucking
SIF IS_INSERT_SINGLE(MPLY:0, "全")
	RETURN 0
;player giving buttjob to anyone
SIF IS_EQUIP_PLAYER(MPLY:0, 15)
	RETURN 0
;player ridden or already riding
SIF IS_RIDDEN(MPLY:0) || IS_RIDE(MPLY:0)
	RETURN 0
;target already ridden or riding
SIF IS_RIDE(MTAR:0) || IS_RIDDEN(MTAR:0)
	RETURN 0
;either is grovelling
SIF IS_EQUIP_PLAYER(MPLY:0, 110) || IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
;player or target footlicking, buttjob, or foam playing each other
SIF SEARCH_EQUIP_IC_M(MPLY:0, MTAR:0, 5, 15, 104) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM101
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;ターゲット側の屈従のソース基本量
LOCAL:11 = 120

;プレイヤー側の嗜虐のソース基本量
LOCAL:12 = 100

;●プレイヤーについて処理
SOURCE:(MPLY:0):液体追加 = 100
SOURCE:(MPLY:0):露出 = 300
SOURCE:(MPLY:0):逸脱 = 150
SOURCE:(MPLY:0):接触 = 60
SOURCE:(MPLY:0):性行動 = 300
EXP:(MPLY:0):嗜虐経験値 += 1
IF HAS_VAGINA(MPLY:0)
	SOURCE:(MPLY:0):快Ｃ = SENSE_HOUSHI(MTAR:0, MPLY:0, 1000)
ELSE
	LOCAL:11 = 150
	LOCAL:12 = 120
ENDIF

;主導権がプレイヤー側にある場合
IF GET_COM_INITIATIVE() == 0
	SOURCE:(MPLY:0):嗜虐 = LOCAL:12
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 120)

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 100

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):被虐経験値 += 1
SOURCE:(MTAR:0):奉仕 = SERVE_HOUSHI(MTAR:0, 300)
SOURCE:(MTAR:0):逸脱 = 100
SOURCE:(MTAR:0):接触 = 60
SOURCE:(MTAR:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, LOCAL:11)

;プレイヤーが１人のとき
IF MPLY_NUM == 1
	IF TALENT:(MTAR:0):舌頭霊活
		TIMES SOURCE:(MPLY:0):快Ｃ, 1.50
	ENDIF
ENDIF

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;キス
CALL KISS_COMMON(MTAR:0, @"%ANAME(MPLY:0)%の\@ IS_MALE(MPLY:0) ? 睾丸 # 秘貝\@", GET_SITUATION_BY_TRAIN_MODE())

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP101
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP101(ARG:0)
;●プレイヤーについて処理
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):液体追加 += 100
SOURCE:(LOCAL:2):露出 += 100
SOURCE:(LOCAL:2):逸脱 += 80
SOURCE:(LOCAL:2):接触 += 30
SOURCE:(LOCAL:2):性行動 += 100
EXP:(LOCAL:2):嗜虐経験値 += 1
IF HAS_VAGINA(LOCAL:2)
	SOURCE:(LOCAL:2):快Ｃ += SENSE_HOUSHI(LOCAL:2, MEQUIP_TARGET:(ARG:0):0, 500)
ENDIF

;倒錯度変化
TCVAR:(LOCAL:2):50 += 2

;●ターゲットについて処理
LOCAL:2 = MEQUIP_TARGET:(ARG:0):0

EXP:(LOCAL:2):性技経験値 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
SOURCE:(LOCAL:2):逸脱 += 60
SOURCE:(LOCAL:2):接触 += 30
SOURCE:(LOCAL:2):性行動 += 100
EXP:(LOCAL:2):被虐経験値 += 1
;倒錯度変化
TCVAR:(LOCAL:2):50 += 2

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE101(ARG:0)
IF IS_MALE(MEQUIP_PLAYER:(ARG:0):0)
	LOCALS:0 = Ｃ
ELSE
	LOCALS:0 = Ｖ
ENDIF
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%顔面騎乗中(%LOCALS%)
;RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%に顔面騎乗中(%LOCALS%)

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP101(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%跨在%EQUIP_TARGET_ANAME(ARG:0)%的脸上…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%の顔の上に跨っている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP101(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%在%EQUIP_TARGET_ANAME(ARG:0)%的脸上动起了腰…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の顔から腰を上げた…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER101(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF
IF TALENT:(ARG:0):討厭女人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭女人", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE101
IF !HAS_VAGINA(MPLY:0)
	LOCALS:0 = 玉
ELSE
	LOCALS:0 =
	SELECTCASE CUM_CUR_AMOUNT:(MPLY:0):射精部位_膣内
		CASE IS >= 15
			LOCALS:0 = 精液%SPLIT_R("沾满的：充满的：溢出的：不断流出的")%
;精液%SPLIT_R("まみれの：の詰まった：の溢れる：が留処なくこぼれる")%
		CASE IS >= 1
			LOCALS:0 = 精液%SPLIT_R("垂下来的：漏出来的")%
;精液%SPLIT_R("を垂らす：の漏れる")%
	ENDSELECT
	LOCALS:0 = %LOCALS:0%%STR_BODY("膣", MPLY:0)%
ENDIF



SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF MPLY:0 == MASTER
			PRINTFORMW %ANAME(MPLY:0)%跨坐在%ANAME(MTAR:0)%的脸上，慢慢地把%LOCALS:0%膣穴压了上去…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の顔に跨り、%LOCALS:0%をグリグリと押し付けた…
		ELSEIF ABL:(MPLY:0):加虐 >= 3
			PRINTFORMW %ANAME(MPLY:0)%带着淫乱的笑容跨在%ANAME(MTAR:0)%的脸上，将%LOCALS:0%膣穴用力压到%ANAME(MTAR:0)%的脸上…
			;TranslatedMark %ANAME(MPLY:0)%はサディスティックな笑みを浮かべながら%ANAME(MTAR:0)%の顔に跨り、%LOCALS:0%をグリグリと押し付け%TARGET_STR("た", "てきた")%…
		ELSEIF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_淫乱)
			PRINTFORMW %ANAME(MPLY:0)%跨在%ANAME(MTAR:0)%的脸上，为了寻求快感而将%LOCALS:0%膣穴不断的压向%ANAME(MTAR:0)%的嘴边…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の顔に跨り、快感を貪るように%LOCALS:0%をグリグリと押し付け%TARGET_STR("た", "てきた")%…
		ELSEIF !TALENT:(MPLY:0):不知羞恥 && ABL:(MPLY:0):露出 < 3
			PRINTFORMW %ANAME(MPLY:0)%在感到羞耻同时，跨上%ANAME(MTAR:0)%的脸，将%LOCALS:0%膣穴压到了%ANAME(MTAR:0)%的嘴边…
			;TranslatedMark %ANAME(MPLY:0)%は恥じらうようにしながらも、%ANAME(MTAR:0)%の顔に跨って%LOCALS:0%を押し付け%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%跨在%ANAME(MTAR:0)%的脸上，慢慢地把%LOCALS:0%膣穴推向了%ANAME(MTAR:0)%的嘴边…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の顔に跨り、%LOCALS:0%をグリグリと押し付け%TARGET_STR("た", "てきた")%…
		ENDIF
	;ターゲットに主導権
	CASE 1
		IF !HAS_VAGINA(MPLY:0)
			PRINTFORMW %ANAME(MTAR:0)%让%ANAME(MPLY:0)%跨在脸上，嘴里含着玉袋，咕哩咕哩地来回舔弄着%ANAME(MTAR:0)%…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を顔の上に跨らせると、玉袋を口に含んでジュルジュルと舐め回し%PLAYER_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MTAR:0)%让%ANAME(MPLY:0)%跨在脸上，从下面来回舔弄着责备着%ANAME(MTAR:0)%…
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を顔の上に跨らせると、下からワレメを舐め回して責め立て%PLAYER_STR("た", "てきた")%…
		ENDIF
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%跨在%ANAME(MTAR:0)%的脸上强行将%LOCALS:0%膣穴压到嘴边
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%の顔に跨って%LOCALS:0%を押し付けるように命令した
		IF !TALENT:(MPLY:0):不知羞恥 && ABL:(MPLY:0):露出 < 3
			PRINTFORMW 虽然%ANAME(MPLY:0)%很害羞，但还是按照她说的那样跨上了%ANAME(MTAR:0)%的脸，将%LOCALS:0%膣穴压了上去…
			;TranslatedMark %ANAME(MPLY:0)%は恥ずかしがりながらも、言われるまま%ANAME(MTAR:0)%の顔に跨り%LOCALS:0%を押し付けた…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%就这样，跨上%ANAME(MTAR:0)%的脸，把%LOCALS:0%膣穴强压给了她…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%の顔に跨り%LOCALS:0%を押し付けた…
		ENDIF
ENDSELECT
IF CUM_CUR_AMOUNT:(MPLY:0):射精部位_膣内
	PRINTFORM %SPLIT_RAND("炙热的/粘稠的/浓厚的/新鲜的", 1)%精液
	;TranslatedMark %SPLIT_RAND("熱い/粘つく/濃厚な/新鮮な", 1)%精液が

	IF ABL:(MTAR:0):精愛 >= 4
		PRINTFORML 一滴到%SPLIT_RAND("喜欢精液的/爱上精液的/渴求精液的", 1)%%ANAME(MTAR:0)%嘴里，%PRONOUN(MTAR:0)%发出了喜悦的呻吟声…
		;TranslatedMark %SPLIT_RAND("精液好きの/精愛味覚を得た/精液に飢えた", 1)%%ANAME(MTAR:0)%の口に滴ると、%PRONOUN(MTAR:0)%は悦びの呻き声を漏らした…
	ELSE
		PRINTFORML 虽然滴到了%ANAME(MTAR:0)%的口中，%PRONOUN(MTAR:0)%却不得不为之服务…
		;TranslatedMark %ANAME(MTAR:0)%の口に滴るが、%PRONOUN(MTAR:0)%は奉仕を余儀なくされた…
	ENDIF
	;target eats 1/4th + 3 cum, up to 20
	LOCAL = LIMIT(MIN(((CUM_CUR_AMOUNT:(MPLY:0):射精部位_膣内 / 4) + 3), CUM_CUR_AMOUNT:(MPLY:0):射精部位_膣内) , 0, 20)
	CUM_CUR_AMOUNT:(MPLY:0):射精部位_膣内 -= LOCAL
	CUM_CUR_AMOUNT:(MTAR:0):射精部位_口 += LOCAL
	SIF CUM_CUR_AMOUNT:(MPLY:0):射精部位_膣内 == 0
		PRINTFORML %ANAME(MPLY:0)%%STR_BODY("膣", MPLY:0)%的精液全部被%ANAME(MTAR:0)%喝干了
		;TranslatedMark %ANAME(MPLY:0)%の%STR_BODY("膣", MPLY:0)%の精液は全て%ANAME(MTAR:0)%が飲み干した
	;Cumlove exp increases by amount of cum
	EXP:(MTAR:0):精愛経験値 += LOCAL
	;Swallowing exp
	SIF ABL:(MTAR:0):精愛 >= 4
		EXP:(MTAR:0):口内射精経験 ++
	WAIT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST101

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN101
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_101()
RETURN POWER(2, 性的嗜好_辱められたい)

@COM_PREFERENCE_TARGET_101()
RETURN POWER(2, 性的嗜好_辱めたい)


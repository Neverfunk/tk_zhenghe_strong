;放屁する

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME150
LOCALS:0 = 放屁

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE150
;共通部分
CALL COM_ABLE_COMMON(150)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人以上
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;多対多は不可
SIF MPLY_NUM >= 2 && MTAR_NUM >= 2
	RETURN 0

LOCAL:1 = -1
;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;屁残量が十分でないなら不可
	SIF TCVAR:(MPLY:(LOCAL:0)):55 < 1000
		RETURN 0
	;Ａが埋まっているなら不可
	SIF IS_A_HOLD(MPLY:(LOCAL:0))
		RETURN 0
	;顔面騎乗中なら不可
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM150
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):排泄経験値 += 1

	LOCAL:3 = 300
	LOCAL:4 = 250

	SELECTCASE ABL:(LOCAL:2):排泄
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:2):排泄 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:2):露出 = 1000
	SOURCE:(LOCAL:2):逸脱 = 400
	SOURCE:(LOCAL:2):中毒充足 = LOCAL:3
	SOURCE:(LOCAL:2):反感 = LOCAL:4

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 150)

	;屁残量を0にする
	TCVAR:(LOCAL:2):55 = 0
NEXT

;主導権の所在を取得
LOCAL:11 = GET_COM_INITIATIVE()

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;ターゲットに主導権
	IF LOCAL:11 == 1 || LOCAL:11 == 2
		SOURCE:(LOCAL:1):嗜虐 = 200
		SOURCE:(LOCAL:1):逸脱 = 200
	;ターゲットに主導権がない
	ELSE
		SOURCE:(LOCAL:1):逸脱 = 400
		SOURCE:(LOCAL:1):恐怖 = MAX(100 - 30 * ABL:(LOCAL:1):欲望, 0)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 100)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 3

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP150
RETURN 0

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER150(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"排泄Lv{ABL:(ARG:0):排泄}", ABL:(ARG:0):排泄 * 7)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -12)
ENDIF
IF TALENT:(ARG:0):不知羞恥
	CALL COM_ORDER_ELEMENT(ARG:0, "不知羞恥", 8)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -6)
ENDIF
IF TALENT:(ARG:0):喜歡引人注目
	CALL COM_ORDER_ELEMENT(ARG:0, "喜歡引人注目", 6)
ENDIF
IF TALENT:(ARG:0):倒錯的
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯的", 8)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE150
IF MPLY_NUM == 1
	LOCALS:1 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:1 = %ANAME(MPLY:0)%和%ANAME(MPLY:1)%
ELSE
	LOCALS:1 = %ANAME(MPLY:0)%们
ENDIF

IF MTAR_NUM == 1
	LOCALS:2 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:2 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
ELSE
	LOCALS:2 = %ANAME(MTAR:0)%们
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:1%用屁股对着%LOCALS:2%放屁…
		;TranslatedMark %LOCALS:1%は%LOCALS:2%に尻を向けてオナラをし%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:2%让%LOCALS:1%在自己的眼前放屁…
		;TranslatedMark %LOCALS:2%は自分の目の前で%LOCALS:1%にオナラをさせた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:1%向%LOCALS:2%放屁
		;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:1%に対して、%LOCALS:2%に向けてオナラをするように命令した
		PRINTFORMW %LOCALS:1%被说的那样屁股朝向%LOCALS:2%放了屁…
		;TranslatedMark %LOCALS:1%は言われるまま%LOCALS:2%に尻を向けてオナラをした…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST150
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):排泄 >= 2
			PRINTFORMW %ANAME(LOCAL:1)%在害羞和解放感中，浮现出兴奋的表情
			;TranslatedMark %ANAME(LOCAL:1)%は恥ずかしさと解放感に、興奮した表情を浮かべている
		ELSEIF ABL:(LOCAL:1):排泄 >= 1
			PRINTFORMW %ANAME(LOCAL:1)%虽然还是很紧张的样子，但对于放屁的解放感不由得放松了脸
			;TranslatedMark %ANAME(LOCAL:1)%はまだ緊張した様子だが、放屁の解放感に思わず顔を緩めた
		ELSE
			PRINTFORMW 因为太害羞了，所以脸都红了
			;TranslatedMark %ANAME(LOCAL:1)%はあまりの恥ずかしさに顔を真っ赤にしている
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):怒主 + CUP:(LOCAL:1):哀主

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):欲情 >= 500
			PRINTFORMW %ANAME(LOCAL:1)%呼吸急促，一脸兴奋地闻着放屁的味道
			;TranslatedMark %ANAME(LOCAL:1)%は息を荒げ、興奮した様子でオナラの臭いを嗅いでいる
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%不由得皱起了眉头
			;TranslatedMark %ANAME(LOCAL:1)%は漂ってくる臭気に思わず顔をしかめた
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN150
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_150()
RETURN POWER(2, 性的嗜好_辱めたい)

@COM_PREFERENCE_TARGET_150()
RETURN POWER(2, 性的嗜好_辱められたい)


;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K40_PEDDLE_RATE(対象)
#DIM 対象
RETURN 100 + KDVAR:対象:にとり_押し売り累計額 * 100 > 0

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K40_PEDDLE_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:にとり_押し売り >= 2
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K40_PEDDLE_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K40_PEDDLE(対象)
#DIM 対象
#DIM 選択肢

IF KDVAR:対象:にとり_押し売り > 0
	PRINTFORML ……%ANAME(対象)%前来拜访了
	;TranslatedMark ……%ANAME(対象)%が訪ねてきた
	PRINTFORMW 「呀%ANAME(MASTER)%，还要再买点什么吗？」
	;TranslatedMark 「やあ%ANAME(MASTER)%。また何か買ってくれない？」
ELSE
	PRINTFORML ……%ANAME(対象)%前来拜访了
	;TranslatedMark ……%ANAME(対象)%が訪ねてきた
	PRINTFORMW 「呀%ANAME(MASTER)%，其实我稍微有个请求」
	;TranslatedMark 「やあ%ANAME(MASTER)%。実はお願いがあるんだけど」
	PRINTFORMW 「实际上，最近的经费稍微有些不足啊……不赚一些发明必要的资金不行了。所以说，不要来买点什么吗？」
	;TranslatedMark 「発明に必要な資金を稼ぎたいんだ。というわけで何か買ってくれない？」
	PRINTFORMW 看来是来卖东西的……
	;TranslatedMark どうやら押し売りに来たようだが……
ENDIF
PRINTFORML 金:{MONEY}
;TranslatedMark 金:{MONEY}

CALL ASK_MULTI_JUDGE("超级宣传君（金10000）", MONEY >= 10000, "广域洗脑装置（金15000）", MONEY >= 15000, "Mr.训练（金20000）", MONEY >= 20000, "不需要", 1)
;TranslatedMark "スーパープロパガンダくん（金10000）", MONEY >= 10000, "広域洗脳装置（金15000）", MONEY >= 15000, "ミスター訓練", MONEY >= 20000, "やめとく", 1

IF RESULT == 3
	PRINTFORML 「诶——？为什么啊——」
	;TranslatedMark 「えー？　なんだよー」
	PRINTFORML 告诉她什么也不想买，%ANAME(対象)%只好不情愿地回去了……
	;TranslatedMark 不要だと伝えると、%ANAME(対象)%は渋々帰っていった……
	RETURN 1
ENDIF

KDVAR:対象:にとり_押し売り = 1
選択肢 = RESULT

PRINTFORML 「欢迎选购！」
;TranslatedMark 「まいどあり！」

SELECTCASE 選択肢
	CASE 0
		PRINTFORML 超级宣传君发挥了效果……
		;TranslatedMark スーパープロパガンダくんが効果を発揮した……
		CALL COLOR_PRINTW("兵数增加了5000", カラー_注意)
		COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += 5000
		MONEY -= 10000
		KDVAR:対象:にとり_押し売り累計額 += 10000
	CASE 1
		PRINTFORML 广域洗脑装置发挥了效果……
		;TranslatedMark 広域洗脳装置が効果を発揮した……
		CALL COLOR_PRINTW("同伴的从属度上升了", カラー_注意)
		FOR LOCAL, 0, CHARANUM
			SIF CFLAG:LOCAL:所属 == CFLAG:MASTER:所属
				CFLAG:LOCAL:従属度 += 500
		NEXT
		MONEY -= 15000
		KDVAR:対象:にとり_押し売り累計額 += 15000
	CASE 2
		PRINTFORML Mr.训练发挥了效果……
		;TranslatedMark ミスター訓練が効果を発揮した……
		CALL COLOR_PRINTW("同伴得到了训练", カラー_注意)
		FOR LOCAL, 0, CHARANUM
			IF CFLAG:LOCAL:所属 == CFLAG:MASTER:所属
				EXP:LOCAL:武闘経験値 += 20
				EXP:LOCAL:防衛経験値 += 20
				EXP:LOCAL:知略経験値 += 20
			ENDIF
			CALL TRAIN_AUTO_ABLUP(LOCAL)
		NEXT
		MONEY -= 20000
		KDVAR:対象:にとり_押し売り累計額 += 20000
ENDSELECT

SIF KDVAR:対象:にとり_押し売り累計額 < 100000
	RETURN 1

PRINTFORML 「谢谢！这样就可以制造新兵器了！」
;TranslatedMark 「ありがとう！　これで新兵器が作れるよ！」
PRINTFORMW %ANAME(対象)%开心地跑掉了……
;TranslatedMark %ANAME(対象)%は嬉しげに立ち去った……
KDVAR:対象:にとり_押し売り = 2

CALL COLOR_PRINTW(@"%ANAME(対象)%的知略上升了5", カラー_注意)
ABL:対象:知略 += 5

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_CAPTURE, NO:対象, "菊一文字压缩机")
IF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%习得了技能＜菊一文字压缩机＞", カラー_注意)
ELSE
	CALL COLOR_PRINTW(@"%ANAME(対象)%的武闘上升了5", カラー_注意)
	ABL:対象:武闘 += 5
	CALL COLOR_PRINTW(@"%ANAME(対象)%的防衛上升了5", カラー_注意)
	ABL:対象:防衛 += 5
ENDIF

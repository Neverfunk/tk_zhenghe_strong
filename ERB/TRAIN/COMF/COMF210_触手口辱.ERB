;触手口辱

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME210
LOCALS:0 = 触手口辱

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE210
;共通部分
CALL COM_ABLE_COMMON(210)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0
;プレイヤーが触手召喚中でないなら不可
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
FOR LOCAL:0, 0, MTAR_NUM
	SIF IS_M_HOLD(MTAR:(LOCAL:0))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM210
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
EXP:(MPLY:0):妖術経験値 += 1
EXP:(MPLY:0):触手経験値 += 1

SOURCE:(MPLY:0):嗜虐 = 500
SOURCE:(MPLY:0):逸脱 = 200
SOURCE:(MPLY:0):触手 = 30

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 180, 50)

;●全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):緊縛経験 += 1
	EXP:(LOCAL:2):触手経験値 += 1

	SOURCE:(LOCAL:2):快Ｍ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 1000 + ABL:(MPLY:0):妖術 * 8) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 500)
	SOURCE:(LOCAL:2):苦痛 = 150 + MIN(ABL:(MPLY:0):妖術, 90) / 2
	SOURCE:(LOCAL:2):触手 = 500  + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 250)
	SOURCE:(LOCAL:2):逸脱 = 500  + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 250)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 0, 180)

	IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド)
		TIMES SOURCE:(LOCAL:2):苦痛, 1.50
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 7

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP210
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP210(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):性行動 += 30

;全てのターゲットについて処理
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	;ソースを退避
	CALL PUTOUT_SOURCE(LOCAL:3)

	EXP:(LOCAL:3):緊縛経験 += 1
	EXP:(LOCAL:3):触手経験値 += 1

	SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 800 + ABL:(LOCAL:2):妖術 * 6) + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 400)
	SOURCE:(LOCAL:3):苦痛 += 80
	SOURCE:(LOCAL:3):触手 += 250 + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 125)
	SOURCE:(LOCAL:3):逸脱 += 150 - (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 75)
	SOURCE:(LOCAL:3):性行動 += 60 + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 30)

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 4

	;退避したソースを加算
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE210(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%的口中被%EQUIP_PLAYER_ANAME(ARG:0)%的触手抽插着
;RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%の口に%EQUIP_PLAYER_ANAME(ARG:0)%の触手を挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP210(ARG:0)
PRINTFORM %EQUIP_PLAYER_ANAME(ARG:0)%命令触手
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%の操る触手
	SELECTCASE RAND:8
	CASE 0
		PRINTFORML 强行侵犯%EQUIP_TARGET_ANAME(ARG:0)%的嘴巴…
		;TranslatedMark が%EQUIP_TARGET_ANAME(ARG:0)%の口を強引に犯している…
	CASE 1
		PRINTFORML 在%EQUIP_TARGET_ANAME(ARG:0)%的喉咙里激烈地抽插着…
		;TranslatedMark が%EQUIP_TARGET_ANAME(ARG:0)%の喉奥を激しく突いている…
	CASE 2
		PRINTFORML 进入%EQUIP_TARGET_ANAME(ARG:0)%的喉咙深处，反复的涂抹着粘液…
		;TranslatedMark が%EQUIP_TARGET_ANAME(ARG:0)%の喉奥へ深く入り込み、粘液を擦り付けている…
	CASE 3
		PRINTFORML 支配着%EQUIP_TARGET_ANAME(ARG:0)%呼吸的节奏…
		;TranslatedMark は%EQUIP_TARGET_ANAME(ARG:0)%の呼吸のリズムを支配している…
	CASE 4
		PRINTFORML 在%EQUIP_TARGET_ANAME(ARG:0)%的口内不断扭动着…
		;TranslatedMark は%EQUIP_TARGET_ANAME(ARG:0)%の口内で触手を波打たせている…
	CASE 5
		PRINTFORML 在%EQUIP_TARGET_ANAME(ARG:0)%的口内跟舌头激烈的缠绕着…
		;TranslatedMark が%EQUIP_TARGET_ANAME(ARG:0)%の口内で舌を絡めとっている…
	CASE 6
		PRINTFORML 从%EQUIP_TARGET_ANAME(ARG:0)%的口中拔出，然后在%EQUIP_TARGET_ANAME(ARG:0)%喘息的时候又激烈的插入了进去…
		;TranslatedMark は%EQUIP_TARGET_ANAME(ARG:0)%の口から一度離れ、間で出来た糸で遊んだ後また突き入れた…
	CASE 7
		PRINTFORML 插入%EQUIP_TARGET_ANAME(ARG:0)%的口内，反复地舔舐着上颚…
		;TranslatedMark が%EQUIP_TARGET_ANAME(ARG:0)%の口内で、上顎を何度も舐め回している…
	ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP210(ARG:0)
PRINTFORMW 从%EQUIP_TARGET_ANAME(ARG:0)%的口中将触手拔了出来
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の口から触手を引き抜いた

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER210(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 125

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"触手Lv{ABL:(ARG:0):触手}", ABL:(ARG:0):触手 * 5)
CALL COM_ORDER_ELEMENT(ARG:0, @"加虐Lv{ABL:(ARG:0):加虐}", ABL:(ARG:0):加虐 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):老実
	CALL COM_ORDER_ELEMENT(ARG:0, "老実", -3)
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "加虐", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE210
LOCAL:1 = 1
FOR LOCAL:0, 0, MTAR_NUM
	IF SEARCH_EQUIP(210, MPLY:0, MTAR:(LOCAL:0)) == -1
		LOCAL:1 = 0
	ENDIF
NEXT

IF MTAR_NUM >= 3
	LOCALS:0 = %ANAME(MTAR:0)%们
	;LOCALS:0 = %ANAME(MTAR:0)%たち
	LOCALS:1 = 全員
ELSEIF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
    ;LOCALS:0 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%	
	LOCALS:1 = 二人
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

;ターゲット全員が既に触手口辱中なら分岐
IF LOCAL:1
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%命令插在%LOCALS:0%嘴里的触手，激烈的在喉咙里前后抽插着…
			;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%の口に入れられた触手を前後させ、激しく喉奥を突い%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %LOCALS:0%对着塞进嘴里的%ANAME(MPLY:0)%的触手，像是狼吞虎咽一样的进行着口交…
			;TranslatedMark %LOCALS:0%は口に突き入れられた%ANAME(MPLY:0)%の触手に、まるでフェラをするかのようにむしゃぶりついている…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用触手进一步侵犯%LOCALS:0%的嘴巴
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、触手で%LOCALS:0%の口をさらに犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%像所说的那样让放在%LOCALS:0%嘴里的触手，激烈地前后抽插着喉咙…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま%LOCALS:0%の口に入れられた触手を前後させ、激しく喉奥を突いた…
	ENDSELECT

ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%强行将触手塞进%LOCALS:0%的嘴里，侵犯了喉咙…
			;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:0%の口に触手を無理やり押し込み、喉奥を犯し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:0%张开嘴，命令%ANAME(MPLY:0)%把触手插进嘴里
			;TranslatedMark %LOCALS:0%は口を開け、%ANAME(MPLY:0)%に触手を口の中に突き込むように命令した
			PRINTFORMW %ANAME(MPLY:0)%如同所说的那样按着%LOCALS:0%的嘴把触手塞进喉咙深处…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま%LOCALS:0%の口に触手を押し込み、喉の奥まで突き入れた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用触手侵犯%LOCALS:0%的嘴
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、触手で%LOCALS:0%の口を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%就这样强行将触手塞进%LOCALS:0%的嘴里，侵犯了喉咙…
			;TranslatedMark %ANAME(MPLY:0)%は言われるまま%LOCALS:0%の口に触手を無理やり押し込み、喉奥を犯した…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN210
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_210()
RETURN POWER(2, 性的嗜好_触手で犯したい) + POWER(2, 性的嗜好_奉仕されたい)

@COM_PREFERENCE_TARGET_210()
RETURN POWER(2, 性的嗜好_触手で犯されたい) + POWER(2, 性的嗜好_奉仕したい)


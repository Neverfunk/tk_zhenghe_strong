;桃源香

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME76
LOCALS:0 = 桃源香

RESULTS:0 = 点燃%LOCALS:0%
;RESULTS:0 = %LOCALS:0%を焚く
RESULTS:1 = 点燃%LOCALS:0%(被动)
;RESULTS:1 = %LOCALS:0%を焚かされる
RESULTS:2 = 点燃%LOCALS:0%(使役)
;RESULTS:2 = %LOCALS:0%を焚かせる
RESULTS:3 = 点燃%LOCALS:0%(被动)
;RESULTS:3 = %LOCALS:0%を焚かれる
RESULTS:4 = 点燃%LOCALS:0%(使役)
;RESULTS:4 = %LOCALS:0%を焚かせる
RESULTS:5 = 点燃%LOCALS:0%(展示)
;RESULTS:5 = %LOCALS:0%使用見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE76
;共通部分
CALL COM_ABLE_COMMON(76)
SIF RESULT == 0
	RETURN 0
	;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
	;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
	;桃源香が必要
SIF ITEM:桃源香 <= 0 && ITEM:A_桃源香 <= 0
	RETURN 0
	;既にターゲットが桃源香を使われているなら不可
SIF TFLAG:69
	RETURN 0
	;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM76
#DIM 対象

;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;媚薬を１つ減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_桃源香 <= 0
	ITEM:桃源香 = MAX(ITEM:桃源香 - 1, 0)
ENDIF


;桃源香使用フラグを立てる
TFLAG:69 = 1

;●プレイヤー側の処理

SOURCE:(MPLY:0):逸脱 = 50


IF MTAR_NUM > 0
	;●ターゲット側の処理

	SOURCE:(MTAR:0):逸脱 = 100

	SELECTCASE ABL:(MTAR:0):欲望
		CASE 0
			SOURCE:(MTAR:0):反感 = 200
			SOURCE:(MTAR:0):不安 = 400
		CASE 1
			SOURCE:(MTAR:0):反感 = 150
			SOURCE:(MTAR:0):不安 = 300
		CASE 2
			SOURCE:(MTAR:0):反感 = 80
			SOURCE:(MTAR:0):不安 = 150
		CASE 3
			SOURCE:(MTAR:0):反感 = 50
			SOURCE:(MTAR:0):不安 = 100
		CASEELSE
			SOURCE:(MTAR:0):反感 = MAX(30 - (ABL:(MTAR:0):欲望 - 5) * 20, 0)
			SOURCE:(MTAR:0):不安 = MAX(50 - (ABL:(MTAR:0):欲望 - 5) * 20, 0)
	ENDSELECT

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 30, 40)

ENDIF

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

;経過時間を無理矢理1減らす
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER76(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE76
IF MTAR_NUM <= 0
	PRINTFORMW %ANAME(MPLY:0)%解开桃源香的封印…
	;TranslatedMark %ANAME(MPLY:0)%は桃源香の封を解い%TARGET_STR("た", "てきた")%…
	RETURN
ENDIF
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%解开桃源香的封印…
		;TranslatedMark %ANAME(MPLY:0)%は桃源香の封を解い%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0)%命令%ANAME(MPLY:0)%使用桃源香
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に桃源香を使うように命令した
		PRINTFORMW %ANAME(MPLY:0)%就这样解开了香炉的封印…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま香炉の封を解いた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%使用桃源香
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、桃源香を使うように命令した
		PRINTFORMW %ANAME(MPLY:0)%就这样解开了香炉的封印…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま香炉の封を解いた…
ENDSELECT

;-------------------------------------------------
;地の文(実行後)
;-------------------------------------------------
@COM_TEXT_AFTER76
IF MTAR_NUM <= 0
	PRINTFORML 四周弥漫着妖艳的香味，将在场者的肉体和思考全都染成桃色…
	;TranslatedMark 辺りに充満する妖艶な香りが、その場に居る者の肉体と思考を桃色に染め上げていく…
	RETURN
ENDIF

IF MTAR:0 != MASTER || CONFIG:441
	IF (TCVAR:(MTAR:0):52) || (TCVAR:(MTAR:0):53 == 1)
		PRINTFORML 不论是否失去意识，充满桃色的香味让%ANAME(MTAR:0)%们的身体像在燃烧一样发热…
		;TranslatedMark 意識を失っているか否かに関わらず、充満する桃色の香りが%ANAME(MTAR:0)%たちの体を熱く火照らせていく…
	ELSE
		SIF TCVAR:(MTAR:0):51
			PRINT 不论疲惫与否，都吸入了香气
			;TranslatedMark 疲れて否が応でも吸ってしまう
		IF TALENT:(MTAR:0):妖術知識 || TALENT:(MTAR:0):技師
			PRINTFORML 当%ANAME(MTAR:0)%注意到周围飘浮着的甜蜜香味时，已经没有一个人能止住身体的疼痛了…
			;TranslatedMark 周囲を漂う甘い香りに%ANAME(MTAR:0)%が気付いた時には、既に身体の疼きを止められる者は誰一人居なかった…
		ELSE
			PRINTFORML 四周弥漫着妖艳的香味，将在场者的肉体和思考全都染成桃色…
			;TranslatedMark 辺りに充満する妖艶な香りが、その場に居る者の肉体と思考を桃色に染め上げていく…
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN76
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_76()
RETURN POWER(2, 性的嗜好_道具を使いたい)

@COM_PREFERENCE_TARGET_76()
RETURN POWER(2, 性的嗜好_道具を使われたい)


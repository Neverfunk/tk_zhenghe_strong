;-------------------------------------------------
;「夜這いする」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME21
RESULTS:0 '= "夜襲"

;-------------------------------------------------
;「夜這いする」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK21
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属
		CONTINUE
	SIF !CFLAG:MASTER:所属 || TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:所属):LOCAL
		GOTO FOUND
NEXT

RETURN 0

$FOUND

SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF CFLAG:MASTER:行動不能状態 == 行動不能_臨月 || COOLTIME:MASTER:0 > 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「夜這いする」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA21(ARG:0)
;隣接している都市がある
IF !CFLAG:MASTER:所属 || TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:所属):(CFLAG:(ARG:0):所属) || CFLAG:(ARG:0):特殊状態 == 特殊状態_放浪
	;非主人公、捕虜ではない、別勢力、どこかの勢力に所属している（放浪、未登場、死亡済みではない）
	{
		IF ARG:0 != MASTER &&
			!CFLAG:(ARG:0):捕虜先 && 
			CFLAG:MASTER:所属 != CFLAG:(ARG:0):所属 && 
			(CFLAG:(ARG:0):所属 || CFLAG:(ARG:0):特殊状態 == 特殊状態_放浪) && 
			!IS_SP_CHARA(ARG:0) && 
			CFLAG:(ARG:0):行動不能状態 != 行動不能_子供
	}
		SIF YOBAI_ABLE(ARG:0)
			RETURN CHECK91(ARG:0, 2, 1)
		RETURN 2
	ENDIF
	RETURN 0
ENDIF
RETURN 0

;-------------------------------------------------
;「夜這いする」の追加チェック
;-------------------------------------------------
@SHOP_LIFE_CHECK_ADDITIONAL_CHECK21()
RETURN !FLAG:クリアフラグ

;-------------------------------------------------
;「夜這いする」の助手の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_SUB21(ARG:0)
;捕虜として捕まっている場合
IF CFLAG:(ARG:0):捕虜先
	;非主人公、通常状態または育児中、合意、主人公勢力の捕虜かつ軟禁中
	RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):行動不能状態, 0, 2) && (TALENT:(ARG:0):合意 || TALENT:(ARG:0):親友) && CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):軟禁中 == 1 && SHOP_LIFE_CHECKCHARA_ZYOSYU(ARG:0) && CFLAG:(ARG:0):行動不能状態 != 行動不能_子供)
ELSE
	;捕虜でない、非主人公、通常状態または育児中、合意か親友ないし所属が特殊勢力なら対応する特殊勢力陥落素質持ち、同一勢力
	RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):行動不能状態, 0, 2) && (TALENT:(ARG:0):合意 || TALENT:(ARG:0):親友 || (IS_SP_COUNTRY(CFLAG:(ARG:0):所属) && GETBIT(TALENT:(ARG:0):特殊勢力陥落系, SP_COUNTRY_TO_CONST(CFLAG:(ARG:0):所属)))) && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):所属 && SHOP_LIFE_CHECKCHARA_ZYOSYU(ARG:0) && CFLAG:(ARG:0):行動不能状態 != 行動不能_子供)
ENDIF

;-------------------------------------------------
;「夜這いする」の陥落/能力判定
;-------------------------------------------------
@YOBAI_ABLE(ARG)
#FUNCTION
LOCAL:1 = YOBAI_ZYOSYU_CALC_POINT(MASTER)
FOR LOCAL,1, CHARANUM
	SIF CFLAG:LOCAL:夜這いの助手
		LOCAL:1 += YOBAI_ZYOSYU_CALC_POINT(LOCAL)
NEXT
RETURNF LOCAL:1 >= YOBAI_TARGET_CALC_POINT(ARG)

;-------------------------------------------------
;「夜這いする」の能力判定用関数
;-------------------------------------------------
@YOBAI_ZYOSYU_CALC_POINT(ARG)
#FUNCTION
RETURNF ABL:ARG:武闘 + ABL:ARG:知略 + FIND_PASSIVE_SKILL(ARG, "职务人员") * 30

;-------------------------------------------------
;「夜這いする」の能力判定用関数
;-------------------------------------------------
@YOBAI_TARGET_CALC_POINT(ARG)
#FUNCTION

SIF GROUPMATCH(1, TALENT:ARG:親愛, TALENT:ARG:恋慕, TALENT:ARG:隷属, TALENT:ARG:服従, TALENT:ARG:主人, TALENT:ARG:所有者)
	RETURNF 0

LOCAL = (ABL:ARG:武闘 + ABL:ARG:知略) + FIND_PASSIVE_SKILL(ARG, "职务人员") * 10

SIF TALENT:(ARG:0):烙印
	RETURNF LOCAL * 2
SIF GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属) == ARG:0
	RETURNF LOCAL * 7 / 2
RETURNF LOCAL * 3


;-------------------------------------------------
;「夜這いする」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY21
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 1
RETURN 0

;-------------------------------------------------
;「夜這いする」の右カラムキャラリスト1のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON21(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, 0, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
RETURN 0

;-------------------------------------------------
;「夜這いする」の右カラムキャラリスト２のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST2_BUTTON21(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):夜這いの助手, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, 0, "BOTTOM")
RETURN 0

;-------------------------------------------------
;「夜這い」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP21(ARG:0)
PRINTFORM P:{YOBAI_TARGET_CALC_POINT(ARG:0), 3, RIGHT}
CALL SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「夜這い」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM21(ARG:0)
PRINTFORM P:{YOBAI_ZYOSYU_CALC_POINT(ARG:0), 3, RIGHT}
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「夜這いする」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW21
CALL COLUMN_RIGHT_TITLE("対象者選択", "0", "1", "1+助手数", "0", "=武闘+知略")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト上とページ移動
;ここで判定前に追加条件として指定されていたTMP_COUNTRY_IS_NEIBORINGは
;SHOP_LIFE_CHECKCHARA内へ移動
CALL COLUMN_RIGHT_CHARALIST_TOP(1)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;助手を数える
LOCAL:1 = 0
LOCAL:2 = YOBAI_ZYOSYU_CALC_POINT(MASTER)
FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうかを判定
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT == 1
		;選択中の人数を数える
		IF CFLAG:(LOCAL:0):夜這いの助手
			LOCAL:1 ++
			LOCAL:2 += YOBAI_ZYOSYU_CALC_POINT(LOCAL)
		ENDIF
	ELSE
		;条件を満たしていなければ強制的にＯＦＦ
		CFLAG:(LOCAL:0):夜這いの助手 = 0
	ENDIF
NEXT

PRINTFORM 助手最多可以选择{MIN(2, CALC_SHOP_TIME() - SHOP_TIME)}人
SIF LOCAL:1 >= MIN(2, CALC_SHOP_TIME() - SHOP_TIME)
	SETCOLOR カラー_オレンジ
PRINTFORM (現在{LOCAL:1}/{MIN(2, CALC_SHOP_TIME() - SHOP_TIME)}人)
RESETCOLOR

PRINTFORM  点数:{LOCAL:2}
PRINTFORM  
PRINTBUTTON "[助手自動選択]", 1
PRINTFORM  
PRINTBUTTON @"[%ZYOSYU_MODE()%]", 2
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト下とページ移動
ソートキー保存 = ソートキー
ソート順保存 = 降順ソート
ソートキー = ソート_夜這い助手
降順ソート = 1
CALL CREATE_SELECTOR_SORT_LIST
CALL COLUMN_RIGHT_CHARALIST_BOTTOM(1)
ソートキー = ソートキー保存
降順ソート = ソート順保存
CALL CREATE_SELECTOR_SORT_LIST
RETURN 0

;-------------------------------------------------
;「夜這いする」の右カラムボタンの入力処理補佐
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB21(ARG:0)
#DIM 候補値, MAX_CHARA_NUM
#DIM 候補, MAX_CHARA_NUM
#DIM 候補数
#DIM 助手数
VARSET 候補値, __INT_MIN__
VARSET 候補, -1
VARSET 候補数
VARSET 助手数
CVARSET CFLAG, GETNUM(CFLAG, "夜這いの助手"), 0

;最適メンバーの決定
IF ARG:0 == 1
	CVARSET CFLAG, GETNUM(CFLAG, "夜這いの助手"), 0
	FOR LOCAL:0, 1, CHARANUM
		;選択可能な条件を満たしているかどうかを判定
		TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
		IF RESULT == 1
			候補:候補数 = LOCAL
			候補値:候補数 = YOBAI_ZYOSYU_CALC_POINT(LOCAL)
			候補数 ++
		ENDIF
	NEXT

	SIF 候補数 == 0
		RETURN 0

	WHILE 1
		LOCAL = FINDELEMENT(候補値, MAXARRAY(候補値), 0, 候補数)
		SIF 候補値:LOCAL == __INT_MIN__ || 候補:LOCAL == -1
			BREAK
		CFLAG:(候補:LOCAL):夜這いの助手 = 1

		候補値:LOCAL = __INT_MIN__
		候補:LOCAL = -1
		助手数 ++
		SIF 助手数 >= MIN(2, CALC_SHOP_TIME() - SHOP_TIME)
			BREAK
	WEND
ELSEIF ARG:0 == 2
	FLAG:助手フィルタ = ROUND_INCREMENT(FLAG:助手フィルタ, 0, 3)
ENDIF
RETURN 0


;-------------------------------------------------
;「夜這いする」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP21(ARG:0)
#DIM 好感度他減少

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0

$SHOW_LOOP_INFO_Y

;対象キャラの情報を表示
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(ARG:0)
CALL SINGLE_DRAWLINE
	PRINTFORML 去襲撃%ANAME(ARG:0)%
	PRINTFORML 確定麼？
CALL ASK_YN
IF RESULT == 0
	CVARSET CFLAG, 6, 0
	FOR LOCAL:0, 0, CHARANUM
		IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):夜這いの助手
			CFLAG:(LOCAL:0):調教参加フラグ = 1
		ENDIF
	NEXT
		;夜襲経験
	EXP:ARG:59 ++
		;調教初期設定
	CALL TRAIN_TARGET_SETTING(ARG:0)
		;メインターゲットを調教対象に設定
	VARSET PRISONER_TARGET, -1
	PRISONER_TARGET:0 = ARG:0
		;初期のウフフフラグをＯＮにする
	FLAG:ウフフフラグ = 1
		;捕虜調教モード
	FLAG:調教モード = 2
		;夜這いフラグ
	FLAG:夜這い = 1
		;行動開始時の共通設定
	;--信仰--触发调教类型0=会见 1=调教俘虏 2=侍寝 3=夜袭
	MIRACLE_LUST_TRAIN_FORM = 3
	;--信仰--玩家制造色欲调教事件变量赋值--
	MIRACLE_LUST_TRAIN_MAIN = MASTER
	MIRACLE_LUST_TRAIN_TARGET:0 = 2 ;人数初始化
	MIRACLE_LUST_TRAIN_TARGET:1 = MASTER ;玩家参与
	MIRACLE_LUST_TRAIN_TARGET:2 = (ARG:0) ;调教参与者记录
	MIRACLE_LUST_PRE_FALL:2 = MIRACLE_FALLCOUNT(ARG:0) ;调教前陷落代码,函数在 祷告_践行教义事件.ERB
	MIRACLE_LUST_PRE_MAKELOVE:2 = TALENT:(ARG:0):合意 ;调教前是否取得对象合意
	MIRACLE_LUST_PRE_LIKE:2 = CFLAG:(ARG:0):好感度 ;调教前对象好感度
	MIRACLE_LUST_PRE_TRUST:2 = CFLAG:(ARG:0):依存度 ;调教前对象依存度
	MIRACLE_LUST_PRE_BELONG:2 = CFLAG:(ARG:0):従属度 ;调教前对象従属度
	MIRACLE_LUST_PRE_CONTROL:2 = CFLAG:(ARG:0):支配度 ;调教前对象支配度
	;--信仰--结束--
	
	CALL START_TRAIN_COMMON

	;陥落していない場合は好感度やら何やらが減ったり関係が悪化したりする
	IF !IS_FALLEN(ARG:0)
		好感度他減少 = LIMIT(RAND(100, 200) - ABL:ARG:欲望 * 20 - ABL:ARG:奉仕 * 20 ,0, 100)
		SIF TALENT:ARG:烙印
			好感度他減少 /= 2
		SIF CFLAG:(ARG:0):好感度 > 0
			CFLAG:(ARG:0):好感度 -= 好感度他減少
		SIF CFLAG:(ARG:0):従属度 > 0
			CFLAG:(ARG:0):従属度 -= 好感度他減少
		SIF CFLAG:(ARG:0):依存度 > 0
			CFLAG:(ARG:0):依存度 -= 好感度他減少
		SIF CFLAG:(ARG:0):支配度 > 0
			CFLAG:(ARG:0):支配度 -= 好感度他減少 / 2

		;相手との関係悪化
		CALL CHANGE_RELATION_O_TO_C(CFLAG:ARG:所属, CFLAG:MASTER:所属, RAND(50, 100) * -1, RAND(50, 100))

		;所属があれば君主からも嫌われる
		IF CFLAG:(ARG:0):所属
			CALL CHANGE_RELATION_O_TO_C(GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属), CFLAG:MASTER:所属, RAND(50, 100) * -1, RAND(50, 100))
				;対象が君主ならおかわり
			SIF GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属) == ARG:0
				CALL CHANGE_RELATION_C_TO_C(CFLAG:ARG:所属, CFLAG:MASTER:所属, RAND(100, 150) * -1, RAND(100, 150))

		;所属がない（放浪中）の場合は全勢力から嫌われる
		ELSE
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				SIF CFLAG:MASTER:所属 == LOCAL
					CONTINUE
				CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -50, 50)
			NEXT
		ENDIF
	ENDIF
	REDRAW 1
	RETURN 1
ENDIF
REDRAW 1

;-------------------------------------------------
;「夜這いする」のリスト２入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_SUB21(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):夜這いの助手
		LOCAL:5 ++
	ENDIF
NEXT
IF CFLAG:(ARG:0):夜這いの助手 || LOCAL:5 < MIN(2, CALC_SHOP_TIME() - SHOP_TIME)
	;夜這い参加フラグを反転
	CFLAG:(ARG:0):夜這いの助手 = !CFLAG:(ARG:0):夜這いの助手
ENDIF
RETURN 0


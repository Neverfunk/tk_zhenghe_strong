;歌唱訓練

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME306
RESULTS:0 = 邀請訓練歌唱
RESULTS:1 = 陪著訓練歌唱

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE306
;共通部分
CALL COM_ABLE_COMMON(306)
SIF RESULT == 0
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;デート中で場所が川辺、花畑、森林以外なら無理
SIF TFLAG:54 && !GROUPMATCH(GET_PLACENAME(TFLAG:54), "川辺", "花畑", "森林")
	RETURN 0
;臨月or怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月 || CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM306
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "歌唱"), 0)

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE306
;主導権が主人公側
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%一起進行了歌唱訓練
ELSE
	PRINTFORMW %ANAME(MPLY:0)%陪著%ANAME(MTAR:0)%進行了歌唱訓練
ENDIF

IF TFLAG:18 == 1
	PRINTFORML 感覺掌握了一點訣竅…
ELSEIF TFLAG:18 == -1
	PRINTFORML ……完全跑調了
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST306
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN306
RETURN コマンド_日常

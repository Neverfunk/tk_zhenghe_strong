;口枷

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME93
LOCALS:0 = 鼻勾

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%装着(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE93
;共通部分
CALL COM_ABLE_COMMON(93)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;鼻フックが必要
SIF !ITEM:鼻勾 && !ITEM:A_鼻勾
	RETURN 0
;ターゲットが既に鼻フック装着中なら不可
SIF IS_EQUIP_TARGET(MTAR:0, 93)
	RETURN 0
SIF !CAN_REACH_BODY(MPLY:0, MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM93
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
SOURCE:(MPLY:0):嗜虐 = 200
SOURCE:(MPLY:0):逸脱 = 80
SOURCE:(MPLY:0):接触 = 20
EXP:(MPLY:0):嗜虐経験値 += 1

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 130, 40)

;●ターゲットについて処理
FOR LOCAL, 0, MTAR_NUM
	LOCAL:2 = MTAR:LOCAL
	EXP:(LOCAL:2):緊縛経験 += 1
	EXP:(LOCAL:2):被虐経験値 += 1

	SOURCE:(LOCAL:2):恐怖 = 40
	SOURCE:(LOCAL:2):不安 = 70
	SOURCE:(LOCAL:2):逸脱 = 150
	SOURCE:(LOCAL:2):接触 = 20

;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 0, 110)
NEXT
;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP93
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP93(ARG:0)
FOR LOCAL, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):LOCAL

	EXP:(LOCAL:3):緊縛経験 += 1
	EXP:(LOCAL:3):被虐経験値 += 1

	SOURCE:(LOCAL:3):恐怖 += 15
	SOURCE:(LOCAL:3):不安 += 30
	SOURCE:(LOCAL:3):逸脱 += 80

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 2
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE93(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%鼻钩装着中
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%が鼻フックを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP93(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%的鼻子被钩子丑陋地扭曲着…
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の鼻はフックにより醜く歪められている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP93(ARG:0)
PRINTFORMW 摘掉了%EQUIP_TARGET_ANAME(ARG:0)%的鼻钩
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の鼻フックを外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER93(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"加虐Lv{ABL:(ARG:0):加虐}", ABL:(ARG:0):加虐 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):老実
	CALL COM_ORDER_ELEMENT(ARG:0, "老実", -3)
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "加虐", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE93
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%给%ANAME(MTAR:0, MTAR_NUM)%戴上了鼻钩…
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0, MTAR_NUM)%に鼻フックを装着し%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %ANAME(MTAR:0, MTAR_NUM)%把鼻钩交给了%ANAME(MPLY:0)%，命令她把鼻钩给%ANAME(MTAR:0, MTAR_NUM)%戴上…
		;TranslatedMark %ANAME(MTAR:0, MTAR_NUM)%は%ANAME(MPLY:0)%に鼻フックを渡すと、それを%ANAME(MTAR:0, MTAR_NUM)%に装着するように命令した…
		PRINTFORML %ANAME(MPLY:0)%听从命令，给%ANAME(MTAR:0, MTAR_NUM)%戴上了鼻钩…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0, MTAR_NUM)%に鼻フックを取り付けた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用鼻钩勾住%ANAME(MTAR:0, MTAR_NUM)%的鼻子
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0, MTAR_NUM)%に鼻フックを付けるように命令した
		PRINTFORML %ANAME(MPLY:0)%听从命令，给%ANAME(MTAR:0, MTAR_NUM)%戴上了鼻钩…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0, MTAR_NUM)%に鼻フックを装着した…
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN93
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_93()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_93()
RETURN POWER(2, 性的嗜好_虐げられたい) + POWER(2, 性的嗜好_縛られたい)


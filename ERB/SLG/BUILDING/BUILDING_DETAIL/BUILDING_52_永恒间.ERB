@BUILDING_GET_NAME_永恒间()
    #FUNCTIONS
    RETURNF "永恒间"

@BUILDING_GET_EXP_永恒间()
    #FUNCTIONS
    RETURNF "提供快乐不再产生角色冷却时间、提供者好感度/从属度上升。"

@BUILDING_SET_INFO_ID_52()
    BUILDING_TYPE:建造物_永恒间 = 建造物_类型_色情类
    BUILDING_LEVEL:建造物_永恒间 = 建造物_等级_二级建筑
    BUILDING_LIMIT_NUM:建造物_永恒间 = 建造物_数量限制_1
    BUILDING_LIMIT_UNI:建造物_永恒间 = 0
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_普通势力 = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_野盗 = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_ホフゴブリン = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_外来人 = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_触手 = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_自警団 = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_サキュバス = 1
    BUILDING_COUNTRY_ID:建造物_永恒间:特殊勢力_狂信者 = 1
    BUILDING_HAVE_SKILL:建造物_永恒间 = 1
    BUILDING_TOHO_REQ_ID:建造物_永恒间:0 = 0

@CHECK_BUILDING_FUNCTION_永恒间(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    RESULT:1 = TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:建造物_永恒间
    IF RESULT:1 > 0
        RESULT:0 = 1
    ENDIF
    RETURNF RESULT:0

@BUILDING_TEXT_永恒间(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F(COLOR_色情建筑)
    PRINTFORM 因为《永恒间》的存在，
    PRINTFORML 性爱的快乐永远刻在所有参与者的身心中
    PRINTFORML 提供快乐不再产生冷却时间了,同时提高提供者的好感与从属度
    RETURNF 1

@BUILDING_SKILL_永恒间(COUNTRY_ID)
    #DIM COUNTRY_ID
    PRINTFORML 发动后选择一个君主已经陷落的势力，使其对我方势力的评价变为1500/0
    PRINTFORML 冷却时间10回合
    PRINTFORML 是否发动
    CALL ASK_YN()
    IF !RESULT
        PRINTFORML 发动了《永恒间》的技能
        CALL BUILDING_SKILL_永恒间_FUNCTION
        IF RESULT
            BUILDING_SKILL_COLDTIME:COUNTRY_ID:建造物_永恒间 = 10
            PRINTFORMW 完毕
        ENDIF
    ENDIF



@BUILDING_SKILL_永恒间_FUNCTION()
    #DIM FIRST_LINE
    FIRST_LINE = LINECOUNT
    CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
    CALL TMP_CREATE_RELATION_MAP
    CALL TMP_CREATE_IS_FREE_MAP
    CALL TMP_CREATE_POLITICS_POWER_MAP
    CALL TMP_CREATE_SUM_SOLDIER_MAP
    CALL SINGLE_DRAWLINE
    PRINTL 请选择要改变评价的势力
    CALL SINGLE_DRAWLINE
    PRINTFORM %"君主",10,LEFT%
    PRINTFORM %TOSTR_SPACE(6)%
    PRINTFORM %"関係",10, RIGHT%
    PRINTFORM %"印象",6, RIGHT%
    PRINTFORM %"友好", 7, RIGHT%
    PRINTFORM %"敵対", 7, RIGHT%
    PRINTFORML
    FOR LOCAL:0, 1, MAX_COUNTRY
        IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:所属 && IS_FALLEN(GET_COUNTRY_BOSS(LOCAL:0))
            LOCALS:0 = %SNAME(GET_COUNTRY_BOSS(LOCAL:0))%
            IF COUNTRY_IS_CLOSED:(LOCAL:0)
                SETCOLOR カラー_選択不可
                PRINTPLAINFORM [%LOCALS:0, MAX_CHARANAME_LENGTH / 2, LEFT%]
                RESETCOLOR
            ELSE
                SETCOLOR COUNTRY_COLOR:(LOCAL:0)
                PRINTBUTTON @"[%LOCALS:0, MAX_CHARANAME_LENGTH / 2, LEFT%]", LOCAL:0
                RESETCOLOR
            ENDIF
            CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:所属, LOCAL:0)
            PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:所属, LOCAL:0, 1), 10, RIGHT%
            RESETCOLOR
            LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
            LOCAL:5 = GET_COUNTRY_BOSS(LOCAL:0)
            PRINT
            CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:4)
            PRINTFORM {REL_LIKE:(LOCAL:5):(LOCAL:4), 7, RIGHT}
            PRINTFORM {REL_HATE:(LOCAL:5):(LOCAL:4), 7, RIGHT}
            RESETCOLOR
            PRINTL
        ENDIF
    NEXT
    CALL SINGLE_DRAWLINE
    PRINTBUTTON "99[取消]", 99
    PRINTL
    REDRAW 0
    INPUT
    IF RESULT == 99
        RETURN 0
    ELSEIF IS_COUNTRY(RESULT) && !COUNTRY_IS_CLOSED:(RESULT) && RESULT != CFLAG:MASTER:所属
        LOCAL = RESULT
        PRINTFORML 改变与%ANAME(GET_COUNTRY_BOSS(LOCAL))%势力的关系？
        CALL ASK_YN()
        IF !RESULT
            REL_LIKE:(GET_COUNTRY_BOSS(LOCAL)):(GET_COUNTRY_BOSS(CFLAG:MASTER:所属)) = 1500
            REL_HATE:(GET_COUNTRY_BOSS(LOCAL)):(GET_COUNTRY_BOSS(CFLAG:MASTER:所属)) = 0
            RETURN 1
        ELSE
            RESTART
        ENDIF
    ELSE
        RESTART
    ENDIF

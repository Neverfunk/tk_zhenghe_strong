;顔騎放屁

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME151
LOCALS:0 = 顔騎放屁

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE151
;共通部分
CALL COM_ABLE_COMMON(151)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人以上
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;屁残量が十分でないなら不可
	SIF TCVAR:(MPLY:(LOCAL:0)):55 < 1000
		RETURN 0
	;Ａが埋まっているなら不可
	SIF IS_A_HOLD(MPLY:(LOCAL:0))
		RETURN 0
	;全員がターゲットに跨っていないと不可
	SIF !IS_RIDE(MPLY:(LOCAL:0), MTAR:0)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM151
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):排泄経験値 += 1

	LOCAL:3 = 300
	LOCAL:4 = 250

	SELECTCASE ABL:(LOCAL:2):排泄
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:2):排泄 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:2):露出 = 1000
	SOURCE:(LOCAL:2):逸脱 = 400
	SOURCE:(LOCAL:2):中毒充足 = LOCAL:3
	SOURCE:(LOCAL:2):反感 = LOCAL:4

	IF IS_INITIATIVE(LOCAL:2)
		SOURCE:(LOCAL:2):嗜虐 = 250
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 150)

	;屁残量を0にする
	TCVAR:(LOCAL:2):55 = 0
NEXT

;主導権の所在を取得
LOCAL:11 = GET_COM_INITIATIVE()

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;ターゲットに主導権
	IF LOCAL:11 == 1 || LOCAL:11 == 2
		SOURCE:(LOCAL:1):嗜虐 = 200
		SOURCE:(LOCAL:1):逸脱 = 300
	;ターゲットに主導権がない
	ELSE
		SOURCE:(LOCAL:1):逸脱 = 600
		SOURCE:(LOCAL:1):恐怖 = MAX(200 - 50 * ABL:(LOCAL:1):欲望, 0)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 150)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP151
RETURN 0

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER151(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 95

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"排泄Lv{ABL:(ARG:0):排泄}", ABL:(ARG:0):排泄 * 7)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -12)
ENDIF
IF TALENT:(ARG:0):不知羞恥
	CALL COM_ORDER_ELEMENT(ARG:0, "不知羞恥", 8)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -6)
ENDIF
IF TALENT:(ARG:0):喜歡引人注目
	CALL COM_ORDER_ELEMENT(ARG:0, "喜歡引人注目", 6)
ENDIF
IF TALENT:(ARG:0):倒錯的
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯的", 8)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE151
IF MPLY_NUM == 1
	LOCALS:1 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:1 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
ELSE
	LOCALS:1 = %ANAME(MPLY:0)%たち
ENDIF

IF MTAR_NUM == 1
	LOCALS:2 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:2 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
ELSE
	LOCALS:2 = %ANAME(MTAR:0)%たち
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF MPLY_NUM == 1 && (MPLY:0 != MASTER || CONFIG:441) && ABL:(MPLY:0):加虐 >= 3
			PRINTFORMW %LOCALS:1%一边将屁股按在%LOCALS:2%的鼻子上一边放着屁…
			;TranslatedMark %LOCALS:1%は%LOCALS:2%の鼻にお尻をグリグリと押し付けながらオナラをし%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %LOCALS:1%在%LOCALS:2%的脸上跨着放屁…
			;TranslatedMark %LOCALS:1%は%LOCALS:2%の顔の上に跨ったままオナラをし%TARGET_STR("た", "てきた")%…
		ENDIF
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:2%在自己的脸上跨着%LOCALS:2%让她放屁…
		;TranslatedMark %LOCALS:2%は自分の顔の上に%LOCALS:2%を跨がらせたままでオナラをさせた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:1%跨上%LOCALS:2%的脸放屁
		;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:1%に対して、%LOCALS:2%の顔に跨ったままオナラをするように命令した
		PRINTFORMW 就像%LOCALS:1%说的那样，一边将屁股压在%LOCALS:2%的脸上一边排出了屁…
		;TranslatedMark %LOCALS:1%は言われた通り、%LOCALS:2%の顔にお尻を押し付けながらオナラをした…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST151
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):排泄 >= 2
			IF IS_INITIATIVE(LOCAL:1) && ABL:(LOCAL:1):加虐 >= 3
				PRINTFORMW %ANAME(LOCAL:1)%沉浸在能让%ANAME(MTAR:0)%闻到屁的味道的优越感中
				;TranslatedMark %ANAME(LOCAL:1)%は%ANAME(MTAR:0)%にオナラを嗅がせる優越感に浸っている
			ELSE
				PRINTFORMW %ANAME(LOCAL:1)%在害羞和解放感中，浮现出兴奋的表情
				;TranslatedMark %ANAME(LOCAL:1)%は恥ずかしさと解放感に、興奮した表情を浮かべている
			ENDIF
		ELSEIF ABL:(LOCAL:1):排泄 >= 1
			PRINTFORMW %ANAME(LOCAL:1)%虽然还是很紧张的样子，但对于放屁的解放感不由得放松了脸
			;TranslatedMark %ANAME(LOCAL:1)%はまだ緊張した様子だが、放屁の解放感に思わず顔を緩めた
		ELSE
			PRINTFORMW 因为太害羞了，所以脸都红了
			;TranslatedMark %ANAME(LOCAL:1)%はあまりの恥ずかしさに顔を真っ赤にしている
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):怒主 + CUP:(LOCAL:1):哀主

	IF ((LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)) && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):欲情 >= 500
			PRINTFORMW %ANAME(LOCAL:1)%闻着放屁的味道，身体兴奋地颤抖着
			;TranslatedMark %ANAME(LOCAL:1)%はオナラの臭いを嗅ぎながら、興奮した様子で体を震わせている
		ELSEIF LOCAL:2 < 0 && CUP:(LOCAL:1):欲情 < 500
			PRINTFORMW %ANAME(LOCAL:1)%因为太臭了，脚扑通扑通扑通地痛苦踢动着
			;TranslatedMark %ANAME(LOCAL:1)%はあまりの臭いに、足をバタつかせて苦しそうにしている
		ELSE
			PRINTFORMW %ANAME(LOCAL:1)%因为太臭了，挣扎着从屁股下面逃走
			;TranslatedMark %ANAME(LOCAL:1)%はあまりの臭いに、尻の下から逃れようと抵抗した
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN151
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_151()
RETURN POWER(2, 性的嗜好_辱められたい) + POWER(2, 性的嗜好_汚されたい)

@COM_PREFERENCE_TARGET_151()
RETURN POWER(2, 性的嗜好_辱めたい)


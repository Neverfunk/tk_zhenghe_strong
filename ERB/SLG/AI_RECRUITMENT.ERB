@SLG_AI_RECRUITMENT(勢力)
#DIM 勢力
#DIM 君主
#DIM 対象
#DIM 候補
#DIM 基礎額
#DIM 評価値, VARSIZE("SCOUT_CANDIDATES")
#DIM 支払額
#DIM 確率
VARSET 評価値, __INT_MIN__

君主 = GET_COUNTRY_BOSS(勢力)


;创建评估值
;只要能用手头的钱支付，基本上都会选择最有能力的家伙，但是讨厌也会影响吧？
FOR 候補, 0, VARSIZE("SCOUT_CANDIDATES")
	対象 = ID_TO_CHARA(SCOUT_CANDIDATES:候補)
	SIF 対象 == -1
		CONTINUE
	IF CFLAG:対象:特殊状態 != 特殊状態_放浪
		SCOUT_CANDIDATES:候補 = 0
		CONTINUE
	ENDIF

	基礎額 = SCOUT_CALC_PRICE_BASIS(対象) * 2 / 3
	基礎額 = SCOUT_CALC_PRICE_TECHNOLOGY_CORRECTION(基礎額, 勢力)
	SIF DAY % 5 == 0
		基礎額 /= 2

	;基本的に値段と同一
	評価値:候補 = 基礎額

	;君主からみたそいつへの関係性を考慮
	評価値:候補 *= 1500 + REL_LIKE:君主:対象 - REL_HATE:君主:対象
	評価値:候補 /= 1500

	;金が足りなければ評価値は低い（確実に雇用できるキャラをより評価する）
	SIF MONEY:勢力 < 基礎額
		評価値:候補 = 評価値:候補 * MONEY:勢力 / 基礎額

	評価値:候補 = 評価値:候補 * RAND(80, 120) / 100
NEXT

;每回合最多尝试3人
FOR LOCAL, 0, 3
	候補 = FINDELEMENT(評価値, MAXARRAY(評価値))
	対象 = ID_TO_CHARA(SCOUT_CANDIDATES:候補)

	SIF 対象 == -1
		BREAK

	支払額 = MIN(MONEY:勢力, 評価値)

	基礎額 = SCOUT_CALC_PRICE_BASIS(対象) * 2 / 3
	基礎額 = SCOUT_CALC_PRICE_TECHNOLOGY_CORRECTION(基礎額, 勢力)

	SIF DAY % 5 == 0
		基礎額 /= 2

	SIF IS_SP_COUNTRY(勢力)
		TIMES 基礎額, 1.5

	;半額なきゃ諦める
	IF 支払額 < 基礎額 / 2
		評価値:候補 = __INT_MIN__
		CONTINUE
	ENDIF
	
	IF 基礎額 == 0
		確率 = 100 * 支払額
	ELSE
		確率 = 100 * 支払額 / 基礎額
		;原版没有判断只有上面这一行，不确定有没有问题
	ENDIF
	確率 = 確率 * (1500 + REL_LIKE:対象:君主 - REL_HATE:対象:君主 - DIPLOMACY_HATE:勢力 * 30) / 1500
	;如果是特殊势力
	IF IS_SP_COUNTRY(勢力)
		PRINTFORML %ANAME(君主)%委託奴隷商人、想要找到%ANAME(対象)%……
		;通常の1/6 ~ 1/2の確率
		IF RAND:100 < 確率 / 7 - SP_COUNTRY_RANK:SP_COUNTRY_TO_CONST(勢力)
			CALL COLOR_PRINTL(@"%ANAME(対象)%中了奴隸商人的策略", カラー_ピンク)
			CALL COLOR_PRINTL(@"%ANAME(対象)%被%ANAME(君主)%捕獲了", カラー_ピンク)
			CALL CAPTURE(対象, 勢力, 1)
			SCOUT_CANDIDATES:候補 = 0
			;跳转信仰事件
			TRYCALL SHOP_LIFE_USERSHOP68_BOSSRECRUITMENT(君主,対象)
		ELSE
			評価値:候補 = __INT_MIN__
		ENDIF
	ELSE
		PRINTFORML %ANAME(君主)%嘗試雇用流浪中的%ANAME(対象)%……
		IF RAND:100 < 確率 
			CALL COLOR_PRINTL(@"%ANAME(対象)%接受了雇用", カラー_注意)
			CALL COLOR_PRINTL(@"%ANAME(対象)%移籍進了%ANAME(君主)%勢力", カラー_注意)
			CALL CHANGE_COUNTRY(対象, 勢力, 1)
			SCOUT_CANDIDATES:候補 = 0
			;跳转信仰事件
			TRYCALL SHOP_LIFE_USERSHOP68_BOSSRECRUITMENT(君主,対象)
		ELSE
			評価値:候補 = __INT_MIN__
		ENDIF
	ENDIF
NEXT


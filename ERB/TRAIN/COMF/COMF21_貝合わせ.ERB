;貝合わせ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME21
LOCALS:0 = 磨鏡

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE21
;共通部分
CALL COM_ABLE_COMMON(21)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーとターゲットが共に行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
SIF !HAS_VAGINA(MPLY:0) || !HAS_VAGINA(MTAR:0)
	RETURN 0
SIF IS_RIDE(MPLY:0) || IS_RIDE(MTAR:0)
	RETURN 0
;grovelling
SIF IS_EQUIP_PLAYER(MPLY:0, 110) || IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
;プレイヤーとターゲットが共に拘束中なら不可
SIF IS_BIND(MPLY:0) && IS_BIND(MTAR:0)
	RETURN 0
;Todo - vibrators and balloons should perhaps be exempted
SIF IS_V_HOLD(MPLY:0) || IS_V_HOLD(MTAR:0)
	RETURN 0
;either is fucking or fucked by anyone
SIF IS_INSERT_SINGLE(MPLY:0, "全") || IS_INSERT_SINGLE(MTAR:0, "全")
	RETURN 0
SIF REACHING_BODY(MPLY, MTAR) || REACHING_BODY(MTAR, MPLY)
	RETURN 0
SIF LICKING_GROIN(MPLY, MTAR) || LICKING_GROIN(MTAR, MPLY)
	RETURN 0
SIF LICKING_BODY(MPLY, MTAR) || LICKING_BODY(MTAR, MPLY)
	RETURN 0
;giving/receiving assjob, trampling, footjob
SIF SEARCH_EQUIP_IC_M(MPLY:0, MTAR:0, 14, 15, 103) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM21
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●潤滑による各種倍率の計算
LOCAL:10 = 0
IF PALAM:(MPLY:0):潤滑 < PALAMLV:1
	LOCAL:10 += 10
ELSEIF PALAM:(MPLY:0):潤滑 < PALAMLV:3
	LOCAL:10 += 20
ELSEIF PALAM:(MPLY:0):潤滑 < PALAMLV:5
	LOCAL:10 += 30
ELSEIF PALAM:(MPLY:0):潤滑 < PALAMLV:7
	LOCAL:10 += 40
ELSE
	LOCAL:10 += 50
ENDIF
IF PALAM:(MTAR:0):潤滑 < PALAMLV:1
	LOCAL:10 += 10
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:3
	LOCAL:10 += 20
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:5
	LOCAL:10 += 30
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:7
	LOCAL:10 += 40
ELSE
	LOCAL:10 += 50
ENDIF

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 120

EXP:(MPLY:0):性技経験値 += 1

SOURCE:(MPLY:0):快Ｃ = SENSE_HOUSHI(MTAR:0, MPLY:0, 1600) * LOCAL:10 / 100
SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 100)
SOURCE:(MPLY:0):接触 = 120
SOURCE:(MPLY:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 40, 40)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 120

EXP:(MTAR:0):性技経験値 += 1

SOURCE:(MTAR:0):快Ｃ = SENSE_HOUSHI(MPLY:0, MTAR:0, 1600) * LOCAL:10 / 100
SOURCE:(MTAR:0):奉仕 = SERVE_HOUSHI(MTAR:0, 100)
SOURCE:(MTAR:0):接触 = 120
SOURCE:(MTAR:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 40, 40)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP21
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP21(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●潤滑による各種倍率の計算
LOCAL:10 = 0
IF PALAM:(LOCAL:2):潤滑 < PALAMLV:1
	LOCAL:10 += 10
ELSEIF PALAM:(LOCAL:2):潤滑 < PALAMLV:3
	LOCAL:10 += 20
ELSEIF PALAM:(LOCAL:2):潤滑 < PALAMLV:5
	LOCAL:10 += 30
ELSEIF PALAM:(LOCAL:2):潤滑 < PALAMLV:7
	LOCAL:10 += 40
ELSE
	LOCAL:10 += 50
ENDIF
IF PALAM:(LOCAL:3):潤滑 < PALAMLV:1
	LOCAL:10 += 10
ELSEIF PALAM:(LOCAL:3):潤滑 < PALAMLV:3
	LOCAL:10 += 20
ELSEIF PALAM:(LOCAL:3):潤滑 < PALAMLV:5
	LOCAL:10 += 30
ELSEIF PALAM:(LOCAL:3):潤滑 < PALAMLV:7
	LOCAL:10 += 40
ELSE
	LOCAL:10 += 50
ENDIF

;●プレイヤーについて処理
DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1

SOURCE:(LOCAL:2):快Ｃ += SENSE_HOUSHI(LOCAL:3, LOCAL:2, 800) * LOCAL:10 / 100
SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 50)
SOURCE:(LOCAL:2):接触 += 60
SOURCE:(LOCAL:2):性行動 += 100

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1


;●ターゲットについて処理
DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1

SOURCE:(LOCAL:3):快Ｃ = SENSE_HOUSHI(LOCAL:2, LOCAL:3, 800) * LOCAL:10 / 100
SOURCE:(LOCAL:3):奉仕 = SERVE_HOUSHI(LOCAL:3, 50)
SOURCE:(LOCAL:3):接触 = 60
SOURCE:(LOCAL:3):性行動 += 100

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE21(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%和%EQUIP_TARGET_ANAME(ARG:0)%磨镜中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP21(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%和%EQUIP_TARGET_ANAME(ARG:0)%用秘唇摩擦着…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%と%EQUIP_TARGET_ANAME(ARG:0)%は秘唇をクチュクチュと擦り合わせている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP21(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER21(ARG:0)
CALL COM_ORDER_COMMON21(ARG:0)
RETURN 1

@COM_ORDER_TARGET21(ARG:0)
CALL COM_ORDER_COMMON21(ARG:0)
RETURN 1

@COM_ORDER_COMMON21(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"Ｃ感Lv{ABL:(ARG:0):Ｃ感}", ABL:(ARG:0):Ｃ感 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"百合Lv{ABL:(ARG:0):百合}", ABL:(ARG:0):百合 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -4)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -3)
ENDIF
IF TALENT:(ARG:0):討厭女人
	CALL COM_ORDER_ELEMENT(ARG:0, "討厭女人", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE21
IF IS_PIERCED(MPLY:0, ピアス_ラビア) && IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = 上著環的
ENDIF
IF GET_INMOU_LEVEL(MPLY:0) >= 1 && GET_INMOU_LEVEL(MTAR:0) >= 1
	LOCALS:1 = 毛发旺盛的
;ふさふさと毛の繁る
ENDIF
;継続中
IF SEARCH_EQUIP_IC(21, MPLY:0, MTAR:0) >= 0
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%将腰用力地压在%ANAME(MTAR:0)%身上，然后摇动着腰责弄%ANAME(MTAR:0)%…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に強く腰を押し付けると、さらに腰を揺り動かして%ANAME(MTAR:0)%を責め%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%一边用脚缠着%ANAME(MPLY:0)%，一边要求%ANAME(MPLY:0)%更加激烈地摆动着腰
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に足を絡めながら、もっと激しく腰を振るよう%ANAME(MPLY:0)%に要求した
			PRINTFORMW %ANAME(MPLY:0)%把腰压在%ANAME(MTAR:0)%身上，拼命地摩擦着%LOCALS:0%%LOCALS:1%们的秘唇来侍奉%ANAME(MTAR:0)%…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に腰を押し付け直すと、必死に%LOCALS:0%%LOCALS:1%秘唇同士を擦り合わせて%ANAME(MTAR:0)%に侍奉した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%和%ANAME(MTAR:0)%，更激烈地扭动腰部，互相纠缠
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MTAR:0)%に、もっと激しく腰を動かして絡み合うように命令した
			PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%按照命令更加激烈地扭动腰部，摩擦了%LOCALS:0%%LOCALS:1%彼此的秘唇…
			;TranslatedMark %ANAME(MPLY:0)%と%ANAME(MTAR:0)%は言われるままさらに腰を動かし、%LOCALS:0%%LOCALS:1%お互いの秘唇を擦り合わせた…
	ENDSELECT

ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%将%ANAME(MTAR:0)%和%LOCALS:0%%LOCALS:1%秘唇重叠后，一边将腰压上去，一边用自己的秘部摩擦起%ANAME(MTAR:0)%的秘唇…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と%LOCALS:0%%LOCALS:1%秘唇を重ね合わせると、腰を押し付けながら自身の秘部で%ANAME(MTAR:0)%のそれを擦り上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%要求%ANAME(MPLY:0)%为进行磨镜侍奉
			;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に貝合わせ侍奉するように要求した
			PRINTFORMW %ANAME(MPLY:0)%在催促下将%ANAME(MTAR:0)%和%LOCALS:0%%LOCALS:1%秘唇重叠，摇晃着腰使秘部相互摩擦…
			;TranslatedMark %ANAME(MPLY:0)%は促されるままに%ANAME(MTAR:0)%と%LOCALS:0%%LOCALS:1%秘唇を重ね合わせると、腰を揺すって秘部同士を擦り合わせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用磨镜的方式和%ANAME(MTAR:0)%纠缠在一起
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、貝合わせで%ANAME(MTAR:0)%と絡み合うように命令した
			PRINTFORMW %ANAME(MPLY:0)%将%ANAME(MTAR:0)%和%LOCALS:0%%LOCALS:1%秘唇重叠后，一边将腰压上去，一边用自己的秘部摩擦%ANAME(MTAR:0)%的秘唇…
			;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と%LOCALS:0%%LOCALS:1%秘唇を重ね合わせると、腰を押し付けながら自身の秘部で%ANAME(MTAR:0)%のそれを擦り上げた…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN21
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_21()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_21()
RETURN POWER(2, 性的嗜好_奉仕されたい)


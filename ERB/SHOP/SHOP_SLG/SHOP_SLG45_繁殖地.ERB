

;-------------------------------------------------
;「繁殖地」の名称
;-------------------------------------------------
@SHOP_SLG_NAME45
RESULTS:0 '= "繁殖地"

;-------------------------------------------------
;「繁殖地」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK45
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
SIF CHANGE_Government_TENTACLE:0 > 0 && CFLAG:MASTER:所属 == CHANGE_Government_TENTACLE:0 && CHANGE_Government_TENTACLE:1 == 2
	RETURN 1
SIF CFLAG:MASTER:所属 != GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_触手)
	RETURN 0
RETURN 1

;-------------------------------------------------
;「繁殖地」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY45
CALL Breeding_AREA_TENTACLE
RETURN 1

;-------------------------------------------------
;「繁殖地」本体
;-------------------------------------------------
@Breeding_AREA_TENTACLE
#DIM 危機レベル上昇可能, 6
#DIM 共感レベル上昇可能, 6
#DIM 表示内容
#DIM 連邦ナンバー
#DIM 再誕の子宮選択可能フラグ
#DIM FIRST_LINE

再誕の子宮選択可能フラグ = 0
FOR LOCAL, 0, CHARANUM
	IF 吸収中苗床:LOCAL:0 == 2
		再誕の子宮選択可能フラグ = 1
		BREAK
	ENDIF
NEXT

FIRST_LINE = LINECOUNT
連邦ナンバー = COUNTRY_Union_REFERENCE:(CFLAG:MASTER:所属):0

CALL SINGLE_DRAWLINE
PRINTL ★繁殖地
PRINTL 　管理内政
CALL SINGLE_DRAWLINE

PRINTFORML 威胁点数［ {TENTACLE_MENACE_POINT} ］　　心之碎片(親友・恋慕的数量)［ {TENTACLE_SYMPATHY_POINT:0} ］
PRINTFORML 

PRINTFORM ［危机等级］　　

FOR LOCAL, 1, 6
	IF TENTACLE_MENACE_LV == LOCAL - 1 && TENTACLE_MENACE_POINT >= 5 * LOCAL * LOCAL * LOCAL && TENTACLE_SYMPATHY_LV < 5
		SETCOLOR カラー_選択中
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 10
		RESETCOLOR
		危機レベル上昇可能:LOCAL = 1
	ELSEIF TENTACLE_MENACE_LV >= LOCAL
		SETCOLOR カラー_緑
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 10
		RESETCOLOR
		危機レベル上昇可能:LOCAL = 0
	ELSEIF TENTACLE_SYMPATHY_LV >= 5
		SETCOLOR カラー_選択不可
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 20
		RESETCOLOR
		危機レベル上昇可能:LOCAL = 0
	ELSE
		SETCOLOR カラー_選択不可
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 10
		RESETCOLOR
		危機レベル上昇可能:LOCAL = 0
	ENDIF
	IF LOCAL == 5
		PRINTL
	ELSEIF TENTACLE_MENACE_LV >= LOCAL
		SETCOLOR カラー_緑
		PRINT 　→　
		RESETCOLOR
	ELSE
		PRINT 　→　
	ENDIF
NEXT

PRINTL
PRINTFORM ［共感等级］　　

FOR LOCAL, 1, 5
	IF TENTACLE_SYMPATHY_LV == LOCAL - 1 && TENTACLE_SYMPATHY_POINT:0 >= 3 + (TENTACLE_MENACE_LV * 2) && TENTACLE_MENACE_LV < 5
		SETCOLOR カラー_選択中
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 20
		RESETCOLOR
		共感レベル上昇可能:LOCAL = 1
	ELSEIF TENTACLE_SYMPATHY_LV >= LOCAL
		SETCOLOR カラー_緑
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 20
		RESETCOLOR
		共感レベル上昇可能:LOCAL = 0
	ELSEIF TENTACLE_MENACE_LV >= 5
		SETCOLOR カラー_選択不可
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 20
		RESETCOLOR
		共感レベル上昇可能:LOCAL = 0
	ELSE
		SETCOLOR カラー_選択不可
		PRINTBUTTON @"[等级{LOCAL}]", LOCAL + 20
		RESETCOLOR
		共感レベル上昇可能:LOCAL = 0
	ENDIF
	IF TENTACLE_SYMPATHY_LV >= LOCAL
		SETCOLOR カラー_緑
		PRINT 　→　
		RESETCOLOR
	ELSE
		PRINT 　→　
	ENDIF
NEXT

IF TENTACLE_SYMPATHY_LV == 4 && TENTACLE_SYMPATHY_POINT:1 >= 1 && TENTACLE_MENACE_LV < 5
	SETCOLOR カラー_選択中
	PRINTBUTTON "[等级5]", 25
	RESETCOLOR
	共感レベル上昇可能:5 = 1
ELSEIF TENTACLE_SYMPATHY_LV >= 5
	SETCOLOR カラー_緑
	PRINTBUTTON "[等级5]", 25
	RESETCOLOR
	共感レベル上昇可能:5 = 0
ELSE
	共感レベル上昇可能:5 = 0
ENDIF
PRINTL
PRINTL
CALL SINGLE_DRAWLINE

SELECTCASE 表示内容
	CASE 11
		PRINTFORML 危机等级1
		PRINTFORML 效果：「触手・信标」可以使用
		PRINTFORML 　　　可以将触手能量转化为威胁点数(需要：拥有从属国或永久同盟)
		IF 危機レベル上昇可能:1 == 1
			PRINTBUTTON "[等级提升]", 111
			PRINTFORML 　花费威胁点数 5 
		ELSEIF TENTACLE_MENACE_LV >= 1
			PRINTFORML 已达成
		ELSEIF TENTACLE_SYMPATHY_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：威胁点数 5
		ENDIF
	CASE 12
		PRINTFORML 危机等级2
		PRINTFORML 效果：苗床的效率提升
		PRINTFORML 
		IF 危機レベル上昇可能:2 == 1
			PRINTBUTTON "[等级提升]", 112
			PRINTFORML 　花费威胁点数 40 
		ELSEIF TENTACLE_MENACE_LV >= 2
			PRINTFORML 已达成
		ELSEIF TENTACLE_SYMPATHY_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：威胁点数 40
		ENDIF
	CASE 13
		PRINTFORML 危机等级3
		PRINTFORML 效果：「触手・戴森炮」可以使用
		PRINTFORML 　　　可以确认所有都市・所有部隊的情报
		IF 危機レベル上昇可能:3 == 1
			PRINTBUTTON "[等级提升]", 113
			PRINTFORML 　花费威胁点数 135 
		ELSEIF TENTACLE_MENACE_LV >= 3
			PRINTFORML 已达成
		ELSEIF TENTACLE_SYMPATHY_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：威胁点数 135
		ENDIF
	CASE 14
		PRINTFORML 危机等级4
		PRINTFORML 效果：可以从孕妇身上提取「生命之素」・可以将「生命之素」转换为威胁点数
		PRINTFORML 　　　所有都市的经济力可以转化为兵力
		IF 危機レベル上昇可能:4 == 1
			PRINTBUTTON "[等级提升]", 114
			PRINTFORML 　花费威胁点数 320 
		ELSEIF TENTACLE_MENACE_LV >= 4
			PRINTFORML 已达成
		ELSEIF TENTACLE_SYMPATHY_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：威胁点数 320
		ENDIF
	CASE 15
		PRINTFORML 危机等级5
		PRINTFORML 效果：「类星触陨」可以使用・所属的触手兵学会新技能
		PRINTFORML 　　　共感等级不能提升了
		IF 危機レベル上昇可能:5 == 1
			PRINTBUTTON "[等级提升]", 115
			PRINTFORML 　花费威胁点数 625 
		ELSEIF TENTACLE_MENACE_LV >= 5
			PRINTFORML 已达成
		ELSEIF TENTACLE_SYMPATHY_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：威胁点数 625
		ENDIF
	CASE 21
		PRINTFORML 共感等级1
		PRINTFORML 效果：战斗中的技能发动率+10％
		PRINTFORML 
		IF 共感レベル上昇可能:1 == 1
			PRINTBUTTON "[等级提升]", 121
			PRINTFORML 　花费心之碎片 {3+(TENTACLE_MENACE_LV * 2)} 
		ELSEIF TENTACLE_SYMPATHY_LV >= 1
			PRINTFORML 已达成
		ELSEIF TENTACLE_MENACE_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：心之碎片 {3+(TENTACLE_MENACE_LV * 2)}
		ENDIF
	CASE 22
		PRINTFORML 共感等级2
		PRINTFORML 效果：部队的攻击力+10％
		PRINTFORML 
		IF 共感レベル上昇可能:2 == 1
			PRINTBUTTON "[等级提升]", 122
			PRINTFORML 　花费心之碎片 {3+(TENTACLE_MENACE_LV * 2)} 
		ELSEIF TENTACLE_SYMPATHY_LV >= 2
			PRINTFORML 已达成
		ELSEIF TENTACLE_MENACE_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：心之碎片 {3+(TENTACLE_MENACE_LV * 2)}
		ENDIF
	CASE 23
		PRINTFORML 共感等级3
		PRINTFORML 效果：部队的防御力+10％
		PRINTFORML
		IF 共感レベル上昇可能:3 == 1
			PRINTBUTTON "[等级提升]", 123
			PRINTFORML 　花费心之碎片 {3+(TENTACLE_MENACE_LV * 2)} 
		ELSEIF TENTACLE_SYMPATHY_LV >= 3
			PRINTFORML 已达成
		ELSEIF TENTACLE_MENACE_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：心之碎片 {3+(TENTACLE_MENACE_LV * 2)}
		ENDIF
	CASE 24
		PRINTFORML 共感等级4
		PRINTFORML 效果：防御倍率+0.2
		PRINTFORML
		IF 共感レベル上昇可能:4 == 1
			PRINTBUTTON "[等级提升]", 124
			PRINTFORML 　花费心之碎片 {3+(TENTACLE_MENACE_LV * 2)} 
		ELSEIF TENTACLE_SYMPATHY_LV >= 4
			PRINTFORML 已达成
		ELSEIF TENTACLE_MENACE_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：心之碎片 {3+(TENTACLE_MENACE_LV * 2)}
		ENDIF
	CASE 25
		PRINTFORML 共感等级5
		PRINTFORML 效果：共感等级1～4的效果倍增
		SETCOLOR カラー_警告
		PRINTFORML 　　　不再是特殊势力・一部分设施无法使用
		RESETCOLOR
		IF 共感レベル上昇可能:5 == 1
			PRINTBUTTON "[等级提升]", 125
			PRINTFORML 
		ELSEIF TENTACLE_SYMPATHY_LV >= 5
			PRINTFORML 已达成
		ELSEIF TENTACLE_MENACE_LV >= 5
			PRINTFORML 不能继续提升
		ELSE
			PRINTFORML 　需要：親愛
		ENDIF
	CASEELSE
		PRINTL
		PRINTL
		PRINTL
		PRINTL
ENDSELECT

CALL SINGLE_DRAWLINE

PRINTBUTTON "[1] 苗床的管理", 1
PRINTL 
IF TENTACLE_MENACE_LV >= 1 && 連邦ナンバー >= 1
	PRINTBUTTON "[2] 转换触手能量", 2
	PRINTL 
ENDIF
IF TENTACLE_MENACE_LV >= 4 && TENTACLE_SYMPATHY_LV < 5
	PRINTBUTTON "[3] 提取生命之素", 3
	PRINTL 
	PRINTBUTTON "[4] 转换生命之素", 4
	PRINTL 
	PRINTBUTTON "[5] 让城市居民成为触手兵", 5
	PRINTL 
ENDIF
IF TENTACLE_MENACE_LV >= 3 && TENTACLE_SYMPATHY_LV < 5
	PRINTBUTTON "[6] 将苗床吸收进触手中", 6
	PRINTL 
ENDIF

IF 再誕の子宮選択可能フラグ == 1
	PRINTBUTTON "[7] 再诞的子宫", 7
	PRINTL 
ENDIF

PRINTBUTTON "[999] 取消", 999
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW 1
	RETURN
	
ELSEIF RESULT == 1
	CALL Breeding_AREA_TENTACLE_01
ELSEIF RESULT == 2 && TENTACLE_MENACE_LV >= 1 && 連邦ナンバー >= 1
	CALL Breeding_AREA_TENTACLE_02
ELSEIF RESULT == 3 && TENTACLE_MENACE_LV >= 4 && TENTACLE_SYMPATHY_LV < 5
	CALL Breeding_AREA_TENTACLE_03
ELSEIF RESULT == 4 && TENTACLE_MENACE_LV >= 4 && TENTACLE_SYMPATHY_LV < 5
	CALL Breeding_AREA_TENTACLE_04
ELSEIF RESULT == 5 && TENTACLE_MENACE_LV >= 4 && TENTACLE_SYMPATHY_LV < 5
	CALL Breeding_AREA_TENTACLE_05
ELSEIF RESULT == 6 && TENTACLE_MENACE_LV >= 3 && TENTACLE_SYMPATHY_LV < 5
	CALL Breeding_AREA_TENTACLE_10_absorbent
ELSEIF RESULT == 7 && 再誕の子宮選択可能フラグ == 1
	CALL Breeding_AREA_TENTACLE_11_womb
ELSEIF RESULT == 111 && 危機レベル上昇可能:1 == 1
	TENTACLE_MENACE_LV = 1
	TENTACLE_MENACE_POINT -= 5
	
	;ミッション達成
	IF ACHIEVEMENT_MISSION:58 == 1
		ACHIEVEMENT_MISSION:58 = 2
		SETCOLOR カラー_注意
		PRINTL 任务完成!
		RESETCOLOR
		WAIT
	ENDIF
	
ELSEIF RESULT == 112 && 危機レベル上昇可能:2 == 1
	TENTACLE_MENACE_LV = 2
	TENTACLE_MENACE_POINT -= 40
	
	;ミッション達成
	IF ACHIEVEMENT_MISSION:58 == 1
		ACHIEVEMENT_MISSION:58 = 2
		SETCOLOR カラー_注意
		PRINTL 任务完成!
		RESETCOLOR
		WAIT
	ENDIF
	
ELSEIF RESULT == 113 && 危機レベル上昇可能:3 == 1
	TENTACLE_MENACE_LV = 3
	TENTACLE_MENACE_POINT -= 135
	
	;ミッション達成
	IF ACHIEVEMENT_MISSION:58 == 1
		ACHIEVEMENT_MISSION:58 = 2
		SETCOLOR カラー_注意
		PRINTL 任务完成!
		RESETCOLOR
		WAIT
	ENDIF
	
ELSEIF RESULT == 114 && 危機レベル上昇可能:4 == 1
	TENTACLE_MENACE_LV = 4
	TENTACLE_MENACE_POINT -= 320
	
	;ミッション達成
	IF ACHIEVEMENT_MISSION:58 == 1
		ACHIEVEMENT_MISSION:58 = 2
		SETCOLOR カラー_注意
		PRINTL 任务完成!
		RESETCOLOR
		WAIT
	ENDIF
	
ELSEIF RESULT == 115 && 危機レベル上昇可能:5 == 1
	TENTACLE_MENACE_LV = 5
	TENTACLE_MENACE_POINT -= 625
	
	;ミッション達成
	IF ACHIEVEMENT_MISSION:58 == 1
		ACHIEVEMENT_MISSION:58 = 2
		SETCOLOR カラー_注意
		PRINTL 任务完成!
		RESETCOLOR
		WAIT
	ENDIF
	
	FOR LOCAL, 0, CHARANUM
		IF TALENT:LOCAL:特殊勢力素質 && CFLAG:LOCAL:所属 == CFLAG:MASTER:所属
			CALL SKILL_LEARN_BY_NAME(LOCAL, スキル_ジャンル_TROOP, 10201, "City・Eater")
			SIF RESULT
				CALL COLOR_PRINTW(@"%ANAME(LOCAL)%学会了技能＜City・Eater＞", カラー_注意)
		ENDIF
	NEXT
	CHANGE_Government_TENTACLE:1 = 1
	
ELSEIF RESULT == 121 && 共感レベル上昇可能:1 == 1
	TENTACLE_SYMPATHY_LV = 1
	TENTACLE_SYMPATHY_POINT:0 -= (3 + (TENTACLE_MENACE_LV * 2))
ELSEIF RESULT == 122 && 共感レベル上昇可能:2 == 1
	TENTACLE_SYMPATHY_LV = 2
	TENTACLE_SYMPATHY_POINT:0 -= (3 + (TENTACLE_MENACE_LV * 2))
ELSEIF RESULT == 123 && 共感レベル上昇可能:3 == 1
	TENTACLE_SYMPATHY_LV = 3
	TENTACLE_SYMPATHY_POINT:0 -= (3 + (TENTACLE_MENACE_LV * 2))
ELSEIF RESULT == 124 && 共感レベル上昇可能:4 == 1
	TENTACLE_SYMPATHY_LV = 4
	TENTACLE_SYMPATHY_POINT:0 -= (3 + (TENTACLE_MENACE_LV * 2))
ELSEIF RESULT == 125 && 共感レベル上昇可能:5 == 1
	TENTACLE_SYMPATHY_LV = 5
	CALL Breeding_AREA_TENTACLE_125
ELSEIF (RESULT >= 11 && RESULT <= 15) || (RESULT >= 21 && RESULT <= 25)
	表示内容 = RESULT
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


;-------------------------------------------------
;「繁殖地」苗床を管理する
;-------------------------------------------------
@Breeding_AREA_TENTACLE_01
#DIM 苗床
#DIMS 名前
#DIM FIRST_LINE
#DIM 残り陥落ターン
#DIM 情報表示キャラ
FIRST_LINE = LINECOUNT

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0

CALL SINGLE_DRAWLINE
PRINTL 苗床的管理
CALL SINGLE_DRAWLINE

FOR 苗床, 0, CHARANUM
	CALL CHANGE_TENTACLE_subject(苗床)
	IF 触手勢力に監禁:苗床:0 >= 1
		残り陥落ターン = TRAIN_FALLEN_TERM:3 - (CFLAG:苗床:所属 == CFLAG:MASTER:所属)
		IF STRLENS(ANAME(苗床)) >= MAX_CHARANAME_LENGTH
			名前 = %SNAME(苗床)%
		ELSE
			名前 = %ANAME(苗床)%
		ENDIF
		CALL PRINTBUTTON_CENTER(@"[%名前, MAX_CHARANAME_LENGTH, LEFT%]", 苗床, 0)
		CALL PRINT_SEX(苗床, 1, 0, 2)
		PRINT  
		PRINTBUTTON "[能力]", 苗床 + 2000
		PRINT  
		SELECTCASE 触手勢力に監禁:苗床:0
			CASE 1
				PRINTFORM [ 待　　机 ]
			CASE 2
				PRINT [
				SETCOLOR カラー_緑
				PRINTFORM  苗　　床 
				RESETCOLOR
				PRINT ]
		ENDSELECT
		IF GETBIT(TALENT:苗床:特殊勢力陥落系, 特殊勢力_触手)
			SETCOLOR カラー_選択中
			PRINTFORML 　已陷落
		ELSEIF SP_TRAIN_COUNT:苗床:特殊勢力_触手 >= 残り陥落ターン - 1
			PRINTFORML 　马上陷落
		ELSEIF SP_TRAIN_COUNT:苗床:特殊勢力_触手 >= 残り陥落ターン - 2
			SETCOLOR カラー_選択不可
			PRINTFORML 　即将陷落
		ELSE
			PRINTL
		ENDIF 
	ENDIF
	RESETCOLOR
NEXT
PRINTL

CALL SINGLE_DRAWLINE
PRINTBUTTON "[ 600][统一　运用中止]", 600
PRINTL 
PRINTBUTTON "[ 700][统一　运用开始]", 700
PRINTL 
PRINTBUTTON "[1000][　　　取消]", 1000
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 1000
	REDRAW 1
	RETURN
ELSEIF RESULT == 600
	FOR 苗床, 0, CHARANUM
		IF 触手勢力に監禁:苗床:0 == 2
			触手勢力に監禁:苗床:0 = 1
		ENDIF
	NEXT
ELSEIF RESULT == 700
	FOR 苗床, 0, CHARANUM
		IF 触手勢力に監禁:苗床:0 == 1
			触手勢力に監禁:苗床:0 = 2
		ENDIF
	NEXT
ELSEIF RESULT >= 2000
	情報表示キャラ = RESULT - 2000
	;対象キャラの情報を表示
	CALL SINGLE_DRAWLINE
	CALL SHOW_INFO(情報表示キャラ)
	CALL SINGLE_DRAWLINE
	WAIT
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF 触手勢力に監禁:RESULT:0 == 1
	触手勢力に監禁:RESULT:0 = 2
ELSEIF 触手勢力に監禁:RESULT:0 == 2
	触手勢力に監禁:RESULT:0 = 1
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


;-------------------------------------------------
;「繁殖地」触手エネルギーを変換する
;-------------------------------------------------
@Breeding_AREA_TENTACLE_02
#DIM 連邦ナンバー
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT
連邦ナンバー = COUNTRY_Union_REFERENCE:(CFLAG:MASTER:所属):0

CALL SINGLE_DRAWLINE
PRINTL 将触手能量转化为威胁点数(比例 触手能量:威胁点数 = 1:5)
CALL SINGLE_DRAWLINE
PRINTL 
PRINTL 请输入转化的触手能量(输入0不会变更)
PRINTFORML 触手能量［ {RESEARCH_UNIVERSITY_SP_POINT:連邦ナンバー:3} ］ 威胁点数［ {TENTACLE_MENACE_POINT} ］
CALL SINGLE_DRAWLINE
PRINTBUTTON "[     0]", 0
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT <= RESEARCH_UNIVERSITY_SP_POINT:連邦ナンバー:3
	TENTACLE_MENACE_POINT += (RESULT * 5)
	RESEARCH_UNIVERSITY_SP_POINT:連邦ナンバー:3 -= RESULT
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE:0
RESTART

;-------------------------------------------------
;「繁殖地」生命の素を取り出す
;-------------------------------------------------
@Breeding_AREA_TENTACLE_03
#DIM 苗床
#DIMS 名前
#DIM 取り出し元
#DIM 加工元識別ナンバー
#DIM 一括取出可能フラグ
#DIM 一括対象, 999
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
一括取出可能フラグ = 0

CALL SINGLE_DRAWLINE
PRINTL 　请选择要提取生命之素的苗床
PRINTL 　（会解除妊娠状态）
CALL SINGLE_DRAWLINE

FOR 苗床, 0, CHARANUM
	IF 触手勢力に監禁:苗床:0 >= 1 && CFLAG:苗床:妊娠期間 >= 1
		IF STRLENS(ANAME(苗床)) >= MAX_CHARANAME_LENGTH
			名前 = %SNAME(苗床)%
		ELSE
			名前 = %ANAME(苗床)%
		ENDIF
		CALL PRINTBUTTON_CENTER(@"[%名前, MAX_CHARANAME_LENGTH, LEFT%]", 苗床, 0)
		一括対象:苗床 = 1
		一括取出可能フラグ = 1
		PRINTL 
	ENDIF
NEXT
PRINTL

CALL SINGLE_DRAWLINE
IF 一括取出可能フラグ == 1
	PRINTBUTTON "1001[统一提取]", 1001
	PRINTL 
ENDIF
PRINTBUTTON "1000[取　　消]", 1000
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 1000
	REDRAW 1
	RETURN
	
ELSEIF RESULT == 1001 && 一括取出可能フラグ == 1
	PRINTFORML 是否要提取所有苗床的生命之素？
	
	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 0
		FOR LOCAL:0, 0, CHARANUM
			IF 触手勢力に監禁:(LOCAL:0):0 >= 1 && CFLAG:(LOCAL:0):妊娠期間 >= 1
				;空いている加工元識別ナンバーを検索・割り当て
				FOR LOCAL:1, 0, 999
					IF IS_GOODS_EXECUTED:(LOCAL:1) == 0
						加工元識別ナンバー = LOCAL:1
						BREAK
					ENDIF
				NEXT
				
				IS_GOODS_EXECUTED:加工元識別ナンバー = 1
				NAME_BEFORE_GOODS_EXECUTED:加工元識別ナンバー = LOCAL:0
				
				NAME_AFTER_GOODS_EXECUTED:加工元識別ナンバー:7 = 从「%ANAME(LOCAL:0)%」身上提取的生命之素
				TYPE_GOODS_EXECUTED:加工元識別ナンバー:7:0 = 1
				HAVE_GOODS_EXECUTED:7 += 1
				NAME_FATHER_FETUS:加工元識別ナンバー = CFLAG:(LOCAL:0):子の父親
				
				CFLAG:(LOCAL:0):行動不能状態 = 0
				CFLAG:(LOCAL:0):妊娠期間 = 0
				CFLAG:(LOCAL:0):子の父親 = 0
				TALENT:(LOCAL:0):妊娠 = 0
				CALL MOTHER_SHRINK_BUSTSIZE((LOCAL:0))
				CALL MOTHER_STOP_MILKING((LOCAL:0))
				
				PRINTFORML 从%ANAME(LOCAL:0)%处获得了★記念品[生命之素]
				一括対象:(LOCAL:0) = 0
				
			ENDIF
		NEXT
		WAIT
	ENDIF
	
ELSEIF 触手勢力に監禁:RESULT:0 >= 1 && CFLAG:RESULT:妊娠期間 >= 1
	取り出し元 = RESULT
	PRINTFORML 要从%ANAME(取り出し元)%处提取生命之素吗？
	
	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 0
		;空いている加工元識別ナンバーを検索・割り当て
		FOR LOCAL:0, 0, 999
			IF IS_GOODS_EXECUTED:(LOCAL:0) == 0
				加工元識別ナンバー = LOCAL:0
				BREAK
			ENDIF
		NEXT
		
		IS_GOODS_EXECUTED:加工元識別ナンバー = 1
		NAME_BEFORE_GOODS_EXECUTED:加工元識別ナンバー = 取り出し元
		
		NAME_AFTER_GOODS_EXECUTED:加工元識別ナンバー:7 = 从「%ANAME(取り出し元)%」身上提取的生命之素
		TYPE_GOODS_EXECUTED:加工元識別ナンバー:7:0 = 1
		HAVE_GOODS_EXECUTED:7 += 1
		NAME_FATHER_FETUS:加工元識別ナンバー = CFLAG:取り出し元:子の父親
		
		CFLAG:取り出し元:行動不能状態 = 0
		CFLAG:取り出し元:妊娠期間 = 0
		CFLAG:取り出し元:子の父親 = 0
		TALENT:取り出し元:妊娠 = 0
		CALL MOTHER_SHRINK_BUSTSIZE(取り出し元)
		CALL MOTHER_STOP_MILKING(取り出し元)
		
		PRINTFORMW 从%ANAME(取り出し元)%处获得了★記念品[生命之素]
		
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


;-------------------------------------------------
;「繁殖地」生命の素を変換する
;-------------------------------------------------
@Breeding_AREA_TENTACLE_04
#DIM 加工元識別ナンバー
#DIM 一括取出可能フラグ
#DIM 一括対象, 999
#DIM FATHER
#DIM 父親ID
#DIM 選択番号
#DIMS 父親名
#DIM 識別ナンバーカウント
#DIM 種類
#DIM 増加ポイント
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
一括取出可能フラグ = 0

CALL SINGLE_DRAWLINE
PRINTL 　将选择的生命之素转化为威胁点数(比例 生命之素:威胁点数 = 1:5)
PRINTFORML 　　威胁点数[ {TENTACLE_MENACE_POINT} ]
CALL SINGLE_DRAWLINE

FOR 加工元識別ナンバー, 0, 999
	IF TYPE_GOODS_EXECUTED:加工元識別ナンバー:7:0 > 0 || TYPE_GOODS_EXECUTED:加工元識別ナンバー:7:1 > 0
		PRINTBUTTON @"{加工元識別ナンバー}[%NAME_AFTER_GOODS_EXECUTED:加工元識別ナンバー:7%]", 加工元識別ナンバー
		
		一括取出可能フラグ = 1
		一括対象:加工元識別ナンバー = 1
		
		父親ID = NAME_FATHER_FETUS:加工元識別ナンバー
		FATHER = ID_TO_CHARA(父親ID)
		IF FATHER == -1
			父親名 = 不明
		ELSE
			父親名 = %ANAME(FATHER)%
		ENDIF
		SIF 父親名 == ""
			父親名 = 不明
		PRINTFORM 　父:%父親名%
		PRINTL 
	ENDIF
NEXT
PRINTL 

CALL SINGLE_DRAWLINE
IF 一括取出可能フラグ == 1
	PRINTBUTTON "1001[统一转化]", 1001
	PRINTL 
ENDIF
PRINTBUTTON "1000[取　　消]", 1000
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 1000
	REDRAW 1
	RETURN
	
ELSEIF RESULT == 1001 && 一括取出可能フラグ == 1
	PRINTFORML 要将生命之素全部转化吗？
	
	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 0
		増加ポイント = 0
		FOR LOCAL:0, 0, 999
			IF 一括対象:(LOCAL:0) == 1
				
				NAME_BEFORE_GOODS_EXECUTED:(LOCAL:0) = 0
				NAME_AFTER_GOODS_EXECUTED:(LOCAL:0):7 = ""
				TYPE_GOODS_EXECUTED:(LOCAL:0):7:0 = 0
				TYPE_GOODS_EXECUTED:(LOCAL:0):7:1 = 0
				HAVE_GOODS_EXECUTED:7 = MAX(HAVE_GOODS_EXECUTED:7 - 1, 0)
				NAME_FATHER_FETUS:(LOCAL:0) = 0
				Incubate_FETUS:(LOCAL:0) = 0
				
				識別ナンバーカウント = 0
				FOR 種類, 1, 7
					IF TYPE_GOODS_EXECUTED:(LOCAL:0):種類:0 == 1 || TYPE_GOODS_EXECUTED:(LOCAL:0):種類:1 == 1
						識別ナンバーカウント ++
					ENDIF
				NEXT
				IF TYPE_GOODS_EXECUTED:(LOCAL:0):8:0 == 1 || TYPE_GOODS_EXECUTED:(LOCAL:0):8:1 == 1
					識別ナンバーカウント ++
				ENDIF
				IF 識別ナンバーカウント == 0
					IS_GOODS_EXECUTED:(LOCAL:0) = 0
				ENDIF
				
				一括対象:(LOCAL:0) = 0
				増加ポイント += 5
				
			ENDIF
			
		NEXT
		
		PRINTFORMW メナスポイント[ {TENTACLE_MENACE_POINT} ] → [ {TENTACLE_MENACE_POINT + 増加ポイント} ]
		TENTACLE_MENACE_POINT += 増加ポイント
		
	ENDIF
	
ELSEIF TYPE_GOODS_EXECUTED:RESULT:7:0 > 0 || TYPE_GOODS_EXECUTED:RESULT:7:1 > 0
	選択番号 = RESULT
	PRINTFORML [%NAME_AFTER_GOODS_EXECUTED:RESULT:7%]
	PRINTFORML 将这个生命之素转化为威胁点数吗？
	
	;はい／いいえ入力処理
	CALL ASK_YN()
	IF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
	
	NAME_BEFORE_GOODS_EXECUTED:選択番号 = 0
	NAME_AFTER_GOODS_EXECUTED:選択番号:7 = ""
	TYPE_GOODS_EXECUTED:選択番号:7:0 = 0
	TYPE_GOODS_EXECUTED:選択番号:7:1 = 0
	HAVE_GOODS_EXECUTED:7 = MAX(HAVE_GOODS_EXECUTED:7 - 1, 0)
	NAME_FATHER_FETUS:選択番号 = 0
	Incubate_FETUS:選択番号 = 0
	
	識別ナンバーカウント = 0
	FOR 種類, 1, 7
		IF TYPE_GOODS_EXECUTED:選択番号:種類:0 == 1 || TYPE_GOODS_EXECUTED:選択番号:種類:1 == 1
			識別ナンバーカウント ++
		ENDIF
	NEXT
	IF TYPE_GOODS_EXECUTED:選択番号:8:0 == 1 || TYPE_GOODS_EXECUTED:選択番号:8:1 == 1
		識別ナンバーカウント ++
	ENDIF
	IF 識別ナンバーカウント == 0
		IS_GOODS_EXECUTED:選択番号 = 0
	ENDIF
	
	PRINTFORMW 威胁点数 [ {TENTACLE_MENACE_POINT} ] → [ {TENTACLE_MENACE_POINT + 5} ]
	TENTACLE_MENACE_POINT += 5
	
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


;-------------------------------------------------
;「繁殖地」都市住民を触手兵にする
;-------------------------------------------------
@Breeding_AREA_TENTACLE_05
#DIM 表示都市
#DIM 都市
#DIM 都市所持勢力
#DIM 変換経済力
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 将本国城市的经济力转化为兵力(比例 经济力:兵力 = 1:100)　　兵力［ {COUNTRY_SOLDIER:(CFLAG:MASTER:所属)} ］

CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

IF 表示都市 > 0
	PRINTL 请输入想要转化的经济力(上限999)(输入0取消)
	PRINTFORML %GET_RELAYNAME(表示都市)% 的经济力［{CITY_ECONOMY:表示都市 / 100}］
	PRINTL 
ELSE
	PRINTL 
	PRINTL 
	PRINTL 
ENDIF

IF 表示都市 > 0
	PRINTFORML [{CITY_ECONOMY:表示都市 / 100}]
ENDIF
PRINTBUTTON " 0[取消]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	CLEARLINE LINECOUNT - FIRST_LINE
	RETURN
ENDIF

IF !INRANGE(RESULT, 0, 1000 + MAX_CITY - 1)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

IF INRANGE(RESULT, 0, 999)
	IF 表示都市 > 0
		変換経済力 = RESULT * 100
		IF CITY_OWNER:表示都市 == CFLAG:MASTER:所属 && CITY_ECONOMY:表示都市 >= 変換経済力
			CITY_ECONOMY:表示都市 -= 変換経済力
			COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += 変換経済力
			
			;ミッション達成
			IF ACHIEVEMENT_MISSION:59 == 1
				ACHIEVEMENT_MISSION:59 = 2
				SETCOLOR カラー_注意
				PRINTL 任务完成!
				RESETCOLOR
				WAIT
			ENDIF
			
		ELSEIF CITY_ECONOMY:表示都市 < 変換経済力
			PRINTW 超越了我们现在的经济力
		ENDIF
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

都市 = RESULT - 1000
都市所持勢力 = CITY_OWNER:都市

IF CITY_TYPE:都市 == 1 || GET_RELAYNAME(都市) == "無名"
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

IF 都市所持勢力 != CFLAG:MASTER:所属
	PRINTFORMW 请选择自己国家的城市
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

表示都市 = 都市

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


;-------------------------------------------------
;「繁殖地」共感レベル5
;-------------------------------------------------
@Breeding_AREA_TENTACLE_125


SETCOLOR カラー_注意
PRINTFORML 触手们已经理解了人类的心灵，了解了社会的存在方式
PRINTFORML 不再是只靠本能进行繁殖的生物了
PRINTFORML 

SETCOLOR カラー_警告
PRINTFORML 触手势力不再是特殊势力了
IF TENTACLE_MENACE_LV >= 4
	PRINTFORML 「提取生命之素」不能再使用了
	PRINTFORML 「转换生命之素」不能再使用了
	PRINTFORML 「让城市居民成为触手兵」不能再使用了
ENDIF
PRINTFORML 
PRINTFORMW 
RESETCOLOR

COUNTRY_EVENT_ID:(CFLAG:MASTER:所属) = 0
FOR LOCAL, 0, CHARANUM
	IF TALENT:LOCAL:特殊勢力素質 && CFLAG:LOCAL:所属 == CFLAG:MASTER:所属
		TALENT:LOCAL:特殊勢力素質 = 0
		非特殊触手:LOCAL:0 = 1
		CALL COLOR_PRINTL(@"%ANAME(LOCAL)%获得了自我！", カラー_注意)
		TALENT:LOCAL:服従 = 0
		TALENT:LOCAL:合意 = 0
		CFLAG:LOCAL:従属度 = 0
		CFLAG:LOCAL:依存度 = 100
		
		FOR LOCAL:1, 0, 5
			IF SKILL_NO_SLOT:LOCAL:0:(LOCAL:1) == 10004
				SKILL_NO_SLOT:LOCAL:0:(LOCAL:1) = -1
				SKILL_ID_SLOT:LOCAL:0:(LOCAL:1) = -1
				CALL COLOR_PRINTL(@"%ANAME(LOCAL)%遗忘了技能＜触手産卵＞！", カラー_注意)
			ENDIF
		NEXT
		CALL SKILL_LEARN_BY_NAME(LOCAL, スキル_ジャンル_TROOP, 10202, "相互理解的交响曲")
		SIF RESULT
			CALL COLOR_PRINTW(@"%ANAME(LOCAL)%学会了＜相互理解的交响曲＞", カラー_注意)
		PRINTFORML 
	ENDIF
NEXT
CHANGE_Government_TENTACLE:1 = 2


;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K27_LUCKY_NORMAL_RATE(対象)
#DIM 対象
RETURN 75

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K27_LUCKY_NORMAL_DECISION(対象)
#DIM 対象

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("鈴仙"), NAME_TO_CHARA("永琳"), NAME_TO_CHARA("輝夜"))
	RETURN 0

SIF KDVAR:対象:てゐ_ラッキー == -1
	RETURN 0

SIF MONEY <= 500
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 0, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K27_LUCKY_NORMAL_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K27_LUCKY_NORMAL(対象)
#DIM 対象
#DIM 代金

代金 = MIN(MONEY / 5, RAND(500, 1000))


IF KDVAR:対象:てゐ_ラッキー == 0
	PRINTFORMW 糟糕了……
	;TranslatedMark しまった……
	PRINTFORMW 在迷途竹林散步的時候迷路了
	;TranslatedMark 迷いの竹林を歩いているうちに、どうやら道に迷ってしまったようだ
	PRINTFORMW 曾経聽聞過一旦在這裏迷失就出不去了。怎麼辦好呢……
	;TranslatedMark ここは一度迷ってしまうとそうそう出られないと聞く。どうしたものか……
	;(画像パッチ)
	CALL 画像表示(対象, 1, 1)
	PRINTFORMW 「咦？　你在做什麼」
	;TranslatedMark 「あれ？　どしたのあんた」
	PRINTFORMW 在走投無路的時候遇到了妖怪兔
	;TranslatedMark 途方にくれていたところ、妖怪兎に出くわした
	PRINTFORMW 雖然看上去沒有敵意……
	;TranslatedMark こちらに敵意はないようだが……
	PRINTFORMW 「哈哈、看来是迷路了吧」
	;TranslatedMark 「ははぁ、さては迷子になったわけだ」
	CALL ICPRINT(@"「運気不錯呢、這位<%NAME_FORMAL(対象)%>大人、能夠安全無事地送閣下到出口」", "W", カラー_注意)
	;TranslatedMark 「運が良かったね、この<%NAME_FORMAL(対象)%>様が、無事に出口に送り届けてやろうじゃないか」
	PRINTFORMW 看来她熟悉附近一帯的地理。真是絶境逢生
	;TranslatedMark どうやらこのあたりの地理に詳しいらしい。地獄に仏とはこのことだ
	PRINTFORMW ……但是、少女咯咯笑的樣子、有種無法信任的感覚……
	;TranslatedMark ……それにしても、少女はにこにこと笑っているが、どこか信用ならないように感じる……
	;(画像パッチ)
	CALL 画像表示(対象, 1, 2)
	PRINTFORMW 「……所以？　你出多少？」
	;TranslatedMark 「……で？　いくら出してくれんの？」
	PRINTFORMW 「真是的、你不会覚得我会免費幇你帯路吧？」
	;TranslatedMark 「いやだなぁ、まさかタダで道案内してもらおうなんて思ってないよね？」
	CALL ICPRINT(@"「対呢、就收你<{代金}>金吧、如何？」", "W", カラー_注意)
	;TranslatedMark 「そうだなぁ、金<{代金}>くらいで受けてあげるよ、どう？」
	PRINTFORMW ……原来如此、是這麼一回事啊
	;TranslatedMark ……なるほど、そういうことか
	PRINTFORMW 対%PRONOUN(対象)%来説、現在是坑我錢的最好機会吧
	;TranslatedMark %PRONOUN(対象)%からすると、今の自分は絶好の金づるなのだろう
	PRINTFORMW 話雖如此、沒有人帯路的話的確難以離開這裏
	;TranslatedMark とはいえ、案内してもらわないと出られないのも確かだ
ELSE
	;(画像パッチ)
	CALL 画像表示(対象, 1, 1)
	PRINTFORMW 「噢？　你又迷路了？」
	;TranslatedMark 「およ？　あんたまた迷ったの？」
	PRINTFORMW ……沒錯
	;TranslatedMark ……そのとおりだ
	PRINTFORMW 又在竹林裏迷路、然後又遇到%ANAME(対象)%了……
	;TranslatedMark また竹林で迷い、また%ANAME(対象)%と出くわした……
	;(画像パッチ)
	CALL 画像表示(対象, 1, 2)
	PRINTFORMW 「那麼、我再幇你帯路吧」
	;TranslatedMark 「んじゃ、また案内してあげましょうかね」
	CALL ICPRINT(@"「対呢、就收你<{代金}>金吧、如何？」", "W", カラー_注意)
	;TranslatedMark 「そうだなぁ、金<{代金}>くらいで受けてあげるよ、どう？」
ENDIF
PRINTFORMW 那麼、要怎麼辦呢……
;TranslatedMark さて、どうするか……
PRINTFORML
CALL ASK_YN("付錢", "靠自己力量離開")
;TranslatedMark "払う", "自力でなんとかする"

IF RESULT == 0
	PRINTFORML 沒有辦法了……
	;TranslatedMark 背に腹は代えられない……
	PRINTFORMW 只能乖乖付錢吧
	;TranslatedMark おとなしく払うことにした
	;(画像パッチ)
	CALL 画像表示(対象, 1, 2)
	PRINTFORMW 「哼哼哼、谢谢惠顾」
	;TranslatedMark 「ふふふ、まいどあり」
	PRINTFORMW 「那麼我来帯路。跟着我吧」
	;TranslatedMark 「んじゃ案内したげるよ。ついてきてね」
	PRINTFORMW 在%ANAME(対象)%的幇助下、成功到達了出口……
	;TranslatedMark %ANAME(対象)%のおかげで、無事に出口まで辿りつけた……
	CALL COLOR_PRINTW(@"支付了{代金}金", カラー_警告)
	;TranslatedMark 金{代金}を支払いました
	MONEY -= 代金
	CFLAG:対象:好感度 += 100
	KDVAR:対象:てゐ_ラッキー ++
ELSE
	;(画像パッチ)
	CALL 画像表示(対象, 4, 2)
	PRINTFORMW 「哼ー？　雖然我沒有所謂」
	;TranslatedMark 「ふーん？　まぁそれならそれでいいけど」
	PRINTFORMW 「那麼、加油吧ー」
	;TranslatedMark 「そんじゃま、頑張ってねー」
	PRINTFORMW %ANAME(対象)%快步離開了……
	;TranslatedMark %ANAME(対象)%はさっさと立ち去ってしまった……
	CALL ADD_COOLTIME(MASTER, 1)
	KDVAR:対象:てゐ_ラッキー = -1
ENDIF

RETURN 1

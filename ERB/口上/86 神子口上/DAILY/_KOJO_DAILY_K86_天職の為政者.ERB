;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K86_NATURAL_BORN_ADMINISTRATOR_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K86_NATURAL_BORN_ADMINISTRATOR_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:神子_天職の為政者
	RETURN 0

SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) != 対象
	RETURN 0

SIF GET_OWN_CITY(CFLAG:対象:所属) != 1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K86_NATURAL_BORN_ADMINISTRATOR_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K86_NATURAL_BORN_ADMINISTRATOR(対象)
#DIM 対象
PRINTFORMW 「……嗯，不愧是在外面横行霸道的乡间，用普通的办法不可以吗」
;TranslatedMark 「……ふむ、さすがに人外の跋扈する郷、一筋縄ではいかぬか」
PRINTFORMW 望着势力图，%ANAME(対象)%自言自语着
;TranslatedMark 勢力図を眺めながら、%ANAME(対象)%はひとりごちる
PRINTFORMW 战况对%PRONOUN(対象)%来说相当糟糕，再过一会儿就要被压垮了
;TranslatedMark 戦況は%PRONOUN(対象)%にとってかなりまずく、あと一息で押しつぶされそうだ
PRINTFORMW 但是，%ANAME(対象)%却露出了无敌的笑容
;TranslatedMark だが、%ANAME(対象)%は不敵な笑みを浮かべていた
PRINTFORMW 「有趣。如果不是那样的话，就没有干劲了」
;TranslatedMark 「面白い。そうでなくては、張り合いがないものな」
PRINTFORMW 「啊，我也一样，要拿出真本事了」
;TranslatedMark 「どれ、私も一つ、本気を出してやるとしよう」
PRINTFORMW 「我的政治手腕是胜利还是……胜负吧，幻想乡？」
;TranslatedMark 「私の政の腕が勝つか、それとも……勝負といこうか、幻想郷よ？」

CALL COLOR_PRINTW(@"%ANAME(対象)%拿出了真本事", カラー_注意)
CALL COLOR_PRINTW(@"%ANAME(対象)%的能力上昇了", カラー_注意)

ABL:対象:武闘 += 10
ABL:対象:防衛 += 5
ABL:対象:知略 += 5
TALENT:対象:妖術知識 = 1
ABL:対象:妖術 += 5

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_TROOP, NO:対象, "小打小闹就到此为止了！")
SIF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%がスキル＜小打小闹就到此为止了！＞を習得しました", カラー_注意)

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_PERSONAL, NO:対象, "东方的影响力")
SIF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%がスキル＜东方的影响力＞を習得しました", カラー_注意)

COUNTRY_SOLDIER:(CFLAG:対象:所属) += 10000

KDVAR:対象:神子_天職の為政者 = 1

RETURN 1


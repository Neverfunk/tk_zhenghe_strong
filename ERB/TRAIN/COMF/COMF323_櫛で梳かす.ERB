;櫛で梳かす

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME323
RESULTS:0 = 用梳子梳理頭髮
RESULTS:1 = 被要求用梳子梳理頭髮
RESULTS:2 = 要求用梳子梳理頭髮
RESULTS:3 = 被用梳子梳理頭髮

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE323
;共通部分
CALL COM_ABLE_COMMON(323)
SIF RESULT == 0
	RETURN 0
SIF !ITEM:梳子
	RETURN 0
;主人公以外が実行する場合、好感度が500以上必要
SIF MPLY:0 != MASTER && CFLAG:(MPLY:0):2 < 500
	RETURN 0
;怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM323
;実行できるかの判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MTAR:0, 5, 10)
	LOCAL:1 = MTAR:0
ELSE
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MPLY:0, 5, 10)
	LOCAL:1 = MPLY:0
ENDIF

;固定で入るソース
SOURCE:(MPLY:0):接触 = 50
SOURCE:(MTAR:0):接触 = 50

;親密に応じた歓楽のソース追加
SOURCE:(MPLY:0):歓楽 = -10
SOURCE:(MTAR:0):歓楽 = -10
CALL ADD_SOURCE_KANRAKU(MPLY:0, 140)
CALL ADD_SOURCE_KANRAKU(MTAR:0, 140)
SOURCE:(MPLY:0):歓楽 = MAX(SOURCE:(MPLY:0):歓楽, 0)
SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

;親密に応じた愛情のソース追加
CALL ADD_SOURCE_AIZYOU(MPLY:0, 120)
CALL ADD_SOURCE_AIZYOU(MTAR:0, 120)

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 70, 70)
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 70, 70)

;失敗
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):歓楽, 0.20
	TIMES SOURCE:(MPLY:0):愛情, 0.20
	TIMES SOURCE:(MTAR:0):歓楽, 0.20
	TIMES SOURCE:(MTAR:0):愛情, 0.20
	SOURCE:(MPLY:0):不満 += 500
	SOURCE:(MTAR:0):不満 += 500
	TFLAG:37 -= 5
;成功
ELSEIF TFLAG:18 == 0

;大成功
ELSE
	TIMES SOURCE:(MPLY:0):歓楽, 2.00
	TIMES SOURCE:(MPLY:0):愛情, 2.00
	TIMES SOURCE:(MTAR:0):歓楽, 2.00
	TIMES SOURCE:(MTAR:0):愛情, 2.00
	TFLAG:37 += 5
ENDIF

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER323(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 25

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)

;好感度
IF CFLAG:(ARG:0):2 < 400
	IF CFLAG:(ARG:0):2 < 50
		LOCAL:0 = -40
	ELSEIF CFLAG:(ARG:0):2 < 150
		LOCAL:0 = -20
	ELSEIF CFLAG:(ARG:0):2 < 300
		LOCAL:0 = -10
	ELSE
		LOCAL:0 = -5
	ENDIF
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度不足", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 75
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):母性
	CALL COM_ORDER_ELEMENT(ARG:0, "母性", 3)
ENDIF
IF TFLAG:54
	CALL COM_ORDER_ELEMENT(ARG:0, "约会中", 3)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE323
;プレイヤーが主人公側
IF MPLY:0 == MASTER
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORMW %ANAME(MPLY:0)%用梳子梳着%ANAME(MTAR:0)%的头发
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の髪を櫛で梳かした
	;主導権が相手側
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%被%ANAME(MTAR:0)%拜托，用梳子梳头发
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に頼まれて、櫛で髪を梳かした
	ENDIF

	IF TFLAG:18 == 1
		PRINTFORML %ANAME(MTAR:0)%闭上眼睛看起来很舒服…
		;TranslatedMark %ANAME(MTAR:0)%は目を閉じて気持ちよさそうにしている…
	ELSEIF TFLAG:18 == 0
		PRINTFORML %ANAME(MTAR:0)%任由%ANAME(MPLY:0)%摆布着…
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%にされるまま身を任せている…
	ELSEIF TFLAG:18 == -1
		PRINTFORML 途中把%ANAME(MTAR:0)%的头发拉拽了…
		;TranslatedMark 途中で%ANAME(MTAR:0)%の髪を引っ掛けてしまった…
		PRINTFORM %ANAME(MTAR:0)%的眼角浮出泪水
		;TranslatedMark %ANAME(MTAR:0)%は目尻に涙を浮かべて
		IF TALENT:(MTAR:0):害羞 || TALENT:(MTAR:0):胆怯 || TALENT:(MTAR:0):老実
			PRINTFORML 痛苦地捂住头…
			;TranslatedMark 痛そうに頭を抑えた…
		ELSE
			PRINTFORML %ANAME(MPLY:0)%被瞪了一眼...
			;TranslatedMark %ANAME(MPLY:0)%を睨んでいる…
		ENDIF
	ENDIF
;プレイヤーが相手側
ELSE
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORMW %ANAME(MTAR:0)%拜托%ANAME(MPLY:0)%用梳子梳头发
		;TranslatedMark %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に頼んで、櫛で髪を梳いてもらった
	;主導権が相手側
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%用梳子梳着%ANAME(MTAR:0)%的头发
		;TranslatedMark %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の髪を櫛で梳かした
	ENDIF

	IF TFLAG:18 == 1
		PRINTFORML %ANAME(MPLY:0)%看起来很开心…
		;TranslatedMark %ANAME(MPLY:0)%はとても楽しそうだ…
	ELSEIF TFLAG:18 == 0
		PRINTFORML %ANAME(MPLY:0)%认真地整理着%ANAME(MTAR:0)%的头发…
		;TranslatedMark %ANAME(MPLY:0)%は丁寧に%ANAME(MTAR:0)%の髪を整えている…
	ELSEIF TFLAG:18 == -1
		PRINTFORML 但是，%ANAME(MPLY:0)%似乎马上就厌倦了…
		;TranslatedMark が、%ANAME(MPLY:0)%はすぐに飽きてしまったようだ…
	ENDIF
ENDIF

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST323
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN323
RETURN コマンド_日常

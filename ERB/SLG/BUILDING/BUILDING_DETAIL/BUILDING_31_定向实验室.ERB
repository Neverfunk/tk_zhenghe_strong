@BUILDING_GET_NAME_定向实验室()
    #FUNCTIONS
    RETURNF "定向实验室"

@BUILDING_GET_EXP_定向实验室()
    #FUNCTIONS
    RETURNF "技术研究的时间减少2期。"

@BUILDING_SET_INFO_ID_31()
    BUILDING_TYPE:建造物_定向实验室 = 建造物_类型_科技类
    BUILDING_LEVEL:建造物_定向实验室 = 建造物_等级_三级建筑
    BUILDING_LIMIT_NUM:建造物_定向实验室 = 建造物_数量限制_1
    BUILDING_LIMIT_UNI:建造物_定向实验室 = 0
    BUILDING_COUNTRY_ID:建造物_定向实验室:特殊勢力_普通势力 = 1
    BUILDING_HAVE_SKILL:建造物_定向实验室 = 1
    BUILDING_TOHO_REQ_ID:建造物_定向实验室:0 = 0

@CHECK_BUILDING_FUNCTION_定向实验室(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    RESULT:1 = TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:建造物_定向实验室
    IF RESULT:1 > 0
        RESULT:0 = 1
    ENDIF
    RETURNF RESULT:0

@BUILDING_SKILL_定向实验室(COUNTRY_ID)
    #DIM COUNTRY_ID
    PRINTFORML 发动后将可以直接获得一个科技
    PRINTFORML 冷却时间10回合
    PRINTFORML 是否发动
    CALL ASK_YN()
    IF !RESULT
        CALL SINGLE_DRAWLINE
        PRINTFORML 发动了《定向实验室》的技能
        CALL BUILDING_SKILL_定向实验室_FUNCTION(COUNTRY_ID)
        IF RESULT
            BUILDING_SKILL_COLDTIME:COUNTRY_ID:建造物_定向实验室 = 10
            PRINTFORMW 完毕
        ENDIF
    ENDIF

@BUILDING_SKILL_定向实验室_FUNCTION(COUNTRY_ID)
    #DIM COUNTRY_ID
    #DIM TREE_ID
    #DIM STEP_ID
    #DIM SELECT_ID
    #DIMS TECH_NAME
    #DIMS TECH_EXPLAIN
    #DIM FIRST_LINE
    FIRST_LINE = LINECOUNT
    CALL SINGLE_DRAWLINE
    CALL SHOW_TECHNOLOGY_TREE(COUNTRY_ID)
    CALL SINGLE_DRAWLINE

    TREE_ID = SELECT_ID / 100
    STEP_ID = SELECT_ID % 100

    IF !IS_TECHNOLOGY_EXISTS(TREE_ID, STEP_ID)
        PRINTL
        PRINTL
        PRINTL
        PRINTL
    ELSE
        TECH_NAME = %GET_TECHNOLOGY_NAME(TREE_ID, STEP_ID)%
        TECH_EXPLAIN = %GET_TECHNOLOGY_DESCRIPTION(TREE_ID, STEP_ID)%
        PRINTFORML %TECH_NAME%
        PRINTFORML 効果：%TECH_EXPLAIN%
        PRINTFORML 距离完成还剩{GET_RESEARCH_PERIOD(COUNTRY_ID, TREE_ID, STEP_ID)}期
        IF COUNTRY_ID == CFLAG:MASTER:所属 && IS_TECHNOLOGY_RESEARCHABLE(COUNTRY_ID, TREE_ID, STEP_ID) && SELECT_ID != TECHNOLOGY_RESEARCH_TARGET:COUNTRY_ID
            PRINTBUTTON "[获得此科技]", 1001
            PRINTL
        ELSE
            PRINTL
        ENDIF
    ENDIF
    CALL SINGLE_DRAWLINE
    PRINTBUTTON "[取消]", 1000
    INPUT
    IF RESULT == 1000
        RETURN 0
    ELSEIF RESULT == 1001 && COUNTRY_ID == CFLAG:MASTER:所属 && IS_TECHNOLOGY_RESEARCHABLE(COUNTRY_ID, TREE_ID, STEP_ID)
        PRINTFORML 获得《%TECH_NAME%》科技吗？
        CALL ASK_YN()
        IF RESULT == 0
            IF TECHNOLOGY_RESEARCH_TARGET:COUNTRY_ID == SELECT_ID
                TECHNOLOGY_STATUS:COUNTRY_ID:TREE_ID ++
                TECHNOLOGY_RESEARCH_TARGET:COUNTRY_ID = -1
                TECHNOLOGY_RESEARCH_PROGRESS:COUNTRY_ID = 0
            ELSE
                TECHNOLOGY_STATUS:COUNTRY_ID:TREE_ID ++
            ENDIF
            PRINTFORMW 获得了《%TECH_NAME%》科技
            RETURN 1
        ENDIF
    ELSE
        TREE_ID = RESULT / 100
        STEP_ID = RESULT % 100
        SIF IS_TECHNOLOGY_EXISTS(TREE_ID, STEP_ID)
            SELECT_ID = RESULT
    ENDIF
    CLEARLINE LINECOUNT - FIRST_LINE
    RESTART

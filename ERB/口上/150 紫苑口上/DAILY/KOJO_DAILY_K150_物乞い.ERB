;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY_RATE(対象)
#DIM 対象
RETURN 80


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY_DECISION(対象)
#DIM 対象
SIF MONEY < 10000
	RETURN 0
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY(対象)
#DIM 対象
#DIM 金額
#DIM 上昇値
#DIM 能力値
金額 = MIN(IFRAND("1000, 5000, 10000, 30000, 50000", 1), MONEY)

PRINTFORMW 「%ANAME(MASTER)%、請給我錢？」
PRINTFORML 工作時、剛想到%ANAME(対象)%、%ANAME(対象)%就突然来訪、還伸出了一個茶碗
PRINTFORMW 好像是来乞討的……
CALL ICPRINT(@"……好想要<{金額}>金啊」", "W", カラー_注意)
PRINTFORMW 相當強硬的価格設定啊……
PRINTFORML 現在的所持金錢:{MONEY}

CALL ASK_YN("給", "不給")

IF RESULT == 1
	PRINTFORMW 「小氣鬼！」
	PRINTFORML %ANAME(対象)%生氣地離開了……
	RETURN 1
ENDIF

PRINTFORMW 「真的要給我麼？　太好了！」
PRINTFORMW 「謝謝你啊！」
PRINTFORMW %ANAME(対象)%露出財迷的表情離開了……
MONEY -= 金額
IF 金額 < 10000
	上昇値 = RAND:10000 < 金額
ELSE
	上昇値 = RAND:((金額 * 3 + 30000) / 20000)
ENDIF

SIF 上昇値 == 0
	RETURN 1

能力値 = IFRAND(@"{GETNUM(ABL, "武闘")}, {GETNUM(ABL, "防衛")}, {GETNUM(ABL, "知略")}, {GETNUM(ABL, "政治")}", 1)

CALL ICPRINT(@"干勁十足的%ANAME(対象)%的%ABLNAME:能力値%提高了<{上昇値}>", "W", カラー_注意)

ABL:対象:能力値 += 上昇値

RETURN 1

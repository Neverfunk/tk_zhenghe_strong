;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K26_ASLEEP_1_RATE(対象)
#DIM 対象
RETURN 40


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K26_ASLEEP_1_DECISION(対象)
#DIM 対象

SIF CFLAG:対象:好感度 < 500
	RETURN 0

SIF KDVAR:対象:慧音_うたたね発見
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K26_ASLEEP_1_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K26_ASLEEP_1(対象)
#DIM 対象

PRINTFORMW 有事情去了%ANAME(対象)%的房间，但是没有反应
PRINTFORMW 这个时间的%PRONOUN(対象)%应该在自己的房间工作才对.
PRINTL
CALL ASK_YN("偷窥一下", "是不是外出了呢")

IF RESULT == 1
	PRINTFORMW 大概是偶然出去了吧
	PRINTFORMW %ANAME(MASTER)%离开了
	KDVAR:対象:慧音_うたたね発見 = 1
	RETURN 1
ENDIF

PRINTFORMW 惊讶地一看，发现%ANAME(対象)%正趴在桌子上。
PRINTFORMW 看来工作中打瞌睡了.
CALL ASK_MULTI("叫醒", "盖上毛毯", "离开")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 轻轻地摇了摇%ANAME(対象)%
		PRINTFORMW 「嗯.。嗯.。？哇，啊、%ANAME(MASTER)%か……」
		PRINTFORMW 「我.。我打瞌睡了吗？被你看到了真是有点害羞」
		PRINTFORMW 「稍微睡一会，头脑也变得清醒了。剩下的工作很快就做完了」
		PRINTFORMW %PRONOUN(対象)%羞答答地回去工作了……
		CFLAG:対象:好感度 += 50
	CASE 1
		PRINTFORMW 大概是累了吧。也不好叫醒她
		PRINTFORMW 给她盖上了毯子，之后就离开了……
		CFLAG:対象:好感度 += 100
	CASE 2
		PRINTFORMW 大概是累了吧。也不好叫醒她
		PRINTFORMW 静静关上了门，悄悄离开了……
ENDSELECT

KDVAR:対象:慧音_うたたね発見 = 1

RETURN 1

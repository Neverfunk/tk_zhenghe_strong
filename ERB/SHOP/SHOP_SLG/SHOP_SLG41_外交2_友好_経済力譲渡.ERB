;-------------------------------------------------
;経済力を譲り渡す
;-------------------------------------------------
@DIPLOMACY_GIVE_ECONOMY(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)

LOCAL:20 = MAX(STRLENS(NAME:(LOCAL:5)), STRLENS(NAME:(LOCAL:4)))
LOCAL:21 = MAX(GET_DIGIT(LOCAL:6 / 100), GET_DIGIT(LOCAL:7 / 100))

PRINTFORML %ANAME(LOCAL:4), LOCAL:20%的経済力:{LOCAL:6 / 100, LOCAL:21}
PRINTFORML %ANAME(LOCAL:5), LOCAL:20%的経済力:{LOCAL:7 / 100, LOCAL:21}
CALL SINGLE_DRAWLINE

IF LOCAL:6 < 250
	PRINTFORMW 最少也需要250以上的経済力
	RETURN 0
ENDIF

PRINTFORML 請輸入要転讓的経済力（輸入0以下取消、最多転讓経済力的20％）

CALL DRAW_ECONOMY_INPUT(LOCAL:6 / 500)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 < 50
	CLEARLINE 1
	PRINTL 最少也需要転讓50以上的経済力
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6 / 500
	CLEARLINE 1
	PRINTFORML 超過了上限(上限{LOCAL:6 / 500})
	GOTO INPUT_LOOP
ENDIF

;経済力の移動処理
CALL SHIFT_ECONOMY(CFLAG:MASTER:所属, ARG:0, LOCAL:2 * 100)

PRINTFORMW %ANAME(MASTER)%向%ANAME(LOCAL:5)%転讓了{LOCAL:2}経済力

;好感度上昇値・敵対値下降値の計算
;同じ経済力でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:12 = SQRT(LOCAL:2 * 230000 / LOCAL:6) + SQRT(LOCAL:2 * 230000 / LOCAL:7)

LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
;跳转信仰事件
TRYCALL SHOP_LIFE_USERSHOP68_CONFERENCE(CFLAG:MASTER:所属,ARG:0)
RETURN 1


;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_LOVER_DIPLOMACY_RATE()
RETURN 100


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_LOVER_DIPLOMACY_DECISION()
#DIM 邀请势力 = 0
#DIM 势力数 = 0
SIF DAY < 10
    RETURN 0
SIF !(IS_MALE(MASTER))
    RETURN 0
;MASTER必须仕官/为君主
SIF !IS_COUNTRY(CFLAG:MASTER:所属)
    RETURN 0
FOR LOCAL, 0, MAX_COUNTRY
    ;是个势力
    SIF !IS_COUNTRY(LOCAL)
        CONTINUE
    ;特殊势力不得入内
    SIF IS_SP_COUNTRY(LOCAL)
        CONTINUE
    ;MASTER:召集
    SIF GET_COUNTRY_BOSS(LOCAL) == MASTER
        CONTINUE
    ;自己人就没来的必要吧？
    SIF CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, LOCAL) > 4
        CONTINUE
    ;需要友好
    势力数 ++
    IF REL_LIKE:(GET_COUNTRY_BOSS(LOCAL)):MASTER >= 300 && REL_HATE:(GET_COUNTRY_BOSS(LOCAL)):MASTER < 300
        FOR LOCAL:1, 1, CHARANUM
            SIF CFLAG:(LOCAL:1):所属 != LOCAL || CFLAG:(LOCAL:1):捕虜先 || !IS_FEMALE(LOCAL:1)
                CONTINUE
            IF CFLAG:(LOCAL:1):好感度 >= 1500 && (REL_LIKE:(LOCAL:1):MASTER - REL_HATE:(LOCAL:1):MASTER) > 0
                邀请势力 ++
                BREAK
            ENDIF
        NEXT
    ENDIF
NEXT
;你的名声必须要好
SIF !CHECK_SKILL_DUPLICATE(MASTER, スキル_ジャンル_PERSONAL, 0, 41) || CHECK_SKILL_DUPLICATE(MASTER, スキル_ジャンル_PERSONAL, 0, 43) || CHECK_SKILL_DUPLICATE(MASTER, スキル_ジャンル_PERSONAL, 0, 44)
    RETURN 0
;开废了
SIF DVAR:联谊会历时 < 0
    RETURN 0
SIF DVAR:联谊会冷却
    RETURN 0
RETURN 邀请势力 >= 5 || (INRANGE(势力数, 3, 10) && 邀请势力 >= 3)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_LOVER_DIPLOMACY_GENRE()
RETURN デイリー_ジャンル_エロ

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_LOVER_DIPLOMACY_SETTARGET()
RETURN 1

;---------------------
;イベントの進行度合いに応じてイベント名を変更したい場合に利用
;変更する必要がない場合、この関数は不要
;---------------------
@EVENT_DAILY_LOVER_DIPLOMACY_NAME()
RESULTS '= @"第{DVAR:联谊会历时 + 1}届全幻想乡联谊大会！"


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_LOVER_DIPLOMACY()
#DIM 参会势力, MAX_COUNTRY
#DIM 参会势力代表, MAX_COUNTRY
#DIM 参会人数 = 0
#DIM 自势力
#DIM 对方势力
#DIM 对方代表
#DIM 现在话题
#DIM 目前关系
#DIM 期限
#DIM 话题, 5 ;要兵/要钱/要经济/要延期/想做了
#DIM 效果量, 3
VARSET 参会势力
VARSET 参会势力代表
VARSET 话题
自势力 = CFLAG:MASTER:所属

FOR LOCAL, 0, MAX_COUNTRY
    ;是个势力
    SIF !IS_COUNTRY(LOCAL)
        CONTINUE
    ;特殊势力不得入内
    SIF IS_SP_COUNTRY(LOCAL)
        CONTINUE
    ;MASTER:召集
    SIF GET_COUNTRY_BOSS(LOCAL) == MASTER
        CONTINUE
    ;自己人就没来的必要吧？
    SIF CHECK_COUNTRY_RELATION_F(自势力, LOCAL) > 4
        CONTINUE
    ;需要友好
    IF REL_LIKE:(GET_COUNTRY_BOSS(LOCAL)):MASTER >= 300 && REL_HATE:(GET_COUNTRY_BOSS(LOCAL)):MASTER < 300
        FOR LOCAL:1, 1, CHARANUM
            SIF CFLAG:(LOCAL:1):所属 != LOCAL || CFLAG:(LOCAL:1):捕虜先 || !IS_FEMALE(LOCAL:1)
                CONTINUE
            IF CFLAG:(LOCAL:1):好感度 >= 1500 && (!参会势力代表:参会人数 || CFLAG:(LOCAL:1):好感度 >= CFLAG:(参会势力代表:参会人数):好感度) && (REL_LIKE:(LOCAL:1):MASTER - REL_HATE:(LOCAL:1):MASTER) > 0
                参会势力代表:参会人数 = LOCAL:1
            ENDIF
        NEXT
        IF 参会势力代表:参会人数 != 0
            参会势力:参会人数 = LOCAL
            参会人数 ++
        ENDIF

    ENDIF
NEXT
PRINTFORML 为了与幻想乡的大家搞好关系，
PRINTFORMW %ANAME(GET_COUNTRY_BOSS(自势力))%处举办了由%ANAME(MASTER)%\@GET_COUNTRY_BOSS(自势力) == MASTER ? 亲自出场 # 作为主办方代表\@的第{DVAR:联谊会历时 + 1}届全幻想乡联谊大会！
PRINTFORMW 现在，%ANAME(MASTER)%的面前坐着——
PRINTFORML 要详细说明一下吗？
CALL ASK_YN
IF !RESULT
    PRINTFORMW 她们分别是：
    FOR LOCAL, 0, 参会人数
        IF GET_COUNTRY_BOSS(参会势力:LOCAL) == 参会势力代表:LOCAL
            CALL COLOR_PRINTW(@"%ANAME(GET_COUNTRY_BOSS(参会势力:LOCAL))%本人", COUNTRY_COLOR:(参会势力:LOCAL))
        ELSE
            CALL COLOR_PRINTW(@"来自%ANAME(GET_COUNTRY_BOSS(参会势力:LOCAL))%势力的%ANAME(参会势力代表:LOCAL)%", COUNTRY_COLOR:(参会势力:LOCAL))
        ENDIF
    NEXT
ELSE
    PRINTFORMW 好吧……
    PRINTFORML 总而言之，
    FOR LOCAL, 0, 参会人数
        CALL COLOR_PRINT(@"%ANAME(参会势力代表:LOCAL)%", COUNTRY_COLOR:(参会势力:LOCAL))
        PRINT ，
    NEXT
    PRINTL
    PRINTFORMW 这些人都出席了
ENDIF
PRINTFORML 作为东道主，你可以向每个人提出一个要求
PRINTFORML 代表出场时会写明显示当前代表所属势力
PRINTFORMW 如果有相关的外交选项，会在确认之前及有关数据
FOR LOCAL, 0, 参会人数
    对方势力 = 参会势力:LOCAL
    对方代表 = 参会势力代表:LOCAL
    目前关系 = CHECK_COUNTRY_RELATION_F(自势力, 对方势力)
    CALL SINGLE_DRAWLINE
    CALL COLOR_PRINTL(@"%ANAME(GET_COUNTRY_BOSS(对方势力))%势力代表：%ANAME(对方代表)%", COUNTRY_COLOR:(对方势力))
    CALL SINGLE_DRAWLINE
    SELECTCASE 目前关系
        CASE 0
        ;敌对
            IF REL_LIKE:对方代表:(GET_COUNTRY_BOSS(自势力)) < REL_HATE:对方代表:(GET_COUNTRY_BOSS(自势力))
                PRINTFORML 「要不是看在%ANAME(MASTER)%的份上，我可没那个时间出席呢(笑)」
            ELSE
                PRINTFORML 「%ANAME(MASTER)%，这么正式的场合之下，我可不会像其他人那样惯着你」
            ENDIF
        CASE 1
        ;停战
            PRINTFORML 「和平可是来之不易的事物，请%ANAME(MASTER)%大人好自珍重」
        CASE 2
        ;联盟
            PRINTFORML 「既然都是聚义讨贼，想必大家都能互相体谅吧？」
        CASE 3
        ;暂时同盟
            PRINTFORML 「我们的合作始终还是有限的」
            PRINTFORML 「分别的时候还望%ANAME(MASTER)%大人可以原谅」
        CASE 4
        ;永久同盟
            PRINTFORML 「我观察了%ANAME(GET_COUNTRY_BOSS(自势力))%大人的城市，果然是一片欣欣向荣的大好江山」
            PRINTFORML 「也不愧当初同盟的决策啊」
        CASEELSE
            THROW %ANAME(参会势力代表:LOCAL)%「我来这里干嘛？？？」
    ENDSELECT
    CALL SINGLE_DRAWLINE("=")
    {
        CALL ASK_MULTI_JUDGE(
            "想要兵力", 1,
            "想要国库资金", 1,
            "想要经济力", 1,
            "想要延长条约的期限", GROUPMATCH(目前关系, 1, 3),
            "要不……交出身体？", HAS_PENIS(MASTER)
            )
    }
    现在话题 = RESULT
    SELECTCASE 现在话题
        CASE 0
            效果量:0 = MIN(GET_SUM_ECONOMY(对方势力) * 8 / 1000, GET_SUM_SOLDIER(对方势力) * 2 / 3)
            效果量:1 = 效果量:0 * SLG_EXTRA_DRAFT_CALC_RATE(对方势力) * GET_SUM_ECONOMY(自势力) / GET_SUM_ECONOMY(对方势力)
            效果量:2 = 效果量:0 * 36 / 7 / 100 * 100
            IF DIPLOMACY_REQUEST_CHECK(自势力, 对方势力, 200)
                PRINTFORMW 「%ANAME(MASTER)%都开口了我就没有拒绝的理由了啊」
                CALL SHIFT_SOLDIER(对方势力, 自势力, 效果量:0)
                CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处获得了{效果量:0}兵力", カラー_注意)
            ELSE
                PRINTFORMW 「这……只怕这个要求没法满足啊……」
                CALL ASK_MULTI_JUDGE("那……算了吧", 1, @"可以用国库换(要求：{效果量:1}，当前国库：{MONEY:自势力})", MONEY:自势力 >= 效果量:1, @"可以用经济力换(要求：{效果量:2 / 100}，当前可支付经济力(总经济力的40\%)：{GET_SUM_ECONOMY(自势力) * 40 / 10000})", GET_SUM_ECONOMY(自势力) * 40 / 100 >= 效果量:2)
                SELECTCASE RESULT
                    CASE 0
                        PRINTFORMW 「那就稍微和亲爱的\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@聊聊天吧～」
                        CALL COLOR_PRINTW(@"和%ANAME(对方代表)%聊了一会，感觉关系变好了", カラー_注意)
                        CFLAG:对方代表:好感度 += 50 * MAX(目前关系, 1)
                        CONTINUE
                    CASE 1
                        PRINTFORMW 「麻烦破费了～」
                        MONEY:自势力 -= 效果量:1
                        MONEY:对方势力 += 效果量:1
                        CALL SHIFT_SOLDIER(对方势力, 自势力, 效果量:0)
                        CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处用国库的{效果量:1}换得了{效果量:0}兵力", カラー_注意)
                    CASE 2
                        PRINTFORML 「居然要用经济力换兵力吗？」
                        PRINTFORMW 「我能理解……」
                        CALL SHIFT_ECONOMY(自势力, 对方势力, 效果量:2)
                        CALL SHIFT_SOLDIER(对方势力, 自势力, 效果量:0)
                        CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处用{效果量:2 / 100}经济力换得了{效果量:0}兵力", カラー_注意)
                ENDSELECT
            ENDIF
        CASE 1
            效果量:0 = MIN(CALC_SALARY(), MONEY:对方势力 / 2)
            效果量:1 = 效果量:0 / SLG_EXTRA_DRAFT_CALC_RATE(对方势力) * 2
            效果量:2 = 效果量:1 * 36 / 7 / 100 * 100
            IF DIPLOMACY_REQUEST_CHECK(自势力, 对方势力, 225)
                PRINTFORMW 「%ANAME(MASTER)%都开口了我就没有拒绝的理由了啊」
                MONEY:对方势力 -= 效果量:0
                MONEY:自势力 += 效果量:0
                CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处获得了{效果量:0}国库资金", カラー_注意)
            ELSE
                PRINTFORMW 「这……只怕这个要求没法满足啊……」
                CALL ASK_MULTI_JUDGE("那……算了吧", 1, @"可以用兵力换(要求：{效果量:1}，当前兵力:{GET_SUM_SOLDIER(自势力)})", GET_SUM_SOLDIER(自势力) >= 效果量:1, @"可以用经济力换(要求：{效果量:2 / 100}，当前可支付经济力(总经济力的40\%):{GET_SUM_ECONOMY(自势力) * 40 / 10000})", GET_SUM_ECONOMY(自势力) * 40 / 100 >= 效果量:2)
                SELECTCASE RESULT
                    CASE 0
                        PRINTFORMW 「那就稍微和亲爱的\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@聊聊天吧～」
                        CALL COLOR_PRINTW(@"和%ANAME(对方代表)%聊了一会，感觉关系变好了", カラー_注意)
                        CFLAG:对方代表:好感度 += 50 * MAX(目前关系, 1)
                        CONTINUE
                    CASE 1
                        PRINTFORML 「这年头，兵力就是你存在的资本」
                        PRINTFORMW 「要拿这换现钱也太……」
                        PRINTFORMW 「算了，我也不好拒绝」
                        CALL SHIFT_SOLDIER(自势力, 对方势力, 效果量:1)
                        MONEY:对方势力 -= 效果量:0
                        MONEY:自势力 += 效果量:0
                        CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处用{效果量:1}兵力换得了{效果量:0}国库资金", カラー_注意)
                    CASE 2
                        PRINTFORML 「手头这么紧吗？」
                        PRINTFORMW 「那就兑换一下吧」
                        CALL SHIFT_ECONOMY(自势力, 对方势力, 效果量:2)
                        MONEY:对方势力 -= 效果量:0
                        MONEY:自势力 += 效果量:0
                        CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处用{效果量:2}经济力换得了{效果量:0}国库资金", カラー_注意)
                ENDSELECT
            ENDIF
        CASE 2
            效果量:0 = GET_SUM_ECONOMY(对方势力) / 20 / 100 * 100
            效果量:1 = 效果量:0 * 7 / 36
            效果量:2 = 效果量:1 * SLG_EXTRA_DRAFT_CALC_RATE(对方势力) * GET_SUM_ECONOMY(自势力) / GET_SUM_ECONOMY(对方势力)
            IF DIPLOMACY_REQUEST_CHECK(自势力, 对方势力, 250)
                PRINTFORMW 「%ANAME(MASTER)%都开口了我就没有拒绝的理由了啊」
                CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处获得了{效果量:0 / 100}经济力", カラー_注意)
                CALL SHIFT_ECONOMY(对方势力, 自势力, 效果量:0)
            ELSE
                PRINTFORMW 「这……只怕这个要求没法满足啊……」
                CALL ASK_MULTI_JUDGE("那……算了吧", 1, @"可以用兵力换(要求：{效果量:1}，当前兵力:{GET_SUM_SOLDIER(自势力)})", GET_SUM_SOLDIER(自势力) >= 效果量:1, @"可以用国库换(要求：{效果量:2}，当前国库:{MONEY:自势力})", MONEY:自势力 >= 效果量:2)
                SELECTCASE RESULT
                    CASE 0
                        PRINTFORMW 「那就稍微和亲爱的\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@聊聊天吧～」
                        CALL COLOR_PRINTW(@"和%ANAME(对方代表)%聊了一会，感觉关系变好了", カラー_注意)
                        CFLAG:对方代表:好感度 += 50 * MAX(目前关系, 1)
                        CONTINUE
                    CASE 1
                        PRINTFORML 「你没有兵力怎么保护你换来的经济？」
                        PRINTFORMW 「悠着点」
                        CALL SHIFT_SOLDIER(自势力, 对方势力, 效果量:1)
                        CALL SHIFT_ECONOMY(对方势力, 自势力, 效果量:0)
                        CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处用{效果量:1}兵力换得了{效果量:0 / 100}经济力", カラー_注意)
                    CASE 2
                        PRINTFORMW 「你可真打了个好算盘啊～」
                        MONEY:自势力 -= 效果量:2
                        MONEY:对方势力 += 效果量:2
                        CALL SHIFT_ECONOMY(对方势力, 自势力, 效果量:0)
                        CALL COLOR_PRINTW(@"从%ANAME(GET_COUNTRY_BOSS(对方势力))%处用国库的{效果量:2}换得了{效果量:0 / 100}经济力", カラー_注意)
                ENDSELECT
            ENDIF
        CASE 3
            期限 = TMP_COUNTRY_RELATION_TERM:自势力:对方势力 + MAX((REL_LIKE:GET_COUNTRY_BOSS(对方势力):GET_COUNTRY_BOSS(自势力) * AI_RELATION:(COUNTRY_AI_TYPE:对方势力):(COUNTRY_AI_TYPE:自势力) / 100 - REL_HATE:GET_COUNTRY_BOSS(对方势力):GET_COUNTRY_BOSS(自势力) * AI_RELATION:(COUNTRY_AI_TYPE:对方势力):(COUNTRY_AI_TYPE:自势力) / 100) / 100, 1)
            效果量:0 = MIN(GET_SUM_ECONOMY(自势力) * 期限 / 1000, GET_SUM_SOLDIER(自势力) / 2)
            效果量:1 = 效果量:0 / SLG_EXTRA_DRAFT_CALC_RATE(自势力) * 期限
            效果量:2 = GET_SUM_ECONOMY(自势力) * 期限 / 100 / 100 * 100
            IF DIPLOMACY_REQUEST_CHECK(自势力, 对方势力, 300)
                PRINTFORMW 「那就让我们的关系更长久一些吧～」
                IF 目前关系 == 1
                    CALL INIT_CEASEFIRE(自势力, 对方势力, 期限)
                ELSEIF 目前关系 == 3
                    CALL INIT_ALLIANCE(自势力, 对方势力, 期限)
                ENDIF
                CALL COLOR_PRINTW(@"延长了和%ANAME(GET_COUNTRY_BOSS(对方势力))%的条约", カラー_注意)
            ELSE
                PRINTFORMW 「我看不出有什么理由……」
                CALL ASK_MULTI_JUDGE("那……算了吧", 1, @"可以用兵力换(要求：{效果量:0}，当前兵力:{GET_SUM_SOLDIER(自势力)})", GET_SUM_SOLDIER(自势力) >= 效果量:0, @"可以用国库换(要求：{效果量:1}，当前国库:{MONEY:自势力})", MONEY:自势力 >= 效果量:1, @"可以用经济力换(要求：{效果量:2 / 100}，当前可支付经济力(总经济力的40\%):{GET_SUM_ECONOMY(自势力) * 40 / 10000})", GET_SUM_ECONOMY(自势力) * 40 / 100 >= 效果量:2)
                SELECTCASE RESULT
                    CASE 0
                        PRINTFORMW 「那就只能和亲爱的\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@聊聊天了～」
                        CALL COLOR_PRINTW(@"和%ANAME(对方代表)%聊了一会，感觉关系变好了", カラー_注意)
                        CFLAG:对方代表:好感度 += 50 * MAX(目前关系, 1)
                        CONTINUE
                    CASEELSE
                        PRINTFORMW 「都这样的话我不同意就不占情理了……」
                        SELECTCASE RESULT
                            CASE 1
                                CALL SHIFT_SOLDIER(自势力, 对方势力, 效果量:0)
                                CALL COLOR_PRINT(@"以{效果量:0}兵力为代价", カラー_注意)
                            CASE 2
                                MONEY:自势力 -= 效果量:1
                                MONEY:对方势力 += 效果量:1
                                CALL COLOR_PRINT(@"以国库的{效果量:1}为代价", カラー_注意)
                            CASE 3
                                CALL SHIFT_ECONOMY(自势力, 对方势力, 效果量:2)
                                CALL COLOR_PRINT(@"以{效果量:2 / 100}经济力为代价", カラー_注意)
                        ENDSELECT
                        IF 目前关系 == 1
                            CALL INIT_CEASEFIRE(自势力, 对方势力, 期限)
                        ELSEIF 目前关系 == 3
                            CALL INIT_ALLIANCE(自势力, 对方势力, 期限)
                        ENDIF
                        CALL COLOR_PRINTW(@"延长了和%ANAME(GET_COUNTRY_BOSS(对方势力))%的条约", カラー_注意)
                ENDSELECT
            ENDIF
        CASE 4
            效果量:0 = MIN(GET_SUM_ECONOMY(自势力) * 20 / 1000, GET_SUM_SOLDIER(自势力) / 2)
            效果量:1 = CALC_SALARY() / 2
            效果量:2 = GET_SUM_ECONOMY(对方势力) * 7 / 100 / 100 * 100
            IF DIPLOMACY_REQUEST_CHECK(自势力, 对方势力, 400)
                PRINTFORMW 「做是可以，但在这里……？」
                PRINTFORMW 「你是想把会场变成炮场吗……？」
                DIPLOMACY_HATE:自势力 += 1
            ELSEIF (REL_LIKE:对方代表:MASTER - REL_HATE:对方代表:MASTER) >= 1000 || TALENT:对方代表:合意 == 1
                PRINTFORMW 「\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@，你这也太……」
                PRINTFORMW 「被其他人知道你在会上要求使用我的身体的话我们都会没脸待的～」
                CALL ASK_MULTI_JUDGE("那……算了吧", 1, @"可以用兵力换(要求：{效果量:0}，当前兵力:{GET_SUM_SOLDIER(自势力)})", GET_SUM_SOLDIER(自势力) >= 效果量:0, @"可以用国库换(要求：{效果量:1}，当前国库:{MONEY:自势力})", MONEY:自势力 >= 效果量:1, @"可以用经济力换(要求：{效果量:2 / 100}，当前可支付经济力(总经济力的40\%):{GET_SUM_ECONOMY(自势力) * 40 / 10000})", GET_SUM_ECONOMY(自势力) * 40 / 100 >= 效果量:2)
                SELECTCASE RESULT
                    CASE 0
                        PRINTFORMW 「那就只能和亲爱的\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@聊聊天了～」
                        CALL COLOR_PRINTW(@"和%ANAME(对方代表)%聊了一会，感觉关系变好了", カラー_注意)
                        CFLAG:对方代表:好感度 += 50 * MAX(目前关系, 1)
                        CONTINUE
                    CASEELSE
                        PRINTFORMW 「为了我，你真的要……」
                        PRINTFORMW 「那我只能『为了国家的利益只能付出我的\@VIRGIN(对方代表) ? 处女身 # 身体\@』了……」
                        SELECTCASE RESULT
                            CASE 1
                                CALL SHIFT_SOLDIER(自势力, 对方势力, 效果量:0)
                                CALL COLOR_PRINTW(@"付出了{效果量:0}兵力", カラー_注意)
                            CASE 2
                                MONEY:自势力 -= 效果量:1
                                MONEY:对方势力 += 效果量:1
                                CALL COLOR_PRINTW(@"付出了国库的{效果量:1}", カラー_注意)
                            CASE 3
                                CALL SHIFT_ECONOMY(自势力, 对方势力, 效果量:2)
                                CALL COLOR_PRINTW(@"付出了{效果量:2 / 100}经济力", カラー_注意)
                        ENDSELECT
                        DIPLOMACY_HATE:自势力 += 3
                        PRINTFORML 签名之后，%ANAME(对方代表)%慢慢走进%ANAME(MASTER)%，敞开了自己的衣服……
                        PRINTFORMW 「那么……」
                ENDSELECT
            ELSE
                PRINTFORMW 「你要干什么！？」
                CALL COLOR_PRINTW(@"被%ANAME(对方代表)%臭骂了一顿……", カラー_注意)
                CFLAG:对方代表:好感度 -= 300 - 50 * MAX(目前关系, 1)
                DIPLOMACY_HATE:自势力 += 5
                CONTINUE
            ENDIF
            PRINTFORMW 「诶你怎么突然……」
            PRINTFORMW 「啊……♥」
            SIF VIRGIN(对方代表)
                PRINTFORMW 「我的第一次，就这样被\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@……啊……♥」
            CALL COLOR_PRINTW(@"这之后和%ANAME(对方代表)%做了个爽……", カラー_ピンク)
            CALL FUCK(对方代表, "欲望, 奉仕, 性技, Ｃ, Ｖ, Ａ, Ｂ, Ｍ, 性交, 精愛, 露出, 射精, 接吻, 口淫, Ｖ性交, Ａ性交", "接吻喪失, 処女喪失, Ａ処女喪失, 膣内射精, 腸内射精, 口内射精", GET_ID(MASTER), @"%ANAME(MASTER)%的嘴唇", ANAME(MASTER), "", "和姦")
            CALL FUCK(MASTER, "Ｃ, 露出, 射精, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(对方代表)%的膣穴")
            IF !TALENT:对方代表:合意
                CALL COLOR_PRINTW(@"获得了%ANAME(对方代表)%的合意♥", カラー_ピンク)
                TALENT:对方代表:合意 = 1
                TALENT:对方代表:チョロイン = 1
            ENDIF
    ENDSELECT
    话题:现在话题 ++
NEXT
CALL SINGLE_DRAWLINE
IF FINDELEMENT(话题, MAXARRAY(话题)) == 4 && MAXARRAY(话题) >= 参会人数 * 2 / 3
    PRINTFORMW 联谊会彻底成为了%ANAME(MASTER)%及其恋人们的炮场……
    IF MAXARRAY(话题) == 参会人数
        PRINTFORMW 包括%ANAME(MASTER)%在内的代表们比起外交，甚至更关心能不能做爱……
    ELSE
        PRINTFORMW 一部分代表们看见自己仰慕着的%ANAME(MASTER)%居然聚众淫乱，甚至也产生了和%ANAME(MASTER)%做的念头……
    ENDIF
    PRINTFORMW %ANAME(MASTER)%见状，关上了门窗，向代表们招了招手……
    FOR LOCAL, 0, 参会人数
        对方代表 = 参会势力代表:LOCAL
        CALL COLOR_PRINT(@"%ANAME(对方代表)%", COUNTRY_COLOR:(参会势力:LOCAL))
        IF VIRGIN(对方代表)
            PRINTDATAW
                DATAFORM 「请\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%桑\@动作轻一点……」
                DATAFORM 「我……还是第一次……」
                DATAFORM 「请\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%桑\@成为我的第一次吧……」
                DATAFORM 「\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@的……终于……♥」
                DATAFORM 「能成为\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君的妻子 # %ANAME(MASTER)%的爱人\@……我最幸福了……♥」
            ENDDATA
        ELSEIF GETBIT(TALENT:对方代表:淫乱系, 素質_淫乱_孕みたがり) || RAND:5
            PRINTDATAW
                DATAFORM 「让我怀上\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@大人的孩子吧……」
                DATAFORM 「就这样……♥在里面出来吧……♥」
                DATAFORM 「啊♥肚子好热♥」
                DATAFORM 「\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@……还要更多……♥」
                DATAFORM 「肚子满满的……♥」
            ENDDATA
        ELSE
            PRINTDATAW
                DATAFORM 「\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@的……顶到最深处了……♥」
                DATAFORM 「要去了……♥咿啊啊啊啊……♥」
                DATAFORM 「啊……♥那里……♥好舒服……♥」
                DATAFORM 「\@(TALENT:对方代表:正妻 || TALENT:对方代表:妾) ? 夫君 # %ANAME(MASTER)%\@的……进来了……♥」
                DATAFORM 「不要停……♥腰停不下来♥」
            ENDDATA
        ENDIF
        CALL FUCK_MAKELOVE(对方代表, GET_ID(MASTER), @"%ANAME(MASTER)%的嘴唇", ANAME(MASTER))
        CALL FUCK(MASTER, "Ｃ, 射精, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(对方代表)%的膣穴")
        IF !TALENT:对方代表:合意
            CALL COLOR_PRINTW(@"获得了%ANAME(对方代表)%的合意♥", カラー_ピンク)
            TALENT:对方代表:合意 = 1
            TALENT:对方代表:チョロイン = 1
        ENDIF
    NEXT
    PRINTFORMW 完事后的代表们，在下体还流着%TEXTR(@"%ANAME(MASTER)%的种子", @"%ANAME(MASTER)%的精液", "被宠幸过的证明")%的情况下红着脸向%ANAME(MASTER)%道谢后便回去了……
    PRINTFORMW 恐怕以后开会，就谈不上什么外交了……
    DVAR:联谊会历时 = -(DVAR:联谊会历时 + 1)
ELSE
    PRINTFORMW 本届联谊会就这样顺利落幕了
    IF 话题:4 > 0
        PRINTFORM 虽然期间发生了相当令代表们脸红的事件，但
    ELSEIF MAXARRAY(话题) < 参会人数 / 3
        PRINTFORM 虽然很大程度上只是%ANAME(MASTER)%和各代表的私人寒暄，但
    ENDIF
    PRINTFORMW 各位代表们也期待着下一届联谊会的召开……
    DVAR:联谊会历时 ++
ENDIF
DVAR:联谊会冷却 = 5
RETURN 1

@BUILDING_GET_NAME_空军基地()
    #FUNCTIONS
    RETURNF "空军基地"

@BUILDING_GET_EXP_空军基地()
    #FUNCTIONS
    RETURNF "解散后将领冷却时间减少。"

@BUILDING_SET_INFO_ID_13()
    BUILDING_TYPE:建造物_空军基地 = 建造物_类型_军事类
    BUILDING_LEVEL:建造物_空军基地 = 建造物_等级_三级建筑
    BUILDING_LIMIT_NUM:建造物_空军基地 = 建造物_数量限制_1
    BUILDING_LIMIT_UNI:建造物_空军基地 = 0
    BUILDING_COUNTRY_ID:建造物_空军基地:特殊勢力_普通势力 = 1
    BUILDING_HAVE_SKILL:建造物_空军基地 = 1
    BUILDING_TOHO_REQ_ID:建造物_空军基地:0 = 0

@CHECK_BUILDING_FUNCTION_空军基地(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    RESULT:1 = TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:建造物_空军基地
    IF RESULT:1 > 0
        RESULT:0 = 1
    ENDIF
    RETURNF RESULT:0

@BUILDING_TEXT_空军基地_1(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F()
    PRINTFORM 因为《空军基地》的存在，
    PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%解散部队后将领不再有冷却时间
    RETURNF 1

@BUILDING_TEXT_空军基地_2(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F()
    PRINTFORM 因为《空军基地》的存在，
    PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%强制解散部队后将领冷却时间减少
    RETURNF 1

@BUILDING_SKILL_空军基地(COUNTRY_ID)
    #DIM COUNTRY_ID
    PRINTFORML 发动后可以将一支部队空投到地图任意位置
    PRINTFORML 冷却时间1回合
    PRINTFORML 是否发动
    CALL ASK_YN()
    IF !RESULT
        CALL SINGLE_DRAWLINE
        PRINTFORML 发动了《空军基地》的技能
        CALL BUILDING_SKILL_空军基地_FUNCTION(COUNTRY_ID)
        IF RESULT
            BUILDING_SKILL_COLDTIME:COUNTRY_ID:建造物_空军基地 = 1
            PRINTFORMW 完毕
        ENDIF
    ENDIF


@BUILDING_SKILL_空军基地_FUNCTION(COUNTRY_ID)
    #DIM COUNTRY_ID
    #DIM UNIT_ID
    LOCAL:3 = 0
    FOR LOCAL:0, 0, MAX_UNIT
        IF UNIT_SOLDIER:(COUNTRY_ID):(LOCAL:0) > 0
            LOCAL:3 = 1
            SIF CHECK_COUNTRY_RELATION_F(COUNTRY_ID, CITY_OWNER:(UNIT_POSITION:(COUNTRY_ID):(LOCAL:0))) == 0 || IS_STAY_ENEMY_UNIT(UNIT_POSITION:(COUNTRY_ID):(LOCAL:0))
                SETCOLOR カラー_警告
            PRINTBUTTON @"{LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]", LOCAL:0 + 1
            PRINT
            CALL SHOW_UNIT_INFO(COUNTRY_ID, LOCAL:0, 12)
            RESETCOLOR
            CALL SINGLE_DRAWLINE
        ENDIF
    NEXT
    IF !LOCAL:3
        PRINTFORML 現在編成中的部隊不存在
    ENDIF
    PRINTL
    PRINTBUTTON " 0[    取消]", 0
    PRINTL
    $INPUT_LOOP
    INPUT
    UNIT_ID = RESULT
    IF UNIT_ID == 0
        RETURN 0
    ELSEIF UNIT_ID >= 1 && UNIT_ID <= MAX_UNIT && UNIT_SOLDIER:(COUNTRY_ID):(UNIT_ID - 1) > 0
        CALL BUILDING_SKILL_AIRBORNE_UNIT(COUNTRY_ID,UNIT_ID-1)
        SIF RESULT
            RETURN 1
    ENDIF
    GOTO INPUT_LOOP

@BUILDING_SKILL_AIRBORNE_UNIT(COUNTRY_ID,UNIT_ID)
    #DIM UNIT_ID
    #DIM CITY_ID
    #DIM COUNTRY_ID
    CALL SHOW_UNIT_INFO(COUNTRY_ID, UNIT_ID, 12)
    CALL SET_CITY_COLOR_COUNTRY
    CALL SET_CITY_COLOR_UNIT
    CALL DRAWMAP
    CALL RESET_CITY_COLOR
    CALL SINGLE_DRAWLINE

    PRINTFORML 请选择部队{UNIT_ID+1}空投的位置
    PRINTBUTTON " 0[取消]", 0
    PRINTL
    $INPUT_LOOP0
    INPUT

    IF RESULT == 0
        RETURN 0
    ELSEIF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
        CITY_ID = RESULT - 1000
        PRINTFORM 将部队{UNIT_ID+1}空投到%CITY_NAME:CITY_ID%?
        IF CITY_OWNER:(CITY_ID) == COUNTRY_ID
            SETCOLOR カラー_緑
            PRINTFORML (我方领土)
            RESETCOLOR
        ELSE
            SETCOLOR カラー_警告
            PRINTFORML (非我方领土)
            RESETCOLOR
        ENDIF
        CALL ASK_YN()
        IF !RESULT
            UNIT_POSITION:(COUNTRY_ID):(UNIT_ID) = CITY_ID
            PRINTFORMW %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%的部队{UNIT_ID+1}空投到了%CITY_NAME:CITY_ID%!
            RETURN 1
        ELSE
            RESTART
        ENDIF
    ELSE
        GOTO INPUT_LOOP0
    ENDIF

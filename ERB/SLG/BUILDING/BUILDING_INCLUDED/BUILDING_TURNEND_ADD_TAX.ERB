
;税收相关建筑功能的集合函数
;因为城市收入与难度相关，因此插入到原城市税收计算中
@SLG_BUILDING_TURNEND_TAX_CITY(CITY_ID)
    #FUNCTION
    #DIM CITY_ID
    #DIM TAX
    TAX = 0
    IF CHECK_BUILDING_FUNCTION_SELECT(CITY_ID,-1,建造物_独占市场) > 0
        TAX += RESULT:1
        CITY_ECONOMY:CITY_ID = MIN(RESULT:2*100+CITY_ECONOMY:CITY_ID,CITY_ECONOMY_LIMIT:CITY_ID)
        BUILDING_TEXT = BUILDING_TEXT_独占市场(CITY_ID,RESULT:2)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(CITY_ID,-1,建造物_基础工业园区) > 0
        TAX += RESULT:1
        CITY_ECONOMY_LIMIT:CITY_ID = MIN(RESULT:2*100+CITY_ECONOMY_LIMIT:CITY_ID,999900)
        BUILDING_TEXT = BUILDING_TEXT_基础工业园区(CITY_ID,RESULT:2)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(CITY_ID,-1,建造物_一片花海) > 0
        TAX += 1000
        CITY_ECONOMY:CITY_ID = MIN(40*100+CITY_ECONOMY:CITY_ID,CITY_ECONOMY_LIMIT:CITY_ID)
        CITY_ECONOMY_LIMIT:CITY_ID = MIN(20*100+CITY_ECONOMY_LIMIT:CITY_ID,999900)
        BUILDING_TEXT = BUILDING_TEXT_一片花海(CITY_ID)
    ENDIF

    IF CHECK_BUILDING_FUNCTION_SELECT(-1,CITY_OWNER:CITY_ID,建造物_河童工业园区) > 0
        CITY_ECONOMY:CITY_ID = MIN(50*100+CITY_ECONOMY:CITY_ID,CITY_ECONOMY_LIMIT:CITY_ID)
        CITY_ECONOMY_LIMIT:CITY_ID = MIN(30*100+CITY_ECONOMY_LIMIT:CITY_ID,999900)
        BUILDING_TEXT = BUILDING_TEXT_河童工业园区(CITY_OWNER:CITY_ID,CITY_ID)
    ENDIF

    RETURNF TAX
@SLG_BUILDING_TURNEND_TAX_COUNTRY(COUNTRY_ID,SPECIAL_TAX)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM SPECIAL_TAX
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_全国市场) > 0
	    SPECIAL_TAX += RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_全国市场(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_高新工业园区) > 0
        SPECIAL_TAX += RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_高新工业园区(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID, 建造物_金融大厦) > 0
		SPECIAL_TAX += RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_金融大厦(COUNTRY_ID)
    ENDIF
	IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID, 建造物_度假区) > 0
		SPECIAL_TAX += RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_度假区(COUNTRY_ID,RESULT:1)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_奴隶市场) > 0
        SPECIAL_TAX += 500 * RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_奴隶市场_2(COUNTRY_ID)
    ENDIF

    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_专业招募所) > 0
        SPECIAL_TAX += 500 * RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_专业招募所_2(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_独占奴隶市场) > 0
        SPECIAL_TAX += 2000 * RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_独占奴隶市场_2(COUNTRY_ID)
    ENDIF

    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID, 建造物_NFT市场) > 0
		SPECIAL_TAX += RESULT:1 * 300
        BUILDING_TEXT = BUILDING_TEXT_NFT市场_2(COUNTRY_ID)
    ENDIF
    SPECIAL_TAX = SPECIAL_TAX - SLG_BUILDING_DECREASE_MONEY(COUNTRY_ID)
    RETURNF SPECIAL_TAX

@SLG_BUILDING_TURNEND_ADD_MASTER_MONEY
    #DIM COUNTRY_ID
    COUNTRY_ID = CFLAG:MASTER:所属
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID, 建造物_金融大厦) > 0
		SELECTCASE RAND:5
            CASE 0
                PRINTFORML 别人恐惧我贪婪！%ANAME(MASTER)%一波ALL IN成功，获取了大量资金
                LOCAL:0 = 50000
            CASE 1
                PRINTFORML %ANAME(MASTER)%把工资存入银行中，获取了利息
                LOCAL:0 = CALC_SALARY()*1/10
            CASE 2
                PRINTFORML 股市小涨，%ANAME(MASTER)%获得了少量资金
                LOCAL:0 = 5000
            CASE 3
                PRINTFORML 在帮助雇主投资成功后，%ANAME(MASTER)%收入了一笔佣金
                LOCAL:0 = 10000
            CASE 4
                PRINTFORML 股市大跌，%ANAME(MASTER)%只能坐着祈祷……
                LOCAL:0 = 0
        ENDSELECT
        MONEY += LOCAL:0
        PRINTFORML 所持金＋{LOCAL:0}（现在:{MONEY}）
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID, 建造物_雾雨魔法店) > 0
        MONEY += 10000
        BUILDING_TEXT = BUILDING_TEXT_雾雨魔法店(COUNTRY_ID)
        PRINTFORML 所持金＋{10000}（现在:{MONEY}）
    ENDIF


@SLG_BUILDING_DECREASE_MONEY(COUNTRY_ID)
    #FUNCTION
    #DIM 外来人_COUNTRY_ID
    #DIM COUNTRY_ID
    外来人_COUNTRY_ID = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_外来人)

    SIF !IS_COUNTRY(外来人_COUNTRY_ID)
        RETURNF 0
    SIF 外来人_COUNTRY_ID == COUNTRY_ID
        RETURNF 0
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_NFT市场) > 0
        LOCAL = RESULT:1
        BUILDING_TEXT = BUILDING_TEXT_NFT市场_1(COUNTRY_ID)
        RETURNF MIN(1000 * MIN((DAY-10) / 10 * 10, 0),LOCAL * 100)
    ENDIF
    RETURNF 0

;政務

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME304
RESULTS:0 = 処理政務
RESULTS:1 = 被幫忙処理政務

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE304
;共通部分
CALL COM_ABLE_COMMON(304)
SIF RESULT == 0
	RETURN 0
;捕虜会話では不可
SIF FLAG:調教モード == 6
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;デート中は無理
SIF TFLAG:54
	RETURN 0
;臨月or怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月 || CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;捕虜なら不可
SIF CFLAG:(MTAR:0):捕虜先
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM304
VARSET LOCAL
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "政治"), 0)

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE304
;主導権が主人公側
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%決定一起処理政務
ELSE
	;相手より政治が10以上劣っている
	IF ABL:(MPLY:0):政治 + 10 <= ABL:(MTAR:0):政治
		PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0)%的幫忙和指導下処理了政務
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%在%ANAME(MTAR:0)%的幫忙下処理了政務
	ENDIF
ENDIF

IF TFLAG:18 == 1
	PRINTFORML 比想象中的更快做完了…
ELSEIF TFLAG:18 == -1
	PRINTFORML 感覺比平時還要累…
ENDIF


;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST304
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN304
RETURN コマンド_日常

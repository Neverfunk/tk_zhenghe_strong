;アナル愛撫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME4
IF IS_EQUIP_TARGET(MTAR:0, 62)
	IF MTAR_NUM >= 2
		LOCALS:0 = 同時Ａ振動棒责弄
	ELSE
		LOCALS:0 = Ａ振動棒责弄
	ENDIF
ELSE
	IF MTAR_NUM >= 2
		LOCALS:0 = 同時愛撫肛門
	ELSE
		LOCALS:0 = 愛撫肛門
	ENDIF
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE4
;共通部分
CALL COM_ABLE_COMMON(4)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で5人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 5
	RETURN 0
;ターゲットはプレイヤーの倍まで
SIF MTAR_NUM <= 0 || MTAR_NUM > MPLY_NUM * 2
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF !CAN_REACH_GROIN(MPLY:(LOCAL:0), MTAR:(LOCAL:1)) 
			RETURN 0
	NEXT
NEXT

FOR LOCAL:0, 0, MTAR_NUM
	;a occupied except by a vibrator
	SIF IS_A_HOLD(MTAR:(LOCAL:0)) && !IS_EQUIP_TARGET(MTAR:(LOCAL:0), 62)
		RETURN 0
	;If any target has a vibe inserted, then they all must
	SIF IS_EQUIP_TARGET(MTAR:0, 62) != IS_EQUIP_TARGET(MTAR:(LOCAL:0), 62)
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM4
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
	CASE 4
		TIMES LOCAL:10, 0.50
	CASE 5
		TIMES LOCAL:10, 0.45
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 60
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 120, 60)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100

		SOURCE:(LOCAL:3):快Ａ += LOCAL:4

	NEXT
NEXT

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):露出 = 120
	SOURCE:(LOCAL:1):接触 = 60
	SOURCE:(LOCAL:1):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 70, 100)


NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP4
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP4(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 30

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400) * LOCAL:10 / 100

	NEXT

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 2
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):露出 += 40
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 30

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 2
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE4(ARG:0)
IF IS_EQUIP_TARGET(MEQUIP_TARGET:(ARG:0):0, 62)
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%用Ａ振動棒责弄%EQUIP_TARGET_ANAME(ARG:0)%中
ELSE
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%用手指挿入%EQUIP_TARGET_ANAME(ARG:0)%的肛門中
ENDIF

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP4(ARG:0)
IF IS_EQUIP_TARGET(MEQUIP_TARGET:(ARG:0):0, 62)
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正前後抽插著挿入到%EQUIP_TARGET_ANAME(ARG:0)%肛門内的振動棒…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正用手指来回搔弄著%EQUIP_TARGET_ANAME(ARG:0)%的肛門…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP4(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER4(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE4
;ターゲットが２人のとき
IF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;ターゲットが１人のとき
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
ENDIF

IF TALENT:(MTAR:0):美臀
	LOCALS:1 = 掰開%STR_BODY("尻：尻肉：長：愛撫：感触", MTAR:0)%、
ELSEIF GET_HIPSIZE(MTAR:0) >= 1
	LOCALS:1 = 撥開%STR_BODY("尻肉：長", MTAR:0)%、
ELSE
	LOCALS:1 = 
ENDIF


SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF IS_EQUIP_TARGET(MTAR:0, 62)
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:1%抜挿著在%LOCALS:0%的肛門內振動的按摩棒進行责弄…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:1%把手指挿入到%LOCALS:0%的肛門中来回搔弄著…
		ENDIF
	;ターゲットに主導権
	CASE 1
		IF IS_EQUIP_TARGET(MTAR:0, 62)
			PRINTFORMW %LOCALS:0%%LOCALS:1%讓%ANAME(MPLY:0)%握住在自己肛門內振動的按摩棒、来回抜插…
		ELSE
			PRINTFORMW %LOCALS:0%%LOCALS:1%讓%ANAME(MPLY:0)%把手指挿入自己的肛門中、愛撫著裡面…
		ENDIF
	;第三者に主導権
	CASEELSE
		IF IS_EQUIP_TARGET(MTAR:0, 62)
			PRINTFORML %ANAME(TFLAG:45)%対%ANAME(MPLY:0)%、下達了抜插挿入在%LOCALS:0%的肛門內的振動棒的命令
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:1%抜挿著在%LOCALS:0%肛門內振動的按摩棒進行责弄…
		ELSE
			PRINTFORML %ANAME(TFLAG:45)%対%ANAME(MPLY:0)%、下達了把手指挿入%LOCALS:0%的肛門內進行愛撫的命令
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:1%把手指挿入到%LOCALS:0%的尻穴中来回摳弄著…
		ENDIF
ENDSELECT

IF !IS_EQUIP_TARGET(MTAR:0, 62) && (MTAR_NUM == 1) && CUM_CUR_AMOUNT:(MTAR:0):射精部位_アナル
	PRINTFORML %SPLIT_RAND("熱的/粘粘的/濃厚的/新鮮的", 1)%精液从%ANAME(MTAR:0)%那里溢了出来

	IF ABL:(MPLY:0):精愛 >= 4
		PRINTFORML %ANAME(MPLY:0)%自己舔舐掉了沾在手指上的白浊…
		EXP:(MPLY:0):精愛経験値 += 2
	ELSE
		PRINTFORML %ANAME(MPLY:0)%虽然很讨厌，但手指被弄脏是不可避免的…
	ENDIF
	WAIT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST4
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF (LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):快Ａ

		IF TCVAR:(LOCAL:1):52
			;気絶中
			PRINTFORML 肛門傳來的刺激、讓%ANAME(LOCAL:1)%在失去意識的情況下発出了小聲的呻吟
		ELSEIF LOCAL:2 < 300 
			PRINTFORML %ANAME(LOCAL:1)%還未開発過的還不能接受異物進入的場所被责弄的時候、%ANAME(LOCAL:1)%的表情有些痛苦
		ELSEIF LOCAL:2 < 1000
			PRINTFORML %ANAME(LOCAL:1)%雖然有些痛苦但是那表情上能看到一絲快楽和困惑
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %ANAME(LOCAL:1)%在無法忍耐的快感下発出了羞澀的聲音
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %ANAME(LOCAL:1)%已經非常習慣肛門傳來的感覚了、不時會為了索求快感而主動的輕扭腰肢
		ELSEIF LOCAL:2 < 10000
			PRINTFORM %ANAME(LOCAL:1)%
			IF TALENT:(LOCAL:1):圧抑 || TALENT:(LOCAL:1):孤高 || TALENT:(LOCAL:1):否定快感
				PRINTFORML 在那無法忍耐的快感下、流著淚不停喘息著
			ELSE
				PRINTFORML 迎合著责弄自己的手指的動作後弓著背、在直腸傳來的刺激下発出了快楽的喘息聲
			ENDIF
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%淫靡的扭動著腰肢、完全沉溺于菊穴的快感之中高聲嬌喘著
		ENDIF
		IF TALENT:(LOCAL:1):美臀
			PRINTFORML %STR_BODY("尻肉：短：外見", MTAR:0)%不停顫抖帯起陣陣臀浪……
		ELSEIF GET_HIPSIZE(LOCAL:1) >= 1
			PRINTFORML %STR_BODY("尻肉：短：外見", MTAR:0)%不停顫抖帯起陣陣臀浪……
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN4
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_4()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_4()
RETURN POWER(2, 性的嗜好_愛撫されたい)


;口枷

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME86
LOCALS:0 = 口枷

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%装着(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE86
;共通部分
CALL COM_ABLE_COMMON(86)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF (MPLY_NUM <= 0 || MPLY_NUM > 1) && !FLAG:RECHECKING
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;口枷が必要
SIF !ITEM:口枷 && !ITEM:A_口枷
	RETURN 0
;口が塞がってたらダメ
FOR LOCAL, 0, MTAR_NUM
	SIF IS_M_HOLD(MTAR:LOCAL)
		RETURN 0
	SIF !CAN_REACH_BODY(MPLY:0, MTAR:LOCAL) && !FLAG:RECHECKING
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM86
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
SOURCE:(MPLY:0):嗜虐 = 200
SOURCE:(MPLY:0):逸脱 = 80
SOURCE:(MPLY:0):接触 = 20
EXP:(MPLY:0):嗜虐経験値 += 1

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 130, 40)

;●ターゲットについて処理

FOR LOCAL, 0, MTAR_NUM
	EXP:(MTAR:(LOCAL:0)):緊縛経験 += 1
	EXP:(MTAR:(LOCAL:0)):被虐経験値 += 1

	SOURCE:(MTAR:(LOCAL:0)):恐怖 = 40
	SOURCE:(MTAR:(LOCAL:0)):不安 = 70
	SOURCE:(MTAR:(LOCAL:0)):逸脱 = 150
	SOURCE:(MTAR:(LOCAL:0)):接触 = 20

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:(LOCAL:0), 0, 110)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP86
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP86(ARG:0)

FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):LOCAL

	EXP:(LOCAL:3):緊縛経験 += 1
	EXP:(LOCAL:3):被虐経験値 += 1

	SOURCE:(LOCAL:3):恐怖 += 15
	SOURCE:(LOCAL:3):不安 += 30
	SOURCE:(LOCAL:3):逸脱 += 80

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 2
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE86(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%口枷装着中
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%が口枷を装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP86(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%的嘴被塞住了…
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%は口を塞がれている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP86(ARG:0)
PRINTFORMW 取下了%EQUIP_TARGET_ANAME(ARG:0)%的口枷
;TranslatedMark %EQUIP_TARGET_ANAME(ARG:0)%の口枷を外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER86(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"加虐Lv{ABL:(ARG:0):加虐}", ABL:(ARG:0):加虐 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):老実
	CALL COM_ORDER_ELEMENT(ARG:0, "老実", -3)
ENDIF
IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "加虐", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE86

IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%们
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%给%LOCALS:1%戴上了口枷…
		;TranslatedMark %ANAME(MPLY:0)%は%LOCALS:1%に口枷を装着し%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:1%把口枷交给了%ANAME(MPLY:0)%，命令他用口枷把%LOCALS:1%的嘴塞上
		;TranslatedMark %LOCALS:1%は%ANAME(MPLY:0)%に口枷を渡すと、それを%LOCALS:1%の口に装着するように命令した…
		PRINTFORML %ANAME(MPLY:0)%就这样给%LOCALS:1%戴上了口枷…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%LOCALS:1%に口枷をはめた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%给%LOCALS:1%戴上口枷
		;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:1%に口枷を付けるように命令した
		PRINTFORML %ANAME(MPLY:0)%就这样给%LOCALS:1%戴上了口枷…
		;TranslatedMark %ANAME(MPLY:0)%は言われるまま、%LOCALS:1%に口枷を装着した…
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN86
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_86()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_86()
RETURN POWER(2, 性的嗜好_虐げられたい) + POWER(2, 性的嗜好_縛られたい)


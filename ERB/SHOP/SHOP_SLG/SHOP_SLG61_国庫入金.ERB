;-------------------------------------------------
;「国庫入金」の名称
;-------------------------------------------------
@SHOP_SLG_NAME61
RESULTS:0 '= "国庫入金"

;-------------------------------------------------
;「国庫入金」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK61
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「国庫入金」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY61
CALL SLG_TREASURY_DEPOSIT
RETURN 1

;-------------------------------------------------
;「特別訓練」の次行説明指定有無
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD61

;-------------------------------------------------
;「国庫入金」の次行説明指定内容
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD_TEXT61
PRINTFORM %"費率:" + NUM_FORMAT(GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100000 + 1), 12%

;-------------------------------------------------
;「国庫入金」本体
;-------------------------------------------------
@SLG_TREASURY_DEPOSIT
#DIM FIRST_LINE
#DIM レート
レート = GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100000 + 1
FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE
PRINTFORML 可以用個人資金補充進国庫
PRINTFORML 但是、不能從国庫取出金錢到個人資金
PRINTFORM 現在的匯率是每
CALL COLOR_PRINT(@"{レート}", カラー_注意)
PRINTFORM 個人資金、換1国庫資金
CALL COLOR_PRINT("1", カラー_注意)
PRINTFORML
PRINTFORML 要向国庫内補充多少錢？(0、輸入負数取消)
CALL SINGLE_DRAWLINE
FOR LOCAL, 1, 10
	SIF レート * POWER(10, LOCAL) <= MONEY
		PRINTFORML [{POWER(10, LOCAL), 10}] 個人資金{レート * POWER(10, LOCAL), 10}
NEXT
PRINTFORML [{MONEY / レート, 10}] 個人資金{MONEY / レート * レート, 10}
PRINTFORML [0] 取消
INPUT
IF RESULT <= 0
	RETURN 0
ELSEIF RESULT * レート <= MONEY
	LOCAL = RESULT
	PRINTFORM 個人資金
	CALL COLOR_PRINT(@"{レート * LOCAL}", カラー_注意)
	PRINTFORM 匯入、換成
	CALL COLOR_PRINT(@"{LOCAL}", カラー_注意)
	PRINTFORML 国庫資金
	PRINTFORML 確定麼？
	CALL ASK_YN()
	IF RESULT == 1
		RESTART
	ELSE
		PRINTFORM 国庫資金増加了
		CALL COLOR_PRINT(@"{LOCAL}", カラー_注意)
		PRINTFORML
		MONEY -= LOCAL * レート
		MONEY:(CFLAG:MASTER:所属) += LOCAL
		RETURN 0
	ENDIF
ENDIF
RESTART


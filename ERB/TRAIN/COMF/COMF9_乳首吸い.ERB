;乳首吸い

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME9
LOCALS:1 = 吸吮乳頭

IF MTAR_NUM >= 2
	LOCALS:0 = 同時%LOCALS:1%
ELSE
	LOCALS:0 = %LOCALS:1%
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE9
;共通部分
CALL COM_ABLE_COMMON(9)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;2-2は不可
SIF MPLY_NUM == 2 && MTAR_NUM == 2
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF !CAN_LICK_BODY(MPLY:(LOCAL:0), MTAR:(LOCAL:1))
			RETURN 0
		SIF IS_RIDE(MPLY:(LOCAL:0), MTAR:(LOCAL:0))
			RETURN 0
	NEXT
NEXT
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;giving titjob
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 12) 
		RETURN 0
	;nipples occupied
	SIF IS_N_HOLD(MTAR:(LOCAL:0))
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM9
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 60
	SOURCE:(LOCAL:2):性行動 = 120

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 100, 40)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1800) * LOCAL:10 / 100

	NEXT
NEXT

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):露出 = 100
	SOURCE:(LOCAL:1):接触 = 60
	SOURCE:(LOCAL:1):性行動 = 240

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 80, 0)

NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP9
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP9(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 45

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600) * LOCAL:10 / 100

	NEXT

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 1
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):露出 += 50
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 90

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 1
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE9(ARG:0)
LOCAL:2 = 0
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	IF IS_FEMALE(MEQUIP_TARGET:(ARG:0):(LOCAL:0))
		LOCAL:2 = 1
	ENDIF
NEXT
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%吸吮乳頭中


;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP9(ARG:0)
LOCAL:2 = 0
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	IF IS_FEMALE(MEQUIP_TARGET:(ARG:0):(LOCAL:0))
		LOCAL:2 = 1
	ENDIF
NEXT

PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%吸着%EQUIP_TARGET_ANAME(ARG:0)%的乳头…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%对%EQUIP_TARGET_ANAME(ARG:0)%の乳首に吸い付いている…


;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP9(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER9(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 60

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE9
;プレイヤーが２人
IF MPLY_NUM == 2
	LOCALS:0 = %ANAME(MPLY:0)%和%ANAME(MPLY:1)%
;プレイヤーが１人
ELSE
	LOCALS:0 = %ANAME(MPLY:0)%
ENDIF

;ターゲットが２人
IF MTAR_NUM == 2
	LOCALS:1 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;ターゲットが１人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

IF MTAR_NUM == 2 && IS_PIERCED(MTAR:0, ピアス_乳首) && IS_PIERCED(MTAR:0, ピアス_乳首)
	LOCALS:2 = 上著環的
ELSEIF MTAR_NUM == 1 && IS_PIERCED(MTAR:0, ピアス_乳首)
	LOCALS:2 = 上著環的
ELSE
	LOCALS:2 = 
ENDIF

;ターゲットが２人
IF MTAR_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			;片方が男性
			IF IS_MALE(MTAR:0) || IS_MALE(MTAR:1)
				PRINTFORMW %LOCALS:0%嘴里含着%LOCALS:1%%LOCALS:2%的乳头，两人同时用舌尖来回舔舐…
				;TranslatedMark %LOCALS:0%は%LOCALS:2%%LOCALS:1%の乳首を口に含み、二人同時に舌先でねぶり回し…
			;二人とも貧乳以下
			ELSEIF GET_BUSTSIZE(MTAR:0) <= -1 && GET_BUSTSIZE(MTAR:1) <= -1
				PRINTFORMW %LOCALS:0%将%LOCALS:1%貧瘠的胸部的突起集中起来慢慢吸吮着…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%の薄い胸の突起を、まとめてじっくりと吸い上げ…
			ELSE
				PRINTFORMW %LOCALS:0%在%LOCALS:1%的，共计4个%LOCALS:2%乳房发出声音地吸着…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%の、合わせて４つの%LOCALS:2%乳房を音を立てて吸引し…
			ENDIF
		;ターゲットに主導権
		CASE 1
			;片方が男性
			IF IS_MALE(MTAR:0) || IS_MALE(MTAR:1)
				PRINTFORMW %LOCALS:1%让%LOCALS:0%吸各自的%LOCALS:2%乳头，让自己舒服起来…
				;TranslatedMark %LOCALS:1%は%LOCALS:2%それぞれの乳首を%LOCALS:0%に吸わせて、気持ち良くするように命令した…
			;二人とも貧乳以下
			ELSEIF GET_BUSTSIZE(MTAR:0) <= -1 && GET_BUSTSIZE(MTAR:1) <= -1
				PRINTFORMW %LOCALS:1%让%LOCALS:0%吸自己薄薄的%LOCALS:2%胸部，让自己舒服起来…
				;TranslatedMark %LOCALS:1%は%LOCALS:2%それぞれの薄い胸を%LOCALS:0%に吸わせて、気持ち良くするように命令した…
			ELSE
				PRINTFORMW %LOCALS:1%把%LOCALS:2%的%LOCALS:0%乳房按到自己的嘴上，命令吸吮乳头使自己舒服起来…
				;TranslatedMark %LOCALS:1%は%LOCALS:2%それぞれの乳房を%LOCALS:0%の口に押し付けると、乳首を吸って気持ち良くするように命令した…
			ENDIF
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%吸%LOCALS:2%%LOCALS:1%的胸
			;TranslatedMark %ANAME(TFLAG:45)%は%LOCALS:0%に%LOCALS:2%%LOCALS:1%の胸を吸うように命令した
			;片方が男性
			IF IS_MALE(MTAR:0) || IS_MALE(MTAR:1)
				PRINTFORMW %LOCALS:0%吸吮着%LOCALS:1%%LOCALS:2%的乳头，两人同时用舌尖轻舔舐着......。
				;TranslatedMark %LOCALS:0%は%LOCALS:2%%LOCALS:1%の乳首を吸い上げ、二人同時に舌先でねぶり回した…
			;二人とも貧乳以下
			ELSEIF GET_BUSTSIZE(MTAR:0) <= -1 && GET_BUSTSIZE(MTAR:1) <= -1
				PRINTFORMW %LOCALS:0%把%LOCALS:1%%LOCALS:2%的貧瘠的胸部集中起来吸吮着…
				;TranslatedMark %LOCALS:0%は%LOCALS:2%%LOCALS:1%の薄い胸をまとめて吸い上げた…
			ELSE
				PRINTFORMW %LOCALS:0%在%LOCALS:1%的，共计4个%LOCALS:2%乳房发出声音地吸吮着…
				;TranslatedMark %LOCALS:0%は%LOCALS:1%の、合わせて４つの%LOCALS:2%乳房を音を立てて吸引した…
			ENDIF
	ENDSELECT

;ターゲットが１人
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %LOCALS:0%把%LOCALS:1%的%LOCALS:2%
			;TranslatedMark %LOCALS:0%は%LOCALS:1%の%LOCALS:2%
			IF IS_MALE(MTAR:0)
				PRINTFORMW 膨胀的乳头在口内吮吸着…
				;TranslatedMark ぷっくりと膨らんだ乳首を口内で吸引し%TARGET_STR("た", "てきた")%…
			ELSE
				SELECTCASE GET_BUSTSIZE(MTAR:0)
					CASE -2
						PRINTFORMW 平坦的胸部慢慢地吸上来…
						;TranslatedMark まっ平らな胸をゆっくりと吸い上げ%TARGET_STR("た", "てきた")%…
					CASE -1
						PRINTFORMW 小小的胸部轻轻地吸着…
						;TranslatedMark 小さな胸をじっくりと吸い上げ%TARGET_STR("だ", "できた")%…
					CASE 0
						PRINTFORMW 适度大小的乳房慢慢地吸起…
						;TranslatedMark 適度な大きさの胸をじっくりと吸い上げ%TARGET_STR("た", "てきた")%…
					CASE 1
						PRINTFORMW 丰满的巨乳慢慢地吸吮…
						;TranslatedMark 巨乳をじっくりと吸い上げ%TARGET_STR("た", "てきた")%…
						
					CASE 2
						PRINTFORMW 从手中溢出的巨大柔嫩的乳房吸住…
						;TranslatedMark 手から溢れるほど大きな胸をじっくりと吸い上げ%TARGET_STR("た", "てきた")%…
				ENDSELECT
			ENDIF
		;ターゲットに主導権
		CASE 1
			IF IS_MALE(MTAR:0)
				PRINTFORMW %LOCALS:1%让%LOCALS:0%吸%LOCALS:2%乳头，命令%LOCALS:0%让自己舒服起来…
				;TranslatedMark %LOCALS:1%は%LOCALS:2%乳首を%LOCALS:0%に吸わせて、気持ち良くするように命令した…
			ELSE
				PRINTFORM %LOCALS:1%将自己的%LOCALS:2%乳头靠到%LOCALS:0%手上
				;TranslatedMark %LOCALS:1%は%LOCALS:0%の手に自身の%LOCALS:2%
				SELECTCASE GET_BUSTSIZE(MTAR:0)
					CASE -2
						PRINTFORMW 把扁平的胸脯按在上面，命令她把自己吸得舒服起来…
						;TranslatedMark まっ平らな胸を押し付けると、吸って気持ち良くするように命令した…
					CASE -1
						PRINTFORMW 把小小的胸脯压在上面，命令她把自己吸得舒服起来…
						;TranslatedMark 小さな胸を押し付けると、吸って気持ち良くするように命令した…
					CASE 0
						PRINTFORMW 把胸部压在上面，命令她把自己吸得舒服起来…
						;TranslatedMark 胸を押し付けると、吸って気持ち良くするように命令した…
					CASE 1
						PRINTFORMW 把丰满的巨乳压在上面，命令她把自己吸得舒服起来…
						;TranslatedMark 大きな胸を押し付けると、吸って気持ち良くするように命令した…
					CASE 2
						PRINTFORMW 把从手中溢出巨大柔嫩的乳房压在上面，命令她把自己吸得舒服起来…
						;TranslatedMark 手から溢れるほど大きな胸を押し付けると、吸って気持ち良くするように命令した…
				ENDSELECT
			ENDIF
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%LOCALS:0%去愛撫%LOCALS:1%的胸部
			PRINTFORM %LOCALS:0%把%LOCALS:1%的%LOCALS:2%
			;TranslatedMark %LOCALS:0%は%LOCALS:1%の%LOCALS:2%
			IF IS_MALE(MTAR:0)
				PRINTFORMW 鼓鼓的乳头放在嘴里吸吮着…
				;TranslatedMark ぷっくりと膨らんだ乳首を口内で吸引した…
			ELSE
				SELECTCASE GET_BUSTSIZE(MTAR:0)
					CASE -2
						PRINTFORMW 平坦的胸部慢慢地吸上来…
						;TranslatedMark まっ平らな胸をゆっくり吸い上げた…
					CASE -1
						PRINTFORMW 小小的胸部轻轻地吸着…
						;TranslatedMark 小さな胸をじっくりと吸い上げだ…
					CASE 0
						PRINTFORMW 适度大小的乳房慢慢地吸起…
						;TranslatedMark 適度な大きさの胸をじっくりと吸い上げた…
					CASE 1
						PRINTFORMW 丰满的巨乳慢慢地吸吮…
						;TranslatedMark 巨乳をじっくりと吸い上げた…
					CASE 2
						PRINTFORMW 从手中溢出的巨大柔嫩的乳房吸住…
						;TranslatedMark 手から溢れるほど大きな胸をじっくりと吸い上げた…
				ENDSELECT
			ENDIF
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST9
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	IF (LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):快Ｂ

		IF IS_MALE(LOCAL:1)
			IF TCVAR:(LOCAL:1):52
				;気絶中
				PRINTFORML 失去意識的%ANAME(LOCAL:1)%因為乳頭傳來的刺激微顫著身體
			ELSEIF LOCAL:2 < 300
				PRINTFORML %ANAME(LOCAL:1)%好像並沒有從乳頭那感到快感的樣子
			ELSEIF LOCAL:2 < 1000
				PRINTFORML %ANAME(LOCAL:1)%開始能感覺到一點點快感、因為瘙癢扭動著身子
			ELSEIF LOCAL:2 < 3000
				PRINTFORML %ANAME(LOCAL:1)%能感覺到從乳頭傳來的刺激、開始発出了火熱的吐息
			ELSEIF LOCAL:2 < 6000
				PRINTFORML %ANAME(LOCAL:1)%因為從乳頭傳來的快感而表情舒緩、任憑擺佈
			ELSEIF LOCAL:2 < 10000
				PRINTFORML %ANAME(LOCAL:1)%感覺太過敏感、每次被责弄乳頭時會感受到激烈難耐的快感
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%完全成为乳头快感的俘虏，被吸住奶头的女人不住扭动身体喘息着
			ENDIF
		ELSE
			IF TCVAR:(LOCAL:1):52
				;気絶中
				PRINTFORML 失去意識的%ANAME(LOCAL:1)%因為胸部傳來的刺激微顫著身體、
			ELSEIF LOCAL:2 < 300
				PRINTFORML %ANAME(LOCAL:1)%好像並沒有從胸部那感到快感的樣子、
			ELSEIF LOCAL:2 < 1000
				PRINTFORML %ANAME(LOCAL:1)%開始能感覺到一點點快感、因為瘙癢扭動著身子、
			ELSEIF LOCAL:2 < 3000
				PRINTFORML %ANAME(LOCAL:1)%的胸部被持续吸入后，乳头开始发硬，开始吐出热气。
			ELSEIF LOCAL:2 < 6000
				PRINTFORML %ANAME(LOCAL:1)%因为乳头的爱抚，表情开始不受控制的变化着。
				;TranslatedMark %ANAME(LOCAL:1)%は乳首への愛撫にトロンとした表情になってされるがままになっている
			ELSEIF LOCAL:2 < 10000
				PRINTFORML %ANAME(LOCAL:1)%感覺太過敏感、光是胸部的刺激就會陷入到極致的愉悅之中、
			ELSE
				PRINTFORM 不停吸着%ANAME(LOCAL:1)%的胸部，仅仅如此就让她愉悦到浑身颤抖，
				IF GET_BUSTSIZE(LOCAL:1) == 1
					PRINTL 看著在眼前弾動著的巨乳真是大飽眼福
				ELSEIF GET_BUSTSIZE(LOCAL:1) == 2
					PRINTL 看著在眼前劇烈弾動著的爆乳真是大飽眼福
				ELSE
					PRINTL 露出陶醉的表情委身于快感
				ENDIF
			ENDIF
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN9
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_9()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_9()
RETURN POWER(2, 性的嗜好_愛撫されたい)


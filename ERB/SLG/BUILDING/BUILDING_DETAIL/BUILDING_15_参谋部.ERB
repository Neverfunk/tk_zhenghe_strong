@BUILDING_GET_NAME_参谋部()
    #FUNCTIONS
    RETURNF "参谋部"

@BUILDING_GET_EXP_参谋部()
    #FUNCTIONS
    RETURNF "战斗时，我方将领技能发动概率提高。"

@BUILDING_SET_INFO_ID_15()
    BUILDING_TYPE:建造物_参谋部 = 建造物_类型_军事类
    BUILDING_LEVEL:建造物_参谋部 = 建造物_等级_三级建筑
    BUILDING_LIMIT_NUM:建造物_参谋部 = 建造物_数量限制_1
    BUILDING_LIMIT_UNI:建造物_参谋部 = 0
    BUILDING_COUNTRY_ID:建造物_参谋部:特殊勢力_普通势力 = 1
    BUILDING_HAVE_SKILL:建造物_参谋部 = 1
    BUILDING_TOHO_REQ_ID:建造物_参谋部:0 = 0

@CHECK_BUILDING_FUNCTION_参谋部(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    RESULT:1 = TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:建造物_参谋部 * 10
    IF RESULT:1 > 0
        RESULT:0 = 1
    ENDIF
    RETURNF RESULT:0

@BUILDING_TEXT_参谋部(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F()
    PRINTFORM 因为《参谋部》的存在，
    PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%方的技能发动率提高了！
    RETURNF 1

@BUILDING_TEXT_参谋部_SKILL(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !CHECK_BUILDING_TEXT_OUTPUT(COUNTRY_ID)
        RETURNF 0
    BUILDING_TEXT = PRINT_BUILDING_TEXT_TITLE_F()
    PRINTFORM 《参谋部》的技能生效了！
    PRINTFORML %ANAME(GET_COUNTRY_BOSS(COUNTRY_ID))%方的技能发动率大幅提高了！
    RETURNF 1

@BUILDING_SKILL_参谋部(COUNTRY_ID)
    #DIM COUNTRY_ID
    PRINTFORML 发动后我方部队本回合作战时技能触发率大幅增加
    PRINTFORML 冷却时间3回合
    PRINTFORML 是否发动
    CALL ASK_YN()
    IF !RESULT
        PRINTFORML 发动了《参谋部》的技能
        CALL BUILDING_SKILL_参谋部_FUNCTION(COUNTRY_ID)
        BUILDING_SKILL_COLDTIME:COUNTRY_ID:建造物_参谋部 = 3
    ENDIF
    PRINTFORMW 完毕

@BUILDING_SKILL_参谋部_FUNCTION(COUNTRY_ID)
    #DIM COUNTRY_ID
    #DIM CITY_ID
    SIF !IS_COUNTRY(COUNTRY_ID)
        RETURN
    BUILDING_SKILL_BUFF:COUNTRY_ID:建造物_参谋部 = 1
    BUILDING_SKILL_BUFF_REMAIN:COUNTRY_ID:建造物_参谋部 = 1

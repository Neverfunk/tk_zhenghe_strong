;-------------------------------------------------
;「民の慰安」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME23
IF CFLAG:MASTER:所属
	RESULTS:0 '= "慰安民衆"
ELSE
	RESULTS:0 '= "売春"
ENDIF

;-------------------------------------------------
;「民の慰安」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK23
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CAN_ADD_RANDOM_CHARA()
	RETURN 0
RETURN 1

;-------------------------------------------------
;「民の慰安」の選択可能キャラ存在判定（ヤられる側）
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA23(ARG:0)
;主人公
IF ARG:0 == MASTER
	SIF COOLTIME:MASTER:0 < 2 && CFLAG:(ARG:0):行動不能状態 != 行動不能_臨月
		RETURN CHECK91(ARG:0, 2, 1)
;主人公でない
ELSE
	;閨に呼ぶの選択可能キャラ存在判定
	CALL SHOP_LIFE_CHECKCHARA_NEYA(ARG:0)
	RETURN CHECK91(ARG:0, 2, RESULT && !IS_ANIMAL(ARG:0) && CFLAG:(ARG:0):所属 && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属)
ENDIF
RETURN 0

;-------------------------------------------------
;「民の慰安」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY23
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「民の慰安」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON23(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値, 行動済みマークをオフにするフラグ
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):閨に呼ぶで選択中, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1)
RETURN 0

;-------------------------------------------------
;「民の慰安」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD23(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT


;-------------------------------------------------
;「民の慰安」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW23
#DIM 選択人数
#DIM 最大人数
最大人数 = MIN(CALC_SHOP_TIME() - SHOP_TIME, 3)

選択人数 = 0
FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうか判定
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT == 1
		;選択中の人数を数える
		IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
			選択人数 ++
			SIF 選択人数 > 最大人数
				CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
		ENDIF
	ELSE
		;条件を満たしていなければ強制的にＯＦＦ
		CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
	ENDIF
NEXT

SIF 選択人数 * 2000 > MONEY
	FLAG:慰安避妊薬 = 0

;右カラムタイトル表示
CALL COLUMN_RIGHT_TITLE("実行者選択", TOSTR(選択人数), TOSTR(最大人数), "人数", "避妊時需2,000×人数")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
IF 選択人数 >= 1
	PRINTBUTTON "[就這樣決定]", 1
	PRINTPLAIN   
	PRINTBUTTON "[復位]", 2
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [就這樣決定]
	PRINTPLAIN   
	PRINTPLAINFORM [復位]
	RESETCOLOR
ENDIF

PRINTPLAIN   
IF FLAG:慰安避妊薬
	PRINTBUTTON "[有避孕]", 3
ELSE
	IF MONEY > 選択人数 * 2000
		PRINTBUTTON "[無避孕]", 3
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [無避孕]
		RESETCOLOR
	ENDIF
ENDIF
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;標準的なキャラリストとページ移動
CALL COLUMN_RIGHT_CHARALIST(1)

RETURN 0

;-------------------------------------------------
;「民の慰安」の右カラムボタンの入力処理補佐
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB23(ARG:0)
;このメンバーで決定
IF ARG:0 == 1
	CALL DECIDE_IAN_MEMBER()
	SIF !RESULT
		RETURN 0
;メンバーをリセット
ELSEIF ARG:0 == 2
	CALL RESET_IAN_MEMBER
;避妊ありなし
ELSEIF ARG:0 == 3
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:閨に呼ぶで選択中
			LOCAL:1 ++
	NEXT
	SIF FLAG:慰安避妊薬 || MONEY >= LOCAL:1 * 2000
		FLAG:慰安避妊薬 = !FLAG:慰安避妊薬
	RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;「民の慰安」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP23(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
		LOCAL:5 ++
	ENDIF
NEXT
IF CFLAG:(ARG:0):閨に呼ぶで選択中 || LOCAL:5 < MIN(CALC_SHOP_TIME() - SHOP_TIME, 3)
	;調教参加フラグを反転
	CFLAG:(ARG:0):閨に呼ぶで選択中 = !CFLAG:(ARG:0):閨に呼ぶで選択中
	LOCAL:5 += CFLAG:(ARG:0):閨に呼ぶで選択中 == 0 ? -1 # 1
	SIF FLAG:慰安避妊薬 && LOCAL:5 * 2000 > MONEY
		FLAG:慰安避妊薬 = 0
ENDIF
RETURN 0

;-------------------------------------------------
;「民の慰安」の右カラムボタンの入力処理補佐本体
;-------------------------------------------------
@DECIDE_IAN_MEMBER()
#DIM 行き先
#DIM 人数
#DIM メイン
#DIM 放浪リスト, MAX_CHARA_NUM
#DIM 放浪者数
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT

人数 = 0
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:(LOCAL:0):閨に呼ぶで選択中
		人数 ++
NEXT

IF 人数 == 0
	CLEARLINE LINECOUNT - FIRST_LINE
	RETURN 0
ENDIF

;放浪者のリスト生成
VARSET 放浪リスト
VARSET 放浪者数
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:特殊状態 == 特殊状態_放浪 && !RAND:3
		SELECTCASE CONFIG:14
			CASE 1
				SIF !IS_MALE(LOCAL)
					CONTINUE
			CASE 2
				SIF !IS_FEMALE(LOCAL)
					CONTINUE
			CASE 3
				SIF !(IS_FEMALE(LOCAL) && HAS_PENIS(LOCAL))
					CONTINUE
		ENDSELECT
		放浪リスト:放浪者数 = LOCAL
		放浪者数 ++
	ENDIF
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML 真的要去那裡進行慰安麼……？
FOR LOCAL, 0, VARSIZE("IAN_PLACE")
	PRINTFORML [{LOCAL}] %IAN_PLACE:LOCAL%
NEXT
PRINTFORML [99] 還是算了
$INPUT_LOOP
INPUT

IF RESULT == 99
	CLEARLINE LINECOUNT - FIRST_LINE
	RETURN 0
ELSEIF !INRANGE(RESULT, 0, VARSIZE("IAN_PLACE") - 1)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

行き先 = RESULT
メイン = -1
;START_TRAIN_COMMONでSHOP_TIMEが+1されるので、それをひいておく
SHOP_TIME --
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
		CFLAG:(LOCAL:0):調教参加フラグ  = 1
		CFLAG:(LOCAL:0):強制友好化 = 1

		IF FLAG:慰安避妊薬
			PRINTFORML %ANAME(LOCAL:0)%服用了避孕薬
			CFLAG:(LOCAL:0):避妊薬残ターン += 1
			MONEY -= 2000
		ENDIF
		SIF LOCAL != MASTER && メイン == -1
			メイン = LOCAL
		SHOP_TIME ++
		CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
	ENDIF
NEXT

;--信仰--触发调教类型0=会见 1=调教俘虏 2=侍寝 3=夜袭 4=慰安民众
MIRACLE_LUST_TRAIN_FORM = 4
;--信仰--玩家制造色欲调教事件变量赋值--
MIRACLE_LUST_TRAIN_TARGET:0 = 0 ;人数初始化
MIRACLE_LUST_TRAIN_MAIN = -1
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):調教参加フラグ
		MIRACLE_LUST_TRAIN_TARGET:0 ++ ;调教人数增加
		MIRACLE_LUST_TRAIN_TARGET:(MIRACLE_LUST_TRAIN_TARGET:0) = LOCAL:0 ;调教参与者记录
		SIF LOCAL:0 == MASTER
			CONTINUE
		MIRACLE_LUST_PRE_FALL:(MIRACLE_LUST_TRAIN_TARGET:0) = MIRACLE_FALLCOUNT(LOCAL:0) ;调教前陷落代码,函数在 祷告_践行教义事件.ERB
		MIRACLE_LUST_PRE_MAKELOVE:(MIRACLE_LUST_TRAIN_TARGET:0) = TALENT:(LOCAL:0):合意 ;调教前是否取得对象合意
		MIRACLE_LUST_PRE_LIKE:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):好感度 ;调教前对象好感度
		MIRACLE_LUST_PRE_TRUST:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):依存度 ;调教前对象依存度
		MIRACLE_LUST_PRE_BELONG:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):従属度 ;调教前对象従属度
		MIRACLE_LUST_PRE_CONTROL:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):支配度 ;调教前对象支配度
	ENDIF
NEXT
;--信仰--结束--

;全員の慰安参加者フラグをクリア（あてがうメニューで操作されるため）
CVARSET CFLAG, GETNUM(CFLAG, "慰安参加者") , 0

;放浪者を追加
IF 放浪者数 > 0 && CONFIG:15 == 0
	FOR LOCAL, 0, 放浪者数
		IF RAND:放浪者数 == 0
			CFLAG:(放浪リスト:LOCAL):慰安参加者 = 1
			CFLAG:(放浪リスト:LOCAL):調教参加フラグ  = 1
			CFLAG:(放浪リスト:LOCAL):強制友好化 = 1
			人数 ++
		ENDIF
	NEXT
ENDIF

LOCAL:1 = RAND(4, MAX(9 - 人数, 5))
FOR LOCAL, 0, LOCAL:1
	CALL CREATE_IAN_MOB(行き先, LOCAL + 1)
	SIF RESULT == -1
		BREAK
NEXT

FLAG:慰安モード = 1
FLAG:慰安場所 = 行き先
ITEM:A_跳蛋 = 1
ITEM:A_振動棒 = 1
ITEM:A_肛用振動棒 = 1
ITEM:A_穿戴式假陰茎 = 1
ITEM:A_飛機杯 = 1
ITEM:A_陰蒂夾 = 1
ITEM:A_乳頭夾 = 1
ITEM:A_麻薬 = 99
ITEM:A_縄 = 1
ITEM:A_鞭 = 1
ITEM:A_眼罩 = 1
ITEM:A_口枷 = 1
ITEM:A_鼻勾 = 1
ITEM:A_馬克筆 = 1
ITEM:A_潤滑液 = 99
ITEM:A_避孕套 = 99
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
ITEM:A_桃源香 = 99
ITEM:A_精力超群丸 = 99

FLAG:調教モード = 7
;ウフフフラグをONに
FLAG:ウフフフラグ = 1

SIF メイン != -1
	CALL SHOW_IAN_MESSAGE(メイン, 人数)



;行動開始時の共通処理
CALL START_TRAIN_COMMON
RETURN 1

;-------------------------------------------------
;「民の慰安」の選択メンバーのリセット
;-------------------------------------------------
@RESET_IAN_MEMBER
FOR LOCAL:0, 0, CHARANUM
	SIF CFLAG:(LOCAL:0):閨に呼ぶで選択中
		CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
NEXT
RETURN 0

;-------------------------------------------------
;「民の慰安」のモブ追加処理
;-------------------------------------------------
@CREATE_IAN_MOB(行き先, 人数, カウント = 1)
#DIM 行き先
#DIM 人数
#DIM 種類
#DIM 男女
#DIMS 名前
#DIM 対象
#DIM カウント
種類 = CREATE_IAN_MOB_GET_CHARA(行き先)
男女 = CREATE_IAN_MOB_GET_SEX(種類)
名前 = %CREATE_IAN_MOB_GET_NAME(種類, 男女)%\@ カウント ? %TOFULL(TOSTR(人数))% # \@

CALL CREATE_RANDOM_CHARA(0, -1, 0)
FLAG:汎用武将カウント --
対象 = RESULT
IF LOCAL == -1
	RETURN -1
ENDIF

NO:対象 = GET_EMPTY_NO()
NAME:対象 = %名前%
CALLNAME:対象 = %名前%
CSTR:対象:0 =
CSTR:対象:1 =
TALENT:対象:性別 = 男女
MAXBASE:対象:体力 = 99999999
MAXBASE:対象:気力 = 99999999
MAXBASE:対象:精神力 = 1500
BASE:対象:体力 = MAXBASE:対象:体力
BASE:対象:気力 = MAXBASE:対象:気力
BASE:対象:精神力 = MAXBASE:対象:精神力
ABL:対象:性知識 = 5
ABL:対象:Ｃ感 = RAND(0,3)
ABL:対象:Ｖ感 = RAND(0,3)
ABL:対象:Ａ感 = RAND(0,3)
ABL:対象:Ｂ感 = RAND(0,3)
ABL:対象:Ｍ感 = RAND(0,3)
ABL:対象:欲望 = RAND(3,5)
ABL:対象:性技 = RAND(4,7)
ABL:対象:性知識 = 5
ABL:対象:奉仕 = RAND(0,3)
ABL:対象:性交 = RAND(0,3)
ABL:対象:百合 = RAND:2
ABL:対象:精愛 = RAND:2
ABL:対象:露出 = RAND:2
ABL:対象:射精 = RAND:3
ABL:対象:排泄 = RAND:2
ABL:対象:主導度Ｕ = RAND(-300, 300)
ABL:対象:主導度Ｎ = RAND(-300, 300)
ABL:対象:倒錯度 = RAND(0, 300)
EXP:対象:絶頂経験 = 500
ABL:対象:武闘 = 30
ABL:対象:防衛 = 30
ABL:対象:知略 = 30
ABL:対象:政治 = 30
ABL:対象:料理 = 10
ABL:対象:肝臓 = 3
IF HAS_PENIS(対象)
	FOR LOCAL, 30, 50
		SIF GROUPMATCH(LOCAL, 36, 46)
			CONTINUE
		COM_TENDENCY:対象:LOCAL = RAND:2
	NEXT
ENDIF

IF HAS_VAGINA(対象)
	FOR LOCAL, 30, 50
		SIF GROUPMATCH(LOCAL, 36, 46)
			CONTINUE
		COM_TENDENCY:対象:LOCAL = RAND:2
	NEXT
ENDIF

TALENT:対象:精力超群 = 1
TALENT:対象:陰毛現在値 = 陰毛_標準
TALENT:対象:陰毛目標値 = 陰毛_標準
TALENT:対象:Ｖ緊緻度 = GET_DEFAULT_TIGHTNESS("普通")
TALENT:対象:Ａ緊緻度 = GET_DEFAULT_TIGHTNESS("普通")
TALENT:対象:処女 = 0
TALENT:対象:無接吻経験 = 0
TALENT:対象:童貞 = 0
TALENT:対象:後庭処女 = 1
TALENT:対象:慰安婦 = 種類
SEXUAL_EXPERIENCE:(対象):初体験_処女 = 不明
SEXUAL_EXPERIENCE:(対象):初体験_キス = 不明
SEXUAL_EXPERIENCE:(対象):初体験_童貞 = 不明
SEXUAL_EXPERIENCE:(対象):初体験_アナル処女 = ----
SEXUAL_LAST_EXPERIENCE:(対象):初体験_処女 = 不明
SEXUAL_LAST_EXPERIENCE:(対象):初体験_キス = 不明
SEXUAL_LAST_EXPERIENCE:(対象):初体験_童貞 = 不明
SEXUAL_LAST_EXPERIENCE:(対象):初体験_アナル処女 = ----
CALL CREATE_IAN_MOB_SET_STATUS(種類, 対象)
CFLAG:対象:慰安参加者 = 1
CFLAG:対象:慰安婦 = 1
CFLAG:対象:調教参加フラグ  = 1
CFLAG:対象:強制友好化 = 1

;-------------------------------------------------
;「民の慰安」のモブ種類取得処理
;-------------------------------------------------
@CREATE_IAN_MOB_GET_CHARA(行き先)
#FUNCTION
#DIM 行き先
SELECTCASE 行き先
	CASE 慰安_行き先_スラム
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_貧民}, {慰安_キャラ_浮浪者}, {慰安_キャラ_ごろつき}", 1,
				@"{慰安_キャラ_民}", RAND:100 < 15,
				@"{慰安_キャラ_露出狂}", RAND:100 < 30)
		}
	CASE 慰安_行き先_一般住宅街
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_民}", 1,
				@"{慰安_キャラ_貧民}, {慰安_キャラ_浮浪者}, {慰安_キャラ_ごろつき}", RAND:100 < 15,
				@"{慰安_キャラ_露出狂}", RAND:100 < 10,
				@"{慰安_キャラ_貴族}, {慰安_キャラ_富豪}", RAND:100 < 10,
				@"{慰安_キャラ_妖怪}", RAND:100 < 10,
				@"{慰安_キャラ_子供}", RAND:100 < 15,
				@"{慰安_キャラ_チャラ男}", RAND:100 < 15)
		}
	CASE 慰安_行き先_貴族の居住地
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_貴族}, {慰安_キャラ_富豪}, {慰安_キャラ_資産家}", 1,
				@"{慰安_キャラ_民}", RAND:100 < 15,
				@"{慰安_キャラ_子供}", RAND:100 < 15)
		}
	CASE 慰安_行き先_妖怪の山
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_天狗}", 1,
				@"{慰安_キャラ_河童}", RAND:100 < 40,
				@"{慰安_キャラ_妖怪}", RAND:100 < 20)
		}
	CASE 慰安_行き先_旧地獄
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_鬼}", 1,
				@"{慰安_キャラ_妖怪}", RAND:100 < 15)
		}
	CASE 慰安_行き先_竹林
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_兎}", 1,
				@"{慰安_キャラ_妖怪}", RAND:100 < 15)
		}
	CASE 慰安_行き先_野盗のアジト
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_野盗}", 1,
				@"{慰安_キャラ_ごろつき}, {慰安_キャラ_浮浪者}", RAND:100 < 15)
		}
	CASE 慰安_行き先_子供たちの秘密基地
		RETURNF 慰安_キャラ_子供
	CASE 慰安_行き先_兵舎
		RETURNF 慰安_キャラ_兵士
	CASE 慰安_行き先_犬舎
		RETURNF 慰安_キャラ_犬
	CASE 慰安_行き先_畜舎
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_豚}", 1,
				@"{慰安_キャラ_馬}", RAND:100 < 30)
		}
	CASE 慰安_行き先_魔界
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_悪魔}", 1,
				@"{慰安_キャラ_妖怪}", RAND:100 < 15)
		}
	CASE 慰安_行き先_魔法の森
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_妖精}", 1,
				@"{慰安_キャラ_妖怪}", RAND:100 < 15)
		}
	CASE 慰安_行き先_彼岸
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_死神}", 1,
				@"{慰安_キャラ_幽霊}", RAND:100 < 30)
		}
	CASE 慰安_行き先_紅魔館
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_ホフゴブリン}", 1,
				@"{慰安_キャラ_妖精}", RAND:100 < 30
				)
		}
	CASE 慰安_行き先_月の都
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_月人}, {慰安_キャラ_月兎}", 1
				)
		}
	CASE 慰安_行き先_天界
		RETURNF 慰安_キャラ_天人
	CASE 慰安_行き先_湖
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_民}, {慰安_キャラ_チャラ男}, {慰安_キャラ_子供}", 1
				)
		}
	CASE 慰安_行き先_森
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_妖怪}", 1,
				@"{慰安_キャラ_犬}, {慰安_キャラ_猿}", RAND:100 < 20
				)
		}
	CASE 慰安_行き先_寺子屋
		{
			RETURNF IFRAND(
				@"{慰安_キャラ_保護者}", 1,
				@"{慰安_キャラ_子供}", RAND:100 < 60
				)
		}
ENDSELECT
RETURNF 慰安_キャラ_民

;-------------------------------------------------
;「民の慰安」のモブ種類に応じた性別の取得処理
;-------------------------------------------------
@CREATE_IAN_MOB_GET_SEX(種類)
#FUNCTION
#DIM 種類
SELECTCASE 種類
	CASE 慰安_キャラ_妖精, 慰安_キャラ_貴族, 慰安_キャラ_天狗, 慰安_キャラ_死神, 慰安_キャラ_河童, 慰安_キャラ_民, 慰安_キャラ_露出狂, 慰安_キャラ_子供, 慰安_キャラ_月人, 慰安_キャラ_月兎, 慰安_キャラ_天人, 慰安_キャラ_保護者
		SELECTCASE CONFIG:14
			CASE 0
				RETURNF IFRAND("0", 1, "1", RAND:100 < 50, "2", RAND:100 < 10)
			CASE 1
				RETURNF 0
			CASE 2
				RETURNF 1
			CASE 3
				RETURNF 2
		ENDSELECT
	CASE 慰安_キャラ_ホフゴブリン
		RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;「民の慰安」のモブ種類に応じた名前の取得処理
;-------------------------------------------------
@CREATE_IAN_MOB_GET_NAME(種類, 性別)
#FUNCTIONS
#DIM 種類
#DIM 性別
SELECTCASE 種類
	CASE 慰安_キャラ_民
		RETURNF "領民"
	CASE 慰安_キャラ_貧民
		RETURNF "貧民"
	CASE 慰安_キャラ_浮浪者
		RETURNF "流浪漢"
	CASE 慰安_キャラ_ごろつき
		RETURNF "地痞"
	CASE 慰安_キャラ_富豪
		RETURNF "富豪"
	CASE 慰安_キャラ_貴族
		RETURNF 性別 == 0 ? "貴族" # "貴婦人"
	CASE 慰安_キャラ_資産家
		RETURNF "資産家"
	CASE 慰安_キャラ_天狗
		RETURNF "天狗"
	CASE 慰安_キャラ_河童
		RETURNF "河童"
	CASE 慰安_キャラ_鬼
		RETURNF "鬼"
	CASE 慰安_キャラ_妖怪
		RETURNF "妖怪"
	CASE 慰安_キャラ_露出狂
		RETURNF 性別 == 0 ? "露出狂" # "痴女"
	CASE 慰安_キャラ_兎
		RETURNF "兎"
	CASE 慰安_キャラ_野盗
		RETURNF "土匪"
	CASE 慰安_キャラ_子供
		RETURNF "小孩"
	CASE 慰安_キャラ_兵士
		RETURNF "士兵"
	CASE 慰安_キャラ_犬
		RETURNF "犬"
	CASE 慰安_キャラ_豚
		RETURNF "豚"
	CASE 慰安_キャラ_悪魔
		RETURNF "悪魔"
	CASE 慰安_キャラ_妖精
		RETURNF "妖精"
	CASE 慰安_キャラ_死神
		RETURNF "死神"
	CASE 慰安_キャラ_幽霊
		RETURNF "幽霊"
	CASE 慰安_キャラ_ホフゴブリン
		RETURNF "哥布林"
	CASE 慰安_キャラ_月人
		RETURNF "月人"
	CASE 慰安_キャラ_月兎
		RETURNF "月兎"
	CASE 慰安_キャラ_天人
		RETURNF 性別 == 0 ? "天人" # "天女"
	CASE 慰安_キャラ_チャラ男
		RETURNF "軽浮男"
	CASE 慰安_キャラ_馬
		RETURNF "馬"
	CASE 慰安_キャラ_猿
		RETURNF "猿"
	CASE 慰安_キャラ_保護者
		RETURNF "保護者"
ENDSELECT

;-------------------------------------------------
;「民の慰安」のモブ種類に応じた能力セット処理
;-------------------------------------------------
@CREATE_IAN_MOB_SET_STATUS(種類, 対象)
#DIM 種類
#DIM 対象
SELECTCASE 種類
	CASE 慰安_キャラ_貧民, 慰安_キャラ_浮浪者
		ABL:対象:奉仕 = 0
		ABL:対象:性技 = RAND:2
		ABL:対象:欲望 = RAND(3, 7)
		ABL:対象:性交 = RAND(4, 7)
		ABL:対象:射精 = RAND(4, 7)
		ABL:対象:加虐 = RAND(1, 4)
		ABL:対象:主導度Ｕ = RAND(0, 500)
		ABL:対象:倒錯度 = RAND(0, 300)
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
		COM_TENDENCY:対象:36 = 1
		COM_TENDENCY:対象:46 = 1
		COM_TENDENCY:対象:55 = 1
		COM_TENDENCY:対象:56 = 1
		COM_TENDENCY:対象:57 = 1
		COM_TENDENCY:対象:58 = 1
	CASE 慰安_キャラ_ごろつき, 慰安_キャラ_兵士
		ABL:対象:奉仕 = 1
		ABL:対象:性技 = RAND(0, 4)
		ABL:対象:欲望 = RAND(3, 7)
		ABL:対象:性交 = RAND(3, 5)
		ABL:対象:射精 = RAND(3, 5)
		ABL:対象:加虐 = RAND(0, 3)
		ABL:対象:主導度Ｕ = RAND(0, 500)
		ABL:対象:倒錯度 = RAND(0, 300)
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
	CASE 慰安_キャラ_富豪, 慰安_キャラ_貴族, 慰安_キャラ_資産家
		ABL:対象:欲望 = RAND(2,6)
		ABL:対象:性技 = RAND(2,5)
		ABL:対象:奉仕 = RAND(0,3)
		ABL:対象:性交 = RAND(1,3)
		ABL:対象:百合 = RAND(0,2)
		ABL:対象:精愛 = RAND(1,3)
		ABL:対象:露出 = RAND:2
		ABL:対象:射精 = RAND(0,2)
		ABL:対象:排泄 = RAND(0,2)
		ABL:対象:主導度Ｕ = RAND(0, 500)
		ABL:対象:倒錯度 = RAND(0, 500)
		TALENT:対象:自尊心高 = 1
		TALENT:対象:自尊心低 = 0
	CASE 慰安_キャラ_天狗, 慰安_キャラ_河童, 慰安_キャラ_死神, 慰安_キャラ_幽霊
		ABL:対象:欲望 = RAND(4,7)
		ABL:対象:性技 = RAND(3,8)
		ABL:対象:奉仕 = RAND(1,4)
		ABL:対象:性交 = RAND(0,4)
		ABL:対象:精愛 = RAND(0,2)
		ABL:対象:射精 = RAND(0,3)
		ABL:対象:排泄 = RAND(0,2)
		SIF 種類 == 慰安_キャラ_天狗
			ABL:対象:加虐 = RAND(1, 4)
		ABL:対象:主導度Ｕ = RAND(300, 500)
		ABL:対象:倒錯度 = RAND(300, 500)
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:自尊心高 = 1
		TALENT:対象:自尊心低 = 0
		IF 種類 == 慰安_キャラ_河童
			TALENT:対象:技師 = 1
			COM_TENDENCY:対象:60 = 1
			COM_TENDENCY:対象:61 = 1
			COM_TENDENCY:対象:62 = 1
			COM_TENDENCY:対象:63 = 1
			COM_TENDENCY:対象:64 = 1
			COM_TENDENCY:対象:65 = 1
		ENDIF
	CASE 慰安_キャラ_鬼
		ABL:対象:Ｃ感 = RAND(0,2)
		ABL:対象:Ｖ感 = RAND(0,2)
		ABL:対象:Ａ感 = RAND(0,2)
		ABL:対象:Ｂ感 = RAND(0,2)
		ABL:対象:Ｍ感 = RAND(0,2)
		ABL:対象:欲望 = RAND(4,8)
		ABL:対象:性技 = RAND(1,4)
		ABL:対象:奉仕 = RAND:2
		ABL:対象:性交 = RAND(5,8)
		ABL:対象:射精 = RAND(0,5)
		ABL:対象:主導度Ｕ = RAND(300, 700)
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:自尊心高 = 1
		TALENT:対象:自尊心低 = 0
	CASE 慰安_キャラ_露出狂
		ABL:対象:欲望 = RAND(5,8)
		ABL:対象:露出 = RAND(5,10)
		ABL:対象:排泄 = RAND(4,8)
		ABL:対象:主導度Ｕ = RAND(600, 1000)
		ABL:対象:倒錯度 = RAND(600, 1000)
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:不知羞恥 = 1
		TALENT:対象:害羞 = 0
		TALENT:対象:好奇心 = 1
		TALENT:対象:解放 = 1
		COM_TENDENCY:対象:85 = 1
		COM_TENDENCY:対象:86 = 1
		COM_TENDENCY:対象:93 = 1
		COM_TENDENCY:対象:1110 = 1
		COM_TENDENCY:対象:1111 = 1
		COM_TENDENCY:対象:112 = 1
		COM_TENDENCY:対象:114 = 1
	CASE 慰安_キャラ_兎, 慰安_キャラ_月兎, 慰安_キャラ_チャラ男
		ABL:対象:欲望 = RAND(5,10)
		ABL:対象:性技 = RAND:2
		ABL:対象:性交 = RAND(5,10)
		ABL:対象:射精 = RAND(5,9)
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
		COM_TENDENCY:対象:36 = 2
		COM_TENDENCY:対象:46 = 2
		COM_TENDENCY:対象:55 = 2
		COM_TENDENCY:対象:56 = 2
		COM_TENDENCY:対象:57 = 2
		COM_TENDENCY:対象:58 = 2
	CASE 慰安_キャラ_野盗
		ABL:対象:Ｃ感 = RAND(1, 5)
		ABL:対象:欲望 = 5
		ABL:対象:性技 = RAND(2, 5)
		ABL:対象:奉仕 = 3
		ABL:対象:性交 = 5
		ABL:対象:露出 = 2
		ABL:対象:射精 = 3
		ABL:対象:排泄 = 2
		ABL:対象:加虐 = RAND(0, 3)
		ABL:対象:主導度Ｕ = 500
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		TALENT:対象:特殊勢力素質 = 特殊勢力_野盗
	CASE 慰安_キャラ_子供, 慰安_キャラ_妖精
		ABL:対象:性知識 = RAND(3, 6)
		ABL:対象:欲望 = RAND:3
		ABL:対象:性技 = RAND:3
		ABL:対象:性交 = RAND:2
		ABL:対象:射精 = RAND:2
		CALL VIRGINIZE(対象, 初体験_処女)
		CALL VIRGINIZE(対象, 初体験_アナル処女)
		CALL VIRGINIZE(対象, 初体験_童貞)
		CALL VIRGINIZE(対象, 初体験_キス)
	CASE 慰安_キャラ_犬, 慰安_キャラ_豚, 慰安_キャラ_馬, 慰安_キャラ_猿
		ABL:対象:Ｃ感 = RAND(3, 6)
		ABL:対象:欲望 = RAND(3, 8)
		ABL:対象:性技 = RAND:3
		ABL:対象:奉仕 = RAND(2, 5)
		ABL:対象:性交 = RAND(5, 8)
		ABL:対象:射精 = RAND(3, 8)
		TALENT:対象:動物耳 = 1
		TALENT:対象:尾巴 = 1
		SELECTCASE 種類
			CASE 慰安_キャラ_犬
				TALENT:対象:動物 = 動物_犬
			CASE 慰安_キャラ_豚
				TALENT:対象:動物 = 動物_豚
			CASE 慰安_キャラ_馬
				TALENT:対象:動物 = 動物_馬
			CASE 慰安_キャラ_猿
				TALENT:対象:動物 = 動物_猿
		ENDSELECT
	CASE 慰安_キャラ_悪魔
		ABL:対象:欲望 = RAND(4,7)
		ABL:対象:性技 = RAND(3,8)
		ABL:対象:奉仕 = RAND(1,4)
		ABL:対象:性交 = RAND(0,4)
		ABL:対象:精愛 = RAND(0,2)
		ABL:対象:射精 = RAND(0,3)
		ABL:対象:排泄 = RAND(0,2)
		ABL:対象:加虐 = RAND(3, 6)
		ABL:対象:主導度Ｕ = RAND(300, 500)
		ABL:対象:倒錯度 = RAND(300, 500)
		TALENT:対象:Ｓ気質 = 1
		SETBIT TALENT:対象:淫乱系, 素質_淫乱_サド
		TALENT:対象:不在乎貞操 = 1
		TALENT:対象:貞操観念 = 0
		TALENT:対象:自尊心高 = 1
		TALENT:対象:自尊心低 = 0
	CASE 慰安_キャラ_ホフゴブリン
		ABL:対象:Ｃ感 = 4
		ABL:対象:欲望 = 6
		ABL:対象:性技 = 4
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 2
		ABL:対象:性交 = 4
		ABL:対象:露出 = 1
		ABL:対象:射精 = 4
		ABL:対象:排泄 = 2
		ABL:対象:加虐 = RAND(0, 3)
		ABL:対象:主導度Ｕ = 700
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		EXP:対象:絶頂経験 = 500
		TALENT:対象:特殊勢力素質 = 特殊勢力_ホフゴブリン
		COM_TENDENCY:対象:36 = 1
		COM_TENDENCY:対象:46 = 1
		COM_TENDENCY:対象:55 = 1
		COM_TENDENCY:対象:56 = 1
		COM_TENDENCY:対象:57 = 1
		COM_TENDENCY:対象:58 = 1
	CASE 慰安_キャラ_天人, 慰安_キャラ_月人
		ABL:対象:欲望 = 5
		ABL:対象:性技 = 5
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 5
		ABL:対象:性交 = 0
		ABL:対象:射精 = 0
		ABL:対象:排泄 = 2
		ABL:対象:加虐 = RAND(0, 4)
		ABL:対象:主導度Ｕ = 700
		ABL:対象:倒錯度 = 600
		TALENT:対象:技師 = 1
		TALENT:対象:自尊心高 = 1
		TALENT:対象:自尊心低 = 0
		TALENT:対象:反抗的 = 1
	CASEELSE
		;デフォ
ENDSELECT

;-------------------------------------------------
;「民の慰安」開始時のメッセージ
;-------------------------------------------------
@SHOW_IAN_MESSAGE(対象, 人数)
#DIM 対象
#DIM 人数
SIF !CFLAG:MASTER:所属
	RETURN 0

SELECTCASE IFRAND("0TO2", 1, "3", IS_FEMALE(対象))
	CASE 0
		PRINTFORML 民衆高興的話勢力也会変強
		PRINTFORML 所以、用這個身体讓民衆開心吧
	CASE 1
		PRINTFORML 民衆的情欲必須祛除
		PRINTFORML 「祛除」情欲是你的責任
	CASE 2
		PRINTFORML 給你下達了去慰安民衆的任務
		PRINTFORML 要好好的讓他們満足
	CASE 3
		PRINTFORML 優秀的士官的孩子肯定也十分優秀
		PRINTFORML 所以必須讓她懷孕
ENDSELECT
PRINTFORMW 下達了命令之後、把%ANAME(対象) + (人数 > 1 ? "們" # "")%送出去了……

SIF RAND:3
	RETURN

IF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_淫乱) && !RAND:3
	PRINTFORMW %ANAME(対象)%期待着淫乱的行為、身体有些按耐不住了……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_精液便女) && !RAND:3
	PRINTFORMW 期待着被人当作肉便器使用、%ANAME(対象)%無法隠藏自己的興奮了……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_娼婦) && !RAND:3
	PRINTFORMW 和娼婦有些不一樣的工作讓%ANAME(対象)%有些困惑、但儘管如此還是展現着積極的態度……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_マゾ) && !RAND:3
	PRINTFORMW %ANAME(対象)%期待着被誰凌虐的場景、身体有些按耐不住了……
ELSEIF GETBIT(TALENT:対象:淫乱系, 素質_淫乱_孕みたがり) && !RAND:3
	PRINTFORMW 想像着完成任務之後懷上了別人的孩子、讓%ANAME(対象)%変得興奮起来了……
ELSEIF IS_LOVER(対象) && !RAND:3
	SELECTCASE ABL:対象:欲望
		CASE IS > ランク閾値:ランク_その他:ランク_B
			PRINTFORMW 和%ANAME(MASTER)%以外的人做愛、讓%ANAME(対象)%変得興奮起来了……
		CASE IS > ランク閾値:ランク_その他:ランク_D
			PRINTFORMW 抗拒和%ANAME(MASTER)%以外的人做愛的%ANAME(対象)%、但是身体違背了意志変得興奮起来了……
		CASEELSE
			PRINTFORMW 雖然%ANAME(対象)%完全無法理解%ANAME(MASTER)%這個命令的意図、但還是默默接受了……
	ENDSELECT
ELSEIF IS_SLAVE(対象) && !RAND:3
	SELECTCASE ABL:対象:欲望
		CASE IS > ランク閾値:ランク_その他:ランク_B
			PRINTFORMW 在之前就是和誰能都做愛的%ANAME(対象)%、因為%ANAME(MASTER)%的命令、変得興奮起来了……
		CASE IS > ランク閾値:ランク_その他:ランク_D
			PRINTFORMW %ANAME(対象)%説道既然這是%ANAME(MASTER)%的命令的話、但是身体違背了意志変得興奮起来了……
		CASEELSE
			PRINTFORMW 沒有辦法%ANAME(対象)%只能接受了%ANAME(MASTER)%的命令……
	ENDSELECT
ELSEIF !RAND:3
	PRINTFORMW %ANAME(対象)%、沒有辦法只能接受了這個任務……
ENDIF


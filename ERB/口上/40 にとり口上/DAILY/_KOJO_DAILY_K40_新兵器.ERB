;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K40_NEW_WEAPON_RATE(対象)
#DIM 対象
RETURN 50 + (KDVAR:対象:にとり_新兵器開発 > 0) * 100


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K40_NEW_WEAPON_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:にとり_新兵器開発 == -1
	RETURN 0

SIF !TALENT:MASTER:技師
	RETURN 0

SIF GET_EMPTY_SKILL_SLOT(対象, スキル_ジャンル_PERSONAL) == -1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K40_NEW_WEAPON_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K40_NEW_WEAPON(対象)
#DIM 対象


IF KDVAR:対象:にとり_新兵器開発 == 0
	PRINTFORMW 「吶吶、%ANAME(MASTER)%、有件事想你幇忙」
	PRINTFORMW %ANAME(MASTER)%在工作的時候、聽到了%ANAME(対象)%的声音
	PRINTFORMW 「其実最近、我正在開発新的兵器、但是遇上了瓶頸」
	PRINTFORMW 「所以呢、如果能夠借用你聰明的点子的話我会很高興的……」
ELSE
	PRINTFORMW 「呀呀%ANAME(MASTER)%。不好意思、又是兵器開発、能再幇我忙嗎？」
ENDIF
PRINTFORMW 怎麼辦……
PRINTFORML
CALL ASK_YN("幇忙", "現在很忙")

IF RESULT == 1
	PRINTFORMW 「誒、這樣嗎？　真沒辦法呢」
	PRINTFORMW 「那麼我就一個人加油吧、嗯」
	PRINTFORMW %ANAME(対象)%離開了……
	KDVAR:対象:にとり_新兵器開発 = -1
	RETURN 1
ENDIF

PRINTFORMW 「嗯嗯、是這樣啊」
PRINTFORMW 「那麼我帯你去我的工房吧、拜託你了！」
PRINTFORMW
PRINTFORMW ……%ANAME(対象)%一整天都在幇忙開発新兵器……

IF KDVAR:対象:にとり_新兵器開発 * 100 > RAND:1000
	PRINTFORMW ……最後、新兵器的開発順利完成！
	PRINTFORMW 「成功了成功了！　終於完成了！」
	PRINTFORMW 「謝謝你%ANAME(MASTER)%、如果只有我一個的話都不知道能否完成呢！」
	PRINTFORMW 獲得了%ANAME(対象)%深深的感謝
	PRINTFORMW 雖然很累、但能夠見到這個笑容的話感覚也不錯……
	CALL COLOR_PRINTW(@"%ANAME(対象)%可以使用技能「射击吧！泡泡龙」了", カラー_注意)
	CALL COLOR_PRINTW(@"%ANAME(対象)%的武闘上昇了5", カラー_注意)
	ABL:対象:武闘 += 5
	KDVAR:対象:にとり_新兵器開発 = -1
	CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_PERSONAL, NO:対象, "射击吧！泡泡龙")
ELSE
	PRINTFORMW ……但是、最後還是未能完成開発
	PRINTFORMW 「嗯、看来再需要一点時間呢」
	PRINTFORMW 「如果可以的話、下次請継續幇忙」
	CALL ADD_COOLTIME(MASTER, 2)
	KDVAR:対象:にとり_新兵器開発 ++
ENDIF

RETURN 1

;-------------------------------------------------
;「投獄する」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME25
RESULTS:0 '= "投獄"
;TranslatedMark 投獄する

;-------------------------------------------------
;「投獄する」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK25
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
;(ホフゴブリン拡張パッチ)「適正手続の保障」制定時は不可
SIF STATUE_GOBLIN_COUNCIL:2 == 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「投獄する」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA25(ARG:0)
;非主人公、主人公より政治が低い、君主でない、捕虜でない、動物でない、育児されている子供でない、特殊キャラでない(従属国パッチ)(処刑拡張パッチ)育児されている子供も投獄可能に
{
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:ARG:捕虜先 == 0 && ARG:0 != GET_COUNTRY_BOSS(CFLAG:MASTER:所属) && !IS_ANIMAL(ARG:0) &&
 !IS_SP_CHARA(ARG:0)
}

;-------------------------------------------------
;「投獄する」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY25
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「投獄する」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW25
CALL COLUMN_RIGHT_TITLE("対象者選択(會被関入監獄的対象討厭)", "0", "1", "0", "0")
;TranslatedMark 対象者選択(投獄した相手には嫌われます)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_CHARALIST()
RETURN 1

;-------------------------------------------------
;「投獄する」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP25(ARG:0)
#DIM SAVE_LINE_SHOP_LIFE

FLAG:能力表示モード = 0
SAVE_LINE_SHOP_LIFE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 用適当的罪名、把%ANAME(ARG:0)%打入大牢
;TranslatedMark %ANAME(ARG:0)%に適当な罪を被せ、投獄します
PRINTFORML 当然、肯定会被本人討厭
;TranslatedMark 当然、本人からは嫌われます
PRINTFORML 確定要実行麼？
;TranslatedMark それでも実行しますか？
CALL ASK_YN
IF RESULT == 0
	SETCOLOR カラー_注意
	PRINTFORMW <%ANAME(ARG:0)%>被打入了大牢
	;TranslatedMark <%ANAME(ARG:0)%>を投獄しました
	RESETCOLOR
	CALL CAPTURE(ARG:0, CFLAG:MASTER:所属)
	CFLAG:(ARG:0):2 -= MAX(500, CFLAG:(ARG:0):2 * 3 / 10)
	CFLAG:(ARG:0):3 -= MAX(500, CFLAG:(ARG:0):3 * 3 / 10)
	CFLAG:(ARG:0):4 -= MAX(500, CFLAG:(ARG:0):4 * 3 / 10)
	RETURN 1
ELSE
	LINES_SHOP = SAVE_LINE_SHOP_LIFE
	RETURN 0
ENDIF



;兵法学習

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME303
RESULTS:0 = 邀請学習兵法
RESULTS:1 = 陪著学習兵法


;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE303
;共通部分
CALL COM_ABLE_COMMON(303)
SIF RESULT == 0
	RETURN 0
;捕虜会話では不可
SIF FLAG:調教モード == 6
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;デート中は無理
SIF TFLAG:54
	RETURN 0
;臨月or怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月 || CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM303
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "知略"), 0)

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE303
;主導権が主人公側
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %ANAME(MPLY:0)%和%ANAME(MTAR:0)%一起学習了兵法
ELSE
	;相手より知略が10以上劣っている
	IF ABL:(MPLY:0):知略 + 10 <= ABL:(MTAR:0):知略
		PRINTFORMW %ANAME(MPLY:0)%被%ANAME(MTAR:0)%認真地教授了兵法
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%陪著%ANAME(MTAR:0)%学習了兵法
	ENDIF
ENDIF

IF TFLAG:18 == 1
	PRINTFORML 理解了至今為止還不明白的部分。度過了充実的時間…
ELSEIF TFLAG:18 == -1
	PRINTFORML ……完全無法理解
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST303
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN303
RETURN コマンド_日常

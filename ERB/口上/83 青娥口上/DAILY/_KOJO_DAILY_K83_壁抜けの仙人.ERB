;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K83_PRISON_BREAK_RATE(対象)
#DIM 対象
RETURN 200

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K83_PRISON_BREAK_DECISION(対象)
#DIM 対象

SIF CFLAG:対象:捕虜先 == CFLAG:MASTER:所属
	RETURN 0

SIF FLAG:クリアフラグ
	RETURN 0
	
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, -1, 1, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K83_PRISON_BREAK_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K83_PRISON_BREAK(対象)
#DIM 対象

PRINTFORMW 「虽然偶尔被这么对待也挺有趣的，但是已经厌倦了呢。我就拿点值钱的东西再回去算了。」
;TranslatedMark 「たまにはこういうのも面白いかと思ったけど、もう飽きたちゃったわね。それじゃあ、金目の物を頂いて帰ろうかしら」
PRINTFORM 被关在牢里的
;TranslatedMark 牢につながれていた
CALL COLOR_PRINT(@"%NAME_FORMAL(対象)%", カラー_注意)
PRINTFORML 用穿墙术逃了出来，顺便物色了一下值钱的东西……
;TranslatedMark は、壁抜けの術で牢から脱出し、ついでに金目の物を物色して行った……
CALL COLOR_PRINTW(@"被%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:捕虜先))%囚禁的%ANAME(対象)%已经不再是俘虏了", カラー_注意)

LOCAL = MIN(RAND(300, 750), MONEY:(CFLAG:対象:捕虜先))
MONEY:(CFLAG:対象:捕虜先) -= LOCAL

CALL COLOR_PRINTW(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:捕虜先))%的国库减少了{LOCAL}", カラー_注意)
CALL CAPTURE(対象, 0)

RETURN 1

@KOJO_DAILY_K83_PRISON_BREAK_ALLOW_WHEN_WANDERING()
RETURN 1

;-------------------------------------------------
;「徴兵解雇」の名称
;-------------------------------------------------
@SHOP_SLG_NAME62
RESULTS:0 '= "徴兵/解雇"

;-------------------------------------------------
;「徴兵解雇」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK62
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「徴兵解雇」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY62
CALL SLG_EXTRA_DRAFT
RETURN 1

;-------------------------------------------------
;「徴兵する」の次行説明指定有無
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD62

;-------------------------------------------------
;「徴兵する」の次行説明指定内容
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD_TEXT62
PRINTFORM %"費率:" + NUM_FORMAT(SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:所属)), 12%

;------------------------------------------------
;「徴兵解雇」本体
;-------------------------------------------------
@SLG_EXTRA_DRAFT()
#DIM 雇用レート
#DIM 解雇レート
#DIM 雇用兵数限界
#DIM 輸入値
#DIM 兵数
雇用レート = SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:所属)
解雇レート = MAX(雇用レート / 2, 1)
兵数 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
$SHOW_LOOP
CALL SINGLE_DRAWLINE
PRINTFORML 消費国庫的資金可以臨時進行徴兵
PRINTFORML 輸入負数的時候是進行解雇可以獲得資金
PRINTFORML 雇傭多少人/解雇多少人？(輸入0時取消)

PRINTFORM 現在的国庫:
SETCOLOR カラー_選択中
PRINTFORM {MONEY:(CFLAG:MASTER:所属)}
RESETCOLOR
PRINT    
PRINTFORM 雇用費率:
SETCOLOR カラー_選択中
PRINTFORM {雇用レート}
RESETCOLOR
PRINTFORM /兵
PRINT    
PRINTFORM 解雇費率:
SETCOLOR カラー_選択中
PRINTFORM {解雇レート}
RESETCOLOR
PRINTFORM /兵
PRINT    
PRINTFORM 現在的兵数（防衛・部隊所属除外）:
SETCOLOR カラー_選択中
PRINTFORM {兵数}
RESETCOLOR
PRINTL 
CALL SINGLE_DRAWLINE
IF MONEY:(CFLAG:MASTER:所属) - 100 * 雇用レート > 0
	PRINTFORM [{100, 8}] {100, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {100, 10} = {兵数 + 100, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {100 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 100 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{100, 8}] {100, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {100, 10} = {兵数 + 100, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {100 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 100 * 雇用レート, 10}
	RESETCOLOR
ENDIF

IF MONEY:(CFLAG:MASTER:所属) - 1000 * 雇用レート > 0
	PRINTFORM [{1000, 8}] {1000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {1000, 10} = {兵数 + 1000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {1000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 1000 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{1000, 8}] {1000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {1000, 10} = {兵数 + 1000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {1000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 1000 * 雇用レート, 10}
	RESETCOLOR
ENDIF

IF MONEY:(CFLAG:MASTER:所属) - 10000 * 雇用レート > 0
	PRINTFORM [{10000, 8}] {10000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {10000, 10} = {兵数 + 10000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {10000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 10000 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{10000, 8}] {10000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {10000, 10} = {兵数 + 10000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {10000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 10000 * 雇用レート, 10}
	RESETCOLOR
ENDIF

雇用兵数限界 = MONEY:(CFLAG:MASTER:所属) / 雇用レート
LOCALS:0 = 直到極限
IF 雇用兵数限界 > 0
	PRINTFORM [{雇用兵数限界, 8}] %LOCALS:0, 8%徴兵
	PRINTFORML   兵数:{兵数, 10} + {雇用兵数限界, 10} = {兵数 + 雇用兵数限界, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {雇用兵数限界 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 雇用兵数限界 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{雇用兵数限界, 8}] %LOCALS:0, 8%徴兵
	PRINTFORML   兵数:{兵数, 10} + {雇用兵数限界, 10} = {兵数 + 雇用兵数限界, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {雇用兵数限界 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 雇用兵数限界 * 雇用レート, 10}
	RESETCOLOR
ENDIF
CALL SINGLE_DRAWLINE
PRINTFORML [{0, 3}] 還是算了
CALL SINGLE_DRAWLINE
$INPUT_LOOP
INPUT 0
輸入値 = RESULT
IF 輸入値 == 0
	RETURN
ELSEIF 輸入値 > 0 && MONEY:(CFLAG:MASTER:所属) >= 雇用レート * 輸入値
	CALL ICPRINT(@"從国庫支出了<{輸入値 * 雇用レート}>、雇用{輸入値}士兵。", "L", カラー_注意)
	CALL ICPRINT(@"雇用後的国庫資金為{MONEY:(CFLAG:MASTER:所属) - 輸入値 * 雇用レート}。確定麼？", "L", カラー_注意)
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 雇用了{輸入値}士兵！
		PRINTFORML 兵数:{兵数} → {兵数 + 輸入値}
		PRINTFORMW 資金:{MONEY:(CFLAG:MASTER:所属)} → {MONEY:(CFLAG:MASTER:所属) - 輸入値 * 雇用レート}
		COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += 輸入値
		TMP_SUM_SOLDIER:(CFLAG:MASTER:所属) += 輸入値
		MONEY:(CFLAG:MASTER:所属) -= 輸入値 * 雇用レート
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSEIF 輸入値 < 0 && 輸入値 * -1 <= 兵数
	輸入値 *= -1
	PRINTFORM 兵
	CALL COLOR_PRINT(@"{輸入値}", カラー_注意)
	PRINTFORM 被解雇了、
	CALL COLOR_PRINT(@"{輸入値 * 解雇レート}", カラー_注意)
	PRINTFORML 資金被得到了。
	PRINTFORM 解雇後的資金為
	CALL COLOR_PRINT(@"{MONEY:(CFLAG:MASTER:所属) + 輸入値 * 解雇レート}", カラー_注意)
	PRINTFORML 。
	PRINTFORML 確定麼？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 解雇了{輸入値}士兵！
		PRINTFORML 兵数:{兵数} → {兵数 - 輸入値}
		PRINTFORMW 資金:{MONEY:(CFLAG:MASTER:所属)} → {MONEY:(CFLAG:MASTER:所属) + 輸入値 * 解雇レート}
		COUNTRY_SOLDIER: (CFLAG:MASTER:所属) -= 輸入値
		TMP_SUM_SOLDIER:(CFLAG:MASTER:所属) -= 輸入値
		MONEY:(CFLAG:MASTER:所属) += 輸入値 * 解雇レート
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSEIF 輸入値 > 0 && MONEY:(CFLAG:MASTER:所属) < 雇用レート * 輸入値
	PRINTFORML 需要{輸入値 * 雇用レート}金、但是並沒有那麼多資金
	GOTO SHOW_LOOP
ELSEIF 輸入値 < 0 && 輸入値 * -1 > 兵数
	輸入値 *= -1
	PRINTFORML 想解雇{輸入値}人、但是並沒有那麼多士兵
	GOTO SHOW_LOOP
ELSE
ENDIF

;-------------------------------------------------
;徴兵解雇の雇用レート計算
;-------------------------------------------------
@SLG_EXTRA_DRAFT_CALC_RATE(勢力)
#FUNCTION
#DIM 都市レート基準
#DIM 雇用レート
#DIM 勢力

;星評価で増減する
SELECTCASE TMP_COUNTRY_STARS:勢力
	CASE 5
		雇用レート = 3
	CASE 3, 4
		雇用レート = 2
	CASE 1, 2
		雇用レート = 1
ENDSELECT

;全都市数の1/10を獲得するごとに1増える
都市レート基準 = MAX(GET_CITY_NUM() / 10, 1)
雇用レート += MAX(TMP_OWN_CITY:勢力 / 都市レート基準, 0)

;徴兵限界を越えているなら+1
;徴兵限界をまたぐように徴兵するとちょっとお得になってしまうんだけど、
;考慮すると処理がめんどくさくなるんでパス
SIF TMP_SUM_SOLDIER:勢力 > GET_RECRUIT_LIMIT(勢力)
	雇用レート += 1


RETURNF 雇用レート

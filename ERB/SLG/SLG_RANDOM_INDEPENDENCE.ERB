@SELECT_CHARA_RANDOM_LOGIC_SLG_RANDOM_INDEPENDENCE(対象)
#DIM 対象
RETURN CAN_RANDOM_INDEPENDENCE(対象)

@SELECT_CHARA_RANDOM_BIAS_SLG_RANDOM_INDEPENDENCE(対象)
#DIM 対象
RETURN ABL:対象:野心

;----------------------------------------------
;謀反を起こすNPCの候補を選択し、確率ロール
;----------------------------------------------
@SLG_RANDOM_INDEPENDENCE
#DIM 結果
#DIM 判定値
#DIM 野心無視フラグ

;10期以降
SIF !(DAY >= 10)
	RETURN 0

;10%の確率で野心をフラットに扱う
野心無視フラグ = RAND:100 < 10

IF 野心無視フラグ
	CALL SELECT_CHARA_RANDOM("SLG_RANDOM_INDEPENDENCE")
ELSE
	CALL SELECT_CHARA_RANDOM("SLG_RANDOM_INDEPENDENCE", "SLG_RANDOM_INDEPENDENCE")
ENDIF

結果 = RESULT

SIF 結果 == -1
	RETURN 0

判定値 = CALC_INDEPENDENCE_RATE(結果)

;確率ロールに失敗したら戻る
IF 判定値 <= RAND:10000
	RETURN 0
ENDIF
IF CHECK_BUILDING_FUNCTION_SELECT(-1,CFLAG:結果:所属,建造物_国安部) > 0
	IF RAND:100 >= 25
		BUILDING_TEXT = BUILDING_TEXT_国安部_1(CFLAG:結果:所属,結果,1)
		RETURN 0
	ELSE
		BUILDING_TEXT = BUILDING_TEXT_国安部_1(CFLAG:結果:所属,結果,0)
	ENDIF
ENDIF
CALL NPC_INDEPENDENCE(結果)

RETURN 1

;----------------------------------------------
;謀反できる状態か
;----------------------------------------------
@CAN_RANDOM_INDEPENDENCE(対象)
#FUNCTION
#DIM 対象
SIF 対象 == MASTER
	RETURNF 0
;無所属でない／いずれかの勢力の捕虜ではない／放浪もしくは死亡状態ではないか
SIF !IS_COUNTRY(CFLAG:対象:所属) || CFLAG:対象:捕虜先 || CFLAG:対象:特殊状態
	RETURNF 0
;さすがに君主が謀反起こしたらダメですよね。
SIF GET_COUNTRY_BOSS(CFLAG:(対象):所属) == 対象
	RETURNF 0
;SPキャラも謀反を起こさない
SIF IS_SP_CHARA(対象)
	RETURNF 0
;特殊勢力所属なら駄目
SIF IS_SP_COUNTRY(CFLAG:対象:所属)
	RETURNF 0
;(従属国パッチ)辺境伯は駄目
SIF CFLAG:対象:1702 == 1
	RETURNF 0
;野心が皆無なやつも謀反を起こさない
SIF ABL:対象:野心 <= 0
	RETURNF 0
;主人公の勢力の士官は、陥落している場合は謀反しない
;ただし印象値が非常に悪い場合はなおも謀反を企てる
;閾値は本人の野心によって変動する
{
	SIF CFLAG:対象:所属 == CFLAG:MASTER:所属 && GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER && IS_FALLEN(対象) &&
		REL_HATE:対象:MASTER - REL_LIKE:対象:MASTER < 1500 * (100 - ABL:対象:野心) / 100
}
	RETURNF 0
RETURNF 1

;----------------------------------------------
;謀反を起こす確率を算出
;----------------------------------------------
@CALC_INDEPENDENCE_RATE(対象, 野心無視フラグ = 0)
#FUNCTION
#DIM 対象
#DIM 判定値
#DIM 野心無視フラグ

;野心とREL_LIKE/HATE、所有都市数、DAYから判定値を算出

IF 野心無視フラグ
	判定値 = 160
ELSE
	判定値 = ABL:対象:野心 * 2
ENDIF

SELECTCASE ABL:対象:野心
	CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_A
		TIMES 判定値, 1.5
	CASE IS  >= ランク閾値:ランク_ＳＬＧ:ランク_B
		TIMES 判定値, 1.25
	CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_C
		TIMES 判定値, 1.1
ENDSELECT

判定値 -= REL_LIKE:対象:(GET_COUNTRY_BOSS(CFLAG:対象:所属)) / 30
判定値 += REL_HATE:対象:(GET_COUNTRY_BOSS(CFLAG:対象:所属)) / 50

;都市数が多いほど離反しやすい
判定値 += GET_OWN_CITY(CFLAG:対象:所属) * 2

;終盤は離反しにくくする（ただでさえ終わらない観戦モードがさらに終わらなくなるので）
判定値 -= LIMIT(DAY / 2 - 50, 0, 200)

SIF TALENT:対象:胆怯
	判定値 -= 5
SIF TALENT:対象:反抗的
	判定値 += 15
SIF TALENT:対象:坦率
	判定値 -= 8
SIF TALENT:対象:老実
	判定値 -= 5
SIF TALENT:対象:自尊心高
	判定値 += 20
SIF TALENT:対象:傲慢
	判定値 += 10
SIF TALENT:対象:自尊心低
	判定値 -= 15
SIF TALENT:対象:自制心
	判定値 -= 5
SIF TALENT:対象:保守的
	判定値 -= 5
SIF TALENT:対象:好奇心
	判定値 += 5
SIF TALENT:対象:難以越過的底線
	判定値 -= 15
SIF TALENT:対象:圧抑
	判定値 -= 5
SIF TALENT:対象:解放
	判定値 += 15
SIF TALENT:対象:孤高
	判定値 += 20

;3能力が200越えてたらプラス補正
SIF ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 > 250
	判定値 += MAX(ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 - 200, 50)

;対象キャラがMASTERと同一勢力に所属していた場合は好感度と従属度で補正
SIF CFLAG:対象:所属 == CFLAG:MASTER:所属
	判定値 -= MIN((CFLAG:対象:好感度 / 500 + CFLAG:対象:従属度 / 500 + CFLAG:対象:支配度), 100)

RETURNF 判定値

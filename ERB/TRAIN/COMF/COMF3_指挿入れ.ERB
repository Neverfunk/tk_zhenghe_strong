;指挿入れ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME3
IF IS_EQUIP_TARGET(MTAR:0, 61)
	IF MTAR_NUM >= 2
		LOCALS:0 = 同時振動棒责弄
	ELSE
		LOCALS:0 = 振動棒责弄
	ENDIF
ELSE
	IF MTAR_NUM >= 2
		LOCALS:0 = 同時指挿入
	ELSE
		LOCALS:0 = 指挿入
	ENDIF
ENDIF

RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE3
;共通部分
CALL COM_ABLE_COMMON(3)
SIF RESULT == 0
	RETURN 0
;1+ players
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットはプレイヤーの倍まで
SIF MTAR_NUM <= 0 || MTAR_NUM > MPLY_NUM * 2
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF !CAN_REACH_GROIN(MPLY:(LOCAL:0), MTAR:(LOCAL:1)) 
			RETURN 0
	NEXT
NEXT

FOR LOCAL:0, 0, MTAR_NUM
	SIF !HAS_VAGINA(MTAR:(LOCAL:0))
		RETURN 0
	;v occupied except by a vibrator
	SIF IS_V_HOLD(MTAR:(LOCAL:0)) && !IS_EQUIP_TARGET(MTAR:(LOCAL:0), 61)
		RETURN 0
	;If any target has a vibe inserted, then they all must
	SIF IS_EQUIP_TARGET(MTAR:0, 61) != IS_EQUIP_TARGET(MTAR:(LOCAL:0), 61)
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM3
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += MTAR_NUM / 2 + 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):接触 = 60
	SOURCE:(LOCAL:2):性行動 = 180

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 100, 40)

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100

		;ターゲットが処女なら快感減少
		IF TALENT:(LOCAL:3):処女 == 1
			TIMES LOCAL:4, 0.75
		ENDIF

		SOURCE:(LOCAL:3):快Ｖ += LOCAL:4

	NEXT
NEXT

;全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):露出 = 80
	SOURCE:(LOCAL:1):接触 = 60
	SOURCE:(LOCAL:1):性行動 = 300

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 50, 50)

	;無自覚処女なら処女喪失フラグを立てる
	IF TALENT:(LOCAL:1):処女 == 2
		IF IS_MPLY(MASTER)
			;処女喪失フラグ(主人に性交以外で奪われる)
			TCVAR:(LOCAL:1):13 = 2
		ELSE
			;処女喪失フラグ(主人以外に性交以外で奪われる)
			TCVAR:(LOCAL:1):13 = 4
		ENDIF
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP3
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP3(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 60

	;侍奉経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｖ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):露出 += 40
	SOURCE:(LOCAL:2):接触 += 30
	SOURCE:(LOCAL:2):性行動 += 100

NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE3(ARG:0)
IF IS_EQUIP_TARGET(MEQUIP_TARGET:(ARG:0):0, 61)
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%用振動棒责弄%EQUIP_TARGET_ANAME(ARG:0)%中
ELSE
	RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%用手指挿入%EQUIP_TARGET_ANAME(ARG:0)%的膣穴中
ENDIF

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP3(ARG:0)
IF IS_EQUIP_TARGET(MEQUIP_TARGET:(ARG:0):0, 61)
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正前後抽插著挿入到%EQUIP_TARGET_ANAME(ARG:0)%膣穴内的振動棒…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%正用手指来回摳弄著%EQUIP_TARGET_ANAME(ARG:0)%的秘所…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP3(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER3(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE3
;ターゲットが２人のとき
IF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;ターゲットが１人のとき
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
ENDIF
IF MTAR_NUM == 2 && IS_PIERCED(MTAR:0, ピアス_ラビア) && IS_PIERCED(MTAR:1, ピアス_ラビア)
	LOCALS:1 = 上著環的
ELSEIF MTAR_NUM == 1 && IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:1 = 上著環的
ELSE
	LOCALS:1 = 
ENDIF
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF IS_EQUIP_TARGET(MTAR:0, 61)
			PRINTFORMW %ANAME(MPLY:0)%抜挿著在%LOCALS:0%的%LOCALS:1%秘所內振動的按摩棒進行责弄…
		ELSEIF TALENT:(MTAR:0):処女 == 1
			PRINTFORMW %ANAME(MPLY:0)%用指尖肆意责弄著%LOCALS:0%的処女膜…
		ELSEIF IS_MPLY(MASTER) && MTAR_NUM == 1 && PALAM:(MTAR:0):潤滑 > PALAMLV:5
			PRINTFORMW %ANAME(MPLY:0)%把手指挿入到%LOCALS:0%的%LOCALS:1%完全濕透的秘唇的內部来回摳弄著…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%把手指挿入到%LOCALS:0%的%LOCALS:1%秘唇中来回摳弄著…
		ENDIF
	;ターゲットに主導権
	CASE 1
		IF IS_EQUIP_TARGET(MTAR:0, 61)
			PRINTFORMW %LOCALS:0%讓%ANAME(MPLY:0)%握住在自己的%LOCALS:1%秘所內振動的按摩棒、来回抜插…
		ELSEIF TALENT:(MTAR:0):処女 == 1
			PRINTFORMW %LOCALS:0%命令%ANAME(MPLY:0)%把手指按在了自己那還是少女的%LOCALS:1%秘唇処、溫柔的愛撫它…
		ELSE
			PRINTFORMW %LOCALS:0%讓%ANAME(MPLY:0)%的手指挿入了自己的%LOCALS:1%秘唇、愛撫著裡面…
		ENDIF
	;第三者に主導権
	CASEELSE
		IF IS_EQUIP_TARGET(MTAR:0, 61)
			PRINTFORML %ANAME(TFLAG:45)%対%ANAME(MPLY:0)%、下達了抜插挿入在%LOCALS:0%的膣穴內的振動棒的命令
			PRINTFORMW %ANAME(MPLY:0)%抜挿著在%LOCALS:0%的%LOCALS:1%秘所內振動的按摩棒進行责弄…
		ELSEIF TALENT:(MTAR:0):処女 == 1
			PRINTFORML %ANAME(TFLAG:45)%対%ANAME(MPLY:0)%、下達了把手指挿入%LOCALS:0%的%LOCALS:1%膣穴內進行愛撫的命令
			PRINTFORMW %ANAME(MPLY:0)%用指尖温柔愛撫著%LOCALS:0%的処女膜…
		ELSE
			PRINTFORML %ANAME(TFLAG:45)%対%ANAME(MPLY:0)%、下達了把手指挿入%LOCALS:0%的%LOCALS:1%膣穴內進行愛撫的命令
			PRINTFORMW %ANAME(MPLY:0)%把手指挿入到%LOCALS:0%的秘唇中来回摳弄著…
		ENDIF
ENDSELECT

IF !IS_EQUIP_TARGET(MTAR:0, 61) && (MTAR_NUM == 1) && CUM_CUR_AMOUNT:(MTAR:0):射精部位_膣内
	PRINTFORML %SPLIT_RAND("熱的/粘粘的/濃厚的/新鮮的", 1)%精液从%ANAME(MTAR:0)%那里溢出来

	IF ABL:(MPLY:0):精愛 >= 4
		PRINTFORML %ANAME(MPLY:0)%自己舔舐掉了沾在手指上的白浊…
		EXP:(MPLY:0):精愛経験値 += 2
	ELSE
		PRINTFORML %ANAME(MPLY:0)%虽然很讨厌，但手指被弄脏是不可避免的…
	ENDIF
	WAIT
ENDIF


;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST3
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF (LOCAL:1 != MASTER || CONFIG:441) && !IS_ANIMAL(LOCAL:1)
		LOCAL:2 = CUP:(LOCAL:1):快Ｖ

		IF TCVAR:(LOCAL:1):52
			;気絶中
			PRINTFORML 被责弄著膣内的時候、%ANAME(LOCAL:1)%即使失去了意識但還是発出了小聲的呻吟
		ELSEIF LOCAL:2 < 300 
			PRINTFORML %ANAME(LOCAL:1)%還未開発過的那裡非常的緊、%ANAME(LOCAL:1)%的表情有些痛苦
		ELSEIF LOCAL:2 < 1000
			PRINTFORML %ANAME(LOCAL:1)%雖然有些痛苦但是那表情上已經能見到快楽的跡象了
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %ANAME(LOCAL:1)%在無法忍耐的快感下発出了可愛的呻吟
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %ANAME(LOCAL:1)%已經非常習慣了、不時會為了索求快感而主動的輕扭腰肢
		ELSEIF LOCAL:2 < 10000
			PRINTFORM %ANAME(LOCAL:1)%
			IF TALENT:(LOCAL:1):圧抑 || TALENT:(LOCAL:1):孤高 || TALENT:(LOCAL:1):否定快感
				PRINTFORML 一直在説著否定快感的話、但是卻無法忍耐的不停喘息著
			ELSE
				PRINTFORML 迎合著责弄自己的手指的動作後弓著背発出了快楽的喘息聲
			ENDIF
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%淫靡的扭動著腰肢、完全沉溺于快感之中高聲嬌喘著
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN3
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_3()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_3()
RETURN POWER(2, 性的嗜好_愛撫されたい)


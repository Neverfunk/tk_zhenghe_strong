;-------------------------------------------------
;会談する
;-------------------------------------------------
@DIPLOMACY_TALK(ARG:0)
#DIM 君主
#DIM FIRST_LINE, 2
#DIM CONST 支払額 = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
FIRST_LINE:0 = LINECOUNT
君主 = GET_COUNTRY_BOSS(ARG:0)

PRINTFORML 和%ANAME(君主)%進行了会談……
PRINTFORML 以前接待的時候、常説道「喜歡」金錢
PRINTFORML 為了讓%ANAME(君主)%満足、打算怎麼辦呢……
CALL SINGLE_DRAWLINE
CALL ASK_MULTI_JUDGE(@"用国庫資金援助 現在:{MONEY:(CFLAG:MASTER:所属)}", 1, "提供「快楽」(対方性知識最少也要3)", ABL:君主:性知識 >= 3, "取消", 1)
SIF RESULT == 2
	RETURN 0
IF RESULT == 1
	CALL DIPLOMACY_TALK_SEX(君主)
	IF RESULT == -1
		CLEARLINE LINECOUNT - FIRST_LINE:0
		RESTART
	ELSE
		PRINTL
	ENDIF
	;好感度上昇・敵対値下降基準値
	LOCAL:2 = RESULT
	LOCAL = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
	LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10

	LOCAL:2 = MAX(LOCAL:2 * (LOCAL + LOCAL:1 + 100) / 100, 5)
	LOCAL:2 = LOCAL:2 * 100 / GET_DIFFICULTY_CORRECTION()
	;互いの国同士の好感度を上昇させ敵対値を低下させる
	LOCAL:2 = SLG_BUILDING_DIPLOMACY_SEXTALK_ADDINCOME(LOCAL:2)
	CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:2)
	CALL SLG_BUILDING_DIPLOMACY_SEXTALK_CHANGE_从属依存(君主)
	CALL SLG_BUILDING_DIPLOMACY_SEXTALK_CHANGE_RELATION(CFLAG:君主:所属)
	;跳转信仰事件
	TRYCALL SHOP_LIFE_USERSHOP68_LUST_COMMON2("性爱外交",MASTER,君主)
ELSEIF RESULT == 0
	FIRST_LINE:1 = LINECOUNT
	$SEND_MONEY_LOOP
	PRINTFORML 要送多少錢……(手動輸入・按鈕輸入、輸入0以下取消, 最大50000)
	CALL ICPRINT(@"現在的国庫:<{MONEY:(CFLAG:MASTER:所属)}>", "L", カラー_注意)
	FOR LOCAL, 0, VARSIZE("支払額")
		SIF MONEY:(CFLAG:MASTER:所属) < 支払額:LOCAL
			BREAK
		PRINTBUTTON @"[支付{支払額:LOCAL}]", 支払額:LOCAL
		PRINTL
	NEXT
	PRINTFORML
	PRINTBUTTON @"[支付{MONEY:(CFLAG:MASTER:所属)}]", MONEY:(CFLAG:MASTER:所属)
	PRINTFORM [0] 取消
	INPUT
	IF RESULT <= 0
		CLEARLINE LINECOUNT - FIRST_LINE:0
		RESTART
	ELSEIF MONEY:(CFLAG:MASTER:所属) < RESULT || RESULT > 50000
		CLEARLINE LINECOUNT - FIRST_LINE:1
		GOTO SEND_MONEY_LOOP
	ENDIF
	CALL SEND_MONEY(ARG:0, RESULT)
	WAIT
ENDIF

RETURN 1

;-------------------------------------------------
;送金処理用関数
;-------------------------------------------------
@CALC_SEND_MONEY_EFFECT(ARG:0)
#FUNCTION
RETURNF LIMIT(SQRT(ARG:0 * 2) * RAND(900, 1100) / 1000, 0, 300)

;-------------------------------------------------
;送金処理
;-------------------------------------------------
@SEND_MONEY(ARG:0, ARG:1)
CALL ICPRINT(@"%ANAME(MASTER)%用国庫資金向%ANAME(GET_COUNTRY_BOSS(ARG:0))%送去了<{ARG:1}>金……", "L", カラー_注意)
MONEY:(CFLAG:MASTER:所属) -= ARG:1
LOCAL = CALC_SEND_MONEY_EFFECT(ARG:1)
;好感度上昇・敵対値下降基準値
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:2 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10


LOCAL:3 = MAX(LOCAL * (LOCAL:1 + LOCAL:2 + 100) / 100, 5)
LOCAL:3 = LOCAL:3 * 100 / GET_DIFFICULTY_CORRECTION()

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:3)
;跳转信仰事件
TRYCALL SHOP_LIFE_USERSHOP68_CONFERENCE(CFLAG:MASTER:1,ARG:0,ARG:1)


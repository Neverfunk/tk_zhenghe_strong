;-------------------------------------------------
;兵を譲り渡す
;-------------------------------------------------
@DIPLOMACY_GIVE_SOLDIER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属)

PRINTFORML 総兵数:  {GET_SUM_SOLDIER(CFLAG:MASTER:所属)}
PRINTFORML 防衛兵数:{GET_SUM_ARMY_DF(CFLAG:MASTER:所属)}
PRINTFORML 残存兵数:{LOCAL:6}
CALL SINGLE_DRAWLINE

IF LOCAL:6 < 500
	PRINTFORMW 最少也需要500以上的残存兵数
	RETURN 0
ENDIF

PRINTFORML 請輸入要転讓的兵数(輸入0以下取消)

CALL DRAW_SOLDIER_INPUT(LOCAL:6)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 > 0 && LOCAL:2 < 500
	CLEARLINE 1
	PRINTL 最少也要転讓500以上的兵数
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6
	CLEARLINE 1
	PRINTL 兵数不足
	GOTO INPUT_LOOP
ENDIF

COUNTRY_SOLDIER:(CFLAG:MASTER:所属) -= LOCAL:2
COUNTRY_SOLDIER:(ARG:0) += LOCAL:2

PRINTFORMW %ANAME(MASTER)%向%ANAME(LOCAL:5)%転讓了{LOCAL:2}兵

;好感度上昇値・敵対値下降値の計算
;同じ兵数でも、自国の規模が小さいほど、また相手勢力の規模が小さいほど効果が高い
LOCAL:10 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
LOCAL:11 = GET_SUM_ECONOMY(ARG:0)
LOCAL:12 = SQRT(LOCAL:2 * 26000 / LOCAL:10) + SQRT(LOCAL:2 * 26000 / LOCAL:11)
LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;政治・料理の補正
CALL TMP_CREATE_IS_FREE_MAP

LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:所属)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;互いの国同士の好感度を上昇させ敵対値を低下させる
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1


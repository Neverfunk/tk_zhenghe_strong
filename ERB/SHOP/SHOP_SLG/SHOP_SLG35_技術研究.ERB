;-------------------------------------------------
;「国家方針・研究」の名称
;-------------------------------------------------
@SHOP_SLG_NAME35
RESULTS:0 '= "技術研究"

;-------------------------------------------------
;「国家方針・研究」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK35
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF !IS_COUNTRY(CFLAG:MASTER:所属)
	RETURN 0
SIF CONFIG:302
	RETURN 0
RETURN 1

;-------------------------------------------------
;「国家方針・研究」の右カラムオン判定
;-------------------------------------------------
;@SHOP_SLG_EVENTBUY_COLUMN_RIGHT_ON_CHECK35

;-------------------------------------------------
;「国家方針・研究」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY35
CALL SHOW_TECHNOLOGY_RESEARCH
RETURN 1

;-------------------------------------------------
;「勢力方針・研究」の表示処理本体
;-------------------------------------------------
@SHOW_TECHNOLOGY_RESEARCH(勢力)
#DIM 勢力
#DIM 勢力方針
#DIM 君主
#DIM 研究予定
#DIMS 技術名
#DIMS 説明
#DIM ジャンル
#DIM ステージ
#DIM 表示中研究番号
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

;ショップから飛んできた場合は引数勢力が0
SIF !IS_COUNTRY(勢力)
	勢力 = CFLAG:MASTER:所属

;UI部分
CALL SINGLE_DRAWLINE
君主 = GET_COUNTRY_BOSS(勢力)

ジャンル = TECHNOLOGY_RESEARCH_TARGET:勢力 / 100
ステージ = TECHNOLOGY_RESEARCH_TARGET:勢力 % 100

PRINTFORML 君主：%ANAME(君主), MAX_CHARANAME_LENGTH, LEFT% 勢力方針：%TOSTR_AI_TYPE(COUNTRY_AI_TYPE:勢力)%

IF IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
	技術名 = %GET_TECHNOLOGY_NAME(ジャンル, ステージ)%
	PRINTFORM 現在進行中的研究(进度/総期間)：
	SETCOLOR カラー_選択中
	PRINTFORM %技術名%
	RESETCOLOR
	PRINTFORML ({TECHNOLOGY_RESEARCH_PROGRESS:勢力}/{GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)}期)
ELSE
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
CALL SHOW_TECHNOLOGY_TREE(勢力)
CALL SINGLE_DRAWLINE

ジャンル = 表示中研究番号 / 100
ステージ = 表示中研究番号 % 100

IF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
	PRINTL
	PRINTL
	PRINTL
	PRINTL
ELSE
	技術名 = %GET_TECHNOLOGY_NAME(ジャンル, ステージ)%
	説明 = %GET_TECHNOLOGY_DESCRIPTION(ジャンル, ステージ)%
	PRINTFORML %技術名%
	PRINTFORML 効果：%説明%
	PRINTFORML 距离完成还剩{GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)}期
	IF 勢力 == CFLAG:MASTER:所属 && IS_TECHNOLOGY_RESEARCHABLE(勢力, ジャンル, ステージ) && 表示中研究番号 != TECHNOLOGY_RESEARCH_TARGET:勢力
		PRINTBUTTON "[研究開始]", 1001
		PRINTL
	ELSE
		PRINTL
	ENDIF
ENDIF
CALL SINGLE_DRAWLINE

PRINTBUTTON "[取消]", 1000

$TECHNOLOGY_RESEARCH_START
INPUT

IF RESULT == 1000
	RETURN
ELSEIF RESULT == 1001 && 勢力 == CFLAG:MASTER:所属 && IS_TECHNOLOGY_RESEARCHABLE(勢力, ジャンル, ステージ)
	PRINTFORML 距离完成还剩{GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)}期
	PRINTL
	PRINTFORML 开始研究吗？（当前研究的进度将被重置）
	CALL ASK_YN()
	IF RESULT == 0 && TECHNOLOGY_RESEARCH_TARGET:勢力 != 表示中研究番号
		TECHNOLOGY_RESEARCH_TARGET:勢力 = 表示中研究番号
		TECHNOLOGY_RESEARCH_PROGRESS:勢力 = 0
		PRINTFORMW 研究开始了
	ENDIF
ELSE
	ジャンル = RESULT / 100
	ステージ = RESULT % 100
	SIF IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
		表示中研究番号 = RESULT
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;------------------
; 技術ツリーの表示
;------------------
@SHOW_TECHNOLOGY_TREE(勢力)
#DIM 勢力
#DIM 技術進捗
#DIMS 技術名
#DIMS CONST 技術ジャンル = "戦闘", "防衛", "外交", "徴兵", "徴税" ,"建築","雇用"
#DIM ジャンル
#DIM ステージ

FOR ジャンル, 0, TECHNOLOGY_MAX_GENRE
	CALL PRINTBUTTON_CENTER(@"%技術ジャンル:ジャンル%", 0, 7, 0)
NEXT
PRINTL

FOR ステージ, 1, TECHNOLOGY_MAX_STAGE
	FOR ジャンル, 0, TECHNOLOGY_MAX_GENRE
		IF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
			PRINTFORM %TOSTR_SPACE(18)%
			CONTINUE
		ENDIF
		技術進捗 = TECHNOLOGY_STATUS:勢力:ジャンル
		技術名 = %GET_TECHNOLOGY_NAME(ジャンル, ステージ)%
		IF 技術進捗 >= ステージ
			SETCOLOR カラー_緑
		ELSEIF (ジャンル * 100 + ステージ) == TECHNOLOGY_RESEARCH_TARGET:勢力
			SETCOLOR カラー_選択中
		ELSE
			SETCOLOR GETDEFCOLOR()
		ENDIF
		CALL PRINTBUTTON_CENTER_TECHTREE(@"[%技術名%]", ジャンル * 100 + ステージ, 14)
	NEXT
	PRINTL

	FOR ジャンル, 0, TECHNOLOGY_MAX_GENRE
		IF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ + 1)
			PRINTFORM %TOSTR_SPACE(0)%
			CONTINUE
		ENDIF
		技術進捗 = TECHNOLOGY_STATUS:勢力:ジャンル
		IF 技術進捗 >= ステージ + 1
			SETCOLOR カラー_緑
		ELSEIF (ジャンル * 100 + ステージ + 1) == TECHNOLOGY_RESEARCH_TARGET:勢力
			SETCOLOR カラー_選択中
		ELSE
			SETCOLOR カラー_選択不可
		ENDIF
		PRINTFORM %TOSTR_SPACE(6)%↓%TOSTR_SPACE(6)%
		RESETCOLOR
	NEXT
	PRINTL
NEXT
RESETCOLOR

;------------------
; 重新排下版的函数，仅用于科技树
;------------------

@PRINTBUTTON_CENTER_TECHTREE(ARGS:0, ARG:1, ARG:2, ARG:3 = 1, ARG:4 = 0)
LOCAL:0 = (ARG:2 - STRLENS(ARGS:0))/2

PRINTPLAINFORM %TOSTR_SPACE(LOCAL:0)%
IF !ARG:3
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM %ARGS:0%
	RESETCOLOR
ELSEIF ARG:4
	SETCOLOR カラー_選択中
	PRINTPLAINFORM %ARGS:0%
	RESETCOLOR
ELSE
	PRINTBUTTON @"%ARGS:0%", ARG:1
ENDIF
PRINTPLAINFORM %TOSTR_SPACE(LOCAL:0)%

;------------------
; 研究期間
;------------------
@GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)
#FUNCTION
#DIM 勢力
#DIM ジャンル
#DIM ステージ
#DIM ウェイト
#DIM CONST 基礎研究期間 = 10
#DIM 研究機関
VARSET 研究機関
SIF !IS_COUNTRY(勢力)
	RETURNF 0
SIF CHECK_BUILDING_FUNCTION_SELECT(-1,勢力,建造物_研究所) > 0
	研究機関 += 1
IF CHECK_BUILDING_FUNCTION_SELECT(-1,勢力,建造物_忍者之里) > 0
	FOR LOCAL, 0, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL)
			CONTINUE
		IF TECHNOLOGY_STATUS:LOCAL:ジャンル >= ステージ
			研究機関 ++
			BREAK
		ENDIF
	NEXT
ENDIF
SIF CHECK_BUILDING_FUNCTION_SELECT(-1,勢力,建造物_科研大楼) > 0
	研究機関 += (RESULT:1) * 1

SIF CHECK_BUILDING_FUNCTION_SELECT(-1,勢力,建造物_定向实验室) > 0
	研究機関 += (RESULT:1) * 3

SIF ジャンル == -1
	RETURNF 基礎研究期間

ウェイト = MIN(AI_TECHNOLOGY_PREFERENCE:(COUNTRY_AI_TYPE:勢力):ジャンル / 10, 5)

RETURNF MAX(基礎研究期間 - ウェイト + ステージ - 研究機関 - 1,1)


;------------------
; 技術名の返却
;------------------
@GET_TECHNOLOGY_NAME(ジャンル, ステージ)
#FUNCTIONS
#DIM ジャンル
#DIM ステージ

;DEBUGPRINTFORML GET_TECHNOLOGY_NAME({ジャンル}, {ステージ})
;DEBUGPRINTFORML %TECHNOLOGY_NAME:ジャンル:ステージ%
SIF !IS_TECHNOLOGY_EXISTS(ジャンル,ステージ)
	RETURNF ""

RETURNF TECHNOLOGY_NAME:ジャンル:ステージ


;------------------
; 技術の説明
;------------------
@GET_TECHNOLOGY_DESCRIPTION(ジャンル, ステージ)
#FUNCTIONS
#DIM ジャンル
#DIM ステージ

SIF !IS_TECHNOLOGY_EXISTS(ジャンル,ステージ)
	RETURNF ""


RETURNF TECHNOLOGY_DESC:ジャンル:ステージ

;------------------
; 存在する技術か
;------------------
@IS_TECHNOLOGY_EXISTS(ジャンル,ステージ)
#FUNCTION
#DIM ジャンル
#DIM ステージ

;DEBUGPRINTFORML IS_TECHNOLOGY_EXISTS({ジャンル}, {ステージ})

SIF !INRANGE(ジャンル, 0, TECHNOLOGY_MAX_GENRE - 1) || !INRANGE(ステージ, 0, TECHNOLOGY_MAX_STAGE - 1)
	RETURNF 0

RETURNF TECHNOLOGY_NAME:ジャンル:ステージ != ""

;------------------
; 研究可能か
;------------------
@IS_TECHNOLOGY_RESEARCHABLE(勢力,ジャンル,ステージ)
#FUNCTION
#DIM 勢力
#DIM ジャンル
#DIM ステージ

SIF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
	RETURNF 0

RETURNF TECHNOLOGY_STATUS:勢力:ジャンル == ステージ - 1

;------------------
;技術の名前など設定する
;------------------
@INIT_TECHNOLOGY()

TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:1 '= "妖精的支援"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:2 '= "妖精参戦"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:3 '= "炎弾幕"
;译者注：神灵庙的灵界BGM
;TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:4 '= "霊界トランス"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:4 '= "灵界出神"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:5 '= "曲射激光"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:6 '= "森羅結界"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:7 '= "妖精応援団"

TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:1 '= "戦闘中技能的发动率+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:2 '= "戦闘中可以发动妖精参战技能。部隊人数增加+1000～2000"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:3 '= "戦闘中可以发动炎弹幕技能。部隊的攻撃力+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:4 '= "戦闘中可以发动灵界Trance技能。部隊的防御力+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:5 '= "戦闘中可以发动曲射激光技能。部隊的攻撃力+7\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:6 '= "戦闘中可以发动森羅結界技能。部隊的防御力+7\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:7 '= "戦闘中技能的发动率+10\%"

TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:1 '= "弾幕塔"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:2 '= "逆茂木"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:3 '= "攻击补给"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:4 '= "整理补给"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:5 '= "弾幕銃眼"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:6 '= "稜堡城郭"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:1 '= "都市的火力支援威力+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:2 '= "防衛倍率+0.07"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:3 '= "防衛時、额外增加敌人的疲劳度"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:4 '= "攻撃時、缓解疲劳度的增加"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:5 '= "都市的火力支援威力额外+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:6 '= "防衛倍率额外增加+0.07"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:7 '= ""

TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:1 '= "高明交涉术"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:2 '= "柔和的态度"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:3 '= "外交官养成"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:4 '= "柔和外表"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:5 '= "停战之轮"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:6 '= "同盟之轮"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:1 '= "停戦・同盟交渉時有利"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:2 '= "外交警戒値的減少量+1"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:3 '= "停戦・同盟交渉時更加有利"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:4 '= "外交警戒値的減少量额外+1"
;AI勢力時: 他のAIから好意的な対応をされやすくなる
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:5 '= "可以停戦的国家数+1"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:6 '= "可以同盟的国家数+1"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:7 '= ""

TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:1 '= "大型兵舍"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:2 '= "街頭演説"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:3 '= "超级兵舍"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:4 '= "天狗传单"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:5 '= "巨型兵舍"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:6 '= "河童广播"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:1 '= "総兵数的上限+10\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:2 '= "回合末的徴兵効率+10\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:3 '= "総兵数的上限额外+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:4 '= "回合末的徴兵効率额外+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:5 '= "総兵数的上限额外+20\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:6 '= "回合末的徴兵効率额外+20\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:7 '= ""

TECHNOLOGY_NAME:TECHNOLOGY_TAX:1 '= "投資信託"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:2 '= "灰吹法"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:3 '= "株取引"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:4 '= "二期作"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:5 '= "不動産経営"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:6 '= "品種改良"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_TAX:1 '= "国庫収入+500"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:2 '= "徴税効率+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:3 '= "国庫収入增加+1000"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:4 '= "徴税効率增加+10\%"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:5 '= "国庫収入增加+1500"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:6 '= "徴税効率增加+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:7 '= ""

;スカウト我实在没法了，翻成中介人了
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:1 '= "本地中介人"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:2 '= "有名中介人"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:3 '= "职业中介人"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:4 '= "中介人之神"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:5 '= ""
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:6 '= ""
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:1 '= "招募費用-5\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:2 '= "招募費用额外-10\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:3 '= "招募費用额外-15\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:4 '= "招募費用额外-20\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:5 '= ""
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:6 '= ""
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:7 '= ""


;建築擴展新增科技線
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:1 '= "土地规划"
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:2 '= "工业升级"
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:3 '= "模板化建设"
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:4 '= "统筹土地规划"
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:5 '= "产业革命"
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:6 '= "新星都市"
TECHNOLOGY_NAME:TECHNOLOGY_BUILDINGS:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:1 '= "所有城市建筑槽位+1"
TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:2 '= "解锁二级建筑"
TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:3 '= "建筑消耗国库资金量减少25\%"
TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:4 '= "所有城市建筑槽位+1"
TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:5 '= "解锁三级建筑"
TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:6 '= "高级建筑的都市经济需求减少25%"
TECHNOLOGY_DESC:TECHNOLOGY_BUILDINGS:7 '= ""


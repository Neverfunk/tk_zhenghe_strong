;-------------------------------------------------
;「捕虜の調教」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME31
RESULTS:0 '= "調教俘虜"

;-------------------------------------------------
;「捕虜の調教」の選択可否判定
;-------------------------------------------------
@SHOP_LIFE_CHECK31
SIF CFLAG:MASTER:行動不能状態 == 行動不能_臨月 || COOLTIME:MASTER:0 > 1
	RETURN 0
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
RETURN 1

;-------------------------------------------------
;「捕虜の調教」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA31(ARG:0)
;非主人公、主人公勢力の捕虜、牢獄で監禁中
RETURN CHECK91(ARG:0, 2, (ARG:0 != MASTER && CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):軟禁中 == 0) || CFLAG:(ARG:0):外交調教中)

;-------------------------------------------------
;「捕虜の調教」の追加チェック
;-------------------------------------------------
@SHOP_LIFE_CHECK_ADDITIONAL_CHECK31()
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:捕虜先 == CFLAG:MASTER:所属 && CFLAG:LOCAL:軟禁中 != 1
		RETURN 1
NEXT


;-------------------------------------------------
;「捕虜の調教」の助手の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_SUB31(ARG:0)
;閨に呼ぶの選択可能キャラ存在判定
CALL SHOP_LIFE_CHECKCHARA_NEYA(ARG:0)
RETURN CHECK91(ARG:0, 2, RESULT && SHOP_LIFE_CHECKCHARA_ZYOSYU(ARG:0) && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属)
RETURN 0

;-------------------------------------------------
;「捕虜の調教」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY31
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「捕虜の調教」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON31(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, FINDELEMENT(PRISONER_TARGET, ARG:0) != -1, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
RETURN

;-------------------------------------------------
;「捕虜の調教」の右カラムキャラリスト２のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST2_BUTTON31(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):捕虜調教の助手, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, 0, "BOTTOM")
RETURN 0

;-------------------------------------------------
;「捕虜の調教」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP31(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「捕虜の調教」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM31(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「捕虜の調教」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW31
#DIM 選択人数
選択人数 = INRANGEARRAY(PRISONER_TARGET, 0, CHARANUM + 1)

CALL COLUMN_RIGHT_TITLE("対象者選択", TOSTR(選択人数), TOSTR(VARSIZE("PRISONER_TARGET")), "1", "0", "助手選択可  ")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

IF 選択人数 >= 1
	PRINTBUTTON "[就這樣決定]", 1
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [就這樣決定]
	RESETCOLOR
ENDIF
PRINTPLAIN   
CALL COLOR_PRINT("管理(軟禁/解放/士兵的性奴隷化/処刑等)的行動不会消費時間", カラー_注釈)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
CALL COLOR_PRINT("一括管理", カラー_注釈)
PRINTPLAIN   
PRINTBUTTON "[解放]", 2
PRINTBUTTON "[放逐]", 3
PRINTBUTTON "[軟禁]", 4
PRINTBUTTON "[処刑]", 5
SIF ITEM:触手部屋 && ID_TO_CHARA(FLAG:触手部屋管理者) >= 0
	PRINTBUTTON "[触手処刑]", 6

CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;右カラムの上下２のキャラリスト上とページ移動を呼ぶ
CALL COLUMN_RIGHT_CHARALIST_TOP(1)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;助手を数える
LOCAL:1 = 0
FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうかを判定
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT == 1
		;選択中の人数を数える
		IF CFLAG:(LOCAL:0):捕虜調教の助手
			LOCAL:1 ++
		ENDIF
	ELSE
		;条件を満たしていなければ強制的にＯＦＦ
		CFLAG:(LOCAL:0):捕虜調教の助手 = 0
	ENDIF
NEXT

PRINTFORM 助手最多可以攜帯4人
SIF LOCAL:1 >= 4
	SETCOLOR カラー_オレンジ
PRINTFORM (現在{LOCAL:1}/4人)
RESETCOLOR
PRINTPLAIN   
PRINTBUTTON @"[%ZYOSYU_MODE()%]", 7
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;右カラムの上下２のキャラリスト下とページ移動を呼ぶ
CALL COLUMN_RIGHT_CHARALIST_BOTTOM(1)
RETURN 0

;-------------------------------------------------
;「捕虜の調教」の右カラムボタンの入力処理補佐
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB31(ARG:0)
;このメンバーで決定
SELECTCASE ARG:0 
	CASE 1
		SELECTCASE INRANGEARRAY(PRISONER_TARGET, 0, CHARANUM + 1)
			CASE 0
				;ないと思うんだけど一応
			CASE 1
				CALL PRISONER_TRAIT(PRISONER_TARGET:0)
			CASEELSE
				CALL DECIDE_PRISONER_MEMBER()
		ENDSELECT
		IF RESULT
			RETURN 1
		ENDIF
	CASE 2, 3, 4, 5, 6
		CALL PRISONER_BULK_MANAGEMENT(ARG:0)
		RETURN RESULT
	CASE 7
		FLAG:助手フィルタ = ROUND_INCREMENT(FLAG:助手フィルタ, 0, 3)
ENDSELECT
RETURN 0

;-------------------------------------------------
;「捕虜の調教」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP31(ARG:0)
LOCAL = FINDELEMENT(PRISONER_TARGET, ARG:0)
;いたら消す
IF LOCAL != -1
	PRISONER_TARGET:LOCAL = -1
	;配列のケツ以外を消した場合は、その一個後ろからケツまでを、一個手前にずらす
	SIF LOCAL != VARSIZE("PRISONER_TARGET") -1
		ARRAYSHIFT PRISONER_TARGET, -1, -1, LOCAL, VARSIZE("PRISONER_TARGET")
;いなかったら追加する
ELSE
	FOR LOCAL, 0, VARSIZE("PRISONER_TARGET")
		IF PRISONER_TARGET:LOCAL == -1
			PRISONER_TARGET:LOCAL = ARG:0
			BREAK
		ENDIF
	NEXT
ENDIF
RETURN 0

;-------------------------------------------------
;「捕虜の調教」のリスト２入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_SUB31(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):捕虜調教の助手
		LOCAL:5 ++
	ENDIF
NEXT
IF CFLAG:(ARG:0):捕虜調教の助手 || LOCAL:5 < 4
	;調教参加フラグを反転
	CFLAG:(ARG:0):捕虜調教の助手 = !CFLAG:(ARG:0):捕虜調教の助手
ENDIF
RETURN 0

;-------------------------------------------------
;「捕虜の調教」の処理
;-------------------------------------------------
@DECIDE_PRISONER_MEMBER()
CVARSET CFLAG, 6, 0
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):捕虜調教の助手
		CFLAG:(LOCAL:0):調教参加フラグ = 1
	ENDIF
NEXT
;このターン調教された
FOR LOCAL:0, 0, VARSIZE("PRISONER_TARGET")
	IF PRISONER_TARGET:LOCAL != -1
		CFLAG:(PRISONER_TARGET:LOCAL):調教参加フラグ = 1
	ENDIF
NEXT

;--信仰--触发调教类型0=会见 1=调教俘虏 2=侍寝 3=夜袭
MIRACLE_LUST_TRAIN_FORM = 1
;--信仰--玩家制造色欲调教事件变量赋值--
MIRACLE_LUST_TRAIN_TARGET:0 = 1 ;人数初始化
MIRACLE_LUST_TRAIN_TARGET:1 = MASTER ;玩家参与
MIRACLE_LUST_TRAIN_MAIN = MASTER
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):調教参加フラグ
		MIRACLE_LUST_TRAIN_TARGET:0 ++ ;调教人数增加
		MIRACLE_LUST_TRAIN_TARGET:(MIRACLE_LUST_TRAIN_TARGET:0) = LOCAL:0 ;调教参与者记录
		MIRACLE_LUST_PRE_FALL:(MIRACLE_LUST_TRAIN_TARGET:0) = MIRACLE_FALLCOUNT(LOCAL:0) ;调教前陷落代码,函数在 祷告_践行教义事件.ERB
		MIRACLE_LUST_PRE_MAKELOVE:(MIRACLE_LUST_TRAIN_TARGET:0) = TALENT:(LOCAL:0):合意 ;调教前是否取得对象合意
		MIRACLE_LUST_PRE_LIKE:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):好感度 ;调教前对象好感度
		MIRACLE_LUST_PRE_TRUST:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):依存度 ;调教前对象依存度
		MIRACLE_LUST_PRE_BELONG:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):従属度 ;调教前对象従属度
		MIRACLE_LUST_PRE_CONTROL:(MIRACLE_LUST_TRAIN_TARGET:0) = CFLAG:(LOCAL:0):支配度 ;调教前对象支配度
	ENDIF
NEXT
;--信仰--结束--

;調教初期設定
IF INRANGEARRAY(PRISONER_TARGET, 0, CHARANUM + 1) == 1
	CALL TRAIN_TARGET_SETTING(PRISONER_TARGET:0)
ENDIF
;初期のウフフフラグをＯＮにする
FLAG:ウフフフラグ = 1
;捕虜調教モード
FLAG:調教モード = 2
;行動開始時の共通設定
CALL START_TRAIN_COMMON
REDRAW 1
RETURN 1

;-------------------------------------------------
;捕虜の調教の一括管理
;-------------------------------------------------
@PRISONER_BULK_MANAGEMENT(ARG:0)
#DIMS 内容
#DIMS 内容英
#DIM 触手管理人
#DIM 対象
SELECTCASE ARG:0
	CASE 2
		内容 = 解放
		内容英 = PURGE
	CASE 3
		内容 = 追放
		内容英 = BANISH
	CASE 4
		内容 = 軟禁
		内容英 = NANKIN
	CASE 5
		内容 = 処刑
		内容英 = EXECUTE
	CASE 6
		内容 = 触手処刑
		内容英 = TENTACLE_EXECUTE
		触手管理人 = ID_TO_CHARA(FLAG:触手部屋管理者)
ENDSELECT

CALL SINGLE_DRAWLINE
PRINTFORML 請選択要%内容%的対象
PRINTFORML （統一管理的時候、不会発生処刑時的事件）
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(-1, "PRISONER_MANAGE_" + 内容英, "NONE")

SIF SELECTED_CHARA_NUM == 0
	RETURN 0

FOR LOCAL, 0, SELECTED_CHARA_NUM
	対象 = SELECTED_CHARA:LOCAL
	PRINTFORML %ANAME(対象)%被%内容%了
	SELECTCASE ARG:0
		CASE 2
			CALL CAPTURE(対象, 0, TALENT:対象:崩壊)
		CASE 3
			CALL CHANGE_COUNTRY(対象, 0, 1)
			CALL CAPTURE(対象, 0, TALENT:対象:崩壊)
		CASE 4
			CFLAG:対象:軟禁中 = 1
		CASE 5
			CALL CHANGE_COUNTRY(対象, 0, 1, 1)
		CASE 6
			CALL CHANGE_COUNTRY(対象, 0, 1, 1)
			;Values based on @PRISONER_TRAIT_EXECUTE_TENTACLE
			CALL PRINT_ADD_EXP(触手管理人, "妖術経験値", (30 + BASE:対象:精神力 / 20 + ABL:対象:触手 * 10))
			CALL TRAIN_AUTO_ABLUP(触手管理人)
	ENDSELECT
NEXT

VARSET PRISONER_TARGET, -1

IF CALC_SHOP_TIME() <= SHOP_TIME
	PRINTFORML 人数不足導致工作増加沒有了自由時間所以據点階段結束了
	LINES_SHOP = LINECOUNT
	BEGIN TURNEND
ENDIF

RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_PRISONER_MANAGE_PURGE(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == CFLAG:MASTER:所属 && !CFLAG:対象:軟禁中

@SELECT_CHARA_LIST_SHOW_LOGIC_PRISONER_MANAGE_BANISH(対象)
#DIM 対象
RETURN ALLSAMES(CFLAG:対象:捕虜先, CFLAG:対象:所属, CFLAG:MASTER:所属)

@SELECT_CHARA_LIST_SHOW_LOGIC_PRISONER_MANAGE_NANKIN(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == CFLAG:MASTER:所属 && CFLAG:対象:所属 != CFLAG:MASTER:所属 && !CFLAG:対象:軟禁中

@SELECT_CHARA_LIST_SHOW_LOGIC_PRISONER_MANAGE_EXECUTE(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == CFLAG:MASTER:所属 && !CFLAG:対象:軟禁中

@SELECT_CHARA_LIST_SHOW_LOGIC_PRISONER_MANAGE_TENTACLE_EXECUTE(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == CFLAG:MASTER:所属 && !CFLAG:対象:軟禁中


;-------------------------------------------------
;「捕虜の調教」で対象が一人のとき、別の捕虜が身代わりになろうとする
;-------------------------------------------------
@PRISONER_SCAPEGOAT(対象)
#DIM 対象
#DIM 候補
#DIM 判定値, MAX_CHARA_NUM
VARSET 判定値, 0
FOR LOCAL, 0, CHARANUM
	SIF GROUPMATCH(LOCAL, 対象, MASTER)
		CONTINUE
	SIF GROUPMATCH(1, TALENT:LOCAL:崩壊, TALENT:LOCAL:空虚)
		CONTINUE
	SIF CFLAG:(LOCAL):所属 == CFLAG:MASTER:所属
		CONTINUE
	IF CFLAG:対象:捕虜先 && CFLAG:LOCAL:捕虜先 == CFLAG:対象:捕虜先
		SIF REL_LIKE:LOCAL:対象 > 0
			判定値:LOCAL += RAND(REL_LIKE:LOCAL:対象, REL_LIKE:LOCAL:対象 * 2) 
		SIF RELATION:LOCAL:対象 >= 100
			判定値:LOCAL += (RELATION:LOCAL:対象 - 100) * RAND(50, 100)
		SIF CFLAG:LOCAL:所属 != CFLAG:対象:所属
			判定値:LOCAL = 判定値:LOCAL * 2 / 3
	ENDIF
NEXT

候補 = FINDELEMENT(判定値, MAXARRAY(判定値, 0, CHARANUM), 0, CHARANUM)

SIF 判定値:候補 <= 0
	RETURN 0

IF 判定値:候補 > 10000
	PRINTFORMW %ANAME(MASTER)%打算開始調教%ANAME(対象)%的時候、%ANAME(候補)%找了過来
	PRINTFORML 希望以自己為替代、要求放過%ANAME(対象)%、発出了這樣的請求……
	CALL ASK_MULTI("接受", @"接受、并展示給%ANAME(対象)%看", "無視")
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW 如果是這樣的話、那就答応好了
			PRINTFORMW %ANAME(MASTER)%於是開始調教%ANAME(候補)%了……
			PRISONER_TARGET:FINDELEMENT(PRISONER_TARGET, 対象) = 候補
		CASE 1
			PRINTFORMW 如果是這樣的話、那就答応好了
			PRINTFORMW 但是、%ANAME(候補)%被侵犯的模樣、要展示給%ANAME(対象)%看
			PRINTFORMW 会露出什麼樣的表情呢、非常的期待啊……
			PRISONER_TARGET:FINDELEMENT(PRISONER_TARGET, 対象) = 候補
			CFLAG:対象:捕虜調教の助手 = 1
			CFLAG:対象:調教中帰らない = 1
		CASE 2
			PRINTFORMW 説着什麼蠢話呢
			PRINTFORMW %ANAME(MASTER)%無視了%ANAME(候補)%、開始調教%ANAME(対象)%
	ENDSELECT
ENDIF

;-------------------------------------------------
;「捕虜の調教とかの」のメニュー表示で使用フィルタリングのモード文字列
;-------------------------------------------------
@ZYOSYU_MODE
#FUNCTIONS
SELECTCASE FLAG:助手フィルタ
	CASE 0
		RETURNF "全"
	CASE 1
		RETURNF "男"
	CASE 2
		RETURNF "女"
	CASE 3
		RETURNF "双"
ENDSELECT

;-------------------------------------------------
;「捕虜の調教とかの」のメニュー表示で、フィルタに性別が適合するか
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_ZYOSYU(ARG:0)
#FUNCTION
SELECTCASE FLAG:助手フィルタ
	CASE 0
		RETURNF 1
	CASE 1
		RETURNF IS_MALE(ARG:0)
	CASE 2
		RETURNF IS_FEMALE(ARG:0)
	CASE 3
		RETURNF (HAS_PENIS(ARG:0) && IS_FEMALE(ARG:0)) || (HAS_VAGINA(ARG:0) && IS_MALE(ARG:0))
ENDSELECT


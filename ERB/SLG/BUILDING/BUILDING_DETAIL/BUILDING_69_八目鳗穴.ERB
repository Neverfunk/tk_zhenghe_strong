@BUILDING_GET_NAME_八目鳗穴()
    #FUNCTIONS
    RETURNF "八目鳗穴"

@BUILDING_GET_EXP_八目鳗穴()
    #FUNCTIONS
    RETURNF "可以发动技能《摆摊》，招募一个放浪中的角色"

@BUILDING_SET_INFO_ID_69()
    BUILDING_TYPE:建造物_八目鳗穴 = 建造物_类型_幻想类
    BUILDING_LEVEL:建造物_八目鳗穴 = 建造物_等级_一级建筑
    BUILDING_LIMIT_NUM:建造物_八目鳗穴 = 建造物_数量限制_1
    BUILDING_LIMIT_UNI:建造物_八目鳗穴 = 0
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_普通势力 = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_野盗 = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_ホフゴブリン = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_外来人 = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_触手 = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_自警団 = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_サキュバス = 1
    BUILDING_COUNTRY_ID:建造物_八目鳗穴:特殊勢力_狂信者 = 1
    BUILDING_HAVE_SKILL:建造物_八目鳗穴 = 1
    BUILDING_TOHO_REQ_ID:建造物_八目鳗穴:0 = 25


@CHECK_BUILDING_FUNCTION_八目鳗穴(COUNTRY_ID)
    #FUNCTION
    #DIM COUNTRY_ID
    SIF !CHECK_BUILDING_CHARA_LIMIT_USING(COUNTRY_ID,建造物_八目鳗穴)
        RETURNF RESULT:0
    RESULT:1 = TMP_BUILDING_EXIST_FLAG:COUNTRY_ID:建造物_八目鳗穴
    IF RESULT:1 > 0
        RESULT:0 = 1
    ENDIF
    RETURNF RESULT:0

@BUILDING_SKILL_八目鳗穴(COUNTRY_ID)
    #DIM COUNTRY_ID
    PRINTFORML 发动后可以从放浪中的士官中选取一名加入我国
    PRINTFORML 冷却时间3回合
    PRINTFORML 是否发动
    CALL ASK_YN()
    IF !RESULT
        PRINTFORML 发动了《八目鳗穴》的技能
        CALL BUILDING_SKILL_八目鳗穴_FUNCTION(COUNTRY_ID)
        IF RESULT
            BUILDING_SKILL_COLDTIME:COUNTRY_ID:建造物_八目鳗穴 = 3
        ENDIF
    ENDIF



@BUILDING_SKILL_八目鳗穴_FUNCTION(COUNTRY_ID)
    #DIM COUNTRY_ID
    #DIM CHARA_ID
    #DIM CHARA_ID_INDEX
    #DIM FIRST_LINE
    FIRST_LINE = LINECOUNT
    PRINTFORML 请选择要加入己方势力的士官
    FOR LOCAL, 0, VARSIZE("SCOUT_CANDIDATES")
        LOCAL:1 = ID_TO_CHARA(SCOUT_CANDIDATES:LOCAL)
        IF LOCAL:1 == -1
            CONTINUE
        ENDIF
        IF CFLAG:(LOCAL:1):特殊状態 != 特殊状態_放浪
            SCOUT_CANDIDATES:(LOCAL) = 0
            CONTINUE
        ENDIF
        CALL SCOUT_SHOW_INFO_八目鳗穴(LOCAL:1, LOCAL)
        SETCOLOR カラー_灰色
        CALL SINGLE_DRAWLINE
        RESETCOLOR
    NEXT
    PRINTBUTTON @"[%"取消", MAX_CHARANAME_LENGTH, LEFT%]", 99
    $INPUT_LOOP
    INPUT
    IF RESULT == 99
        RETURN 0
    ENDIF
    IF RESULT < 0
        CLEARLINE 1
        GOTO INPUT_LOOP
    ELSEIF VARSIZE("SCOUT_CANDIDATES") <= RESULT
        CLEARLINE 1
        GOTO INPUT_LOOP
    ENDIF
    CHARA_ID = ID_TO_CHARA(SCOUT_CANDIDATES:RESULT)
    CHARA_ID_INDEX = RESULT
    IF CHARA_ID == -1
        CLEARLINE 1
        GOTO INPUT_LOOP
    ENDIF
    PRINTFORML 要邀请%ANAME(CHARA_ID)%来我国吗
    PRINTL
    CALL ASK_MULTI_JUDGE("确认",1, "再想想", 1)
    IF RESULT == 1
        CLEARLINE LINECOUNT - FIRST_LINE
        RESTART
    ELSE
        LOCAL = SCOUT_PRICE:CHARA_ID_INDEX
    ENDIF
    CALL COLOR_PRINTW(@"%ANAME(CHARA_ID)%成为了同伴", カラー_注意)
    CALL CHANGE_COUNTRY(CHARA_ID, CFLAG:MASTER:所属, 1)

    SCOUT_CANDIDATES:(CHARA_ID_INDEX) = 0
    SCOUT_COUNT ++
    FOR LOCAL, 0, VARSIZE("SCOUT_CANDIDATES")
        CHARA_ID = ID_TO_CHARA(SCOUT_CANDIDATES:LOCAL)
        SIF CHARA_ID != -1
            SCOUT_PRICE:LOCAL = SCOUT_CALC_PRICE(CHARA_ID)
    NEXT
    RETURN 1

@SCOUT_SHOW_INFO_八目鳗穴(対象, 番号)
    #DIM 対象
    #DIM 番号

    PRINTBUTTON @"[%ANAME(対象), MAX_CHARANAME_LENGTH, LEFT%]", 番号
    CALL TMP_PRINT_CHARA_STARS_NUM(対象)
    PRINT   武:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:武闘)
    PRINTFORM {ABL:対象:武闘, 3, LEFT}
    PRINT  防:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:防衛)
    PRINTFORM {ABL:対象:防衛, 3, LEFT}
    PRINT  知:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:知略)
    PRINTFORM {ABL:対象:知略, 3, LEFT}
    PRINT  政:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:政治)
    PRINTFORM {ABL:対象:政治, 3, LEFT}
    PRINT  妖:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:妖術)
    PRINTFORM {ABL:対象:妖術, 3, LEFT}
    PRINT  料:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:料理)
    PRINTFORM {ABL:対象:料理, 3, LEFT}
    PRINT  歌唱:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:歌唱)
    PRINTFORM {ABL:対象:歌唱, 3, LEFT}
    PRINT  野心:
    CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:野心)
    PRINTFORM {ABL:対象:野心, 3, LEFT}
    PRINTFORML

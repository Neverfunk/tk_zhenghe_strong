;触手搾乳

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME214
IF TALENT:(MTAR:(LOCAL:0)):母乳体質
	LOCALS:0 = 触手乳腺責め
ELSE
	LOCALS:0 = 触手乳腺開発
ENDIF
RESULTS:0 = %LOCALS:0%(主動)
RESULTS:1 = %LOCALS:0%(被役)
RESULTS:2 = %LOCALS:0%(使役)
RESULTS:3 = %LOCALS:0%(被動)
RESULTS:4 = %LOCALS:0%(使役)
RESULTS:5 = %LOCALS:0%(展示)

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE214
;共通部分
CALL COM_ABLE_COMMON(214)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0
;プレイヤーが触手召喚中でないなら不可
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
;全てのターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;nipples occupied
	SIF IS_N_HOLD(MTAR:(LOCAL:0))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM214
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;触手胸愛撫を解除
;FOR LOCAL:0, 0, MTAR_NUM
;	CALL RELEASE_EQUIP_EX(204, MPLY:0, MTAR:(LOCAL:0))
;NEXT

;●プレイヤーについて処理
EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):妖術経験値 += 1
EXP:(MPLY:0):触手経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):嗜虐 = 50
SOURCE:(MPLY:0):逸脱 = 100
SOURCE:(MPLY:0):触手 = 30
SOURCE:(MPLY:0):性行動 = 90

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 140, 30)

;侍奉経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):触手経験値 += 1
	EXP:(LOCAL:2):搾乳経験値 += !RAND:3

	LOCAL:3 = 1000 + ABL:(MPLY:0):妖術 * 8
	SOURCE:(LOCAL:2):快Ｂ = SENSE_HOUSHI(MPLY:0, LOCAL:2, LOCAL:3 * 3 / 5) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 500)
	SIF TALENT:(LOCAL:2):母乳体質
		SOURCE:(LOCAL:2):搾乳 = SENSE_HOUSHI(MPLY:0, LOCAL:2, LOCAL:3 * 3 / 5) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 500)
	SOURCE:(LOCAL:2):苦痛 = 1000
	SOURCE:(LOCAL:2):逸脱 = 500 - (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 250)
	SOURCE:(LOCAL:2):触手 = 400 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
	SOURCE:(LOCAL:2):性行動 = 180 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 90)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP214
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP214(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):性行動 += 30

;全てのターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	;ソースを退避
	CALL PUTOUT_SOURCE(LOCAL:3)

	EXP:(LOCAL:3):触手経験値 += 1
	EXP:(LOCAL:3):搾乳経験値 += 1
	LOCAL:4 = 800 + ABL:(LOCAL:2):妖術 * 6
	SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, LOCAL:4 * 3 / 5) + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 400)
	SIF TALENT:(LOCAL:3):母乳体質
		SOURCE:(LOCAL:3):搾乳 += SENSE_HOUSHI(LOCAL:2, LOCAL:3, LOCAL:4 * 3 / 5) + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 400)
	SOURCE:(LOCAL:3):苦痛 += 500
	SOURCE:(LOCAL:3):逸脱 += 150 - (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 75)
	SOURCE:(LOCAL:3):触手 += 200 + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 100)
	SOURCE:(LOCAL:3):性行動 += 60 + (GETBIT(TALENT:(LOCAL:3):淫乱系, 素質_淫乱_苗床) * 30)

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 2

	;退避したソースを加算
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE214(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%被%EQUIP_PLAYER_ANAME(ARG:0)%的触手开发着乳腺

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP214(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%操纵的触手潜入%EQUIP_TARGET_ANAME(ARG:0)%的乳头，责怪乳腺…
;TranslatedMark %EQUIP_PLAYER_ANAME(ARG:0)%の操る触手が%EQUIP_TARGET_ANAME(ARG:0)%の乳首に潜り込み乳腺を責め立てている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP214(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER214(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"侍奉Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"触手Lv{ABL:(ARG:0):触手}", ABL:(ARG:0):触手 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):害羞
	CALL COM_ORDER_ELEMENT(ARG:0, "害羞", -2)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 2)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -7)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 4)
ENDIF
IF TALENT:(ARG:0):否定快感
	CALL COM_ORDER_ELEMENT(ARG:0, "否定快感", -2)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド) || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ) || ABL:(ARG:0):倒錯度 >= 800
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 32)
ELSEIF ABL:(ARG:0):倒錯度 >= 500
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 24)
ELSEIF ABL:(ARG:0):倒錯度 >= 300
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 16)
ELSEIF ABL:(ARG:0):倒錯度 >= 100
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 8)
ELSE
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意無", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE214
;ターゲットが2人以上
IF MTAR_NUM >= 2
	IF MTAR_NUM >= 3
		LOCALS:1 = %ANAME(MTAR:0)%们
;%ANAME(MTAR:0)%たち
	ELSEIF MTAR_NUM == 2
		LOCALS:1 = %ANAME(MTAR:0)%和%ANAME(MTAR:1)%
;%ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	ELSE
		LOCALS:1 = %ANAME(MTAR:0)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%把比头发细的触手插在%LOCALS:1%的乳头上，让它进入乳腺%TARGET_STR("た", "てきた")%…
			;TranslatedMark %ANAME(MPLY:0)%は髪の毛より細い触手を%LOCALS:1%の乳首に突き立て、乳腺へと入り込ませ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:1%在%ANAME(MPLY:0)%里，用触手触摸乳腺\@ TALENT:(MTAR:0):母乳体質 ? 开发 # 责弄 \@命令，命令
			;TranslatedMark %LOCALS:1%は%ANAME(MPLY:0)%に、触手で乳腺を\@ TALENT:(MTAR:0):母乳体質 ? 開発する # 責める \@よう命じた
			PRINTFORMW %ANAME(MPLY:0)%把比头发细的触手插在%LOCALS:1%的乳头上，让她进入了乳腺…
			;TranslatedMark %ANAME(MPLY:0)%は髪の毛より細い触手を%LOCALS:1%の乳首に突き立て、乳腺へと入り込ませた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%对%ANAME(MPLY:0)%用触手将%LOCALS:1%的乳腺切开\@ TALENT:(MTAR:0):母乳体質 ? 开发 # 责弄 \@命令，命令
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、触手で%LOCALS:1%の乳腺を\@ TALENT:(MTAR:0):母乳体質 ? 開発する # 責める \@よう命じた
			PRINTFORMW %ANAME(MPLY:0)%把比头发细的触手插在%LOCALS:1%的乳头上，让她进入了乳腺…
			;TranslatedMark %ANAME(MPLY:0)%は髪の毛より細い触手を%LOCALS:1%の乳首に突き立て、乳腺へと入り込ませた…
	ENDSELECT

;ターゲットが1人
ELSE
	IF GET_BUSTSIZE(MTAR:0) < 0
		LOCALS:0 = 扁平的胸部
;平たい胸
	ELSEIF GET_BUSTSIZE(MTAR:0) > 0
		LOCALS:0 = 丰满的胸部
;豊満な胸
	ELSE
		LOCALS:0 = 胸
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%用稍粗的触手刺入%LOCALS:1%的乳头，让其进入乳腺%TARGET_STR("た", "てきた")%…
			;TranslatedMark %ANAME(MPLY:0)%は一際太い触手を%LOCALS:1%の乳首に突き立て、乳腺へと入り込ませ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %LOCALS:1%在%ANAME(MPLY:0)%里，用触手触摸乳腺\@ TALENT:(MTAR:0):母乳体質 ? 开发 # 责弄 \@命令，命令
			;TranslatedMark %LOCALS:1%は%ANAME(MPLY:0)%に、触手で乳腺を\@ TALENT:(MTAR:0):母乳体質 ? 開発する # 責める \@よう命じた
			PRINTFORMW %ANAME(MPLY:0)%把比头发细的触手插在%LOCALS:1%的乳头上，让她进入了乳腺…
			;TranslatedMark %ANAME(MPLY:0)%は髪の毛より細い触手を%LOCALS:1%の乳首に突き立て、乳腺へと入り込ませた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%命令%ANAME(MPLY:0)%用触手榨取%LOCALS:1%的母乳
			;TranslatedMark %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、触手で%LOCALS:1%の母乳を搾るように命令した
			PRINTFORMW %ANAME(MPLY:0)%把比头发细的触手插在%LOCALS:1%的乳头上，让她进入了乳腺…
			;TranslatedMark %ANAME(MPLY:0)%は髪の毛より細い触手を%LOCALS:1%の乳首に突き立て、乳腺へと入り込ませた…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST214
FOR LOCAL, 0, MTAR_NUM
	LOCAL:1 = MTAR:LOCAL
	SIF EXP:(LOCAL:1):搾乳経験値 < 50 - MIN(ABL:(LOCAL:1):Ｂ感, 10) - MIN(ABL:(LOCAL:1):触手, 10)
		CONTINUE
	SETCOLOR カラー_ピンク
	IF CFLAG:(LOCAL:1):一時的母乳体質
		PRINTFORML %ANAME(LOCAL:1)%的乳房好像是由触手改造而成的……
		;TranslatedMark %ANAME(LOCAL:1)%の乳房は触手の改造によって作り替えられたようだ……
		PRINTFORML 曾经是一时性的%ANAME(LOCAL:1)%的母乳体质扎根了
		;TranslatedMark 一時的なものだった%ANAME(LOCAL:1)%の母乳体質が定着した
		CFLAG:(LOCAL:1):一時的母乳体質 = 0
	ELSEIF !TALENT:(LOCAL:1):母乳体質
		PRINTFORML %ANAME(LOCAL:1)%的乳房好像是由触手改造而成的……
		;TranslatedMark %ANAME(LOCAL:1)%の乳房は触手の改造によって作り替えられたようだ……
		PRINTFORML 明明没有怀孕却变成了"母乳体质"
		;TranslatedMark %ANAME(LOCAL:1)%は孕んでもいないのに＜母乳体質＞になった
		TALENT:(LOCAL:1):母乳体質 = 1
	ENDIF
NEXT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN214
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_214()
RETURN POWER(2, 性的嗜好_触手で犯したい) + POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_214()
RETURN POWER(2, 性的嗜好_触手で犯されたい) + POWER(2, 性的嗜好_虐げられたい)


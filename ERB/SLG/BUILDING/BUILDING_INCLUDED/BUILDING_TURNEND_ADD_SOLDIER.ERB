@SLG_BUILDING_TURNEND_ADD_SOLDIER(COUNTRY_ID,ADD_NUM)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM ADD_NUM
    #DIM SOLDIER_BUILDING_NUM
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_集群巢穴) > 0
        LOCAL = 200
        LOCAL += MIN(SLG_BUILDING_GET_FEMALE_BOSS_NUM(),5)*100

        IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_哥布林王巢穴) > 0
            BUILDING_TEXT = BUILDING_TEXT_哥布林王巢穴(COUNTRY_ID,1)
            ADD_NUM += 2 * LOCAL * RESULT:1
        ELSE
            ADD_NUM += LOCAL * RESULT:1
        ENDIF
        BUILDING_TEXT = BUILDING_TEXT_集群巢穴(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_奴隶市场) > 0
        SOLDIER_BUILDING_NUM = RESULT:1
        ADD_NUM += MIN(GET_SLAVE_MARKET_TOHO_NUM(),5) * 100 * SOLDIER_BUILDING_NUM
        BUILDING_TEXT = BUILDING_TEXT_奴隶市场_1(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_皮套人演出中心) > 0
        SOLDIER_BUILDING_NUM = RESULT:1
        ADD_NUM += SOLDIER_BUILDING_NUM * 300
        BUILDING_TEXT = BUILDING_TEXT_皮套人演出中心_2(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_前线哨站) > 0
        SOLDIER_BUILDING_NUM = RESULT:1
        ADD_NUM += SOLDIER_BUILDING_NUM * 100
        BUILDING_TEXT = BUILDING_TEXT_前线哨站_2(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_纵深阵地) > 0
        SOLDIER_BUILDING_NUM = RESULT:1
        ADD_NUM += SOLDIER_BUILDING_NUM * 200
        BUILDING_TEXT = BUILDING_TEXT_纵深阵地_2(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_地下工事) > 0
        SOLDIER_BUILDING_NUM = RESULT:1
        ADD_NUM += SOLDIER_BUILDING_NUM * 400
        BUILDING_TEXT = BUILDING_TEXT_地下工事_6(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_全息界标) > 0
        SOLDIER_BUILDING_NUM = RESULT:1
        ADD_NUM += SOLDIER_BUILDING_NUM * (200 + GET_OWN_CITY(COUNTRY_ID)*100)
        BUILDING_TEXT = BUILDING_TEXT_全息界标(COUNTRY_ID)
    ENDIF
    SIF ADD_NUM > 1000
        ADD_NUM = MAX(ADD_NUM - SLG_BUILDING_DECREASE_SOLDIER(COUNTRY_ID), 1000)
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_兵舎) > 0
        ADD_NUM += ADD_NUM * (RESULT:1)  / 100
        BUILDING_TEXT = BUILDING_TEXT_兵舎(COUNTRY_ID,RESULT:1)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_军宣部) > 0
        TIMES ADD_NUM, 1.10
        BUILDING_TEXT = BUILDING_TEXT_军宣部(COUNTRY_ID)
    ENDIF
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_长波电台) > 0
        TIMES ADD_NUM, 1.30
        BUILDING_TEXT = BUILDING_TEXT_长波电台_2(COUNTRY_ID)
    ENDIF
    RETURNF ADD_NUM


@SLG_BUILDING_GET_ADD_SOLDIER_LIMIT(COUNTRY_ID,ADD_NUM)
    #FUNCTION
    #DIM COUNTRY_ID
    #DIM ADD_NUM

    SIF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_兵舎) > 0
	    ADD_NUM += (RESULT:1) * 300
    SIF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_大型仓库)
	    TIMES ADD_NUM,1.10

    RETURNF ADD_NUM

@SLG_BUILDING_TURNEND_ADD_SOLDIER_DIRECTLY()
    #DIM COUNTRY_ID
    #DIM ADD_NUM
    FOR COUNTRY_ID,0,MAX_COUNTRY
        SIF !IS_COUNTRY(COUNTRY_ID)
            CONTINUE
        IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_叛军电台) > 0
            COUNTRY_SOLDIER:(COUNTRY_ID) += 5000
            BUILDING_TEXT = BUILDING_TEXT_叛军电台_1(COUNTRY_ID)
        ENDIF

        IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_人偶剧场) > 0
            COUNTRY_SOLDIER:(COUNTRY_ID) += 3000
            BUILDING_TEXT = BUILDING_TEXT_人偶剧场(COUNTRY_ID)
        ENDIF

        IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_自然培养皿) > 0
            LOCAL = RESULT:1
            IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_孵化所) > 0
                LOCAL *= 2
                BUILDING_TEXT = BUILDING_TEXT_孵化所(COUNTRY_ID)
            ENDIF
            IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_外神接入点) > 0
                LOCAL *= 2
                BUILDING_TEXT = BUILDING_TEXT_外神接入点(COUNTRY_ID)
            ENDIF
            COUNTRY_SOLDIER:(COUNTRY_ID) += LOCAL * 500
            BUILDING_TEXT = BUILDING_TEXT_自然培养皿(COUNTRY_ID)
        ENDIF
    NEXT


@SLG_BUILDING_TURNEND_GET_SOLDIER_FROM_ADJOIN
    #DIM CITY_ID
    #DIM TARGET_CITY_ID
    #DIM SOLDIER_BUILDING_NUM
    #DIM ADD_NUM
    FOR CITY_ID,0,MAX_CITY
        SOLDIER_BUILDING_NUM = 0
        IF CHECK_BUILDING_FUNCTION_SELECT(CITY_ID,-1,建造物_专业招募所) > 0
            SOLDIER_BUILDING_NUM  = RESULT:2
            FOR TARGET_CITY_ID , 1 , MAX_CITY
                IF CITY_TYPE:(TARGET_CITY_ID) == 0 && !IS_SP_COUNTRY(TARGET_CITY_ID)
                    IF CITY_OWNER:(CITY_ID) != CITY_OWNER:(TARGET_CITY_ID) && IS_ROUTE(CITY_ID, TARGET_CITY_ID)
                        CALL SHIFT_SOLDIER(CFLAG:TARGET_CITY_ID:所属, CFLAG:CITY_ID:所属, 500*SOLDIER_BUILDING_NUM)
                    ENDIF
                ENDIF
            NEXT
            BUILDING_TEXT = BUILDING_TEXT_专业招募所_1(CITY_OWNER:CITY_ID)
        ENDIF
    NEXT


@SLG_BUILDING_DECREASE_SOLDIER(COUNTRY_ID)
    #FUNCTION
    #DIM 外来人_COUNTRY_ID
    #DIM COUNTRY_ID
    外来人_COUNTRY_ID = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_外来人)
    SIF !IS_COUNTRY(外来人_COUNTRY_ID)
        RETURNF 0
    SIF 外来人_COUNTRY_ID == COUNTRY_ID
        RETURNF 0
    IF CHECK_BUILDING_FUNCTION_SELECT(-1,COUNTRY_ID,建造物_皮套人演出中心) > 0
        RETURNF MAX(3000 * MIN((DAY-10) / 10 * 10, 0),RESULT:1 * 300)
        BUILDING_TEXT = BUILDING_TEXT_皮套人演出中心_1(COUNTRY_ID)
    ENDIF
    RETURNF 0


